OBJECT Table 17240 Tax Reg. Norm Term
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ValidateChangeDeclaration(TRUE);
             END;

    OnDelete=BEGIN
               ValidateChangeDeclaration(TRUE);

               TaxRegNormTermFormula.RESET;
               TaxRegNormTermFormula.SETRANGE("Norm Jurisdiction Code","Norm Jurisdiction Code");
               TaxRegNormTermFormula.SETRANGE("Term Code","Term Code");
               TaxRegNormTermFormula.DELETEALL;
             END;

    CaptionML=[ENU=Tax Reg. Norm Term;
               RUS=Термин нормы налог. регистра];
    LookupPageID=Page17282;
  }
  FIELDS
  {
    { 1   ;   ;Norm Jurisdiction Code;Code10      ;TableRelation="Tax Register Norm Jurisdiction";
                                                   CaptionML=[ENU=Norm Jurisdiction Code;
                                                              RUS=Код нормы юрисдикции];
                                                   NotBlank=Yes }
    { 2   ;   ;Term Code           ;Code20        ;CaptionML=[ENU=Term Code;
                                                              RUS=Код термина];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text150       ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 4   ;   ;Expression Type     ;Option        ;OnValidate=BEGIN
                                                                IF "Expression Type" <> xRec."Expression Type" THEN BEGIN
                                                                  IF xRec."Expression Type" = xRec."Expression Type"::Compare THEN BEGIN
                                                                    IF NOT CONFIRM(Text21000900,FALSE,xRec."Expression Type") THEN
                                                                      ERROR('');
                                                                    ValidateChangeDeclaration(TRUE);
                                                                    TaxRegNormTermFormula.RESET;
                                                                    TaxRegNormTermFormula.SETRANGE("Norm Jurisdiction Code","Norm Jurisdiction Code");
                                                                    TaxRegNormTermFormula.SETRANGE("Term Code","Term Code");
                                                                    TaxRegNormTermFormula.DELETEALL;
                                                                    VALIDATE(Expression,'');
                                                                  END ELSE
                                                                    ValidateChangeDeclaration(TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Expression Type;
                                                              RUS=Тип выражения];
                                                   OptionCaptionML=[ENU=Plus/Minus,Multiply/Divide,Compare;
                                                                    RUS=Сложение/вычитание,Умножение/деление,Сравнение];
                                                   OptionString=Plus/Minus,Multiply/Divide,Compare }
    { 5   ;   ;Expression          ;Text250       ;OnLookup=BEGIN
                                                              IF "Term Code" = '' THEN
                                                                EXIT;
                                                              TaxRegNormTermFormula.RESET;
                                                              TaxRegNormTermFormula.FILTERGROUP(2);
                                                              TaxRegNormTermFormula.SETRANGE("Norm Jurisdiction Code","Norm Jurisdiction Code");
                                                              TaxRegNormTermFormula.FILTERGROUP(0);
                                                              TaxRegNormTermFormula.SETRANGE("Term Code","Term Code");
                                                              IF ("Expression Type" = "Expression Type"::Compare) AND NOT (TaxRegNormTermFormula.COUNT = 3) THEN BEGIN
                                                                IF NOT (TaxRegNormTermFormula.COUNT = 0) THEN
                                                                  TaxRegNormTermFormula.DELETEALL;
                                                                TaxRegNormTermFormula.INIT;
                                                                TaxRegNormTermFormula."Norm Jurisdiction Code" := "Norm Jurisdiction Code";
                                                                TaxRegNormTermFormula."Term Code" := "Term Code";
                                                                TaxRegNormTermFormula."Account Type" := TaxRegNormTermFormula."Account Type"::Termin;
                                                                TaxRegNormTermFormula.Operation := TaxRegNormTermFormula.Operation::Negative;
                                                                TaxRegNormTermFormula."Line No." := 10000;
                                                                TaxRegNormTermFormula.INSERT;
                                                                TaxRegNormTermFormula.Operation := TaxRegNormTermFormula.Operation::Zero;
                                                                TaxRegNormTermFormula."Line No." := 20000;
                                                                TaxRegNormTermFormula.INSERT;
                                                                TaxRegNormTermFormula.Operation := TaxRegNormTermFormula.Operation::Positive;
                                                                TaxRegNormTermFormula."Line No." := 30000;
                                                                TaxRegNormTermFormula.INSERT;
                                                                TaxRegNormTermFormula."Line No." := 10000;
                                                                TaxRegNormTermFormula.FIND;
                                                                COMMIT;
                                                              END;
                                                              PAGE.RUNMODAL(0,TaxRegNormTermFormula);
                                                              Expression :=
                                                                TaxRegTermMgt.MakeTermExpressionText("Term Code","Norm Jurisdiction Code",
                                                                  DATABASE::"Tax Reg. Norm Term",DATABASE::"Tax Reg. Norm Term Formula");
                                                            END;

                                                   CaptionML=[ENU=Expression;
                                                              RUS=Выражение] }
    { 6   ;   ;Rounding Precision  ;Decimal       ;OnValidate=BEGIN
                                                                ValidateChangeDeclaration("Rounding Precision" <> xRec."Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Rounding Precision;
                                                              RUS=Точность округления];
                                                   DecimalPlaces=0: }
    { 7   ;   ;Process Sign        ;Option        ;OnValidate=BEGIN
                                                                ValidateChangeDeclaration("Process Sign" <> xRec."Process Sign");
                                                              END;

                                                   CaptionML=[ENU=Process Sign;
                                                              RUS=Обработка знака];
                                                   OptionCaptionML=[ENU=" ,Skip Negative,Skip Positive,Always Positive,Always Negative";
                                                                    RUS=" ,Пропустить отрицательные,Пропустить положительные,Только положительные,Только отрицательные"];
                                                   OptionString=[ ,Skip Negative,Skip Positive,Always Positive,Always Negative] }
    { 8   ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              RUS=Фильтр по дате] }
    { 9   ;   ;Check               ;Boolean       ;CaptionML=[ENU=Check;
                                                              RUS=Проверка] }
  }
  KEYS
  {
    {    ;Norm Jurisdiction Code,Term Code        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Term Code,Description                    }
  }
  CODE
  {
    VAR
      TaxRegNormTermFormula@1000 : Record 17241;
      TaxRegTermMgt@1002 : Codeunit 17200;
      Text21000900@1003 : TextConst 'ENU=Delete linked line %1?;RUS=Удалить связанную строку %1?';

    PROCEDURE ValidateChangeDeclaration@1(Incident@1000 : Boolean);
    VAR
      TaxRegNormJurisdiction@1001 : Record 17220;
    BEGIN
      IF NOT Incident THEN
        EXIT;

      TaxRegNormJurisdiction.GET("Norm Jurisdiction Code");
      // NormJurisdiction.ValidateChangeDeclaration();
    END;

    PROCEDURE GenerateProfile@2();
    VAR
      GenTermProfile@1000 : Record 17238;
    BEGIN
      IF NOT GenTermProfile.GET(DATABASE::"Tax Reg. Norm Term") THEN BEGIN
        GenTermProfile."Table No." := DATABASE::"Tax Reg. Norm Term";
        GenTermProfile."Section Code (Hdr)" := FIELDNO("Norm Jurisdiction Code");
        GenTermProfile."Term Code (Hdr)" := FIELDNO("Term Code");
        GenTermProfile."Expression Type (Hdr)" := FIELDNO("Expression Type");
        GenTermProfile."Expression (Hdr)" := FIELDNO(Expression);
        GenTermProfile."Description (Hdr)" := FIELDNO(Description);
        GenTermProfile."Check (Hdr)" := FIELDNO(Check);
        GenTermProfile."Process Sign (Hdr)" := FIELDNO("Process Sign");
        GenTermProfile."Rounding Precision (Hdr)" := FIELDNO("Rounding Precision");
        GenTermProfile."Date Filter (Hdr)" := FIELDNO("Date Filter");

        GenTermProfile."Section Code (Line)" := TaxRegNormTermFormula.FIELDNO("Norm Jurisdiction Code");
        GenTermProfile."Term Code (Line)" := TaxRegNormTermFormula.FIELDNO("Term Code");
        GenTermProfile."Line No. (Line)" := TaxRegNormTermFormula.FIELDNO("Line No.");
        GenTermProfile."Expression Type (Line)" := TaxRegNormTermFormula.FIELDNO("Expression Type");
        GenTermProfile."Operation (Line)" := TaxRegNormTermFormula.FIELDNO(Operation);
        GenTermProfile."Account Type (Line)" := TaxRegNormTermFormula.FIELDNO("Account Type");
        GenTermProfile."Account No. (Line)" := TaxRegNormTermFormula.FIELDNO("Account No.");
        GenTermProfile."Amount Type (Line)" := TaxRegNormTermFormula.FIELDNO("Amount Type");
        GenTermProfile."Bal. Account No. (Line)" := TaxRegNormTermFormula.FIELDNO("Bal. Account No.");
        GenTermProfile."Norm Jurisd. Code (Line)" := TaxRegNormTermFormula.FIELDNO("Jurisdiction Code");
        GenTermProfile."Process Sign (Line)" := TaxRegNormTermFormula.FIELDNO("Process Sign");
        GenTermProfile."Process Division by Zero(Line)" := TaxRegNormTermFormula.FIELDNO("Process Division by Zero");

        GenTermProfile.INSERT;
      END;
    END;

    BEGIN
    END.
  }
}

