OBJECT Table 12407 CD No. Header
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 TestNoSeries;
                 NoSeriesMgt.InitSeries(GetNoSeriesCode,xRec."No. Series","Declaration Date","No.","No. Series");
               END;
             END;

    OnDelete=VAR
               ItemLedgEntry@1210000 : Record 32;
             BEGIN
               ItemLedgEntry.RESET;
               ItemLedgEntry.SETCURRENTKEY("CD No.");
               ItemLedgEntry.SETRANGE("CD No.","No.");
               IF ItemLedgEntry.FINDFIRST THEN
                 ERROR(Text002,"No.");

               CDNoInfo.RESET;
               CDNoInfo.SETRANGE("CD Header No.","No.");
               CDNoInfo.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               CDNoInfo.RESET;
               CDNoInfo.SETRANGE("CD Header No.",xRec."No.");
               IF CDNoInfo.FINDFIRST THEN
                 ERROR(Text000,xRec."No.");
             END;

    CaptionML=[ENU=CD No. Header;
               RUS=Заголовок номера ДТ];
    LookupPageID=Page12419;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code30        ;CaptionML=[ENU=No.;
                                                              RUS=Номер] }
    { 3   ;   ;Country/Region of Origin Code;Code10;
                                                   TableRelation=Country/Region;
                                                   OnValidate=BEGIN
                                                                CDNoInfo.RESET;
                                                                CDNoInfo.SETRANGE("CD Header No.","No.");
                                                                IF NOT CDNoInfo.ISEMPTY THEN
                                                                  IF CONFIRM(Text001,TRUE) THEN
                                                                    CDNoInfo.MODIFYALL("Country/Region Code","Country/Region of Origin Code");
                                                              END;

                                                   CaptionML=[ENU=Country/Region of Origin Code;
                                                              RUS=Код страны/региона происхождения] }
    { 4   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 5   ;   ;Declaration Date    ;Date          ;CaptionML=[ENU=Declaration Date;
                                                              RUS=Дата декларации] }
    { 7   ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              RUS=Тип источника];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Item";
                                                                    RUS=" ,Клиент,Поставщик,Товар"];
                                                   OptionString=[ ,Customer,Vendor,Item] }
    { 8   ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Customer)) Customer
                                                                 ELSE IF (Source Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Source Type=CONST(Item)) Item;
                                                   OnValidate=BEGIN
                                                                CASE "Source Type" OF
                                                                  "Source Type"::Customer:
                                                                    BEGIN
                                                                      Cust.GET("Source No.");
                                                                      VALIDATE("Country/Region of Origin Code",Cust."Country/Region Code");
                                                                    END;
                                                                  "Source Type"::Vendor:
                                                                    BEGIN
                                                                      Vend.GET("Source No.");
                                                                      VALIDATE("Country/Region of Origin Code",Vend."Country/Region Code");
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Source No.;
                                                              RUS=Номер источника] }
    { 9   ;   ;Stockout Warning    ;Boolean       ;OnValidate=BEGIN
                                                                CDNoInfo.RESET;
                                                                CDNoInfo.SETRANGE("CD Header No.","No.");
                                                                IF NOT CDNoInfo.ISEMPTY THEN
                                                                  IF CONFIRM(Text001,TRUE) THEN
                                                                    CDNoInfo.MODIFYALL("Stockout Warning","Stockout Warning");
                                                              END;

                                                   CaptionML=[ENU=Stockout Warning;
                                                              RUS=Предупреждение о дефиците запасов];
                                                   Description=Not used }
    { 12  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Purch. Comment Line" WHERE (Document Type=CONST(Custom Declaration),
                                                                                                  No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              RUS=Комментарий];
                                                   Editable=No }
    { 17  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              RUS=Серия номеров];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Declaration Date,Source Type,Source No.,Country/Region of Origin Code }
  }
  CODE
  {
    VAR
      CDNoInfo@1210001 : Record 12408;
      Text000@1210002 : TextConst 'ENU=You cannot rename Custom Declaration %1.;RUS=Невозможно переименовать декларацию на товар %1.';
      Text001@1210003 : TextConst 'ENU=You have changed the header. Do you want to change lines?;RUS=Вы изменили заголовок. Изменить строки?';
      Text002@1210004 : TextConst 'ENU=You cannot delete Custom Declaration %1.;RUS=Невозможно удалить декларацию на товар %1.';
      Vend@1210000 : Record 23;
      Cust@1210005 : Record 18;
      InvtSetup@1210006 : Record 313;
      NoSeriesMgt@1210007 : Codeunit 396;

    PROCEDURE AssistEdit@2(OldCDNoHeader@1000 : Record 12407) : Boolean;
    BEGIN
      InvtSetup.GET;
      TestNoSeries;
      IF NoSeriesMgt.SelectSeries(GetNoSeriesCode,OldCDNoHeader."No. Series","No. Series") THEN BEGIN
        InvtSetup.GET;
        TestNoSeries;
        NoSeriesMgt.SetSeries("No.");
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE TestNoSeries@6();
    BEGIN
      InvtSetup.GET;
      InvtSetup.TESTFIELD("CD Header Nos.");
    END;

    LOCAL PROCEDURE GetNoSeriesCode@9() : Code[10];
    BEGIN
      InvtSetup.GET;
      EXIT(InvtSetup."CD Header Nos.");
    END;

    BEGIN
    END.
  }
}

