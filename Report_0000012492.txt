OBJECT Report 12492 FA Posted Release Act FA-1
{
  OBJECT-PROPERTIES
  {
    Date=23.01.17;
    Time=12:00:00;
    Version List=NAVRU10.00.00.15052;
  }
  PROPERTIES
  {
    CaptionML=[ENU=FA Posted Release Act FA-1;
               RUS=Учт. акт приема-передачи ОС-1];
    ProcessingOnly=Yes;
    OnInitReport=VAR
                   Employee@1470000 : Record 5200;
                 BEGIN
                   CompanyInfo.GET;
                   IF Employee.GET(CompanyInfo."Director No.") THEN
                     DirectorPosition := Employee.GetJobTitleName;
                   FASetup.GET;
                 END;

    OnPreReport=BEGIN
                  FA1Helper.InitReportTemplate;
                END;

    OnPostReport=BEGIN
                   IF FileName = '' THEN
                     FA1Helper.ExportData
                   ELSE
                     FA1Helper.ExportDataFile(FileName);
                 END;

  }
  DATASET
  {
    { 3950;    ;DataItem;                    ;
               DataItemTable=Table12471;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Release));
               OnAfterGetRecord=BEGIN
                                  FASetup.GET;
                                  FA1Helper.CheckPostedSignature(
                                    StoredBy,DATABASE::"Posted FA Doc. Header","Document Type","No.",StoredBy."Employee Type"::StoredBy);
                                  FA1Helper.CheckPostedSignature(
                                    ReceivedBy,DATABASE::"Posted FA Doc. Header","Document Type","No.",ReceivedBy."Employee Type"::ReceivedBy);
                                  FA1Helper.CheckPostedSignature(
                                    Chairman,DATABASE::"Posted FA Doc. Header","Document Type","No.",Chairman."Employee Type"::Chairman);
                                  FA1Helper.CheckPostedSignature(
                                    Member1,DATABASE::"Posted FA Doc. Header","Document Type","No.",Member1."Employee Type"::Member1);
                                  FA1Helper.CheckPostedSignature(
                                    Member2,DATABASE::"Posted FA Doc. Header","Document Type","No.",Member2."Employee Type"::Member2);

                                  FA1Helper.SetReportHeaderSheet;
                                END;

               OnPostDataItem=BEGIN
                                FA1Helper.SetReportHeaderSheet;
                              END;

               ReqFilterFields=No. }

    { 6338;1   ;DataItem;                    ;
               DataItemTable=Table12472;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnPreDataItem=BEGIN
                               IsHeaderPrinted := FALSE;
                             END;

               OnAfterGetRecord=VAR
                                  OrgInfoArray@1210000 : ARRAY [9] OF Text;
                                BEGIN
                                  FA.GET("FA No.");
                                  TESTFIELD("Depreciation Book Code");
                                  TESTFIELD("FA Posting Group");

                                  IF FASetup."FA Location Mandatory" THEN
                                    TESTFIELD("FA Location Code");
                                  IF "FA Location Code" <> '' THEN
                                    FALocation.GET("FA Location Code");

                                  IF FA."Initial Release Date" <> 0D THEN
                                    InitialReleaseDate := FA."Initial Release Date"
                                  ELSE
                                    InitialReleaseDate := "Posted FA Doc. Header"."Posting Date";

                                  FADepreciationBook.GET("FA No.","New Depreciation Book Code");
                                  FAPostingGroup.GET(FADepreciationBook."FA Posting Group");

                                  GetFAComments(Characteristics,PostedFAComment.Type::Characteristics);
                                  GetFAComments(ExtraWork,PostedFAComment.Type::"Extra Work");
                                  GetFAComments(Conclusion,PostedFAComment.Type::Conclusion);
                                  GetFAComments(Appendix,PostedFAComment.Type::Appendix);
                                  GetFAComments(Result,PostedFAComment.Type::Result);
                                  GetFAComments(Reason,PostedFAComment.Type::Reason);

                                  IF NOT IsHeaderPrinted THEN BEGIN
                                    OrgInfoArray[1] := SenderDirectorPosition;
                                    OrgInfoArray[2] := SenderDirectorName;
                                    OrgInfoArray[3] := DirectorPosition;
                                    OrgInfoArray[4] := ReceivedBy."Employee Org. Unit";
                                    OrgInfoArray[5] := SenderName;
                                    OrgInfoArray[6] := SenderAddress;
                                    OrgInfoArray[7] := SenderBank;
                                    OrgInfoArray[8] := SenderDepartment;
                                    OrgInfoArray[9] := Reason[1];

                                    FA1Helper.FillHeader(
                                      OrgInfoArray,"Posted FA Doc. Header"."Reason Document No.",
                                      FORMAT("Posted FA Doc. Header"."Reason Document Date"),
                                      "Posted FA Doc. Header"."No.",
                                      FORMAT("Posted FA Doc. Header"."Posting Date"),FORMAT("Posted FA Doc. Header"."FA Posting Date"),
                                      FORMAT(FADepreciationBook."Disposal Date"),FAPostingGroup."Acquisition Cost Account",FA."Depreciation Code",
                                      FA."Depreciation Group",FA."Inventory Number",FA."Factory No.",
                                      FA."No." + ' ' + FA.Description +' ' + FA."Description 2",
                                      FALocation.Name,FA.Manufacturer,SupplementalInformation1,SupplementalInformation2);
                                    IsHeaderPrinted := TRUE;
                                  END;
                                END;

               ReqFilterFields=Line No.;
               DataItemLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.) }

    { 2285;2   ;DataItem;                    ;
               DataItemTable=Table5612;
               DataItemTableView=SORTING(FA No.,Depreciation Book Code);
               OnPreDataItem=BEGIN
                               FA1Helper.SetBodySectionSheet;
                               FA1Helper.FillDataPageHeader;
                             END;

               OnAfterGetRecord=BEGIN
                                  ActualUse := FA1Helper.CalcActualUse("Posted FA Doc. Header"."Posting Date",FA."Initial Release Date");

                                  IF "No. of Depreciation Months" = 0 THEN
                                    "No. of Depreciation Months" := "No. of Depreciation Years" * 12;

                                  FA1Helper.FillDataLine(
                                    FA."Manufacturing Year",FORMAT(InitialReleaseDate),FORMAT("Last Maintenance Date"),ActualUse,
                                    FORMAT("No. of Depreciation Months"),Depreciation,"Book Value","Acquisition Cost",FORMAT("Initial Acquisition Cost"),
                                    FORMAT("No. of Depreciation Months"),FORMAT("Depreciation Method"),
                                    FORMAT(FA1Helper.CalcDepreciationRate("FA Depreciation Book")),FA1Helper.IsPrintFADeprBookLine("FA Depreciation Book"));
                                END;

               OnPostDataItem=BEGIN
                                CALCFIELDS("Initial Acquisition Cost","Acquisition Cost",Depreciation);
                              END;

               CalcFields=Initial Acquisition Cost,Depreciation,Acquisition Cost;
               DataItemLink=FA No.=FIELD(FA No.),
                            Depreciation Book Code=FIELD(New Depreciation Book Code) }

    { 8951;2   ;DataItem;                    ;
               DataItemTable=Table12474;
               DataItemTableView=SORTING(Item Type);
               OnPreDataItem=BEGIN
                               FA1Helper.FillCharPageHeader;
                             END;

               OnAfterGetRecord=BEGIN
                                  FA1Helper.FillCharLine(
                                    Name,"Nomenclature No.","Unit of Measure Code",FORMAT(Quantity),FORMAT(Mass));
                                END;

               DataItemLink=No.=FIELD(FA No.) }

    { 5444;2   ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               FA1Helper.FillCharPageFooter(Characteristics);
                               FA1Helper.SetFooterSectionSheet;
                               FA1Helper.FillReportFooter(
                                 Result[1],Result[2],ExtraWork[1],ExtraWork[2],Conclusion[1],Conclusion[2],Appendix[1],Appendix[2],
                                 Chairman."Employee Job Title",Chairman."Employee Name",
                                 Member1."Employee Job Title",Member1."Employee Name",
                                 Member2."Employee Job Title",Member2."Employee Name",
                                 ReceivedBy."Employee Job Title",ReceivedBy."Employee Name",
                                 StoredBy."Employee Job Title",StoredBy."Employee Name");
                             END;
                              }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1210022;2;Field     ;
                  CaptionML=[ENU=Sender Director Title;
                             RUS=Отправитель - обращ. к директору];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=SenderDirectorPosition }

      { 1210018;2;Field     ;
                  CaptionML=[ENU=Sender Director Full Name;
                             RUS=Отправитель - ФИО директора];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=SenderDirectorName }

      { 1210015;2;Field     ;
                  CaptionML=[ENU=Sender Organization;
                             RUS=Организация-отправитель];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=SenderName }

      { 1210011;2;Field     ;
                  CaptionML=[ENU=Sender Address;
                             RUS=Адрес отправителя];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=SenderAddress }

      { 1210007;2;Field     ;
                  CaptionML=[ENU=Sender Bank Information;
                             RUS=Сведения о банке отправителя];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=SenderBank }

      { 1210002;2;Field     ;
                  CaptionML=[ENU=Sender Org. Unit;
                             RUS=Подразделение отправителя];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=SenderDepartment }

      { 1470001;2;Field     ;
                  CaptionML=[ENU=Owners of shared property;
                             RUS=Владельцы совместной собственности];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=SupplementalInformation1 }

      { 1470000;2;Field     ;
                  CaptionML=[ENU=Foreign currency;
                             RUS=Иностранная валюта];
                  ApplicationArea=#Suite;
                  SourceExpr=SupplementalInformation2 }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      FASetup@1210006 : Record 5603;
      CompanyInfo@1210005 : Record 79;
      FADepreciationBook@1210007 : Record 5612;
      FAPostingGroup@1210008 : Record 5606;
      FALocation@1210000 : Record 5609;
      FA@1210001 : Record 5600;
      PostedFAComment@1470004 : Record 12479;
      ReceivedBy@1470007 : Record 12421;
      StoredBy@1470006 : Record 12421;
      Chairman@1470005 : Record 12421;
      Member1@1470003 : Record 12421;
      Member2@1470000 : Record 12421;
      DocSignMgt@1470002 : Codeunit 12420;
      FA1Helper@1210003 : Codeunit 14946;
      Characteristics@1210049 : ARRAY [5] OF Text[80];
      ExtraWork@1210050 : ARRAY [5] OF Text[80];
      Conclusion@1210051 : ARRAY [5] OF Text[80];
      Appendix@1210053 : ARRAY [5] OF Text[80];
      Result@1470008 : ARRAY [5] OF Text[80];
      Reason@1470010 : ARRAY [5] OF Text[80];
      ActualUse@1101457000 : Text[30];
      SenderName@1210030 : Text[100];
      SenderAddress@1210029 : Text[100];
      SenderBank@1210028 : Text[150];
      SenderDepartment@1210027 : Text[100];
      SenderDirectorPosition@1210026 : Text[100];
      SenderDirectorName@1210025 : Text[100];
      SupplementalInformation1@1210016 : Text[100];
      SupplementalInformation2@1210015 : Text[100];
      DirectorPosition@1470001 : Text[50];
      FileName@1210002 : Text;
      InitialReleaseDate@1470011 : Date;
      IsHeaderPrinted@1210004 : Boolean;

    PROCEDURE SetFileNameSilent@1210005(NewFileName@1210000 : Text);
    BEGIN
      FileName := NewFileName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

