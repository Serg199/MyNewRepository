OBJECT Report 1092 Job Transfer to Credit Memo
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job Transfer to Credit Memo;
               RUS=Работа - перем. в кредит-ноту];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   Done := FALSE;
                 END;

    OnPostReport=BEGIN
                   Done := TRUE;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   InitReport;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 5   ;2   ;Field     ;
                  Name=CreateNewCreditMemo;
                  CaptionML=[ENU=Create New Credit Memo;
                             RUS=Создать новую кредит-ноту];
                  ToolTipML=[ENU=Specifies if the batch job creates a new sales credit memo.;
                             RUS=Указывает, что пакетное задание создает кредит-ноту продажи.];
                  ApplicationArea=#Jobs;
                  SourceExpr=NewCreditMemo;
                  OnValidate=BEGIN
                               IF NewCreditMemo THEN BEGIN
                                 CreditMemoNo := '';
                                 IF PostingDate = 0D THEN
                                   PostingDate := WORKDATE;
                                 CrMemoPostingDate := 0D;
                               END;
                             END;
                              }

      { 7   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             RUS=Дата учета];
                  ToolTipML=[ENU=Specifies the posting date for the document.;
                             RUS=Определяет дату учета документа.];
                  ApplicationArea=#Jobs;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               IF PostingDate = 0D THEN
                                 NewCreditMemo := FALSE;
                             END;
                              }

      { 1   ;2   ;Field     ;
                  Name=AppendToCreditMemoNo;
                  CaptionML=[ENU=Append to Credit Memo No.;
                             RUS=Добавить в кредит-ноту номер];
                  ToolTipML=[ENU=Specifies the number of the credit memo that you want to append the lines to if you did not select the Create New Credit Memo field.;
                             RUS=Определяет номер кредит-ноты, в которую необходимо добавить строки, если не был установлен флажок в поле "Создать новую кредит-ноту".];
                  ApplicationArea=#Jobs;
                  SourceExpr=CreditMemoNo;
                  OnValidate=BEGIN
                               IF CreditMemoNo <> '' THEN BEGIN
                                 SalesHeader.GET(SalesHeader."Document Type"::"Credit Memo",CreditMemoNo);
                                 CrMemoPostingDate := SalesHeader."Posting Date";
                                 NewCreditMemo := FALSE;
                                 PostingDate := 0D;
                               END;
                               IF CreditMemoNo = '' THEN
                                 InitReport;
                             END;

                  OnLookup=BEGIN
                             CLEAR(SalesHeader);
                             SalesHeader.FILTERGROUP := 2;
                             SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::"Credit Memo");
                             SalesHeader.SETRANGE("Bill-to Customer No.",Job."Bill-to Customer No.");
                             SalesHeader.FILTERGROUP := 0;
                             IF PAGE.RUNMODAL(0,SalesHeader) = ACTION::LookupOK THEN
                               CreditMemoNo := SalesHeader."No.";
                             IF CreditMemoNo <> '' THEN BEGIN
                               SalesHeader.GET(SalesHeader."Document Type"::"Credit Memo",CreditMemoNo);
                               CrMemoPostingDate := SalesHeader."Posting Date";
                               NewCreditMemo := FALSE;
                               PostingDate := 0D;
                             END;
                             IF CreditMemoNo = '' THEN
                               InitReport;
                           END;
                            }

      { 3   ;2   ;Field     ;
                  Name=CrMemoPostingDate;
                  CaptionML=[ENU=Cr. Memo Posting Date;
                             RUS=Дата учета кредит-ноты];
                  ToolTipML=[ENU=Specifies the posting date of that credit memo if you filled the Append to Credit Memo No. field.;
                             RUS=Определяет дату учета кредит-ноты, если было заполнено поле "Добавить в кредит-ноту номер".];
                  ApplicationArea=#Jobs;
                  SourceExpr=CrMemoPostingDate;
                  Editable=FALSE;
                  OnValidate=BEGIN
                               IF PostingDate = 0D THEN
                                 NewCreditMemo := FALSE;
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Job@1006 : Record 167;
      SalesHeader@1001 : Record 36;
      NewCreditMemo@1002 : Boolean;
      CreditMemoNo@1000 : Code[20];
      PostingDate@1003 : Date;
      CrMemoPostingDate@1005 : Date;
      Done@1004 : Boolean;

    PROCEDURE GetCreditMemoNo@1(VAR Done2@1003 : Boolean;VAR NewCreditMemo2@1000 : Boolean;VAR PostingDate2@1001 : Date;VAR CreditMemoNo2@1002 : Code[20]);
    BEGIN
      Done2 := Done;
      NewCreditMemo2 := NewCreditMemo;
      PostingDate2 := PostingDate;
      CreditMemoNo2 := CreditMemoNo;
    END;

    PROCEDURE InitReport@2();
    BEGIN
      PostingDate := WORKDATE;
      NewCreditMemo := TRUE;
      CreditMemoNo := '';
      CrMemoPostingDate := 0D;
    END;

    PROCEDURE SetCustomer@3(JobNo@1000 : Code[20]);
    BEGIN
      Job.GET(JobNo);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

