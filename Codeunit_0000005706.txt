OBJECT Codeunit 5706 TransferOrder-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00,NAVRU10.00;
  }
  PROPERTIES
  {
    TableNo=5740;
    OnRun=BEGIN
            TransHeader.COPY(Rec);
            Code;
            Rec := TransHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Ship,&Receive;RUS=&Отгрузить,&Получить';
      TransHeader@1001 : Record 5740;
      Text001@1210000 : TextConst 'ENU=Do you want to post the %1?;RUS=Учесть %1?';

    LOCAL PROCEDURE Code@1();
    VAR
      TransLine@1000 : Record 5741;
      TransferPostShipment@1002 : Codeunit 5704;
      TransferPostReceipt@1003 : Codeunit 5705;
      TransferPostTransfer@1210000 : Codeunit 12469;
      DefaultNumber@1001 : Integer;
      Selection@1004 : ' ,Shipment,Receipt';
    BEGIN
      WITH TransHeader DO BEGIN
        IF "Direct Transfer" THEN BEGIN
          IF CONFIRM(Text001,TRUE,FIELDCAPTION("Direct Transfer")) THEN
            TransferPostTransfer.RUN(TransHeader)
        END ELSE BEGIN
          TransLine.SETRANGE("Document No.","No.");
          IF TransLine.FIND('-') THEN BEGIN
            REPEAT
              IF (TransLine."Quantity Shipped" < TransLine.Quantity) AND
                 (DefaultNumber = 0)
              THEN
                DefaultNumber := 1;
              IF (TransLine."Quantity Received" < TransLine.Quantity) AND
                 (DefaultNumber = 0)
              THEN
                DefaultNumber := 2;
            UNTIL (TransLine.NEXT = 0) OR (DefaultNumber > 0);
          END;
          IF DefaultNumber = 0 THEN
            DefaultNumber := 1;
          Selection := STRMENU(Text000,DefaultNumber);
          CASE Selection OF
            0:
              EXIT;
            1:
              TransferPostShipment.RUN(TransHeader);
            2:
              TransferPostReceipt.RUN(TransHeader);
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

