OBJECT Codeunit 17308 FA Entry - Edit
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    TableNo=5601;
    Permissions=TableData 5601=imd;
    OnRun=BEGIN
            FALedgerEntry := Rec;
            FALedgerEntry.LOCKTABLE;
            FALedgerEntry.FIND;

            IF FALedgerEntry."Tax Difference Code" <> "Tax Difference Code" THEN
              CheckTaxDifference(FALedgerEntry,"Tax Difference Code");

            FALedgerEntry."Depr. Bonus" := "Depr. Bonus";
            FALedgerEntry."Tax Difference Code" := "Tax Difference Code";
            FALedgerEntry.MODIFY;
            Rec := FALedgerEntry;
          END;

  }
  CODE
  {
    VAR
      Text001@1210000 : TextConst 'ENU=%1 cannot be changed.;RUS=Невозможно изменить %1.';
      FALedgerEntry@1210002 : Record 5601;
      DepreciationBook@1210003 : Record 5611;
      TaxRegisterSetup@1210001 : Record 17236;

    PROCEDURE CheckTaxDifference@1210000(OldFALedgerEntry@1210000 : Record 5601;NewTaxDiffCode@1210001 : Code[10]);
    VAR
      FixedAsset@1210003 : Record 5600;
      TaxDiffLedgerEntry@1210002 : Record 17306;
    BEGIN
      DepreciationBook.GET(OldFALedgerEntry."Depreciation Book Code");
      DepreciationBook.TESTFIELD("Control FA Acquis. Cost",TRUE);

      TaxRegisterSetup.GET;
      FixedAsset.GET(OldFALedgerEntry."FA No.");

      OldFALedgerEntry.TESTFIELD(Reversed,FALSE);
      OldFALedgerEntry.TESTFIELD("Canceled from FA No.",'');

      IF NewTaxDiffCode = TaxRegisterSetup."Default FA TD Code" THEN
        EXIT;

      TaxDiffLedgerEntry.SETCURRENTKEY("Tax Diff. Code","Source Type","Source No.","Posting Date");
      TaxDiffLedgerEntry.SETRANGE("Source Type",FixedAsset.GetTDESourceType);
      TaxDiffLedgerEntry.SETRANGE("Source No.",OldFALedgerEntry."FA No.");
      TaxDiffLedgerEntry.SETFILTER("Tax Diff. Code",OldFALedgerEntry."Tax Difference Code");
      TaxDiffLedgerEntry.CALCSUMS(Difference);
      IF TaxDiffLedgerEntry.Difference <> 0 THEN
        ERROR(Text001,FALedgerEntry.FIELDCAPTION("Tax Difference Code"));
    END;

    BEGIN
    END.
  }
}

