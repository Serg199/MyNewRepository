OBJECT Page 5129 Update Opportunity
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Update Opportunity;
               RUS=Обновление возможности];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table5093;
    DataCaptionExpr=Caption;
    PageType=Card;
    OnInit=BEGIN
             CancelOldToDoEnable := TRUE;
             SalesQuoteEnable := TRUE;
             OptionSixEnable := TRUE;
             OptionFiveEnable := TRUE;
             OptionFourEnable := TRUE;
             OptionThreeEnable := TRUE;
             OptionTwoEnable := TRUE;
             OptionOneEnable := TRUE;
             SalesCycleStageEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 IsOnMobile := CURRENTCLIENTTYPE = CLIENTTYPE::Phone;
                 CreateStageList;
                 UpdateEditable;
                 IF Opp.GET("Opportunity No.") THEN
                   IF Opp."Sales Document No." <> '' THEN
                     SalesQuoteEnable := TRUE
                   ELSE
                     SalesQuoteEnable := FALSE;

                 UpdateCntrls;
                 UpdateEstimatedValues;
               END;

    OnQueryClosePage=BEGIN
                       IF CloseAction IN [ACTION::OK,ACTION::LookupOK] THEN
                         FinishPage;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 10      ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=[ENU=&Finish;
                                 RUS=&Готово];
                      ToolTipML=[ENU=Finish updating the opportunity.;
                                 RUS=Завершение обновления возможности.];
                      ApplicationArea=#RelationshipMgmt;
                      Promoted=Yes;
                      Visible=IsOnMobile;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 FinishPage;
                                 CurrPage.CLOSE;
                               END;
                                }
      { 31      ;1   ;Action    ;
                      Name=SalesQuote;
                      CaptionML=[ENU=&Sales Quote;
                                 RUS=&Предложение по продаже];
                      ToolTipML=[ENU=Create a sales quote based on the opportunity.;
                                 RUS=Создание предложения по продаже на основании возможной сделки.];
                      Promoted=Yes;
                      Enabled=SalesQuoteEnable;
                      InFooterBar=Yes;
                      Image=Quote;
                      OnAction=VAR
                                 SalesHeader@1001 : Record 36;
                               BEGIN
                                 IF Opp.GET("Opportunity No.") THEN BEGIN
                                   Opp.ShowQuote;
                                   IF SalesHeader.GET(SalesHeader."Document Type"::Quote,Opp."Sales Document No.") THEN BEGIN
                                     "Estimated Value (LCY)" := GetSalesDocValue(SalesHeader);
                                     CurrPage.UPDATE;
                                   END;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Field     ;
                Name=Action type;
                ToolTipML=[ENU=Specifies options that you can take when you reenter an opportunity to update it in the Update Opportunity window. Certain options are not available, depending on what stage you are in for your opportunity. For example, if you are in stage 1, you cannot select the Previous option.;
                           RUS=Определяет параметры, которые можно выбрать при повторном вводе возможности для ее обновления в окне "Обновление возможности". Некоторые параметры могут быть недоступны в зависимости от текущей стадии возможности. Например, на стадии 1 невозможно выбрать параметр "Предыдущий".];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Action Type";
                ValuesAllowed=[First;Next;Previous;Skip;Jump;Update];
                OnValidate=BEGIN
                             IF "Action Type" = "Action Type"::Update THEN
                               UpdateActionTypeOnValidate;
                             IF "Action Type" = "Action Type"::Jump THEN
                               JumpActionTypeOnValidate;
                             IF "Action Type" = "Action Type"::Skip THEN
                               SkipActionTypeOnValidate;
                             IF "Action Type" = "Action Type"::Previous THEN
                               PreviousActionTypeOnValidate;
                             IF "Action Type" = "Action Type"::Next THEN
                               NextActionTypeOnValidate;
                             IF "Action Type" = "Action Type"::First THEN
                               FirstActionTypeOnValidate;

                             WizardActionTypeValidate2;
                             UpdateCntrls;
                           END;
                            }

    { 5   ;1   ;Field     ;
                ToolTipML=[ENU=Specifies the sales cycle stage currently of the opportunity.;
                           RUS=Определяет текущую стадию цикла продаж для возможности.];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Sales Cycle Stage";
                CaptionClass=FORMAT("Sales Cycle Stage Description");
                Editable=SalesCycleStageEditable;
                OnValidate=BEGIN
                             WizardSalesCycleStageValidate2;
                             SalesCycleStageOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           LookupSalesCycleStage;
                           ValidateSalesCycleStage;
                         END;
                          }

    { 2   ;1   ;Field     ;
                ToolTipML=[ENU=Specifies the date this opportunity entry was last changed.;
                           RUS=Определяет дату последнего изменения операции возможности.];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Date of Change" }

    { 24  ;1   ;Field     ;
                CaptionML=[ENU=Estimated sales value (LCY);
                           RUS=Ожидаемая стоимость продажи (руб.)];
                ToolTipML=[ENU=Specifies the estimated value of the opportunity entry.;
                           RUS=Определяет оценочную сумму операции возможности.];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Estimated Value (LCY)" }

    { 27  ;1   ;Field     ;
                CaptionML=[ENU=Chances of Success (%);
                           RUS=Вероятность успеха (%)];
                ToolTipML=[ENU=Specifies the chances of success of the opportunity entry.;
                           RUS=Определяет шансы на успех операции возможности.];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Chances of Success %" }

    { 22  ;1   ;Field     ;
                CaptionML=[ENU=Estimated Closing Date;
                           RUS=Ожидаемая дата закрытия];
                ToolTipML=[ENU=Specifies the estimated date when the opportunity entry will be closed.;
                           RUS=Определяет предположительную дату закрытия операции возможности.];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr="Estimated Close Date" }

    { 30  ;1   ;Field     ;
                CaptionML=[ENU=Cancel Existing Open To-dos;
                           RUS=Отменить существующие открытые задачи];
                ToolTipML=[ENU=Specifies a to-do is to be cancelled from the opportunity.;
                           RUS=Определяет задачу, которую следует отменить в возможности.];
                SourceExpr="Cancel Old To Do";
                Enabled=CancelOldToDoEnable }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=untitled;RUS=без названия';
      Cont@1007 : Record 5050;
      SalesCycleStage@1008 : Record 5091;
      Opp@1023 : Record 5092;
      SalesCycleStageEditable@19046319 : Boolean INDATASET;
      OptionOneEnable@19038483 : Boolean INDATASET;
      OptionTwoEnable@19021894 : Boolean INDATASET;
      OptionThreeEnable@19072745 : Boolean INDATASET;
      OptionFiveEnable@19023658 : Boolean INDATASET;
      OptionFourEnable@19026445 : Boolean INDATASET;
      OptionSixEnable@19042740 : Boolean INDATASET;
      SalesQuoteEnable@19040029 : Boolean INDATASET;
      CancelOldToDoEnable@19073952 : Boolean INDATASET;
      Text666@19003950 : TextConst 'ENU=%1 is not a valid selection.;RUS=%1 - недопустимый вариант.';
      IsOnMobile@1001 : Boolean;

    LOCAL PROCEDURE Caption@1() : Text[260];
    VAR
      CaptionStr@1000 : Text[260];
    BEGIN
      IF Cont.GET("Contact Company No.") THEN
        CaptionStr := COPYSTR(Cont."No." + ' ' + Cont.Name,1,MAXSTRLEN(CaptionStr));
      IF Cont.GET("Contact No.") THEN
        CaptionStr := COPYSTR(CaptionStr + ' ' + Cont."No." + ' ' + Cont.Name,1,MAXSTRLEN(CaptionStr));
      IF CaptionStr = '' THEN
        CaptionStr := Text000;

      EXIT(CaptionStr);
    END;

    LOCAL PROCEDURE UpdateEditable@3();
    BEGIN
      OptionOneEnable := NoOfSalesCyclesFirst > 0;
      OptionTwoEnable := NoOfSalesCyclesNext > 0;
      OptionThreeEnable := NoOfSalesCyclesPrev > 0;
      OptionFourEnable := NoOfSalesCyclesSkip > 1;
      OptionFiveEnable := NoOfSalesCyclesUpdate > 0;
      OptionSixEnable := NoOfSalesCyclesJump > 1;
    END;

    LOCAL PROCEDURE UpdateCntrls@4();
    VAR
      ToDo@1000 : Record 5080;
    BEGIN
      CASE "Action Type" OF
        "Action Type"::First:
          BEGIN
            SalesCycleStageEditable := FALSE;
            CancelOldToDoEnable := FALSE;
          END;
        "Action Type"::Next:
          BEGIN
            SalesCycleStageEditable := FALSE;
            CancelOldToDoEnable := TRUE;
          END;
        "Action Type"::Previous:
          BEGIN
            SalesCycleStageEditable := FALSE;
            CancelOldToDoEnable := TRUE;
          END;
        "Action Type"::Skip:
          BEGIN
            SalesCycleStageEditable := TRUE;
            CancelOldToDoEnable := TRUE;
          END;
        "Action Type"::Update:
          BEGIN
            SalesCycleStageEditable := FALSE;
            CancelOldToDoEnable := FALSE;
          END;
        "Action Type"::Jump:
          BEGIN
            SalesCycleStageEditable := TRUE;
            CancelOldToDoEnable := TRUE;
          END;
      END;
      ToDo.RESET;
      ToDo.SETCURRENTKEY("Opportunity No.");
      ToDo.SETRANGE("Opportunity No.","Opportunity No.");
      IF ToDo.FINDFIRST THEN
        CancelOldToDoEnable := TRUE;
      MODIFY;
    END;

    LOCAL PROCEDURE SalesCycleStageOnAfterValidate@19013183();
    BEGIN
      IF SalesCycleStage.GET("Sales Cycle Code","Sales Cycle Stage") THEN
        "Sales Cycle Stage Description" := SalesCycleStage.Description;
    END;

    LOCAL PROCEDURE FirstActionTypeOnValidate@19058659();
    BEGIN
      IF NOT OptionOneEnable THEN
        ERROR(Text666,"Action Type");
    END;

    LOCAL PROCEDURE NextActionTypeOnValidate@19014484();
    BEGIN
      IF NOT OptionTwoEnable THEN
        ERROR(Text666,"Action Type");
    END;

    LOCAL PROCEDURE PreviousActionTypeOnValidate@19062437();
    BEGIN
      IF NOT OptionThreeEnable THEN
        ERROR(Text666,"Action Type");
    END;

    LOCAL PROCEDURE SkipActionTypeOnValidate@19009201();
    BEGIN
      IF NOT OptionFourEnable THEN
        ERROR(Text666,"Action Type");
    END;

    LOCAL PROCEDURE JumpActionTypeOnValidate@19068273();
    BEGIN
      IF NOT OptionSixEnable THEN
        ERROR(Text666,"Action Type");
    END;

    LOCAL PROCEDURE UpdateActionTypeOnValidate@19068895();
    BEGIN
      IF NOT OptionFiveEnable THEN
        ERROR(Text666,"Action Type");
    END;

    LOCAL PROCEDURE FinishPage@5();
    BEGIN
      CheckStatus2;
      FinishWizard2;
    END;

    LOCAL PROCEDURE UpdateEstimatedValues@2();
    VAR
      SalesCycleStage@1000 : Record 5091;
      SalesHeader@1001 : Record 36;
    BEGIN
      IF SalesCycleStage.GET("Sales Cycle Code","Sales Cycle Stage") THEN
        "Estimated Close Date" := CALCDATE(SalesCycleStage."Date Formula","Date of Change");

      IF SalesHeader.GET(SalesHeader."Document Type"::Quote,Opp."Sales Document No.") THEN
        "Estimated Value (LCY)" := GetSalesDocValue(SalesHeader);

      MODIFY;
    END;

    BEGIN
    END.
  }
}

