OBJECT Codeunit 409 Dimension Value-Indent
{
  OBJECT-PROPERTIES
  {
    Date=30.03.17;
    Time=17:43:47;
    Modified=Yes;
    Version List=NAVW17.10,NOS-26;
  }
  PROPERTIES
  {
    TableNo=349;
    OnRun=BEGIN
            IF NOT
               CONFIRM(
                 STRSUBSTNO(
                   Text000 +
                   Text001 +
                   Text002 +
                   Text003,"Dimension Code"),TRUE)
            THEN
              EXIT;

            DimVal.SETRANGE("Dimension Code","Dimension Code");
            Indent;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="This function updates the indentation of all the dimension values for dimension %1. ";RUS="Эта функция обновляет отступы для всех значений измерения для измерения %1. "';
      Text001@1001 : TextConst 'ENU="All dimension values between a Begin-Total and the matching End-Total are indented by one level. ";RUS="Все значения измерения между ""Сумма (от)"" и соответствующим ""Сумма (до)"" сдвигаются вправо на один уровень. "';
      Text002@1002 : TextConst 'ENU=The Totaling field for each End-Total is also updated.\\;RUS=Поле "Группировка" для каждого "Сумма (до)" также обновлено.\\';
      Text003@1003 : TextConst 'ENU=Do you want to indent the dimension values?;RUS=Сделать отступ для значений измерения?';
      Text004@1004 : TextConst 'ENU=Indenting Dimension Values @1@@@@@@@@@@@@@@@@@@;RUS=Отступы для знач. измерений @1@@@@@@@@@@@@@@@@@@';
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.;RUS=Для счета типа "Сумма (до)" %1 отсутствует соответствующий счет "Сумма (от)".';
      DimVal@1006 : Record 349;
      Window@1007 : Dialog;
      DimValCode@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    PROCEDURE Indent@1();
    VAR
      NoOfDimVals@1000 : Integer;
      Progress@1001 : Integer;
    BEGIN
      Window.OPEN(Text004);

      NoOfDimVals := DimVal.COUNT;
      IF NoOfDimVals = 0 THEN
        NoOfDimVals := 1;
      WITH DimVal DO
        IF FIND('-') THEN
          REPEAT
            Progress := Progress + 1;
            Window.UPDATE(1,10000 * Progress DIV NoOfDimVals);

            IF "Dimension Value Type" = "Dimension Value Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  Code);
              Totaling := DimValCode[i] + '..' + Code;
              i := i - 1;
            END;

            Indentation := i;

            //NC MDB01 > MP
            IF ("Dimension Value Type" <> "Dimension Value Type"::Heading) AND (i <> 0) THEN BEGIN
              "Hierarchy Parent Code" := DimValCode[i];
            END ELSE
              IF i = 0 THEN
                "Hierarchy Parent Code" := '';
            //NC MDB01 < MP

            //NC PIF001 > DP
            {
            MODIFY;
            }
            MODIFY(TRUE);
            //NC PIF001 < DP

            IF "Dimension Value Type" = "Dimension Value Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              DimValCode[i] := Code;
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    {
      NC PIF001 Изменения в коде
      NC MDB01 MP Прописываем родителя при создании отступов
    }
    END.
  }
}

