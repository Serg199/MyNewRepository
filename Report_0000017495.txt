OBJECT Report 17495 Employee Vacation Balance
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Employee Vacation Balance;
               RUS=Баланс отпуска сотрудников];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ReportToDate := TODAY;
                 END;

    OnPreReport=BEGIN
                  ExcelMgt.CreateBook;
                  ExcelMgt.OpenSheetByNumber(1);
                END;

    OnPostReport=BEGIN
                   ExcelMgt.GiveUserControl;
                 END;

  }
  DATASET
  {
    { 7528;    ;DataItem;                    ;
               DataItemTable=Table5200;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               Text001@1210002 : TextConst 'ENU=Unused Calendar Days;RUS=Неисп. календарные дни';
                               Employee@1210001 : Record 5200;
                               EmployeeAbsenceEntry@1210003 : Record 17389;
                               CellColumn@1210000 : Code[10];
                             BEGIN
                               ExcelMgt.FillCell('A1',EmployeeAbsenceEntry.FIELDCAPTION("Time Activity Code"));
                               ExcelMgt.FillCell('B1',TimeActivityCode);
                               ExcelMgt.FillCell('C1',FORMAT(ReportToDate));

                               RowCounter := 3;
                               CellColumn := 'A';

                               ExcelMgt.SetColumnSize(CellColumn + FORMAT(RowCounter),15);
                               FillCell(CellColumn,RowCounter,EmployeeAbsenceEntry.FIELDCAPTION("Employee No."));
                               ExcelMgt.SetColumnSize(CellColumn + FORMAT(RowCounter),15);
                               FillCell(CellColumn,RowCounter,Employee.FIELDCAPTION("Short Name"));
                               ExcelMgt.SetColumnSize(CellColumn + FORMAT(RowCounter),15);
                               FillCell(CellColumn,RowCounter,Employee.FIELDCAPTION("Job Title"));
                               ExcelMgt.SetColumnSize(CellColumn + FORMAT(RowCounter),15);
                               FillCell(CellColumn,RowCounter,EmployeeAbsenceEntry.FIELDCAPTION("Time Activity Code"));
                               ExcelMgt.SetColumnSize(CellColumn + FORMAT(RowCounter),15);
                               FillCell(CellColumn,RowCounter,EmployeeAbsenceEntry.FIELDCAPTION("Calendar Days"));
                               ExcelMgt.SetColumnSize(CellColumn + FORMAT(RowCounter),15);
                               FillCell(CellColumn,RowCounter,EmployeeAbsenceEntry.FIELDCAPTION("Used Calendar Days"));
                               ExcelMgt.SetColumnSize(CellColumn + FORMAT(RowCounter),15);
                               FillCell(CellColumn,RowCounter,Text001);

                               ExcelMgt.BoldRow(RowCounter);

                               RowCounter += 1;
                             END;

               OnAfterGetRecord=VAR
                                  VacationCalculation@1210000 : Codeunit 17375;
                                  CellColumn@1210001 : Code[10];
                                BEGIN
                                  CellColumn := 'A';

                                  FillCell(CellColumn,RowCounter,"No.");
                                  FillCell(CellColumn,RowCounter,"Short Name");
                                  FillCell(CellColumn,RowCounter,"Job Title");
                                  FillCell(CellColumn,RowCounter,TimeActivityCode);

                                  FillCell(CellColumn,RowCounter,FORMAT(ROUND(
                                        VacationCalculation.CalculateVacationDays("No.",ReportToDate,TimeActivityCode),0.01)));
                                  FillCell(CellColumn,RowCounter,FORMAT(ROUND(
                                        VacationCalculation.CalculateUsedVacationDays("No.",ReportToDate,TimeActivityCode),0.01)));
                                  FillCell(CellColumn,RowCounter,FORMAT(ROUND(
                                        VacationCalculation.CalculateUnusedVacationDays("No.",ReportToDate,TimeActivityCode),0.01)));

                                  RowCounter += 1;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1210001;2;Field     ;
                  CaptionML=[ENU=Time Activity Code;
                             RUS=Код временной активности];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=TimeActivityCode;
                  TableRelation="Time Activity" }

      { 1210000;2;Field     ;
                  CaptionML=[ENU=Report Date;
                             RUS=Дата отчета];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ReportToDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ExcelMgt@1210001 : Codeunit 12416;
      RowCounter@1210000 : Integer;
      TimeActivityCode@1210002 : Code[10];
      ReportToDate@1210003 : Date;

    PROCEDURE FillCell@1210019(VAR CellColumn@1210000 : Code[10];RowCounter@1210001 : Integer;CellText@1210002 : Text[250]);
    BEGIN
      ExcelMgt.FillCell(CellColumn + FORMAT(RowCounter),CellText);
      CellColumn := ExcelMgt.GetNextColumn(CellColumn,1);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

