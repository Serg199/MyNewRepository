OBJECT Report 50217 Deal Event 2 Copy Branch
{
  OBJECT-PROPERTIES
  {
    Date=13.07.17;
    Time=14:27:23;
    Modified=Yes;
    Version List=NOS-26;
  }
  PROPERTIES
  {
    CaptionML=RUS=Загрузка Событий Копировать Ветвь Настроек;
    ProcessingOnly=Yes;
    OnPostReport=VAR
                   rDealEvent2@1101495009 : Record 50169;
                   rLine@1101495008 : Record 50170;
                   rDim@1101495007 : Record 50171;
                   rDealEvent2Buf@1101495004 : TEMPORARY Record 50169;
                   rLineBuf@1101495003 : TEMPORARY Record 50170;
                   rDimBuf@1101495002 : TEMPORARY Record 50171;
                   i@1101495001 : Integer;
                 BEGIN
                   IF NewMappingCode = '' THEN
                     ERROR(Text001);

                   WITH DealEventBuf DO BEGIN
                     FINDFIRST;
                     REPEAT

                       rLineBuf.RESET;
                       rLineBuf.DELETEALL;

                       rLine.RESET;

                       rLine.SETRANGE("Mapping Code","Mapping Code");
                       rLine.SETRANGE("Event Code","Event Code");
                       rLine.SETRANGE("Deal Type","Deal Type");
                       rLine.SETRANGE("Settlement Type","Settlement Type");
                       rLine.SETRANGE("Deal Between Books","Deal Between Books");
                       rLine.SETRANGE("Company Name","Company Name");
                       IF rLine.FINDFIRST THEN REPEAT
                         rLineBuf := rLine;
                         rLineBuf.INSERT;
                       UNTIL rLine.NEXT = 0;
                       rLine.RESET;

                       rDimBuf.RESET;
                       rDimBuf.DELETEALL;

                       rDim.SETRANGE("Mapping Code","Mapping Code");
                       rDim.SETRANGE("Event Code","Event Code");
                       rDim.SETRANGE("Deal Type","Deal Type");
                       rDim.SETRANGE("Settlement Type","Settlement Type");
                       rDim.SETRANGE("Deal Between Books","Deal Between Books");
                       rDim.SETRANGE("Company Name","Company Name");
                       IF rDim.FINDFIRST THEN REPEAT
                         rDimBuf := rDim;
                         rDimBuf.INSERT;
                       UNTIL rDim.NEXT = 0;
                       rDim.RESET;

                       rLineBuf.RESET;
                       IF rLineBuf.FINDFIRST THEN REPEAT
                         rLine.GET(
                           rLineBuf."Mapping Code",
                           rLineBuf."Event Code",
                           rLineBuf."Deal Type",
                           rLineBuf."Settlement Type",
                           rLineBuf."Deal Between Books",
                           rLineBuf."Company Name",
                           rLineBuf."Line No."
                         );
                         rLine."Company Name" := NewCompanyCode;
                         rLine."Mapping Code" := NewMappingCode;
                         IF rLine.INSERT THEN;
                       UNTIL rLineBuf.NEXT = 0;

                       rDimBuf.RESET;
                       IF rDimBuf.FINDFIRST THEN REPEAT
                         rDim.GET(
                           rDimBuf."Mapping Code",
                           rDimBuf."Event Code",
                           rDimBuf."Deal Type",
                           rDimBuf."Settlement Type",
                           rDimBuf."Deal Between Books",
                           rDimBuf."Company Name",
                           rDimBuf."Line No.",
                           rDimBuf."Dimension Code"
                         );
                         rDim."Company Name" := NewCompanyCode;
                         rDim."Mapping Code" := NewMappingCode;
                         IF rDim.INSERT THEN;
                       UNTIL rDimBuf.NEXT = 0;

                       rDealEvent2Buf := DealEventBuf;
                       rDealEvent2Buf.INSERT;

                     UNTIL NEXT = 0;
                     FINDFIRST;

                     rDealEvent2Buf.RESET;
                     IF rDealEvent2Buf.FINDFIRST THEN REPEAT

                       rDealEvent2.GET(
                         rDealEvent2Buf."Event Code",
                         rDealEvent2Buf."Deal Type",
                         rDealEvent2Buf."Settlement Type",
                         rDealEvent2Buf."Deal Between Books",
                         rDealEvent2Buf."Mapping Code",
                         rDealEvent2Buf."Company Name"
                       );
                       rDealEvent2."Company Name" := NewCompanyCode;
                       rDealEvent2."Mapping Code" := NewMappingCode;
                       IF rDealEvent2.INSERT THEN
                         i+=1;
                     UNTIL rDealEvent2Buf.NEXT = 0;
                     MESSAGE(Text002,i);
                   END;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   NewMappingCode := DealEventBuf."Mapping Code";
                 END;

    }
    CONTROLS
    {
      { 1101495000;;Container;
                  ContainerType=ContentArea }

      { 1101495003;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры];
                  GroupType=Group }

      { 1101495001;2;Field  ;
                  CaptionML=[ENU=New mapping code;
                             RUS=Новая схема трансформации];
                  SourceExpr=NewMappingCode;
                  TableRelation=Mapping }

      { 1101495002;2;Field  ;
                  CaptionML=[ENU=New Company Code;
                             RUS=Новая фирма];
                  SourceExpr=NewCompanyCode;
                  TableRelation=Company }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DealEventBuf@1101495000 : TEMPORARY Record 50169;
      NewMappingCode@1101495001 : Code[20];
      NewCompanyCode@1101495002 : Text[50];
      Text001@1101495003 : TextConst 'ENU=Mapping code should not be empty;RUS=Новый код трансформации должен быть указан';
      Text002@1101495004 : TextConst 'ENU=%1 branches has been processed;RUS=Обработано %1 ветвей настроек';

    PROCEDURE SetBuf@1101495001(locDealEventBuf@1101495000 : TEMPORARY Record 50169);
    BEGIN
      DealEventBuf.TRANSFERFIELDS(locDealEventBuf,TRUE);
      DealEventBuf.INSERT;
    END;

    PROCEDURE ClearBuf@1101495002();
    BEGIN
      DealEventBuf.RESET;
      DealEventBuf.DELETEALL;
    END;

    BEGIN
    {
      NCS-2188 AAP Created
    }
    END.
  }
  RDLDATA
  {
  }
}

