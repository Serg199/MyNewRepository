OBJECT Table 17312 Tax Calc. Term Formula
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TaxCalcSection.GET("Section Code");
               TaxCalcSection.ValidateChange;
             END;

    OnModify=BEGIN
               TaxCalcSection.GET("Section Code");
               TaxCalcSection.ValidateChange;
             END;

    OnDelete=BEGIN
               TaxCalcSection.GET("Section Code");
               TaxCalcSection.ValidateChange;
             END;

    CaptionML=[ENU=Tax Calc. Term Formula;
               RUS=Формула термина налогового расчета];
    LookupPageID=Page17317;
  }
  FIELDS
  {
    { 1   ;   ;Term Code           ;Code20        ;TableRelation="Tax Calc. Term"."Term Code";
                                                   CaptionML=[ENU=Term Code;
                                                              RUS=Код термина];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              RUS=Номер строки];
                                                   NotBlank=Yes }
    { 3   ;   ;Expression Type     ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Tax Calc. Term"."Expression Type" WHERE (Term Code=FIELD(Term Code)));
                                                   CaptionML=[ENU=Expression Type;
                                                              RUS=Тип выражения];
                                                   OptionCaptionML=[ENU=Plus/Minus,Multiply/Divide,Compare;
                                                                    RUS=Сложение/вычитание,Умножение/деление,Сравнение];
                                                   OptionString=Plus/Minus,Multiply/Divide,Compare }
    { 4   ;   ;Operation           ;Option        ;OnValidate=BEGIN
                                                                CALCFIELDS("Expression Type");
                                                                IF ("Expression Type" = "Expression Type"::"Plus/Minus") AND
                                                                   NOT (Operation = Operation::"+") AND NOT (Operation = Operation::"-")
                                                                THEN
                                                                  ERROR(Text000);

                                                                IF ("Expression Type" = "Expression Type"::"Multiply/Divide") AND
                                                                   NOT (Operation = Operation::"*") AND NOT (Operation = Operation::"/")
                                                                THEN
                                                                  ERROR(Text001);

                                                                IF ("Expression Type" = "Expression Type"::Compare) AND
                                                                   NOT (Operation = Operation::"Less 0") AND NOT (Operation = Operation::"Equ 0") AND
                                                                   NOT (Operation = Operation::"Grate 0")
                                                                THEN
                                                                  ERROR(Text002);
                                                                IF Operation <> xRec.Operation THEN BEGIN
                                                                  TaxCalcSection.GET("Section Code");
                                                                  TaxCalcSection.ValidateChange;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Operation;
                                                              RUS=Операция];
                                                   OptionCaptionML=[ENU=+,-,*,/,Less 0,Equ 0,Grate 0;
                                                                    RUS=+,-,*,/,Меньше 0,Равно 0,Больше 0];
                                                   OptionString=+,-,*,/,Less 0,Equ 0,Grate 0 }
    { 5   ;   ;Account Type        ;Option        ;OnValidate=BEGIN
                                                                CALCFIELDS("Expression Type");
                                                                IF "Expression Type" = "Expression Type"::Compare THEN
                                                                  "Account Type" := "Account Type"::Termin
                                                                ELSE BEGIN
                                                                  "Account No." := '';
                                                                  "Bal. Account No." := '';
                                                                  "Amount Type" := "Amount Type"::Debit
                                                                END;
                                                                IF "Account Type" <> xRec."Account Type" THEN BEGIN
                                                                  TaxCalcSection.GET("Section Code");
                                                                  TaxCalcSection.ValidateChange;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Account Type;
                                                              RUS=Тип счета];
                                                   OptionCaptionML=[ENU=Constant,GL Acc,Termin,Net Change,Norm;
                                                                    RUS=Постоянная,Счет ГК,Термин,Оборот,Норма];
                                                   OptionString=Constant,GL Acc,Termin,Net Change,Norm }
    { 6   ;   ;Account No.         ;Code100       ;TableRelation=IF (Expression Type=FILTER(<>Compare),
                                                                     Account Type=CONST(GL Acc)) "G/L Account".No.
                                                                     ELSE IF (Expression Type=FILTER(<>Compare),
                                                                              Account Type=CONST(Net Change)) "G/L Account".No.
                                                                              ELSE IF (Account Type=CONST(Termin)) "Tax Calc. Term"."Term Code"
                                                                              ELSE IF (Account Type=CONST(Norm)) "Tax Register Norm Group".Code WHERE (Norm Jurisdiction Code=FIELD(Norm Jurisdiction Code),
                                                                                                                                                       Has Details=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Expression Type");
                                                                IF "Expression Type" = "Expression Type"::Compare THEN BEGIN
                                                                  TaxCalcTermFormula.RESET;
                                                                  TaxCalcTermFormula.SETRANGE("Section Code","Section Code");
                                                                  TaxCalcTermFormula.SETRANGE("Term Code","Term Code");
                                                                  TaxCalcTermFormula.SETFILTER("Line No.",'<>%1',"Line No.");
                                                                  TaxCalcTermFormula.MODIFYALL("Account No.","Account No.",FALSE);
                                                                  TaxCalcTermFormula.RESET;
                                                                END;
                                                                IF "Account No." <> xRec."Account No." THEN BEGIN
                                                                  TaxCalcSection.GET("Section Code");
                                                                  TaxCalcSection.ValidateChange;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Account No.;
                                                              RUS=Номер счета] }
    { 7   ;   ;Amount Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Account Type" = "Account Type"::"Net Change" THEN BEGIN
                                                                  IF "Amount Type" = "Amount Type"::"Net Change" THEN
                                                                    "Amount Type" := "Amount Type"::Debit;
                                                                  IF NOT ("Amount Type" IN ["Amount Type"::Debit,"Amount Type"::Credit]) THEN
                                                                    FIELDERROR("Amount Type",
                                                                      STRSUBSTNO(Text003,"Amount Type","Account Type"));
                                                                END ELSE
                                                                  IF "Account Type" <> "Account Type"::"GL Acc" THEN
                                                                    TESTFIELD("Amount Type","Amount Type"::" ")
                                                                  ELSE
                                                                    TESTFIELD("Amount Type");
                                                                IF "Amount Type" <> xRec."Amount Type" THEN BEGIN
                                                                  TaxCalcSection.GET("Section Code");
                                                                  TaxCalcSection.ValidateChange;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Amount Type;
                                                              RUS=Тип суммы];
                                                   OptionCaptionML=[ENU=" ,Net Change,Debit,Credit";
                                                                    RUS=" ,Оборот,Дебет,Кредит"];
                                                   OptionString=[ ,Net Change,Debit,Credit] }
    { 8   ;   ;Bal. Account No.    ;Code100       ;TableRelation=IF (Expression Type=CONST(Compare),
                                                                     Account Type=CONST(Termin)) "Tax Calc. Selection Setup"."Register No."
                                                                     ELSE IF (Account Type=CONST(Net Change)) "G/L Account".No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Expression Type");
                                                                IF ("Account Type" = "Account Type"::"Net Change") OR
                                                                   ("Expression Type" = "Expression Type"::Compare)
                                                                THEN
                                                                  TESTFIELD("Bal. Account No.");
                                                                IF "Bal. Account No." <> xRec."Bal. Account No." THEN BEGIN
                                                                  TaxCalcSection.GET("Section Code");
                                                                  TaxCalcSection.ValidateChange;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Bal. Account No.;
                                                              RUS=Номер баланс. счета] }
    { 9   ;   ;Process Sign        ;Option        ;OnValidate=BEGIN
                                                                IF "Process Sign" <> xRec."Process Sign" THEN BEGIN
                                                                  TaxCalcSection.GET("Section Code");
                                                                  TaxCalcSection.ValidateChange;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Process Sign;
                                                              RUS=Обработка знака];
                                                   OptionCaptionML=[ENU=" ,Skip Negative,Skip Positive,Always Positve,Always Negative";
                                                                    RUS=" ,Пропустить отрицательные,Пропустить положительные,Только положительные,Только отрицательные"];
                                                   OptionString=[ ,Skip Negative,Skip Positive,Always Positve,Always Negative] }
    { 10  ;   ;Process Division by Zero;Option    ;CaptionML=[ENU=Process Division by Zero;
                                                              RUS=Деление на ноль];
                                                   OptionCaptionML=[ENU=Zero,One;
                                                                    RUS=Ноль,Единица];
                                                   OptionString=Zero,One }
    { 13  ;   ;Section Code        ;Code10        ;TableRelation="Tax Calc. Section";
                                                   CaptionML=[ENU=Section Code;
                                                              RUS=Код секции];
                                                   NotBlank=Yes }
    { 14  ;   ;Norm Jurisdiction Code;Code10      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Tax Calc. Section"."Norm Jurisdiction Code" WHERE (Code=FIELD(Section Code)));
                                                   CaptionML=[ENU=Norm Jurisdiction Code;
                                                              RUS=Код нормы юрисдикции];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Section Code,Term Code,Line No.         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TaxCalcTermFormula@1000 : Record 17312;
      Text000@1001 : TextConst 'ENU=Operation must be + or -.;RUS=Операция должна быть сложением или вычитанием';
      Text001@1002 : TextConst 'ENU=Operation must be * or /.;RUS=Операция должна быть умножением или делением';
      Text002@1003 : TextConst 'ENU=Operation must be Compare.;RUS=Операция должна быть сравнением.';
      TaxCalcSection@1004 : Record 17307;
      Text003@1005 : TextConst 'ENU=cannot be %1 if Account Type is %2.;RUS=не может быть %1, если тип счета - %2.';

    BEGIN
    END.
  }
}

