OBJECT Page 5814 Item Charge Assignment (Sales)
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Charge Assignment (Sales);
               RUS=Назначение товарной издержки продажи];
    InsertAllowed=No;
    SourceTable=Table5809;
    DataCaptionExpr=DataCaption;
    DelayedInsert=Yes;
    PopulateAllFields=Yes;
    PageType=Worksheet;
    AutoSplitKey=Yes;
    RefreshOnActivate=Yes;
    OnOpenPage=BEGIN
                 FILTERGROUP(2);
                 SETRANGE("Document Type",SalesLine2."Document Type");
                 SETRANGE("Document No.",SalesLine2."Document No.");
                 SETRANGE("Document Line No.",SalesLine2."Line No.");
                 SETRANGE("Item Charge No.",SalesLine2."No.");
                 FILTERGROUP(0);
               END;

    OnAfterGetRecord=BEGIN
                       UpdateQty;
                     END;

    OnDeleteRecord=BEGIN
                     IF "Document Type" = "Applies-to Doc. Type" THEN BEGIN
                       SalesLine2.TESTFIELD("Shipment No.",'');
                       SalesLine2.TESTFIELD("Return Receipt No.",'');
                     END;
                   END;

    OnAfterGetCurrRecord=BEGIN
                           UpdateQtyAssgnt;
                         END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 RUS=Ф&ункции];
                      Image=Action }
      { 20      ;2   ;Action    ;
                      Name=GetShipmentLines;
                      AccessByPermission=TableData 110=R;
                      CaptionML=[ENU=Get &Shipment Lines;
                                 RUS=&Получить строки расх. накладной];
                      Image=Shipment;
                      OnAction=VAR
                                 ItemChargeAssgntSales@1001 : Record 5809;
                                 ShipmentLines@1002 : Page 5824;
                               BEGIN
                                 ItemChargeAssgntSales.SETRANGE("Document Type","Document Type");
                                 ItemChargeAssgntSales.SETRANGE("Document No.","Document No.");
                                 ItemChargeAssgntSales.SETRANGE("Document Line No.","Document Line No.");

                                 ShipmentLines.SETTABLEVIEW(SalesShptLine);
                                 IF ItemChargeAssgntSales.FINDLAST THEN
                                   ShipmentLines.InitializeSales(ItemChargeAssgntSales,SalesLine2."Sell-to Customer No.",UnitCost)
                                 ELSE
                                   ShipmentLines.InitializeSales(Rec,SalesLine2."Sell-to Customer No.",UnitCost);

                                 ShipmentLines.LOOKUPMODE(TRUE);
                                 ShipmentLines.RUNMODAL;
                               END;
                                }
      { 5       ;2   ;Action    ;
                      Name=GetReturnReceiptLines;
                      AccessByPermission=TableData 6660=R;
                      CaptionML=[ENU=Get &Return Receipt Lines;
                                 RUS=Получить строки &возвратной накладной];
                      Image=ReturnReceipt;
                      OnAction=VAR
                                 ItemChargeAssgntSales@1001 : Record 5809;
                                 ReceiptLines@1002 : Page 6667;
                               BEGIN
                                 ItemChargeAssgntSales.SETRANGE("Document Type","Document Type");
                                 ItemChargeAssgntSales.SETRANGE("Document No.","Document No.");
                                 ItemChargeAssgntSales.SETRANGE("Document Line No.","Document Line No.");

                                 ReceiptLines.SETTABLEVIEW(ReturnRcptLine);
                                 IF ItemChargeAssgntSales.FINDLAST THEN
                                   ReceiptLines.InitializeSales(ItemChargeAssgntSales,SalesLine2."Sell-to Customer No.",UnitCost)
                                 ELSE
                                   ReceiptLines.InitializeSales(Rec,SalesLine2."Sell-to Customer No.",UnitCost);

                                 ReceiptLines.LOOKUPMODE(TRUE);
                                 ReceiptLines.RUNMODAL;
                               END;
                                }
      { 33      ;2   ;Action    ;
                      Name=SuggestItemChargeAssignment;
                      AccessByPermission=TableData 5800=R;
                      CaptionML=[ENU=Suggest Item &Charge Assignment;
                                 RUS=Предложить распред. товарных &издержек];
                      Image=Suggest;
                      OnAction=VAR
                                 AssignItemChargeSales@1001 : Codeunit 5807;
                               BEGIN
                                 AssignItemChargeSales.SuggestAssignment(SalesLine2,AssignableQty,AssignableAmount);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the type of document that the item charge is assigned to.;
                           RUS=Определяет тип документа, для которого заданы товарные издержки.];
                SourceExpr="Applies-to Doc. Type";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the document that you are assigning the item charge to.;
                           RUS=Определяет номер документа, для которого задаются товарные издержки.];
                SourceExpr="Applies-to Doc. No.";
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the line number of the line that you are assigning the item charge to.;
                           RUS=Определяет номер строки, для которой задаются товарные издержки.];
                SourceExpr="Applies-to Doc. Line No.";
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the item number of the document line assigned to the item charge.;
                           RUS=Определяет номенклатурный номер в строке документа, заданной для товарных издержек.];
                SourceExpr="Item No.";
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies a description of the item on the sales line that you will assign the item charge to.;
                           RUS=Определяет описание товара в строке продажи, для которого будут заданы товарные издержки.];
                SourceExpr=Description }

    { 16  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of units of the item charge that will be assigned to this line.;
                           RUS=Определяет количество единиц товарных издержек, которое будет задано для этой строки.];
                SourceExpr="Qty. to Assign";
                OnValidate=BEGIN
                             IF SalesLine2.Quantity * "Qty. to Assign" < 0 THEN
                               ERROR(Text000,
                                 FIELDCAPTION("Qty. to Assign"),SalesLine2.FIELDCAPTION(Quantity));
                             QtytoAssignOnAfterValidate;
                           END;
                            }

    { 14  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity of the item charge that has been assigned.;
                           RUS=Определяет количество товарных издержек, которые уже заданы.];
                SourceExpr="Qty. Assigned" }

    { 18  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the amount that you will assign to this assignment line.;
                           RUS=Определяет сумму, которая будет установлена для этой строки задания.];
                SourceExpr="Amount to Assign";
                Editable=FALSE }

    { 36  ;2   ;Field     ;
                CaptionML=[ENU=Qty. to Ship (Base);
                           RUS=Кол-во для отгрузки (баз.)];
                ToolTipML=[ENU=Specifies a value if the sales line on this assignment line Specifies units that have not been posted as shipped.;
                           RUS=Задает значение, если строка продажи в этой строке задания, содержит единицы, которые еще не были учтены как отгруженные.];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=QtyToShipBase;
                Editable=FALSE }

    { 38  ;2   ;Field     ;
                CaptionML=[ENU=Qty. Shipped (Base);
                           RUS=Отгруженное кол-во (баз.)];
                ToolTipML=[ENU=Specifies the number of units that have been posted as shipped from the sales line that you inserted on this assignment line.;
                           RUS=Задает количество единиц, которое было учтено как отгруженное из строки продажи, вставленной в эту строку задания.];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=QtyShippedBase;
                Editable=FALSE }

    { 40  ;2   ;Field     ;
                CaptionML=[ENU=Return Qty. to Receive (Base);
                           RUS=Кол-во возврата для получения (баз.)];
                ToolTipML=[ENU=Specifies a value if the sales line on this assignment line Specifies units that have not been posted as a received return from your customer.;
                           RUS=Задает значение, если строка продажи в этой строке задания, содержит единицы, которые еще не были учтены как полученный возврат от клиента.];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=QtyToRetReceiveBase;
                Editable=FALSE }

    { 42  ;2   ;Field     ;
                CaptionML=[ENU=Return Qty. Received (Base);
                           RUS=Полученное кол-во возвр. (баз.)];
                ToolTipML=[ENU=Specifies the number of returned units that have been posted as received on the sales line on this assignment line.;
                           RUS=Задает количество возвращенных единиц, которое было учтено как полученное из строки продажи в этой строке задания.];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=QtyRetReceivedBase;
                Editable=FALSE }

    { 22  ;1   ;Group      }

    { 1900669001;2;Group  ;
                GroupType=FixedLayout }

    { 1901991801;3;Group  ;
                CaptionML=[ENU=Assignable;
                           RUS=К распределению] }

    { 26  ;4   ;Field     ;
                CaptionML=[ENU=Total (Qty.);
                           RUS=Всего (кол-во)];
                ToolTipML=[ENU=Specifies the total quantity of the item charge that you can assign in this window.;
                           RUS=Определяет общее количество товарных издержек, которые можно распределить в этом окне.];
                DecimalPlaces=0:5;
                SourceExpr=AssignableQty;
                Editable=FALSE }

    { 28  ;4   ;Field     ;
                CaptionML=[ENU=Total (Amount);
                           RUS=Всего (сумма)];
                ToolTipML=[ENU=Specifies the total item charge amount that you can assign in this window.;
                           RUS=Определяет общую сумму товарных издержек, которые можно распределить в этом окне.];
                DecimalPlaces=0:5;
                SourceExpr=AssignableAmount;
                Editable=FALSE }

    { 1903099001;3;Group  ;
                CaptionML=[ENU=To Assign;
                           RUS=Для назначения] }

    { 24  ;4   ;Field     ;
                CaptionML=[ENU=Qty. to Assign;
                           RUS=Кол-во для назначения];
                ToolTipML=[ENU=Specifies the quantity of the item charge that is assigned.;
                           RUS=Определяет количество распределяемых товарных издержек.];
                DecimalPlaces=0:5;
                SourceExpr=TotalQtyToAssign;
                Editable=FALSE }

    { 29  ;4   ;Field     ;
                CaptionML=[ENU=Amount to Assign;
                           RUS=Сумма для распределения];
                ToolTipML=[ENU=Specifies the amount of the item charge that will be assigned to the assignment lines.;
                           RUS=Определяет сумму товарных издержек, которые распределяются в строках задания.];
                DecimalPlaces=0:5;
                SourceExpr=TotalAmountToAssign;
                Editable=FALSE }

    { 1901313401;3;Group  ;
                CaptionML=[ENU=Rem. to Assign;
                           RUS=Остаток для распред.] }

    { 23  ;4   ;Field     ;
                CaptionML=[ENU=Rem. Qty. to Assign;
                           RUS=Остат. кол-во для распред.];
                ToolTipML=[ENU=Specifies the quantity of the item charge that you have not yet assigned to items in the assignment lines.;
                           RUS=Определяет количество товарных издержек, которые еще не распределены по товарам в строках задания.];
                DecimalPlaces=0:5;
                SourceExpr=RemQtyToAssign;
                Editable=FALSE }

    { 30  ;4   ;Field     ;
                Name=RemAmountToAssign;
                CaptionML=[ENU=Rem. Amount to Assign;
                           RUS=Сумма остатка для распред.];
                ToolTipML=[ENU=Specifies the amount of the item charge that you have not yet assigned to items in the assignment lines.;
                           RUS=Определяет сумму товарных издержек, которые еще не распределены по товарам в строках задания.];
                DecimalPlaces=0:5;
                SourceExpr=RemAmountToAssign;
                Editable=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The sign of %1 must be the same as the sign of %2 of the item charge.;RUS=Знак %1 должен совпадать со знаком %2 для товарных издержек.';
      SalesLine@1001 : Record 37;
      SalesLine2@1002 : Record 37;
      ReturnRcptLine@1003 : Record 6661;
      SalesShptLine@1004 : Record 111;
      AssignableQty@1005 : Decimal;
      TotalQtyToAssign@1006 : Decimal;
      RemQtyToAssign@1007 : Decimal;
      AssignableAmount@1008 : Decimal;
      TotalAmountToAssign@1009 : Decimal;
      RemAmountToAssign@1010 : Decimal;
      QtyToRetReceiveBase@1011 : Decimal;
      QtyRetReceivedBase@1012 : Decimal;
      QtyToShipBase@1013 : Decimal;
      QtyShippedBase@1014 : Decimal;
      UnitCost@1015 : Decimal;
      DataCaption@1016 : Text[250];

    LOCAL PROCEDURE UpdateQtyAssgnt@2();
    VAR
      ItemChargeAssgntSales@1000 : Record 5809;
    BEGIN
      SalesLine2.CALCFIELDS("Qty. to Assign","Qty. Assigned");
      AssignableQty := SalesLine2."Qty. to Invoice" + SalesLine2."Quantity Invoiced" -
        SalesLine2."Qty. Assigned";

      IF AssignableQty <> 0 THEN
        UnitCost := AssignableAmount / AssignableQty
      ELSE
        UnitCost := 0;

      ItemChargeAssgntSales.RESET;
      ItemChargeAssgntSales.SETCURRENTKEY("Document Type","Document No.","Document Line No.");
      ItemChargeAssgntSales.SETRANGE("Document Type","Document Type");
      ItemChargeAssgntSales.SETRANGE("Document No.","Document No.");
      ItemChargeAssgntSales.SETRANGE("Document Line No.","Document Line No.");
      ItemChargeAssgntSales.CALCSUMS("Qty. to Assign","Amount to Assign");
      TotalQtyToAssign := ItemChargeAssgntSales."Qty. to Assign";
      TotalAmountToAssign := ItemChargeAssgntSales."Amount to Assign";

      RemQtyToAssign := AssignableQty - TotalQtyToAssign;
      RemAmountToAssign := AssignableAmount - TotalAmountToAssign;
    END;

    LOCAL PROCEDURE UpdateQty@1();
    BEGIN
      CASE "Applies-to Doc. Type" OF
        "Applies-to Doc. Type"::Order,"Applies-to Doc. Type"::Invoice:
          BEGIN
            SalesLine.GET("Applies-to Doc. Type","Applies-to Doc. No.","Applies-to Doc. Line No.");
            QtyToShipBase := SalesLine."Qty. to Ship (Base)";
            QtyShippedBase := SalesLine."Qty. Shipped (Base)";
            QtyToRetReceiveBase := 0;
            QtyRetReceivedBase := 0;
          END;
        "Applies-to Doc. Type"::"Return Order","Applies-to Doc. Type"::"Credit Memo":
          BEGIN
            SalesLine.GET("Applies-to Doc. Type","Applies-to Doc. No.","Applies-to Doc. Line No.");
            QtyToRetReceiveBase := SalesLine."Return Qty. to Receive (Base)";
            QtyRetReceivedBase := SalesLine."Return Qty. Received (Base)";
            QtyToShipBase := 0;
            QtyShippedBase := 0;
          END;
        "Applies-to Doc. Type"::"Return Receipt":
          BEGIN
            ReturnRcptLine.GET("Applies-to Doc. No.","Applies-to Doc. Line No.");
            QtyToRetReceiveBase := 0;
            QtyRetReceivedBase := ReturnRcptLine."Quantity (Base)";
            QtyToShipBase := 0;
            QtyShippedBase := 0;
          END;
        "Applies-to Doc. Type"::Shipment:
          BEGIN
            SalesShptLine.GET("Applies-to Doc. No.","Applies-to Doc. Line No.");
            QtyToRetReceiveBase := 0;
            QtyRetReceivedBase := 0;
            QtyToShipBase := 0;
            QtyShippedBase := SalesShptLine."Quantity (Base)";
          END;
      END;
    END;

    PROCEDURE Initialize@3(NewSalesLine@1000 : Record 37;NewLineAmt@1001 : Decimal);
    BEGIN
      SalesLine2 := NewSalesLine;
      DataCaption := SalesLine2."No." + ' ' + SalesLine2.Description;
      AssignableAmount := NewLineAmt;
    END;

    LOCAL PROCEDURE QtytoAssignOnAfterValidate@19000177();
    BEGIN
      CurrPage.UPDATE(FALSE);
      UpdateQtyAssgnt;
    END;

    BEGIN
    END.
  }
}

