OBJECT Table 9062 User Security Status
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    CaptionML=[ENU=User Security Status;
               RUS=Статус безопасности пользователя];
    LookupPageID=Page9818;
  }
  FIELDS
  {
    { 1   ;   ;User Security ID    ;GUID          ;TableRelation=User."User Security ID";
                                                   CaptionML=[ENU=User Security ID;
                                                              RUS=Код безопасности пользователя] }
    { 2   ;   ;User Name           ;Code50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."User Name" WHERE (User Security ID=FIELD(User Security ID)));
                                                   CaptionML=[ENU=User Name;
                                                              RUS=Имя пользователя] }
    { 3   ;   ;Full Name           ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."Full Name" WHERE (User Security ID=FIELD(User Security ID)));
                                                   CaptionML=[ENU=Full Name;
                                                              RUS=ФИО] }
    { 13  ;   ;Reviewed            ;Boolean       ;CaptionML=[ENU=Reviewed;
                                                              RUS=Проверено] }
    { 14  ;   ;Belongs To Subscription Plan;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("User Plan" WHERE (User Security ID=FIELD(User Security ID)));
                                                   CaptionML=[ENU=Belongs To Subscription Plan;
                                                              RUS=Относится к плану подписки] }
    { 15  ;   ;Belongs to User Group;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Exist("User Group Member" WHERE (User Security ID=FIELD(User Security ID)));
                                                   CaptionML=[ENU=Belongs to User Group;
                                                              RUS=Относится к группе пользователей] }
    { 20  ;   ;Users - To review   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=[Count("User Security Status" WHERE (Reviewed=CONST(No),
                                                                                                    User Security ID=FILTER(<>{00000000-0000-0000-0000-000000000000})))];
                                                   CaptionML=[ENU=Users - To review;
                                                              RUS=Пользователи - на проверку] }
    { 21  ;   ;Users - Without Subscriptions;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=[Count("User Security Status" WHERE (Belongs To Subscription Plan=CONST(No),
                                                                                                    User Security ID=FILTER(<>{00000000-0000-0000-0000-000000000000})))];
                                                   CaptionML=[ENU=Users - Without Subscriptions;
                                                              RUS=Пользователи - без подписок] }
    { 22  ;   ;Users - Not Group Members;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=[Count("User Security Status" WHERE (Belongs to User Group=CONST(No),
                                                                                                    User Security ID=FILTER(<>{00000000-0000-0000-0000-000000000000})))];
                                                   CaptionML=[ENU=Users - Not Group Members;
                                                              RUS=Пользователи - не члены группы] }
  }
  KEYS
  {
    {    ;User Security ID                        ;Clustered=Yes }
    {    ;Reviewed                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      UserNotReviewedTxt@1001 : TextConst '@@@="%1 = user name";ENU=User %1: not reviewed.;RUS=Пользователь %1: не проверен.';
      UserReviewedTxt@1000 : TextConst '@@@=%1: User name;ENU=User %1: reviewed.;RUS=Пользователь %1: проверен.';
      SecurityActivityTok@1003 : TextConst 'ENU=User review;RUS=Проверка пользователя';
      SecurityContextTok@1002 : TextConst 'ENU=Security administration;RUS=Администрирование безопасности';

    PROCEDURE KeepOnlyEnabledUsers@1();
    VAR
      User@1000 : Record 2000000120;
      UserSecurityStatus@1001 : Record 9062;
    BEGIN
      User.SETRANGE(State,User.State::Disabled);
      IF NOT User.FINDSET THEN
        EXIT;
      REPEAT
        IF UserSecurityStatus.GET(User."User Security ID") THEN
          UserSecurityStatus.DELETE;
      UNTIL User.NEXT = 0;
    END;

    PROCEDURE LoadUsers@2();
    VAR
      User@1000 : Record 2000000120;
      UserSecurityStatus@1001 : Record 9062;
      UserPlan@1002 : Record 9005;
    BEGIN
      User.SETRANGE(State,User.State::Enabled);
      HideExternalUsers(User);
      IF NOT User.FINDSET THEN
        EXIT;

      REPEAT
        IF UserSecurityStatus.GET(User."User Security ID") THEN BEGIN
          UserPlan.SETRANGE("User Security ID","User Security ID");
          IF UserPlan.COUNT <= 1 THEN BEGIN
            UserSecurityStatus.Reviewed := FALSE;
            UserSecurityStatus.MODIFY(TRUE);
          END;
        END ELSE BEGIN
          UserSecurityStatus.INIT;
          UserSecurityStatus."User Security ID" := User."User Security ID";
          UserSecurityStatus.Reviewed := FALSE;
          UserSecurityStatus.INSERT;
        END;
      UNTIL User.NEXT = 0;
    END;

    PROCEDURE LogUserReviewActivity@3();
    VAR
      ActivityLog@1001 : Record 710;
      ActivityMessage@1000 : Text[250];
    BEGIN
      CALCFIELDS("User Name");
      IF Reviewed THEN
        ActivityMessage := STRSUBSTNO(UserReviewedTxt,"User Name")
      ELSE
        ActivityMessage := STRSUBSTNO(UserNotReviewedTxt,"User Name");
      ActivityLog.LogActivity(RECORDID,ActivityLog.Status::Success,SecurityContextTok,SecurityActivityTok,ActivityMessage);
    END;

    LOCAL PROCEDURE HideExternalUsers@6(VAR User@1000 : Record 2000000120);
    VAR
      PermissionManager@1001 : Codeunit 9002;
    BEGIN
      IF PermissionManager.SoftwareAsAService THEN
        User.SETFILTER("License Type",'<>%1',User."License Type"::"External User");
    END;

    BEGIN
    END.
  }
}

