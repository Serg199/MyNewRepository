OBJECT Table 959 Time Sheet Chart Setup
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Time Sheet Chart Setup;
               RUS=Настройка диаграммы табеля];
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Text132       ;CaptionML=[ENU=User ID;
                                                              RUS=Код пользователя] }
    { 2   ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              RUS=Дата начала] }
    { 3   ;   ;Show by             ;Option        ;CaptionML=[ENU=Show by;
                                                              RUS=Показывать по];
                                                   OptionCaptionML=[ENU=Status,Type,Posted;
                                                                    RUS=Статус,Тип,Разнесено];
                                                   OptionString=Status,Type,Posted }
    { 4   ;   ;Measure Type        ;Option        ;CaptionML=[ENU=Measure Type;
                                                              RUS=Тип меры];
                                                   OptionCaptionML=[ENU=Open,Submitted,Rejected,Approved,Scheduled,Posted,Not Posted,Resource,Job,Service,Absence,Assembly Order;
                                                                    RUS=Открыто,Отправлено,Отклонено,Утверждено,Запланировано,Разнесено,Не разнесено,Ресурс,Работа,Сервис,Отсутствие,Заказ на сборку];
                                                   OptionString=Open,Submitted,Rejected,Approved,Scheduled,Posted,Not Posted,Resource,Job,Service,Absence,Assembly Order }
  }
  KEYS
  {
    {    ;User ID                                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst '@@@=Period: (date)..(date) | show by (Status or Posted) | updated (time).;ENU=Period: %1..%2 | Show by: %3 | Updated: %4.;RUS=Период: %1..%2 | Показано: %3 | Обновлено: %4.';

    PROCEDURE GetCurrentSelectionText@1() : Text[250];
    BEGIN
      EXIT(STRSUBSTNO(Text001,"Starting Date",GetEndingDate,"Show by",TIME));
    END;

    PROCEDURE SetStartingDate@2(StartingDate@1000 : Date);
    BEGIN
      "Starting Date" := StartingDate;
      MODIFY;
    END;

    PROCEDURE GetEndingDate@4() : Date;
    BEGIN
      EXIT(CALCDATE('<1W>',"Starting Date") - 1);
    END;

    PROCEDURE FindPeriod@3(Which@1000 : 'Previous,Next');
    BEGIN
      CASE Which OF
        Which::Previous:
          "Starting Date" := CALCDATE('<-1W>',"Starting Date");
        Which::Next:
          "Starting Date" := CALCDATE('<+1W>',"Starting Date");
      END;
      MODIFY;
    END;

    PROCEDURE SetShowBy@5(ShowBy@1000 : Option);
    BEGIN
      "Show by" := ShowBy;
      MODIFY;
    END;

    PROCEDURE MeasureIndex2MeasureType@6(MeasureIndex@1000 : Integer) : Integer;
    BEGIN
      CASE "Show by" OF
        "Show by"::Status:
          EXIT(MeasureIndex);
        "Show by"::Posted:
          CASE MeasureIndex OF
            0:
              EXIT("Measure Type"::Posted);
            1:
              EXIT("Measure Type"::"Not Posted");
            2:
              EXIT("Measure Type"::Scheduled);
          END;
        "Show by"::Type:
          BEGIN
            IF MeasureIndex = 5 THEN
              EXIT("Measure Type"::Scheduled);
            EXIT(MeasureIndex + 7);
          END;
      END;
    END;

    BEGIN
    END.
  }
}

