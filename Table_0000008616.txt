OBJECT Table 8616 Config. Package Field
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               DeleteConfigFieldMapping;
             END;

    CaptionML=[ENU=Config. Package Field;
               RUS=Поле пакета конфигурации];
  }
  FIELDS
  {
    { 1   ;   ;Package Code        ;Code20        ;TableRelation="Config. Package";
                                                   CaptionML=[ENU=Package Code;
                                                              RUS=Код пакета];
                                                   NotBlank=Yes }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table ID;
                                                              RUS=Код таблицы];
                                                   NotBlank=Yes }
    { 3   ;   ;Field ID            ;Integer       ;TableRelation=IF (Dimension=CONST(No)) Field.No. WHERE (TableNo=FIELD(Table ID));
                                                   CaptionML=[ENU=Field ID;
                                                              RUS=Код поля];
                                                   NotBlank=Yes }
    { 4   ;   ;Field Name          ;Text30        ;CaptionML=[ENU=Field Name;
                                                              RUS=Имя поля] }
    { 5   ;   ;Field Caption       ;Text250       ;CaptionML=[ENU=Field Caption;
                                                              RUS=Метка поля] }
    { 6   ;   ;Validate Field      ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT Dimension THEN BEGIN
                                                                  IF xRec."Validate Field" AND NOT "Validate Field" AND "Primary Key" THEN
                                                                    ERROR(Text000,"Field Caption");
                                                                  UpdateFieldErrors;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Validate Field;
                                                              RUS=Проверить поле] }
    { 7   ;   ;Include Field       ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT Dimension THEN BEGIN
                                                                  IF xRec."Include Field" AND NOT "Include Field" AND "Primary Key" THEN
                                                                    ERROR(Text000,"Field Caption");
                                                                  "Validate Field" := "Include Field";
                                                                  UpdateFieldErrors;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Include Field;
                                                              RUS=Включить поле] }
    { 8   ;   ;Localize Field      ;Boolean       ;CaptionML=[ENU=Localize Field;
                                                              RUS=Локализовать поле] }
    { 9   ;   ;Relation Table ID   ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Relation Table ID;
                                                              RUS=Код таблицы связей];
                                                   Editable=No }
    { 10  ;   ;Relation Table Caption;Text250     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Relation Table ID)));
                                                   CaptionML=[ENU=Relation Table Caption;
                                                              RUS=Метка таблицы связей];
                                                   Editable=No }
    { 11  ;   ;Dimension           ;Boolean       ;CaptionML=[ENU=Dimension;
                                                              RUS=Измерение];
                                                   Editable=No }
    { 12  ;   ;Primary Key         ;Boolean       ;CaptionML=[ENU=Primary Key;
                                                              RUS=Первичный ключ];
                                                   Editable=No }
    { 13  ;   ;Processing Order    ;Integer       ;CaptionML=[ENU=Processing Order;
                                                              RUS=Порядок обработки] }
    { 14  ;   ;Create Missing Codes;Boolean       ;OnValidate=BEGIN
                                                                IF "Create Missing Codes" THEN
                                                                  TESTFIELD("Relation Table ID");
                                                              END;

                                                   CaptionML=[ENU=Create Missing Codes;
                                                              RUS=Создать отсутствующие коды] }
    { 15  ;   ;Mapping Exists      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Config. Field Mapping" WHERE (Package Code=FIELD(Package Code),
                                                                                                    Table ID=FIELD(Table ID),
                                                                                                    Field ID=FIELD(Field ID)));
                                                   CaptionML=[ENU=Mapping Exists;
                                                              RUS=Существует сопоставление];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Package Code,Table ID,Field ID          ;Clustered=Yes }
    {    ;Package Code,Table ID,Include Field      }
    {    ;Package Code,Table ID,Validate Field     }
    {    ;Package Code,Table ID,Processing Order   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 is part of the primary key and must be included.;RUS=%1 входит в состав первичного ключа и включается обязательно.';
      Text001@1001 : TextConst 'ENU=%1 value ''%2'' does not exist.;RUS=Значение ''%2'' для %1 не существует.';
      Text002@1003 : TextConst 'ENU=Updating validation errors;RUS=Обновление ошибок проверки';

    LOCAL PROCEDURE UpdateFieldErrors@1000();
    VAR
      ConfigPackageData@1002 : Record 8615;
      ConfigPackageField@1007 : Record 8616;
      TempConfigPackageTable@1004 : TEMPORARY Record 8613;
      ConfigPackageMgt@1000 : Codeunit 8611;
      ConfigValidateMgt@1005 : Codeunit 8617;
      ConfigProgressBar@1006 : Codeunit 8615;
      RecRef@1001 : RecordRef;
      FieldRef@1003 : FieldRef;
    BEGIN
      IF NOT Dimension THEN BEGIN
        RecRef.OPEN("Table ID",TRUE);
        FieldRef := RecRef.FIELD("Field ID");

        ConfigPackageData.SETRANGE("Package Code","Package Code");
        ConfigPackageData.SETRANGE("Table ID","Table ID");
        ConfigPackageData.SETRANGE("Field ID","Field ID");
        IF ConfigPackageData.FINDSET THEN BEGIN
          ConfigProgressBar.Init(ConfigPackageData.COUNT,1,Text002);
          REPEAT
            ConfigProgressBar.Update(ConfigPackageData.Value);
            ConfigPackageMgt.CleanFieldError(ConfigPackageData);
            IF "Include Field" THEN BEGIN
              ConfigPackageMgt.FieldError(ConfigPackageData,ConfigValidateMgt.EvaluateValue(FieldRef,ConfigPackageData.Value,FALSE),0);
              IF "Validate Field" THEN BEGIN
                CLEAR(TempConfigPackageTable);
                ConfigPackageField.INIT;
                ConfigPackageField.RESET;
                ConfigPackageField.SETRANGE("Package Code","Package Code");
                ConfigPackageField.SETRANGE("Table ID","Table ID");
                ConfigPackageField.SETRANGE("Field ID","Field ID");
                IF NOT ConfigPackageMgt.ValidateFieldRelation(ConfigPackageField,TempConfigPackageTable) THEN
                  ConfigPackageMgt.FieldError(ConfigPackageData,STRSUBSTNO(Text001,FieldRef.CAPTION,ConfigPackageData.Value),0);
              END;
            END;
          UNTIL ConfigPackageData.NEXT = 0;
          ConfigProgressBar.Close;
        END;
      END;
    END;

    LOCAL PROCEDURE DeleteConfigFieldMapping@1();
    VAR
      ConfigFieldMapping@1000 : Record 8628;
    BEGIN
      ConfigFieldMapping.SETRANGE("Package Code","Package Code");
      ConfigFieldMapping.SETRANGE("Table ID","Table ID");
      ConfigFieldMapping.SETRANGE("Field ID","Field ID");
      ConfigFieldMapping.DELETEALL;
    END;

    BEGIN
    END.
  }
}

