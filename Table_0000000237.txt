OBJECT Table 237 Job Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               JobJnlTemplate.GET("Journal Template Name");
             END;

    OnDelete=BEGIN
               JobJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               JobJnlLine.SETRANGE("Journal Batch Name",Name);
               JobJnlLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               JobJnlLine.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               JobJnlLine.SETRANGE("Journal Batch Name",xRec.Name);
               WHILE JobJnlLine.FINDFIRST DO
                 JobJnlLine.RENAME("Journal Template Name",Name,JobJnlLine."Line No.");
             END;

    CaptionML=[ENU=Job Journal Batch;
               RUS=Раздел журнала работ];
    LookupPageID=Page276;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Job Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              RUS=Имя шаблона журнала];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              RUS=Название];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  JobJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  JobJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                  JobJnlLine.MODIFYALL("Reason Code","Reason Code");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reason Code;
                                                              RUS=Код причины] }
    { 5   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  JobJnlTemplate.GET("Journal Template Name");
                                                                  IF JobJnlTemplate.Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    VALIDATE("Posting No. Series",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              RUS=Серия номеров] }
    { 6   ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                                JobJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                JobJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                JobJnlLine.MODIFYALL("Posting No. Series","Posting No. Series");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              RUS=Учетная серия номеров] }
    { 22  ;   ;Recurring           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Job Journal Template".Recurring WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Recurring;
                                                              RUS=Типовой];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;RUS=Только поле %1 может быть заполнено в типовых журналах.';
      Text001@1001 : TextConst 'ENU=must not be %1;RUS=не должно быть %1';
      JobJnlTemplate@1002 : Record 209;
      JobJnlLine@1003 : Record 210;

    PROCEDURE SetupNewBatch@3();
    BEGIN
      JobJnlTemplate.GET("Journal Template Name");
      "No. Series" := JobJnlTemplate."No. Series";
      "Posting No. Series" := JobJnlTemplate."Posting No. Series";
      "Reason Code" := JobJnlTemplate."Reason Code";
    END;

    BEGIN
    END.
  }
}

