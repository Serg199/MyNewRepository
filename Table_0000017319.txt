OBJECT Table 17319 Tax Calc. G/L Corr. Entry
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TaxCalcGLCorrEntry.SETCURRENTKEY("Section Code","Entry No.");
               TaxCalcGLCorrEntry.SETRANGE("Section Code","Section Code");
               IF TaxCalcGLCorrEntry.FINDLAST THEN
                 "Entry No." := TaxCalcGLCorrEntry."Entry No." + 1
               ELSE
                 "Entry No." := 1;
             END;

    OnDelete=BEGIN
               TaxCalcDimCorrFilter.SETRANGE("Section Code","Section Code");
               TaxCalcDimCorrFilter.SETRANGE("Corresp. Entry No.","Entry No.");
               TaxCalcDimCorrFilter.DELETEALL;
             END;

    CaptionML=[ENU=Tax Calc. G/L Corr. Entry;
               RUS=Корр. операция ГК налогового расчета];
    LookupPageID=Page17329;
  }
  FIELDS
  {
    { 1   ;   ;Debit Account No.   ;Code20        ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Debit Account No.;
                                                              RUS=Номер дебетового счета] }
    { 2   ;   ;Credit Account No.  ;Code20        ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Credit Account No.;
                                                              RUS=Номер кредитового счета] }
    { 3   ;   ;Register Type       ;Option        ;CaptionML=[ENU=Register Type;
                                                              RUS=Тип регистра];
                                                   OptionCaptionML=[ENU=" ,Item,Payroll";
                                                                    RUS=" ,Товар,Зарплата"];
                                                   OptionString=[ ,Item,Payroll] }
    { 4   ;   ;Section Code        ;Code10        ;TableRelation="Tax Calc. Section";
                                                   CaptionML=[ENU=Section Code;
                                                              RUS=Код секции];
                                                   NotBlank=Yes }
    { 5   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              RUS=Номер операции];
                                                   Editable=No }
    { 6   ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              RUS=Дата начала] }
    { 7   ;   ;Ending Date         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              RUS=Дата окончания] }
    { 10  ;   ;Tax Register ID Totaling;Code61    ;CaptionML=[ENU=Tax Register ID Totaling;
                                                              RUS=Группировка кодов налоговых регистров] }
    { 11  ;   ;Where Used Register IDs;Code61     ;CaptionML=[ENU=Where Used Register IDs;
                                                              RUS=Коды регистра применимости] }
    { 21  ;   ;Debit Account Name  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("G/L Account".Name WHERE (No.=FIELD(Debit Account No.)));
                                                   CaptionML=[ENU=Debit Account Name;
                                                              RUS=Название дебетового счета];
                                                   Editable=No }
    { 22  ;   ;Credit Account Name ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("G/L Account".Name WHERE (No.=FIELD(Credit Account No.)));
                                                   CaptionML=[ENU=Credit Account Name;
                                                              RUS=Название кредитового счета];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Section Code,Debit Account No.,Credit Account No.,Register Type;
                                                   Clustered=Yes }
    {    ;Section Code,Entry No.                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TaxCalcGLCorrEntry@1000 : Record 17319;
      TaxCalcDimCorrFilter@1001 : Record 17320;

    PROCEDURE TaxCalcName@1() Result : Text[150];
    VAR
      TaxCalcHeader@1000 : Record 17308;
    BEGIN
      IF "Where Used Register IDs" <> '' THEN BEGIN
        TaxCalcHeader.SETRANGE("Section Code","Section Code");
        TaxCalcHeader.SETFILTER("Register ID",CONVERTSTR(DELCHR("Where Used Register IDs",'<>','~'),'~','|'));
        IF TaxCalcHeader.FINDSET THEN BEGIN
          Result := TaxCalcHeader."No.";
          WHILE TaxCalcHeader.NEXT <> 0 DO
            Result := COPYSTR(STRSUBSTNO('%1, %2',Result,TaxCalcHeader."No."),1,MAXSTRLEN(Result));
          IF Result = TaxCalcHeader."No." THEN
            Result := COPYSTR(TaxCalcHeader.Description,1,MAXSTRLEN(Result));
        END;
      END;
    END;

    PROCEDURE LookupTaxCalcHeader@2();
    VAR
      TaxCalcHeader@1000 : Record 17308;
    BEGIN
      TaxCalcHeader.FILTERGROUP(2);
      TaxCalcHeader.SETRANGE("Section Code","Section Code");
      TaxCalcHeader.SETFILTER("Register ID",CONVERTSTR(DELCHR("Where Used Register IDs",'<>','~'),'~','|'));
      TaxCalcHeader.FILTERGROUP(0);
      IF ACTION::OK = PAGE.RUNMODAL(0,TaxCalcHeader) THEN ;
    END;

    PROCEDURE DrillDownTaxCalcHeader@3();
    VAR
      TaxCalcDimCorFilters@1000 : Page 17330;
    BEGIN
      CLEAR(TaxCalcDimCorFilters);
      TaxCalcDimCorFilters.SetTemplateCorresp("Section Code","Entry No.");
      TaxCalcDimCorFilters.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

