OBJECT Report 1191 Suggest Res. Price Chg. (Res.)
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Suggest Res. Price Chg. (Res.);
               RUS=Предлож. изменение цен ресурсов (рес.)];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);

                  IF ToCurrency.Code = '' THEN BEGIN
                    ToCurrency.InitRoundingPrecision;
                  END ELSE BEGIN
                    ToCurrency.FIND;
                    ToCurrency.TESTFIELD("Unit-Amount Rounding Precision");
                  END;

                  WITH ResPriceChg DO BEGIN
                    "Currency Code" := ToCurrency.Code;
                    "Work Type Code" := ToWorkType.Code;
                  END;
                END;

  }
  DATASET
  {
    { 5508;    ;DataItem;                    ;
               DataItemTable=Table156;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000)
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");
                                  WITH ResPriceChg DO BEGIN
                                    Type := Type::Resource;
                                    Code := Resource."No.";
                                    "New Unit Price" :=
                                      ROUND(
                                        CurrExchRate.ExchangeAmtLCYToFCY(
                                          WORKDATE,ToCurrency.Code,
                                          Resource."Unit Price",
                                          CurrExchRate.ExchangeRate(
                                            WORKDATE,ToCurrency.Code)),
                                        ToCurrency."Unit-Amount Rounding Precision");

                                    IF "New Unit Price" > PriceLowerLimit THEN
                                      "New Unit Price" := "New Unit Price" * UnitPriceFactor;
                                    IF RoundingMethod.Code <> '' THEN BEGIN
                                      RoundingMethod."Minimum Amount" := "New Unit Price";
                                      IF RoundingMethod.FIND('=<') THEN BEGIN
                                        "New Unit Price" := "New Unit Price" + RoundingMethod."Amount Added Before";

                                        IF RoundingMethod.Precision > 0 THEN
                                          "New Unit Price" :=
                                            ROUND(
                                              "New Unit Price",
                                              RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                                        "New Unit Price" := "New Unit Price" + RoundingMethod."Amount Added After";
                                      END;
                                    END;

                                    ResPrice.SETRANGE(Type,Type);
                                    ResPrice.SETRANGE(Code,Code);
                                    ResPrice.SETRANGE("Currency Code",ToCurrency.Code);
                                    ResPrice.SETRANGE("Work Type Code",ToWorkType.Code);
                                    IF ResPrice.FINDLAST THEN BEGIN
                                      "Current Unit Price" := ResPrice."Unit Price";
                                      PriceAlreadyExists := TRUE
                                    END ELSE BEGIN
                                      "Current Unit Price" := 0;
                                      PriceAlreadyExists := FALSE;
                                    END;

                                    IF PriceAlreadyExists OR CreateNewPrices THEN BEGIN
                                      ResPriceChg2 := ResPriceChg;
                                      IF ResPriceChg2.FIND('=') THEN
                                        MODIFY
                                      ELSE
                                        INSERT;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Resource Group No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF UnitPriceFactor = 0 THEN
                     UnitPriceFactor := 1;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 15  ;2   ;Group     ;
                  CaptionML=[ENU=Copy to Res. Price Change...;
                             RUS=Копировать в изменение цен ресурсов...] }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Currency Code;
                             RUS=Код валюты];
                  ToolTipML=[ENU=Specifies the code for the currency that amounts are shown in.;
                             RUS=Определяет код валюты, в которой отображаются суммы.];
                  ApplicationArea=#Jobs;
                  SourceExpr=ToCurrency.Code;
                  TableRelation=Currency }

      { 4   ;3   ;Field     ;
                  CaptionML=[ENU=Work Type;
                             RUS=Вид работы];
                  ToolTipML=[ENU=Specifies the work types to which the new prices apply. If you want the new price to be calculated for all work types, enter ALL.;
                             RUS=Определяет типы работ, к которым относятся новые цены. Если новая цена должна быть рассчитана для всех типов работ, введите ВСЕ.];
                  ApplicationArea=#Jobs;
                  SourceExpr=ToWorkType.Code;
                  TableRelation="Work Type" }

      { 16  ;2   ;Field     ;
                  CaptionML=[ENU=Only Amounts Above;
                             RUS=Только суммы свыше];
                  ToolTipML=[ENU=Specifies an amount to determine the lowest unit price that is changed. Only prices that are higher than this are changed. If a price is lower than or equal to this amount, a line for it is created in the Resource Price Changes window, but with the same unit price as on the resource card.;
                             RUS=Определяет значение минимальной цены единицы, которая будет изменена. Будут изменены только цены свыше этой суммы. Если цена меньше или равна этой сумме, для нее будет создана строка в окне "Изменение цен ресурсов", но с той же ценой единицы, что и в карточке ресурса.];
                  ApplicationArea=#Jobs;
                  DecimalPlaces=2:5;
                  SourceExpr=PriceLowerLimit }

      { 13  ;2   ;Field     ;
                  CaptionML=[ENU=Adjustment Factor;
                             RUS=Коэффициент коррекции];
                  ToolTipML=[ENU=Specifies an adjustment factor to multiply the amounts that you want suggested. By entering an adjustment factor, you can increase or decrease the amounts that are suggested.;
                             RUS=Определяет коэффициент коррекции, на который умножаются предлагаемые суммы. Этот коэффициент позволяет увеличивать или уменьшать суммы, которые предлагаются.];
                  ApplicationArea=#Jobs;
                  DecimalPlaces=0:5;
                  SourceExpr=UnitPriceFactor;
                  MinValue=0 }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=Rounding Method;
                             RUS=Метод округления];
                  ToolTipML=[ENU=Specifies a code for the rounding method that you want to apply to costs or prices that you adjust.;
                             RUS=Определяет код метода округления, который применяется к корректируемым значениям себестоимости и цены.];
                  ApplicationArea=#Jobs;
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Create New Prices;
                             RUS=Создать новые цены];
                  ToolTipML=[ENU=Specifies if you want the batch job to create new price suggestions, such as a new combination of currency, job number, or work type. If you only want to adjust existing alternative prices, do not select.;
                             RUS=Указывает, что при выполнении пакетного задания должны формироваться предложения по новым ценам, например новая комбинация валюты, номера работы или типа работы. Если требуется скорректировать только существующие альтернативные цены, не устанавливайте этот флажок.];
                  ApplicationArea=#Jobs;
                  SourceExpr=CreateNewPrices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing items  #1##########;RUS=Обработка товаров #1##########';
      RoundingMethod@1001 : Record 42;
      ToCurrency@1002 : Record 4;
      CurrExchRate@1003 : Record 330;
      ToWorkType@1010 : Record 200;
      ResPriceChg2@1009 : Record 335;
      ResPriceChg@1008 : Record 335;
      ResPrice@1013 : Record 201;
      Window@1012 : Dialog;
      PriceAlreadyExists@1004 : Boolean;
      CreateNewPrices@1005 : Boolean;
      UnitPriceFactor@1006 : Decimal;
      PriceLowerLimit@1007 : Decimal;

    PROCEDURE InitializeCopyToResPrice@2(CurrencyCode@1000 : Code[10];WorkTypeCode@1002 : Code[10]);
    BEGIN
      ToCurrency.Code := CurrencyCode;
      ToWorkType.Code := WorkTypeCode;
    END;

    PROCEDURE InitializeRequest@1(PriceLowerLimitFrom@1003 : Decimal;UnitPriceFactorFrom@1004 : Decimal;RoundingMethodCode@1005 : Code[10];CreateNewPricesFrom@1006 : Boolean);
    BEGIN
      PriceLowerLimit := PriceLowerLimitFrom;
      UnitPriceFactor := UnitPriceFactorFrom;
      RoundingMethod.Code := RoundingMethodCode;
      CreateNewPrices := CreateNewPricesFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

