OBJECT Report 14977 Transfer Receipt TORG-13
{
  OBJECT-PROPERTIES
  {
    Date=09.09.14;
    Time=12:00:00;
    Version List=NAVRU8.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Transfer Receipt TORG-13;
               RUS=Приходная накладная на перемещение ТОРГ-13];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  TORG13Helper.InitReportTemplate;
                END;

    OnPostReport=BEGIN
                   IF FileName = '' THEN
                     TORG13Helper.ExportData
                   ELSE
                     TORG13Helper.ExportDataToClientFile(FileName);
                 END;

  }
  DATASET
  {
    { 4354;    ;DataItem;                    ;
               DataItemTable=Table5746;
               DataItemTableView=SORTING(No.);
               OnAfterGetRecord=BEGIN
                                  CheckSignature(ReleasedBy,ReleasedBy."Employee Type"::ReleasedBy);
                                  CheckSignature(ReceivedBy,ReceivedBy."Employee Type"::ReceivedBy);

                                  TORG13Helper.FillHeader("No.",FORMAT("Posting Date"),
                                    ReleasedBy."Employee Org. Unit",ReceivedBy."Employee Org. Unit");
                                  TORG13Helper.FillPageHeader;
                                END;

               ReqFilterFields=No. }

    { 4146;1   ;DataItem;                    ;
               DataItemTable=Table5747;
               DataItemTableView=SORTING(Document No.,Line No.);
               OnAfterGetRecord=BEGIN
                                  Item.GET("Item No.");
                                  GrossWeight := Quantity * Item."Gross Weight";
                                  NetWeight := Quantity * Item."Net Weight";
                                  TotalCost := Quantity * Item."Unit Cost";

                                  TransferLineValues;
                                END;

               OnPostDataItem=BEGIN
                                TransferFooterValues;
                              END;

               DataItemLink=Document No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Item@1470007 : Record 27;
      ReleasedBy@1470000 : Record 12421;
      ReceivedBy@1470005 : Record 12421;
      DocSignMgt@1470004 : Codeunit 12420;
      TORG13Helper@1210000 : Codeunit 14935;
      StdRepMgt@1210001 : Codeunit 12401;
      GrossWeight@1470002 : Decimal;
      NetWeight@1470003 : Decimal;
      TotalCost@1470006 : Decimal;
      FileName@1210002 : Text;

    PROCEDURE CheckSignature@1470001(VAR PostedDocSign@1470000 : Record 12421;EmpType@1470001 : Integer);
    BEGIN
      DocSignMgt.GetPostedDocSign(
        PostedDocSign,DATABASE::"Transfer Receipt Header",
        0,"Transfer Receipt Header"."No.",EmpType,FALSE);
    END;

    PROCEDURE TransferLineValues@1210002();
    VAR
      BodyDetails@1210001 : ARRAY [6] OF Text;
      AmountValues@1210000 : ARRAY [4] OF Decimal;
    BEGIN
      WITH "Transfer Receipt Line" DO BEGIN
        AmountValues[1] := TotalCost;
        AmountValues[2] := Quantity;
        AmountValues[3] := GrossWeight;
        AmountValues[4] := NetWeight;

        BodyDetails[1] := Description;
        BodyDetails[2] := "Item No.";
        BodyDetails[3] := StdRepMgt.GetUoMDesc("Unit of Measure Code");
        BodyDetails[4] := "Unit of Measure Code";
        BodyDetails[5] := FORMAT(Item."Units per Parcel");
        BodyDetails[6] := FORMAT(Item."Unit Cost");
      END;

      TORG13Helper.FillLine(BodyDetails,AmountValues);
    END;

    PROCEDURE TransferFooterValues@1210007();
    VAR
      FooterDetails@1210000 : ARRAY [4] OF Text;
    BEGIN
      FooterDetails[1] := ReleasedBy."Employee Job Title";
      FooterDetails[2] := ReleasedBy."Employee Name";
      FooterDetails[3] := ReceivedBy."Employee Job Title";
      FooterDetails[4] := ReceivedBy."Employee Name";

      TORG13Helper.FillFooter(FooterDetails);
    END;

    PROCEDURE InitializeRequest@1210005(NewFileName@1210000 : Text);
    BEGIN
      FileName := NewFileName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

