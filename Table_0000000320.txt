OBJECT Table 320 Tax Jurisdiction
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00,NAVRU10.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               SetDefaults;
               InsertDetailLines;
             END;

    OnDelete=BEGIN
               DeleteDetailLines;
             END;

    CaptionML=[ENU=Tax Jurisdiction;
               RUS=Налоговая юрисдикция];
    LookupPageID=Page466;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              RUS=Код];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 3   ;   ;Tax Account (Sales) ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Tax Account (Sales)");
                                                              END;

                                                   CaptionML=[ENU=Tax Account (Sales);
                                                              RUS=Счет налога (продажи)] }
    { 4   ;   ;Tax Account (Purchases);Code20     ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Tax Account (Purchases)");
                                                              END;

                                                   CaptionML=[ENU=Tax Account (Purchases);
                                                              RUS=Счет налога (покупки)] }
    { 5   ;   ;Report-to Jurisdiction;Code10      ;TableRelation="Tax Jurisdiction";
                                                   CaptionML=[ENU=Report-to Jurisdiction;
                                                              RUS=Юрисдикция для отчетности] }
    { 6   ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              RUS=Фильтр по дате] }
    { 7   ;   ;Tax Group Filter    ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Tax Group";
                                                   CaptionML=[ENU=Tax Group Filter;
                                                              RUS=Налоговая группа - фильтр] }
    { 8   ;   ;Unreal. Tax Acc. (Sales);Code20    ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Unreal. Tax Acc. (Sales)");
                                                              END;

                                                   CaptionML=[ENU=Unreal. Tax Acc. (Sales);
                                                              RUS=Счет нереал. налога (продажи)] }
    { 9   ;   ;Unreal. Tax Acc. (Purchases);Code20;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Unreal. Tax Acc. (Purchases)");
                                                              END;

                                                   CaptionML=[ENU=Unreal. Tax Acc. (Purchases);
                                                              RUS=Счет нереал. налога (покупки)] }
    { 10  ;   ;Reverse Charge (Purchases);Code20  ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Reverse Charge (Purchases)");
                                                              END;

                                                   CaptionML=[ENU=Reverse Charge (Purchases);
                                                              RUS=Обратный НДС - счет ГК (Покупки)] }
    { 11  ;   ;Unreal. Rev. Charge (Purch.);Code20;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Unreal. Rev. Charge (Purch.)");
                                                              END;

                                                   CaptionML=[ENU=Unreal. Rev. Charge (Purch.);
                                                              RUS=Нереал. обратный НДС - счет ГК (покупки)] }
    { 12  ;   ;Unrealized VAT Type ;Option        ;OnValidate=BEGIN
                                                                IF "Unrealized VAT Type" > 0 THEN BEGIN
                                                                  GLSetup.GET;
                                                                  GLSetup.TESTFIELD("Unrealized VAT",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Unrealized VAT Type;
                                                              RUS=Тип нереал. НДС];
                                                   OptionCaptionML=[ENU=" ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)";
                                                                    RUS=" ,Процент,Первый,Последний,Первый оплаченный,Последний оплаченный"];
                                                   OptionString=[ ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)] }
    { 13  ;   ;Calculate Tax on Tax;Boolean       ;OnValidate=BEGIN
                                                                TaxDetail.SETRANGE("Tax Jurisdiction Code",Code);
                                                                TaxDetail.MODIFYALL("Calculate Tax on Tax","Calculate Tax on Tax");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Calculate Tax on Tax;
                                                              RUS=Рассчитывать налог на налог] }
    { 14  ;   ;Adjust for Payment Discount;Boolean;OnValidate=BEGIN
                                                                IF "Adjust for Payment Discount" THEN BEGIN
                                                                  GLSetup.GET;
                                                                  GLSetup.TESTFIELD("Adjust for Payment Disc.",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Adjust for Payment Discount;
                                                              RUS=Корр. для скидки оплаты] }
    { 12400;  ;Sales Tax Amount Type;Option       ;CaptionML=[ENU=Sales Tax Amount Type;
                                                              RUS=Тип суммы налога для счета-фактуры];
                                                   OptionCaptionML=[ENU=VAT,Excise,Sales Tax;
                                                                    RUS=НДС,Акциз,Налог с продаж];
                                                   OptionString=VAT,Excise,Sales Tax }
    { 12401;  ;Tax Jurisdiction Data;Code10       ;TableRelation="Tax Jurisdiction".Code;
                                                   CaptionML=[ENU=Tax Jurisdiction Data;
                                                              RUS=Данные налоговой юрисдикции] }
    { 12402;  ;Not Include into Ledger;Option     ;CaptionML=[ENU=Not Include into Ledger;
                                                              RUS=Не включать в книгу];
                                                   OptionCaptionML=[ENU=" ,Purchase,Sales,Purchase & Sales";
                                                                    RUS=" ,Покупка,Продажа,Покупка и продажа"];
                                                   OptionString=[ ,Purchase,Sales,Purchase & Sales] }
    { 12403;  ;Trans. VAT Type     ;Option        ;CaptionML=[ENU=Trans. VAT Type;
                                                              RUS=Тип транз. НДС];
                                                   OptionCaptionML=[ENU=" ,Amount + Tax,Amount & Tax";
                                                                    RUS=" ,Сумма + налог,Сумма и налог"];
                                                   OptionString=[ ,Amount + Tax,Amount & Tax] }
    { 12404;  ;Trans. VAT Account  ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Trans. VAT Account");
                                                              END;

                                                   CaptionML=[ENU=Trans. VAT Account;
                                                              RUS=НДС транз. счет ГК] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Report-to Jurisdiction                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      TaxDetail@1001 : Record 322;

    PROCEDURE GetSalesAccount@4(Unrealized@1000 : Boolean) : Code[20];
    BEGIN
      IF Unrealized THEN BEGIN
        TESTFIELD("Unreal. Tax Acc. (Sales)");
        EXIT("Unreal. Tax Acc. (Sales)");
      END;
      TESTFIELD("Tax Account (Sales)");
      EXIT("Tax Account (Sales)");
    END;

    PROCEDURE GetPurchAccount@5(Unrealized@1000 : Boolean) : Code[20];
    BEGIN
      IF Unrealized THEN BEGIN
        TESTFIELD("Unreal. Tax Acc. (Purchases)");
        EXIT("Unreal. Tax Acc. (Purchases)");
      END;
      TESTFIELD("Tax Account (Purchases)");
      EXIT("Tax Account (Purchases)");
    END;

    PROCEDURE GetRevChargeAccount@6(Unrealized@1000 : Boolean) : Code[20];
    BEGIN
      IF Unrealized THEN BEGIN
        TESTFIELD("Unreal. Rev. Charge (Purch.)");
        EXIT("Unreal. Rev. Charge (Purch.)");
      END;
      TESTFIELD("Reverse Charge (Purchases)");
      EXIT("Reverse Charge (Purchases)");
    END;

    PROCEDURE CreateTaxJurisdiction@7(NewJurisdictionCode@1000 : Code[10]);
    BEGIN
      INIT;
      Code := NewJurisdictionCode;
      Description := NewJurisdictionCode;
      SetDefaults;
      IF INSERT(TRUE) THEN;
    END;

    LOCAL PROCEDURE SetDefaults@3();
    VAR
      TaxSetup@1000 : Record 326;
    BEGIN
      TaxSetup.GET;
      "Tax Account (Sales)" := TaxSetup."Tax Account (Sales)";
      "Tax Account (Purchases)" := TaxSetup."Tax Account (Purchases)";
      "Unreal. Tax Acc. (Sales)" := TaxSetup."Unreal. Tax Acc. (Sales)";
      "Unreal. Tax Acc. (Purchases)" := TaxSetup."Unreal. Tax Acc. (Purchases)";
      "Reverse Charge (Purchases)" := TaxSetup."Reverse Charge (Purchases)";
      "Unreal. Rev. Charge (Purch.)" := TaxSetup."Unreal. Rev. Charge (Purch.)";
    END;

    LOCAL PROCEDURE InsertDetailLines@1();
    VAR
      TaxDetail@1000 : Record 322;
      TaxSetup@1001 : Record 326;
    BEGIN
      TaxSetup.GET;
      IF NOT TaxSetup."Auto. Create Tax Details" THEN
        EXIT;

      TaxDetail.SETRANGE("Tax Jurisdiction Code",Code);
      IF NOT TaxDetail.ISEMPTY THEN
        EXIT;

      TaxDetail.INIT;
      TaxDetail."Tax Jurisdiction Code" := Code;
      TaxDetail."Tax Group Code" := '';
      TaxDetail."Tax Type" := TaxDetail."Tax Type"::"Sales Tax";
      TaxDetail."Effective Date" := WORKDATE;
      TaxDetail.INSERT;

      IF TaxSetup."Non-Taxable Tax Group Code" <> '' THEN BEGIN
        TaxDetail.INIT;
        TaxDetail."Tax Jurisdiction Code" := Code;
        TaxDetail."Tax Group Code" := TaxSetup."Non-Taxable Tax Group Code";
        TaxDetail."Tax Type" := TaxDetail."Tax Type"::"Sales Tax";
        TaxDetail."Effective Date" := WORKDATE;
        TaxDetail.INSERT;
      END;
    END;

    LOCAL PROCEDURE DeleteDetailLines@2();
    VAR
      TaxAreaLine@1000 : Record 319;
      TaxDetail@1001 : Record 322;
    BEGIN
      TaxAreaLine.SETRANGE("Tax Jurisdiction Code",Code);
      TaxAreaLine.DELETEALL;

      TaxDetail.SETRANGE("Tax Jurisdiction Code",Code);
      TaxDetail.DELETEALL;
    END;

    LOCAL PROCEDURE CheckGLAcc@1210000(AccNo@1210001 : Code[20]);
    VAR
      GLAcc@1210002 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
      END;
    END;

    BEGIN
    END.
  }
}

