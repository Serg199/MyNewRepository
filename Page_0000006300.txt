OBJECT Page 6300 Azure AD App Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=SETUP AZURE ACTIVE DIRECTORY;
               RUS=НАСТРОЙКА AZURE ACTIVE DIRECTORY];
    PageType=NavigatePage;
    OnInit=VAR
             AzureADAppSetup@1000 : Record 6300;
           BEGIN
             // Checks user permissions and closes the wizard with an error message if necessary.
             IF NOT AzureADAppSetup.WRITEPERMISSION THEN
               ERROR(PermissionsErr);
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 // Always start on the introduction step
                 SetStep(CurrentStep::Intro);
               END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      Name=Actions;
                      ActionContainerType=ActionItems }
      { 11      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 RUS=Назад];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 GoToNextStep(FALSE);
                               END;
                                }
      { 12      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 RUS=Далее];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 GoToNextStep(TRUE);
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 RUS=Готово];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 CurrPage.AzureAdSetup.PAGE.Save;

                                 // notify Assisted Setup that this setup has been completed
                                 AssistedSetup.SetStatus(PAGE::"Azure AD App Setup Wizard",AssistedSetup.Status::Completed);
                                 CurrPage.UPDATE(FALSE);
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Azure AD Application Setup;
                CaptionML=[ENU=Azure AD Application Setup;
                           RUS=Установка приложения Azure AD];
                ContainerType=ContentArea }

    { 14  ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 5   ;2   ;Field     ;
                Name=<MediaRepositoryStandard>;
                CaptionML=[ENU="";
                           RUS=""];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaRepositoryStandard.Image;
                Editable=FALSE }

    { 4   ;1   ;Group     ;
                Visible=TopBannerVisible AND DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 15  ;2   ;Field     ;
                Name=<MediaRepositoryDone>;
                CaptionML=[ENU="";
                           RUS=""];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaRepositoryDone.Image;
                Editable=FALSE }

    { 8   ;1   ;Group     ;
                Name=Intro;
                CaptionML=[ENU=Intro;
                           RUS=Введение];
                Visible=IntroVisible;
                GroupType=Group }

    { 16  ;2   ;Group     ;
                Name=Para1.1;
                CaptionML=[ENU=Welcome to Azure Active Directory (Azure AD) Setup;
                           RUS=Добро пожаловать в настройку Azure Active Directory (Azure AD)];
                GroupType=Group }

    { 9   ;3   ;Group     ;
                Name=Para1.1.1;
                CaptionML=[ENU="";
                           RUS=""];
                GroupType=Group;
                InstructionalTextML=[ENU=When you register an application in the Azure Portal, it enables on premise applications to communicate with Power BI Services, Office 365 Exchange Online and other Azure services directly.;
                                     RUS=При регистрации приложения на портале Azure оно включает локальные приложения для обмена данными со службами Power BI, Office 365 Exchange Online и другими службами Azure напрямую.] }

    { 17  ;3   ;Group     ;
                Name=Para1.1.2;
                CaptionML=[ENU="";
                           RUS=""];
                GroupType=Group;
                InstructionalTextML=[ENU=This wizard will guide you through the steps required to register Microsoft Dynamics NAV in the Azure Portal, how to select the Azure services you want to use and what permissions to grant to each of the services you selected.;
                                     RUS=В этом мастере приводятся шаги, которые необходимо выполнить для регистрации Microsoft Dynamics NAV на портале Azure, инструкции по выбору служб Azure, которые требуется использовать, и сведения о разрешениях, которые требуется предоставить каждой выбранной службе.] }

    { 18  ;3   ;Group     ;
                Name=Paral1.1.3;
                CaptionML=[ENU="";
                           RUS=""];
                GroupType=Group;
                InstructionalTextML=[ENU=At the end of the registration process, the Azure Portal will provide you with an Application ID and Key that will be required to complete this setup.;
                                     RUS=В конце процесса регистрации портал Azure предоставит код и ключ приложения, которые потребуются для завершения настройки.] }

    { 19  ;2   ;Group     ;
                Name=Para1.2;
                CaptionML=[ENU=Let's go!;
                           RUS=Приступим!];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Next to step through the process of registering Microsoft Dynamics NAV in the Azure Portal and obtaining the necessary information to complete this setup.;
                                     RUS=Нажмите кнопку "Далее", чтобы выполнить шаги процесса регистрации Microsoft Dynamics NAV на портале Azure и получить необходимые сведения для завершения настройки.] }

    { 2   ;1   ;Group     ;
                Name=Step1;
                CaptionML=[ENU=Step 1;
                           RUS=Шаг 1];
                Visible=Step1Visible;
                GroupType=Group }

    { 20  ;2   ;Group     ;
                Name=Para2.1;
                CaptionML=[ENU=Registering Microsoft Dynamics NAV;
                           RUS=Регистрация Microsoft Dynamics NAV];
                GroupType=Group }

    { 21  ;3   ;Group     ;
                Name=Para2.1.1;
                CaptionML=[ENU="";
                           RUS=""];
                GroupType=Group;
                InstructionalTextML=[ENU=To complete the setup process, you will need to obtain an Application ID and Key from the Azure Portal.  Choose the Help icon, or find the topic called "How to: Register Dynamics NAV in the Azure Management Portal" in Help.  If the topic is not available in Help, verify that that this topic has been deployed to your Help server.;
                                     RUS=Для завершения процесса настройки потребуется получить код и ключ приложения на портале Azure. Щелкните значок справки или найдите раздел "Практическое руководство. Регистрация Dynamics NAV на портале управления Azure" в справке. Если этот раздел недоступен в справке, убедитесь, что он развернут на вашем сервере справки.] }

    { 23  ;3   ;Group     ;
                Name=Para2.1.2;
                CaptionML=[ENU="";
                           RUS=""];
                GroupType=Group;
                InstructionalTextML=[ENU=Before closing the Azure Management Portal, copy and paste the Application ID and Key into the associated fields provided below and choose Next.;
                                     RUS=Прежде чем закрыть портал управления Azure, скопируйте код и ключ приложения и вставьте их в связанные поля, представленные ниже, затем нажмите кнопку "Далее".] }

    { 3   ;4   ;Part      ;
                Name=AzureAdSetup;
                CaptionML=[ENU=" ";
                           RUS=" "];
                ApplicationArea=#Basic,#Suite;
                PagePartID=Page6301;
                PartType=Page }

    { 6   ;1   ;Group     ;
                Name=Done;
                CaptionML=[ENU=Done;
                           RUS=Готово];
                Visible=DoneVisible;
                GroupType=Group }

    { 25  ;2   ;Group     ;
                Name=Para3.1;
                CaptionML=[ENU=That's it!;
                           RUS=Вот и все!];
                GroupType=Group }

    { 7   ;3   ;Group     ;
                Name=Para3.1.1;
                CaptionML=[ENU="";
                           RUS=""];
                GroupType=Group;
                InstructionalTextML=[ENU=To begin using the Azure Active Directory services, choose Finish.;
                                     RUS=Чтобы приступить к использованию служб Azure Active Directory, нажмите кнопку "Готово".] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1010 : Record 9400;
      MediaRepositoryDone@1011 : Record 9400;
      AssistedSetup@1008 : Record 1803;
      CurrentStep@1000 : 'Intro,AzureAD,Done';
      IntroVisible@1001 : Boolean;
      Step1Visible@1002 : Boolean;
      DoneVisible@1004 : Boolean;
      NextEnabled@1005 : Boolean;
      BackEnabled@1006 : Boolean;
      FinishEnabled@1007 : Boolean;
      StepOutOfRangeErr@1003 : TextConst 'ENU=Wizard step out of range.;RUS=Шаг мастера находится вне допустимого диапазона.';
      PermissionsErr@1009 : TextConst 'ENU=Please contact an administrator to set up your Azure Active Directory application.;RUS=Обратитесь к администратору, чтобы он настроил ваше приложение Azure Active Directory.';
      TopBannerVisible@1012 : Boolean;

    LOCAL PROCEDURE SetStep@1(NewStep@1000 : Option);
    BEGIN
      IF (NewStep < CurrentStep::Intro) OR (NewStep > CurrentStep::Done) THEN
        ERROR(StepOutOfRangeErr);

      ClearStepControls;
      CurrentStep := NewStep;

      CASE NewStep OF
        CurrentStep::Intro:
          BEGIN
            IntroVisible := TRUE;
            NextEnabled := TRUE;
          END;
        CurrentStep::AzureAD:
          BEGIN
            Step1Visible := TRUE;
            BackEnabled := TRUE;
            NextEnabled := TRUE;
          END;
        CurrentStep::Done:
          BEGIN
            DoneVisible := TRUE;
            BackEnabled := TRUE;
            FinishEnabled := TRUE;
          END;
      END;

      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE ClearStepControls@2();
    BEGIN
      // hide all tabs
      IntroVisible := FALSE;
      Step1Visible := FALSE;
      DoneVisible := FALSE;

      // disable all buttons
      BackEnabled := FALSE;
      NextEnabled := FALSE;
      FinishEnabled := FALSE;
    END;

    LOCAL PROCEDURE CalculateNextStep@7(Forward@1000 : Boolean) NextStep : Integer;
    BEGIN
      // // Calculates the next step and hides steps based on whether the Power BI setup is enabled or not

      // General cases
      IF Forward AND (CurrentStep < CurrentStep::Done) THEN
        // move forward 1 step
        NextStep := CurrentStep + 1
      ELSE
        IF NOT Forward AND (CurrentStep > CurrentStep::Intro) THEN
          // move backward 1 step
          NextStep := CurrentStep - 1
        ELSE
          // stay on the current step
          NextStep := CurrentStep;
    END;

    LOCAL PROCEDURE GoToNextStep@10(Forward@1000 : Boolean);
    BEGIN
      IF Forward THEN
        ValidateStep(CurrentStep);

      SetStep(CalculateNextStep(Forward));
    END;

    LOCAL PROCEDURE ValidateStep@11(Step@1000 : Option);
    BEGIN
      IF Step = CurrentStep::AzureAD THEN
        CurrPage.AzureAdSetup.PAGE.ValidateFields;
    END;

    LOCAL PROCEDURE LoadTopBanners@4();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE))
      THEN
        TopBannerVisible := MediaRepositoryDone.Image.HASVALUE;
    END;

    BEGIN
    END.
  }
}

