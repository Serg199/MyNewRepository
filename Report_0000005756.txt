OBJECT Report 5756 Create Warehouse Location
{
  OBJECT-PROPERTIES
  {
    Date=23.01.17;
    Time=12:00:00;
    Version List=NAVW110.00.00.15052,NAVRU10.00.00.15052;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Warehouse Location;
               RUS=Создание склада];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  WhseEntry@1000 : Record 7312;
                BEGIN
                  IF LocCode = '' THEN
                    ERROR(Text001);
                  IF AdjBinCode = '' THEN
                    ERROR(Text002);

                  ItemLedgEntry.RESET;
                  ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
                  IF ItemLedgEntry.FIND('-') THEN
                    REPEAT
                      ItemLedgEntry.SETRANGE("Item No.",ItemLedgEntry."Item No.");
                      ItemLedgEntry.SETRANGE(Open,TRUE);
                      ItemLedgEntry.SETRANGE("Location Code",LocCode);
                      Found := ItemLedgEntry.FIND('-');
                      IF Found AND NOT HideValidationDialog THEN
                        IF NOT CONFIRM(STRSUBSTNO('%1 %2 %3 %4 %5 %6 %7 %8',
                               Text010,Text011,Text012,Text013,Text014,
                               Text015,STRSUBSTNO(Text016,LocCode),Text017),FALSE)
                        THEN
                          CurrReport.QUIT;
                      ItemLedgEntry.SETRANGE("Location Code");
                      ItemLedgEntry.SETRANGE(Open);
                      ItemLedgEntry.FIND('+');
                      ItemLedgEntry.SETRANGE("Item No.");
                    UNTIL (ItemLedgEntry.NEXT = 0) OR Found;

                  IF NOT Found THEN
                    ERROR(Text018,Location.TABLECAPTION,Location.FIELDCAPTION(Code),LocCode);
                  CLEAR(ItemLedgEntry);

                  WhseEntry.SETRANGE("Location Code",LocCode);
                  IF NOT WhseEntry.ISEMPTY THEN
                    ERROR(
                      Text019,LocCode,WhseEntry.TABLECAPTION);

                  TempWhseJnlLine.RESET;
                  TempWhseJnlLine.DELETEALL;

                  LastLineNo := 0;

                  WITH ItemLedgEntry DO BEGIN
                    SETCURRENTKEY(
                      "Item No.","Location Code",Open,"Variant Code","Unit of Measure Code","Lot No.","Serial No.","CD No.");

                    Location.GET(LocCode);
                    Location.TESTFIELD("Adjustment Bin Code",'');
                    CheckWhseDocs;

                    Bin.GET(LocCode,AdjBinCode);

                    IF FIND('-') THEN BEGIN
                      IF NOT HideValidationDialog THEN BEGIN
                        Window.OPEN(STRSUBSTNO(Text020,"Location Code") + Text003);
                        i := 1;
                        CountItemLedgEntries := COUNT;
                      END;

                      REPEAT
                        IF NOT HideValidationDialog THEN BEGIN
                          Window.UPDATE(100,i);
                          Window.UPDATE(102,ROUND(i / CountItemLedgEntries * 10000,1));
                        END;

                        SETRANGE("Item No.","Item No.");
                        IF FIND('-') THEN BEGIN
                          SETRANGE("Location Code",LocCode);
                          SETRANGE(Open,TRUE);
                          IF FIND('-') THEN
                            REPEAT
                              SETRANGE("Variant Code","Variant Code");
                              IF FIND('-') THEN
                                REPEAT
                                  SETRANGE("Unit of Measure Code","Unit of Measure Code");
                                  IF FIND('-') THEN
                                    REPEAT
                                      SETRANGE("Lot No.","Lot No.");
                                      IF FIND('-') THEN
                                        REPEAT
                                          SETRANGE("CD No.","CD No.");
                                          IF FIND('-') THEN
                                            REPEAT
                                              SETRANGE("Serial No.","Serial No.");

                                          CALCSUMS("Remaining Quantity");

                                              IF "Remaining Quantity" < 0 THEN
                                                ERROR(
                                                  STRSUBSTNO(Text005,BuildErrorText) +
                                                  STRSUBSTNO(Text009,CheckNegInv.OBJECTID));

                                              IF "Remaining Quantity" > 0 THEN
                                                CreateWhseJnlLine;
                                              FIND('+');
                                              SETRANGE("Serial No.");
                                            UNTIL NEXT = 0;

                                          FIND('+');
                                          SETRANGE("CD No.");
                                        UNTIL NEXT = 0;

                                      FIND('+');
                                      SETRANGE("Lot No.");
                                    UNTIL NEXT = 0;

                                  FIND('+');
                                  SETRANGE("Unit of Measure Code")
                                UNTIL NEXT = 0;

                              FIND('+');
                              SETRANGE("Variant Code");
                            UNTIL NEXT = 0;
                        END;

                        SETRANGE(Open);
                        SETRANGE("Location Code");
                        FIND('+');
                        IF NOT HideValidationDialog THEN
                          i := i + COUNT;
                        SETRANGE("Item No.");
                      UNTIL NEXT = 0;
                    END;
                  END;
                END;

    OnPostReport=VAR
                   WhseJnlRegisterLine@1000 : Codeunit 7301;
                 BEGIN
                   Location."Bin Mandatory" := TRUE;
                   Location.VALIDATE("Directed Put-away and Pick",TRUE);
                   Location.VALIDATE("Adjustment Bin Code",AdjBinCode);
                   Location.MODIFY;

                   IF TempWhseJnlLine.FIND('-') THEN
                     REPEAT
                       WhseJnlRegisterLine.RegisterWhseJnlLine(TempWhseJnlLine);
                     UNTIL TempWhseJnlLine.NEXT = 0;

                   IF NOT HideValidationDialog THEN BEGIN
                     Window.CLOSE;
                     MESSAGE(Text004);
                   END;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Location Code;
                             RUS=Код склада];
                  SourceExpr=LocCode;
                  OnValidate=BEGIN
                               Location.GET(LocCode);
                               Location.TESTFIELD("Bin Mandatory",FALSE);
                               Location.TESTFIELD("Use As In-Transit",FALSE);
                               Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                             END;

                  OnLookup=BEGIN
                             CLEAR(Location);
                             IF LocCode <> '' THEN
                               Location.Code := LocCode;
                             IF PAGE.RUNMODAL(0,Location) = ACTION::LookupOK THEN BEGIN
                               Location.TESTFIELD("Bin Mandatory",FALSE);
                               Location.TESTFIELD("Use As In-Transit",FALSE);
                               Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                               LocCode := Location.Code;
                             END;
                           END;
                            }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Adjustment Bin Code;
                             RUS=Код ячейки коррекции];
                  SourceExpr=AdjBinCode;
                  OnValidate=BEGIN
                               IF AdjBinCode <> '' THEN
                                 IF LocCode <> '' THEN
                                   Bin.GET(LocCode,AdjBinCode)
                                 ELSE BEGIN
                                   Bin.SETRANGE(Code,AdjBinCode);
                                   Bin.FINDFIRST;
                                   LocCode := Bin."Location Code";
                                 END;
                             END;

                  OnLookup=BEGIN
                             Bin.RESET;
                             IF LocCode <> '' THEN
                               Bin.SETRANGE("Location Code",LocCode);

                             IF PAGE.RUNMODAL(0,Bin) = ACTION::LookupOK THEN BEGIN
                               IF LocCode = '' THEN
                                 LocCode := Bin."Location Code";
                               AdjBinCode := Bin.Code;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempWhseJnlLine@1007 : TEMPORARY Record 7311;
      Item@1020 : Record 27;
      ItemLedgEntry@1011 : Record 32;
      ItemUnitOfMeasure@1001 : Record 5404;
      Location@1002 : Record 14;
      Bin@1003 : Record 7354;
      CheckNegInv@1010 : Report 5757;
      WMSMgt@1019 : Codeunit 7302;
      Window@1013 : Dialog;
      LocCode@1000 : Code[10];
      Text001@1042 : TextConst 'ENU=Enter a location code.;RUS=Введите код склада.';
      Text002@1012 : TextConst 'ENU=Enter an adjustment bin code.;RUS=Введите код ячейки коррекции.';
      Text003@1004 : TextConst 'ENU=Count #100##### @102@@@@@@@@;RUS=Счет  #100##### @102@@@@@@@@';
      AdjBinCode@1009 : Code[20];
      i@1005 : Integer;
      CountItemLedgEntries@1029 : Integer;
      LastLineNo@1008 : Integer;
      Text004@1015 : TextConst 'ENU=The conversion was successfully completed.;RUS=Преобразование выполнено успешно.';
      Text005@1016 : TextConst 'ENU=Negative inventory was found in the location. You must clear this negative inventory in the program before you can proceed with the conversion.\\%1.\\;RUS=На складе обнаружены отрицательные остатки. Необходимо устранить эти отрицательные остатки в программе, прежде чем можно будет продолжить преобразование.\\%1.\\';
      HideValidationDialog@1018 : Boolean;
      Text006@1022 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 is not completely posted yet.\\Post or delete all of them before restarting the conversion batch job.;RUS=Склад %1 не может быть преобразован, так как по меньшей мере один %2 еще не учтен.\\Учтите или удалите их все, затем запустите пакетное задание преобразования еще раз.';
      Text007@1023 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 is not completely registered yet.\\Register or delete all of them before restarting the conversion batch job.;RUS=Склад %1 не может быть преобразован, так как по меньшей мере один %2 еще не полностью зарегистрирован.\\Зарегистрируйте или удалите их все, затем запустите пакетное задание преобразования еще раз.';
      Text008@1024 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 exists.\\Delete all of them before restarting the conversion batch job.;RUS=Склад %1 не может быть преобразован, так как существует по меньшей мере один %2.\\Удалите их все, затем запустите пакетное задание преобразования еще раз.';
      Text009@1030 : TextConst 'ENU=Run %1 for a report of all negative inventory in the location.;RUS=Запустите %1 для отчета о всех отрицательных остатки на складе.';
      Text010@1039 : TextConst 'ENU=Inventory exists on this location. By choosing Yes from this warning, you are confirming that you want to enable this location to use Warehouse Management Systems by running a batch job to create warehouse entries for the inventory in this location.\\;RUS=На этом складе есть запасы. Выбрав "Да" в ответ на это сообщение, вы подтверждаете, что хотите переключить этот склад на работу через систему управления складом (WMS), запустив пакетное задание, которое создаст складские операции для запасов на этом складе.\\';
      Text011@1038 : TextConst 'ENU="If you want to proceed, you must first ensure that no negative inventory exists in the location. Negative inventory is not allowed in a location that uses warehouse management logic and must be cleared by posting a suitable quantity to inventory. ";RUS="Для продолжения необходимо убедиться, что на складе нет отрицательных остатков. Отрицательные остатки недопустимы на складе, работающем под управлением системы управления складом, и должны быть устранены путем учета достаточного количества запасов. "';
      Text012@1037 : TextConst 'ENU=You can perform a check for negative inventory by using the Check on Negative Inventory report.\\;RUS=Можно выполнить проверку на отрицательные остатки с помощью отчета "Проверка на отрицательные остатки".\\';
      Text013@1036 : TextConst 'ENU="If you can confirm that no negative inventory exists in the location, proceed with the conversion batch job. If negative inventory is found, the batch job will stop with an error message. ";RUS="Если вы уверены, что на складе нет товаров с отрицательными остатками, запустите пакетное задание преобразования. В случае обнаружения отрицательных остатков пакетное задание будет остановлено и отобразится сообщение об ошибке. "';
      Text014@1035 : TextConst 'ENU=The result of this batch job is that initial warehouse entries will be created. You must balance these initial warehouse entries on the adjustment bin by posting a warehouse physical inventory journal or a warehouse item journal to assign zones and bins to items.\;RUS=В результате выполнения этого задания будут созданы исходные складские операции. Необходимо сбалансировать эти исходные складские операции в ячейке для коррекций посредством учета журнала складских инвентарных описей или журнала складских товаров, чтобы назначить товарам зоны и ячейки.\';
      Text015@1034 : TextConst 'ENU=You must create zones and bins before posting a warehouse physical inventory.\\;RUS=До учета инвентаризации склада необходимо создать зоны и ячейки.\\';
      Text016@1033 : TextConst 'ENU=Location %1 will be a warehouse management location after the batch job has run successfully. This conversion cannot be reversed or undone after it has run.;RUS=После успешного выполнения пакетного задания склад %1 будет преобразован для работы через систему управления складом. Это преобразование невозможно отменить или обратить после запуска.';
      Text017@1032 : TextConst 'ENU=\\Do you really want to proceed?;RUS=\\Вы действительно хотите продолжить?';
      Text018@1031 : TextConst 'ENU=There is nothing to convert for %1 %2 ''%3''.;RUS=Нечего преобразовывать для %1 %2 ''%3''.';
      Text019@1041 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 exists for this location.;RUS=Склад %1 преобразовать невозможно, так как на этом складе существует как минимум один %2.';
      Text020@1006 : TextConst 'ENU=Location %1 will be converted to a WMS location.\\This might take some time so please be patient.;RUS="Склад %1 будет преобразован для работы через систему управления складом WMS!\\Это займет некоторое время; дождитесь завершения преобразования."';
      Found@1017 : Boolean;

    LOCAL PROCEDURE CheckWhseDocs@5();
    VAR
      WhseRcptHeader@1000 : Record 7316;
      WarehouseShipmentHeader@1001 : Record 7320;
      WhseActivHeader@1002 : Record 5766;
      WhseWkshLine@1003 : Record 7326;
    BEGIN
      WhseRcptHeader.SETRANGE("Location Code",Location.Code);
      IF NOT WhseRcptHeader.ISEMPTY THEN
        ERROR(
          Text006,
          Location.Code,
          WhseRcptHeader.TABLECAPTION);

      WarehouseShipmentHeader.SETRANGE("Location Code",Location.Code);
      IF NOT WarehouseShipmentHeader.ISEMPTY THEN
        ERROR(
          Text006,
          Location.Code,
          WarehouseShipmentHeader.TABLECAPTION);

      WhseActivHeader.SETCURRENTKEY("Location Code");
      WhseActivHeader.SETRANGE("Location Code",Location.Code);
      IF WhseActivHeader.FINDFIRST THEN
        ERROR(
          Text007,
          Location.Code,
          WhseActivHeader.Type);

      WhseWkshLine.SETRANGE("Location Code",Location.Code);
      IF NOT WhseWkshLine.ISEMPTY THEN
        ERROR(
          Text008,
          Location.Code,
          WhseWkshLine.TABLECAPTION);
    END;

    LOCAL PROCEDURE CreateWhseJnlLine@4();
    BEGIN
      LastLineNo := LastLineNo + 10000;

      WITH ItemLedgEntry DO BEGIN
        TempWhseJnlLine.INIT;
        TempWhseJnlLine."Entry Type" := TempWhseJnlLine."Entry Type"::"Positive Adjmt.";
        TempWhseJnlLine."Line No." := LastLineNo;
        TempWhseJnlLine."Location Code" := "Location Code";
        TempWhseJnlLine."Registering Date" := TODAY;
        TempWhseJnlLine."Item No." := "Item No.";
        TempWhseJnlLine.Quantity := ROUND("Remaining Quantity" / "Qty. per Unit of Measure",0.00001);
        TempWhseJnlLine."Qty. (Base)" := "Remaining Quantity";
        TempWhseJnlLine."Qty. (Absolute)" := ROUND(ABS("Remaining Quantity") / "Qty. per Unit of Measure",0.00001);
        TempWhseJnlLine."Qty. (Absolute, Base)" := ABS("Remaining Quantity");
        TempWhseJnlLine."User ID" := USERID;
        TempWhseJnlLine."Variant Code" := "Variant Code";
        IF "Unit of Measure Code" = '' THEN BEGIN
          Item.GET("Item No.");
          "Unit of Measure Code" := Item."Base Unit of Measure";
        END;
        TempWhseJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        TempWhseJnlLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        TempWhseJnlLine."Lot No." := "Lot No.";
        TempWhseJnlLine."Serial No." := "Serial No.";
        TempWhseJnlLine."CD No." := "CD No.";
        TempWhseJnlLine.VALIDATE("Zone Code",Bin."Zone Code");
        TempWhseJnlLine."Bin Code" := AdjBinCode;
        TempWhseJnlLine."To Bin Code" := AdjBinCode;
        GetItemUnitOfMeasure("Item No.","Unit of Measure Code");
        TempWhseJnlLine.Cubage :=
          TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Cubage;
        TempWhseJnlLine.Weight :=
          TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Weight;

        WMSMgt.CheckWhseJnlLine(TempWhseJnlLine,0,0,FALSE);
        TempWhseJnlLine.INSERT;
      END;
    END;

    LOCAL PROCEDURE GetItemUnitOfMeasure@15(ItemNo@1000 : Code[20];UOMCode@1001 : Code[10]);
    BEGIN
      IF (ItemUnitOfMeasure."Item No." <> ItemNo) OR
         (ItemUnitOfMeasure.Code <> UOMCode)
      THEN
        IF NOT ItemUnitOfMeasure.GET(ItemNo,UOMCode) THEN
          ItemUnitOfMeasure.INIT;
    END;

    LOCAL PROCEDURE BuildErrorText@7() : Text[250];
    VAR
      Text@1000 : Text[250];
    BEGIN
      WITH ItemLedgEntry DO BEGIN
        Text :=
          STRSUBSTNO(
            '%1: %2, %3: %4',
            FIELDCAPTION("Location Code"),"Location Code",
            FIELDCAPTION("Item No."),"Item No.");
        IF "Variant Code" <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Variant Code"),"Variant Code");
        IF "Unit of Measure Code" <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Unit of Measure Code"),
              "Unit of Measure Code");
        IF "Lot No." <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Lot No."),"Lot No.");
        IF "Serial No." <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Serial No."),"Serial No.");
        IF "CD No." <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("CD No."),"CD No.");
      END;
      EXIT(Text);
    END;

    PROCEDURE InitializeRequest@2(LocationCode@1000 : Code[10];AdjustmentBinCode@1001 : Code[20]);
    BEGIN
      LocCode := LocationCode;
      AdjBinCode := AdjustmentBinCode;
    END;

    PROCEDURE SetHideValidationDialog@7380(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

