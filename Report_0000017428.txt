OBJECT Report 17428 Create Calendar Line
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Calendar Line;
               RUS=Создать строку календаря];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  PayrollCalendarSetup@1210000 : Record 17429;
                BEGIN
                  IF StartDate = 0D THEN
                    ERROR(Text14800);
                  IF EndDate = 0D THEN
                    ERROR(Text14801);
                  IF StartDate > EndDate THEN
                    ERROR(Text14802);

                  CalendarLine.RESET;
                  CalendarLine.SETRANGE("Calendar Code",Calendar.Code);

                  IF NOT ClearLines THEN BEGIN
                    IF CalendarLine.FINDLAST THEN
                      IF StartDate > CALCDATE('<+1D>',CalendarLine.Date) THEN
                        ERROR(Text14803,StartDate,CALCDATE('<1D>',CalendarLine.Date));
                    IF CalendarLine.FINDFIRST THEN
                      IF EndDate < CALCDATE('<-1D>',CalendarLine.Date) THEN
                        ERROR(Text14804,EndDate,CALCDATE('<-1D>',CalendarLine.Date));
                    CalendarLine.SETRANGE(Date,StartDate,EndDate);
                    IF CalendarLine.FINDFIRST THEN
                      IF NOT CONFIRM(Text14805,FALSE,StartDate,EndDate) THEN
                        EXIT;
                  END;

                  IF Calendar."Shift Start Date" <> 0D THEN
                    IF Calendar."Shift Days" < PayrollCalendarSetup.GetMaxShiftDay(Calendar.Code) THEN
                      ERROR(Text14807,PayrollCalendarSetup.GetMaxShiftDay(Calendar.Code));

                  CalendarLine.SETRANGE(Date);

                  WITH CalendarLine DO BEGIN
                    IF ClearLines THEN
                      DELETEALL(TRUE)
                    ELSE BEGIN
                      SETRANGE(Date,StartDate,EndDate);
                      DELETEALL(TRUE);
                    END;
                    Window.OPEN(Text14806);

                    "Calendar Code" := Calendar.Code;
                    Date := StartDate;
                    REPEAT
                      INIT;
                      VALIDATE(Date);
                      INSERT;
                      Window.UPDATE(1,Date);
                      Date := Date + 1;
                    UNTIL Date > EndDate;
                    Window.CLOSE;
                  END;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Start Date;
                             RUS=Дата начала];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=StartDate }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=End Date;
                             RUS=Дата окончания];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=EndDate }

      { 14  ;2   ;Field     ;
                  CaptionML=[ENU=Clear Lines;
                             RUS=Очистить строки];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ClearLines;
                  MultiLine=Yes }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Calendar@1210003 : Record 17428;
      CalendarLine@1210002 : Record 17430;
      Window@1210004 : Dialog;
      StartDate@1210000 : Date;
      EndDate@1210001 : Date;
      ClearLines@1210005 : Boolean;
      Text14800@1210007 : TextConst 'ENU=Please enter Start Date.;RUS=Введите дату начала.';
      Text14801@1210008 : TextConst 'ENU=Please enter End Date.;RUS=Введите дату окончания.';
      Text14802@1210009 : TextConst 'ENU=Start Date cannot be later than End Date.;RUS=Дата начала не может следовать за датой окончания.';
      Text14803@1210010 : TextConst 'ENU=Start Date %1 cannot be later than %2.;RUS=Дата начала %1 не может быть позже %2.';
      Text14804@1210011 : TextConst 'ENU=End Date %1 cannot be earlier than %2.;RUS=Дата окончания %1 не может быть ранее %2.';
      Text14805@1210012 : TextConst 'ENU=Calendar lines for period from %1 to %2 already exists. Do you want to replace these lines?;RUS=Строки календаря за период с %1 по %2 уже существуют. Заменить эти строки?';
      Text14806@1210013 : TextConst 'ENU=Create calendar lines  #1########;RUS=Создать строки календаря  #1########';
      Text14807@1210006 : TextConst 'ENU=Shift Days must be greater or equal than %1;RUS=Продолжительность смены в днях должна быть больше или равна %1';

    PROCEDURE GetCalendar@1(NewCalendar@1210000 : Record 17428);
    BEGIN
      Calendar := NewCalendar;
    END;

    PROCEDURE SetCalendar@2(CalendarCode@1210004 : Code[20];NewStartDate@1210000 : Date;NewEndDate@1210001 : Date;NewClearLines@1210002 : Boolean);
    BEGIN
      StartDate := NewStartDate;
      EndDate := NewEndDate;
      ClearLines := NewClearLines;
      CurrReport.USEREQUESTPAGE := FALSE;
      Calendar.GET(CalendarCode);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

