OBJECT Table 50185 FiRe Export Scheme Attribute
{
  OBJECT-PROPERTIES
  {
    Date=24.02.16;
    Time=15:18:36;
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=[ENU=FiRe Export Scheme Attribute;
               RUS=Атрибут схемы экспорта FiRe];
  }
  FIELDS
  {
    { 10  ;   ;Scheme Code         ;Code20        ;TableRelation="FiRe Export Scheme";
                                                   CaptionML=[ENU=Scheme Code;
                                                              RUS=Код схемы] }
    { 20  ;   ;Attribute Code      ;Code10        ;TableRelation="FiRe Attribute";
                                                   OnValidate=VAR
                                                                FiReAttribute@1101495000 : Record 50182;
                                                              BEGIN
                                                                IF "Attribute Code" <> '' THEN BEGIN
                                                                  FiReAttribute.GET("Attribute Code");
                                                                  "Data Type" := FiReAttribute."Data Type";
                                                                  "Sequence No." := FiReAttribute."Sequence No.";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Attribute Code;
                                                              RUS=Код атрибута] }
    { 22  ;   ;Attribute Name      ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("FiRe Attribute".Name WHERE (Code=FIELD(Attribute Code)));
                                                   CaptionML=[ENU=Attribute Name;
                                                              RUS=Название атрибута];
                                                   Editable=No }
    { 30  ;   ;Term Code           ;Code20        ;TableRelation="FiRe Term" WHERE (Data Type=FIELD(Data Type));
                                                   OnValidate=BEGIN
                                                                IF "Term Code" <> '' THEN BEGIN
                                                                  TESTFIELD("Constant Value",'');
                                                                  TESTFIELD("Function","Function"::" ");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Term Code;
                                                              RUS=Код термина] }
    { 40  ;   ;Constant Value      ;Text30        ;OnValidate=BEGIN
                                                                IF "Constant Value" <> '' THEN BEGIN
                                                                  TESTFIELD("Term Code",'');
                                                                  TESTFIELD("Function","Function"::" ");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Constant Value;
                                                              RUS=Значение константы] }
    { 45  ;   ;Function            ;Option        ;OnValidate=BEGIN
                                                                IF "Function" <> "Function"::" " THEN BEGIN
                                                                  TESTFIELD("Constant Value",'');
                                                                  TESTFIELD("Term Code",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Function;
                                                              RUS=Функция];
                                                   OptionCaptionML=ENU=" ,Export to FiRe date";
                                                   OptionString=[ ,Export to FiRe date] }
    { 50  ;   ;Data Type           ;Option        ;CaptionML=[ENU=Data Type;
                                                              RUS=Тип данных];
                                                   OptionCaptionML=[ENU=Text,Decimal,Date,Integer;
                                                                    RUS=Текст,Десятичное,Дата,Целое];
                                                   OptionString=Text,Decimal,Date,Integer }
    { 60  ;   ;Is Mandatory        ;Boolean       ;OnValidate=VAR
                                                                FiReTerm@1101495000 : Record 50183;
                                                              BEGIN
                                                                IF NOT xRec."Is Mandatory" THEN BEGIN
                                                                  IF "Is Mandatory" THEN BEGIN
                                                                    IF "Term Code" = '' THEN
                                                                      ERROR(IsMandatoryErr);

                                                                    FiReTerm.GET("Term Code");
                                                                    IF FiReTerm."Attribute Type" = FiReTerm."Attribute Type"::Measure THEN
                                                                      ERROR(IsMandatoryErr);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Is Mandatory;
                                                              RUS=Обязательный] }
    { 70  ;   ;Sequence No.        ;Integer       ;FieldClass=Normal;
                                                   CaptionML=ENU=Sequence No. }
  }
  KEYS
  {
    {    ;Scheme Code,Attribute Code              ;Clustered=Yes }
    {    ;Scheme Code,Sequence No.                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      IsMandatoryErr@1101495000 : TextConst 'ENU="Is Mandatory is allowed only for terms with Attribute Type=Dimension."';

    PROCEDURE GetSchemeAttributeType@1101495000() : Integer;
    VAR
      SchemeAttributeType@1101495000 : 'Constant,Term,Function';
    BEGIN
      CASE TRUE OF
        "Term Code" <> '':
          EXIT(SchemeAttributeType::Term);
        ("Constant Value" <> '') OR (("Term Code" = '') AND ("Constant Value" = '') AND ("Function" = "Function"::" ")):
          EXIT(SchemeAttributeType::Constant);
        "Function" <> "Function"::" ":
          EXIT(SchemeAttributeType::"Function");
      END;
    END;

    BEGIN
    {
      NCS-1897 ORM Table created
    }
    END.
  }
}

