OBJECT Page 681 Report Inbox Part
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Report Inbox;
               RUS=Входящие отчеты];
    SourceTable=Table477;
    SourceTableView=SORTING(User ID,Created Date-Time)
                    ORDER(Descending);
    PageType=ListPart;
    OnOpenPage=BEGIN
                 SETRANGE("User ID",USERID);
                 SETAUTOCALCFIELDS;
                 ShowAll := TRUE;
                 UpdateVisibility;
                 AddInReady := FALSE;
               END;

    OnQueryClosePage=BEGIN
                       IF AddInReady THEN
                         CurrPage.PingPong.Stop;
                       EXIT(TRUE);
                     END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      CaptionML=[ENU=Report;
                                 RUS=Отчет];
                      ActionContainerType=ActionItems }
      { 8       ;1   ;Action    ;
                      Name=Show;
                      ShortCutKey=Return;
                      CaptionML=[ENU=Show;
                                 RUS=Показать];
                      ToolTipML=[ENU=Open your report inbox.;
                                 RUS=Открытие входящих отчетов.];
                      ApplicationArea=#Basic,#Suite;
                      Image=Report;
                      OnAction=BEGIN
                                 ShowReport;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 11      ;1   ;Separator  }
      { 12      ;1   ;Action    ;
                      Name=Unread;
                      CaptionML=[ENU=Unread Reports;
                                 RUS=Непрочитанные отчеты];
                      ToolTipML=[ENU=Show only unread reports in your inbox.;
                                 RUS=Просмотр во входящих только непрочитанных отчетов.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=ShowAll;
                      Image=FilterLines;
                      OnAction=BEGIN
                                 ShowAll := FALSE;
                                 UpdateVisibility;
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=All;
                      CaptionML=[ENU=All Reports;
                                 RUS=Все отчеты];
                      ToolTipML=[ENU=View all reports in your inbox.;
                                 RUS=Просмотр во входящих всех отчетов.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NOT ShowAll;
                      Image=AllLines;
                      OnAction=BEGIN
                                 ShowAll := TRUE;
                                 UpdateVisibility;
                               END;
                                }
      { 14      ;1   ;Separator  }
      { 9       ;1   ;Action    ;
                      Name=Delete;
                      CaptionML=[ENU=Delete;
                                 RUS=Удалить];
                      ToolTipML=[ENU=Delete the record.;
                                 RUS=Удаление записи.];
                      ApplicationArea=#Basic,#Suite;
                      Image=Delete;
                      OnAction=VAR
                                 ReportInbox@1000 : Record 477;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ReportInbox);
                                 ReportInbox.DELETEALL;
                               END;
                                }
      { 18      ;1   ;Separator  }
      { 19      ;1   ;Action    ;
                      Name=ShowQueue;
                      CaptionML=[ENU=Show Queue;
                                 RUS=Показать очередь];
                      ToolTipML=[ENU=Show scheduled reports.;
                                 RUS=Просмотр запланированных отчетов.];
                      ApplicationArea=#Basic,#Suite;
                      Image=List;
                      OnAction=VAR
                                 JobQueueEntry@1000 : Record 472;
                               BEGIN
                                 JobQueueEntry.FILTERGROUP(2);
                                 JobQueueEntry.SETRANGE("User ID",USERID);
                                 JobQueueEntry.FILTERGROUP(0);
                                 PAGE.RUN(PAGE::"Job Queue Entries",JobQueueEntry);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the Microsoft Dynamics NAV user who scheduled the report to run.;
                           RUS=Определяет пользователя Microsoft Dynamics NAV, который запланировал запуск отчета.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User ID";
                Visible=false;
                Style=Strong;
                StyleExpr=NOT Read }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the date and time that the scheduled report was processed from the job queue.;
                           RUS=Определяет дату и время, когда был обработан запланированный отчет из очереди работ.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Created Date-Time";
                Style=Strong;
                StyleExpr=NOT Read }

    { 5   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the ID of the report that was scheduled and processed by job queue.;
                           RUS=Определяет идентификатор отчета, который был запланирован и обработан очередью работ.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Report ID";
                Visible=false;
                Style=Strong;
                StyleExpr=NOT Read }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the name of the scheduled report that was processed from the job queue.;
                           RUS=Определяет имя запланированного отчета, который был обработан из очереди работ.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Report Name";
                Visible=FALSE;
                Style=Strong;
                StyleExpr=NOT Read;
                OnDrillDown=BEGIN
                              ShowReport;
                              CurrPage.UPDATE;
                            END;
                             }

    { 15  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the description of the scheduled report that was processed from the job queue.;
                           RUS=Определяет описание запланированного отчета, который был обработан из очереди работ.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                Style=Strong;
                StyleExpr=NOT Read;
                OnDrillDown=BEGIN
                              ShowReport;
                              CurrPage.UPDATE;
                            END;
                             }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the output type of the scheduled report.;
                           RUS=Определяет тип вывода запланированного отчета.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Output Type";
                Style=Strong;
                StyleExpr=NOT Read }

    { 16  ;1   ;Group     ;
                GroupType=Group }

    { 17  ;2   ;Field     ;
                Name=PingPong;
                ApplicationArea=#Basic,#Suite;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.PingPong;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      ShowAll@1000 : Boolean;
      PrevNumberOfRecords@1001 : Integer;
      AddInReady@1002 : Boolean;

    LOCAL PROCEDURE UpdateVisibility@1();
    BEGIN
      IF ShowAll THEN
        SETRANGE(Read)
      ELSE
        SETRANGE(Read,FALSE);
      IF FINDFIRST THEN;
      CurrPage.UPDATE(FALSE);
    END;

    EVENT PingPong@-17::AddInReady@2();
    BEGIN
      AddInReady := TRUE;
      PrevNumberOfRecords := COUNT;
      CurrPage.PingPong.Ping(10000);
    END;

    EVENT PingPong@-17::Pong@3();
    VAR
      CurrNumberOfRecords@1000 : Integer;
    BEGIN
      CurrNumberOfRecords := COUNT;
      IF PrevNumberOfRecords <> CurrNumberOfRecords THEN
        CurrPage.UPDATE(FALSE);
      PrevNumberOfRecords := CurrNumberOfRecords;
      CurrPage.PingPong.Ping(10000);
    END;

    BEGIN
    END.
  }
}

