OBJECT Report 50108 Insert Currency Exchange Rate
{
  OBJECT-PROPERTIES
  {
    Date=05.07.17;
    Time=12:07:36;
    Modified=Yes;
    Version List=NOS-26;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Insert Currency Exchange Rate;
               RUS=Вставить Курс Валюты];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   IF StartDate = 0D THEN
                     ERROR(Text001);

                   IF CurrencyCode = '' THEN
                     ERROR(Text002);

                   IF (ExchangeRateAmount = 0) OR (RelationalExchRateAmount = 0) THEN
                     ERROR(Text003);

                   InsertExchRate(CurrencyCode,StartDate);

                   MESSAGE(Text004);
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1101495000;;Container;
                  ContainerType=ContentArea }

      { 1101495001;1;Group  ;
                  CaptionML=[ENU=Report options;
                             RUS=Параметры отчета];
                  GroupType=Group }

      { 1101495002;2;Field  ;
                  CaptionML=[ENU=Rate Date;
                             RUS=Дата Курса];
                  SourceExpr=StartDate;
                  OnValidate=BEGIN
                               UpdateExchRate;
                             END;
                              }

      { 1101495008;2;Group  ;
                  GroupType=GridLayout;
                  Layout=Columns }

      { 1101495009;3;Group  ;
                  GroupType=Group;
                  Layout=Columns }

      { 1101495003;4;Field  ;
                  CaptionML=[ENU=Currency Code;
                             RUS=Код Валюты];
                  SourceExpr=CurrencyCode;
                  OnValidate=BEGIN
                               UpdateExchRate;
                             END;

                  OnLookup=VAR
                             Currency@1101495001 : Record 4;
                             Currencies@1101495000 : Page 5;
                           BEGIN
                             Currencies.LOOKUPMODE(TRUE);
                             Currency.RESET;
                             Currency.SETFILTER("Relational Currency Code", '<>%1','');
                             Currencies.SETTABLEVIEW(Currency);
                             IF NOT (Currencies.RUNMODAL = ACTION::LookupOK) THEN
                               EXIT(FALSE)
                             ELSE BEGIN
                               Currencies.GETRECORD(Currency);
                               CurrencyCode := Currency.Code;
                               Text := CurrencyCode;
                             END;

                             UpdateExchRate;

                             EXIT(TRUE);
                           END;
                            }

      { 1101495005;4;Field  ;
                  CaptionML=[ENU=Exchange Rate Amount;
                             RUS=Курс Валюты Сумма];
                  SourceExpr=ExchangeRateAmount }

      { 1101495010;3;Group  ;
                  GroupType=Group;
                  Layout=Columns }

      { 1101495004;4;Field  ;
                  CaptionML=[ENU=Relational Currency Code;
                             RUS=Связанный Код Валюты];
                  SourceExpr=RelCurr;
                  Editable=FALSE }

      { 1101495006;4;Field  ;
                  CaptionML=[ENU=Relational Exch. Rate Amount;
                             RUS=Связанный Курс Валюты Сумма];
                  SourceExpr=RelationalExchRateAmount }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      StartDate@1101495009 : Date;
      CurrencyCode@1101495008 : Code[20];
      CurrExchRate@1101495007 : Record 330;
      ExchangeRateAmount@1101495006 : Decimal;
      RelationalExchRateAmount@1101495005 : Decimal;
      GLSetup@1101495004 : Record 98;
      LocalCurrency@1101495003 : Code[20];
      LocalRelCurrencyCode@1101495002 : Code[20];
      Currency@1101495001 : Record 4;
      RelCurr@1101495000 : Code[20];
      Text000@1101495014 : TextConst 'RUS=Вы не можете задать курс валюты для %1';
      Text001@1101495013 : TextConst 'RUS=Задайте Дату Начала';
      Text002@1101495012 : TextConst 'RUS=Введите Код Валюты';
      Text003@1101495011 : TextConst 'RUS=Курс Валюты и Связанный Курс Валюты не должны быть равными нулю.';
      Text004@1101495010 : TextConst 'RUS=Курс был создан';

    PROCEDURE UpdateExchRate@1101495000();
    BEGIN
      IF (CurrencyCode <> '') THEN BEGIN
        Currency.GET(CurrencyCode);
        RelCurr := ' / ' + Currency."Relational Currency Code";
        IF Currency."Relational Currency Code" = '' THEN
          ERROR(Text000, CurrencyCode);
      END ELSE
        RelCurr := '';

      IF (StartDate <> 0D) AND (CurrencyCode <> '') THEN BEGIN
        GLSetup.GET;
        CurrExchRate.RESET;
        CurrExchRate.SETFILTER("Starting Date", '<=%1', StartDate);

        IF GLSetup."LCY Code" = Currency.Code THEN
          LocalCurrency := ''
        ELSE
          LocalCurrency := Currency.Code;

        IF GLSetup."LCY Code" = Currency."Relational Currency Code" THEN
          LocalRelCurrencyCode := ''
        ELSE
          LocalRelCurrencyCode := Currency."Relational Currency Code";

        IF (LocalCurrency = '') AND (LocalRelCurrencyCode <> '') THEN
          CurrExchRate.SETRANGE("Currency Code", LocalRelCurrencyCode)
        ELSE
          CurrExchRate.SETRANGE("Currency Code", LocalCurrency);

        IF CurrExchRate.FINDLAST THEN BEGIN
          IF (LocalCurrency = '') AND (LocalRelCurrencyCode <> '') THEN BEGIN
            RelationalExchRateAmount := CurrExchRate."Exchange Rate Amount";
            ExchangeRateAmount := CurrExchRate."Relational Exch. Rate Amount";
          END ELSE BEGIN
            ExchangeRateAmount := CurrExchRate."Exchange Rate Amount";
            RelationalExchRateAmount := CurrExchRate."Relational Exch. Rate Amount";
          END;
        END ELSE BEGIN
          ExchangeRateAmount := 0;
          RelationalExchRateAmount := 0;
        END;
      END;
    END;

    PROCEDURE InsertExchRate@1101495001(CurrCode@1101495000 : Code[20];S_Date@1101495001 : Date);
    VAR
      Company@1101495002 : Record 2000000006;
      CurrencyExchangeRate@1101495003 : Record 330;
      GeneralLedgerSetup@1101495004 : Record 98;
    BEGIN
      Company.RESET;
      //Company.SETFILTER(Name,'Roinco|TD Investments');
      IF Company.FINDSET THEN
      REPEAT
        GeneralLedgerSetup.CHANGECOMPANY(Company.Name);
        GeneralLedgerSetup.GET;

        IF GeneralLedgerSetup."LCY Code" = CurrCode THEN
          LocalCurrency := ''
        ELSE
          LocalCurrency := Currency.Code;

        IF GeneralLedgerSetup."LCY Code" = Currency."Relational Currency Code" THEN
          LocalRelCurrencyCode := ''
        ELSE
          LocalRelCurrencyCode := Currency."Relational Currency Code";

        CurrencyExchangeRate.CHANGECOMPANY(Company.Name);
        CurrencyExchangeRate.INIT;
        CurrencyExchangeRate."Starting Date" := S_Date;
        CurrencyExchangeRate."System Create" := TRUE;
        IF (LocalCurrency = '') AND (LocalRelCurrencyCode <> '') THEN BEGIN
          CurrencyExchangeRate."Currency Code" := LocalRelCurrencyCode;
          CurrencyExchangeRate."Relational Currency Code" := LocalCurrency;

          CurrencyExchangeRate."Exchange Rate Amount" := RelationalExchRateAmount;
          CurrencyExchangeRate."Relational Exch. Rate Amount" := ExchangeRateAmount;
          CurrencyExchangeRate."Adjustment Exch. Rate Amount" := RelationalExchRateAmount;
          CurrencyExchangeRate."Relational Adjmt Exch Rate Amt" := ExchangeRateAmount;
        END ELSE BEGIN
          CurrencyExchangeRate."Currency Code" := LocalCurrency;
          CurrencyExchangeRate."Relational Currency Code" := LocalRelCurrencyCode;

          CurrencyExchangeRate."Exchange Rate Amount" := ExchangeRateAmount;
          CurrencyExchangeRate."Relational Exch. Rate Amount" := RelationalExchRateAmount;
          CurrencyExchangeRate."Adjustment Exch. Rate Amount" := ExchangeRateAmount;
          CurrencyExchangeRate."Relational Adjmt Exch Rate Amt" := RelationalExchRateAmount;
        END;
        IF NOT CurrencyExchangeRate.INSERT THEN
          CurrencyExchangeRate.MODIFY
      UNTIL Company.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

