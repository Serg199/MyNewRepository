OBJECT Page 17209 Tax Register Accumulation
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Tax Register Accumulation;
               RUS=Накопление налогового регистра];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    SourceTable=Table17200;
    PageType=Card;
    OnOpenPage=BEGIN
                 PeriodType := PeriodType::Month;
                 FindPeriod('');
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 12      ;1   ;Action    ;
                      CaptionML=[ENU=Previous Set;
                                 RUS=Пред. набор];
                      ToolTipML=[ENU=Previous Set;
                                 RUS=Пред. набор];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FindPeriod('<=');
                               END;
                                }
      { 11      ;1   ;Action    ;
                      CaptionML=[ENU=Next Set;
                                 RUS=След. набор];
                      ToolTipML=[ENU=Next Set;
                                 RUS=След. набор];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FindPeriod('>=');
                               END;
                                }
      { 1210002 ;1   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[ENU=Entries;
                                 RUS=Операции];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Entries;
                      OnAction=BEGIN
                                 ShowTaxRegEntries;
                               END;
                                }
      { 1210000 ;0   ;ActionContainer;
                      ActionContainerType=Reports }
      { 1210001 ;1   ;Action    ;
                      Name=Print;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Print;
                      OnAction=BEGIN
                                 PrintReport(DateFilter);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           RUS=Общее] }

    { 5   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the tax register name.;
                           RUS=Определяет номер наименования налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="No." }

    { 13  ;2   ;Field     ;
                CaptionML=[ENU=View by;
                           RUS=Просмотр по];
                OptionCaptionML=[ENU=,,Month,Quarter,Year;
                                 RUS=,,Месяц,Квартал,Год];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             FindPeriod('');
                           END;
                            }

    { 14  ;2   ;Field     ;
                CaptionML=[ENU=Date Filter;
                           RUS=Фильтр по дате];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DateFilter;
                Editable=FALSE }

    { 2   ;1   ;Part      ;
                Name=TaxRegAccLines;
                ApplicationArea=#Basic,#Suite;
                SubPageView=SORTING(Section Code,Tax Register No.,Template Line No.,Starting Date,Ending Date);
                SubPageLink=Section Code=FIELD(Section Code),
                            Tax Register No.=FIELD(No.),
                            Date Filter=FIELD(Date Filter);
                PagePartID=Page17210;
                PartType=Page }

  }
  CODE
  {
    VAR
      PeriodType@1021 : ',,Month,Quarter,Year';
      DateFilter@1030 : Text[250];

    LOCAL PROCEDURE FindPeriod@1210000(SearchText@1000 : Code[10]);
    VAR
      Calendar@1210001 : Record 2000000007;
      PeriodFormMgt@1001 : Codeunit 359;
    BEGIN
      IF DateFilter <> '' THEN BEGIN
        Calendar.SETFILTER("Period Start",DateFilter);
        IF NOT PeriodFormMgt.FindDate('+',Calendar,PeriodType) THEN
          PeriodFormMgt.FindDate('+',Calendar,PeriodType::Month);
        Calendar.SETRANGE("Period Start");
      END;
      PeriodFormMgt.FindDate(SearchText,Calendar,PeriodType);
      DateFilter := STRSUBSTNO('%1..%2',Calendar."Period Start",Calendar."Period End");
      SETFILTER("Date Filter",'%1..%2',CALCDATE('<-CM>',Calendar."Period End"),Calendar."Period End");
      CurrPage.TaxRegAccLines.PAGE.UpdatePage(DateFilter);
    END;

    LOCAL PROCEDURE ShowTaxRegEntries@3();
    VAR
      TaxRegGLEntry@1001 : Record 17209;
      TaxRegCVEntry@1002 : Record 17210;
      TaxRegItemEntry@1003 : Record 17212;
      TaxRegFAEntry@1005 : Record 17211;
      TaxRegFEEntry@1006 : Record 17213;
    BEGIN
      IF ("Page ID" = 0) OR ("Table ID" = 0) OR
         ("Storing Method" = "Storing Method"::Calculation)
      THEN
        EXIT;

      CASE "Table ID" OF
        DATABASE::"Tax Register G/L Entry":
          BEGIN
            TaxRegGLEntry.SETFILTER("Where Used Register IDs",'*~' + "Register ID" + '~*');
            PAGE.RUNMODAL("Page ID",TaxRegGLEntry);
          END;
        DATABASE::"Tax Register CV Entry":
          BEGIN
            TaxRegCVEntry.SETFILTER("Where Used Register IDs",'*~' + "Register ID" + '~*');
            PAGE.RUNMODAL("Page ID",TaxRegCVEntry);
          END;
        DATABASE::"Tax Register Item Entry":
          BEGIN
            TaxRegItemEntry.SETFILTER("Where Used Register IDs",'*~' + "Register ID" + '~*');
            PAGE.RUNMODAL("Page ID",TaxRegItemEntry);
          END;
        DATABASE::"Tax Register FA Entry":
          BEGIN
            TaxRegFAEntry.SETFILTER("Where Used Register IDs",'*~' + "Register ID" + '~*');
            PAGE.RUNMODAL("Page ID",TaxRegFAEntry);
          END;
        DATABASE::"Tax Register FE Entry":
          BEGIN
            TaxRegFEEntry.SETFILTER("Where Used Register IDs",'*~' + "Register ID" + '~*');
            PAGE.RUNMODAL("Page ID",TaxRegFEEntry);
          END;
      END;
    END;

    BEGIN
    END.
  }
}

