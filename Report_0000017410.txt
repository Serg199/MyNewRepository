OBJECT Report 17410 Suggest Salary Setup Lines
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Suggest Salary Setup Lines;
               RUS=Предложить строки настройки зарплаты];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   SourceCodeSetup.GET;
                 END;

  }
  DATASET
  {
    { 7528;    ;DataItem;                    ;
               DataItemTable=Table5200;
               OnPreDataItem=BEGIN
                               IF ElementCode = '' THEN
                                 ERROR(Text000,EmplJnlLine.FIELDCAPTION("Element Code"));

                               IF StartDate = 0D THEN
                                 StartDate := CALCDATE('<-CM>',TODAY);
                               IF EndDate = 0D THEN
                                 EndDate := CALCDATE('<CM>',TODAY);

                               LineNo := 10000;
                               EmplJnlLine.SETRANGE("Journal Template Name",EmplJnlLine."Journal Template Name");
                               EmplJnlLine.SETRANGE("Journal Batch Name",EmplJnlLine."Journal Batch Name");
                               IF ClearLine THEN BEGIN
                                 IF NOT EmplJnlLine.ISEMPTY THEN
                                   EmplJnlLine.DELETEALL
                               END ELSE
                                 IF EmplJnlLine.FINDLAST THEN
                                   LineNo := EmplJnlLine."Line No." + 10000;

                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,Employee."No.");

                                  EmplJnlLine2.INIT;
                                  EmplJnlLine2."Journal Template Name" := EmplJnlLine."Journal Template Name";
                                  EmplJnlLine2."Journal Batch Name" := EmplJnlLine."Journal Batch Name";
                                  EmplJnlLine2."Line No." := LineNo;
                                  EmplJnlLine2.VALIDATE("Employee No.","No.");
                                  EmplJnlLine2.VALIDATE("Element Code",ElementCode);
                                  EmplJnlLine2.VALIDATE("Posting Date",PostingDate);
                                  EmplJnlLine2.VALIDATE("Period Code",PeriodCode);
                                  EmplJnlLine2.VALIDATE("Document Date",DocumentDate);
                                  EmplJnlLine2.VALIDATE("Document No.",DocumentNo);
                                  EmplJnlLine2.VALIDATE("Starting Date",StartDate);
                                  EmplJnlLine2.VALIDATE("Ending Date",EndDate);
                                  CASE AmountType OF
                                    AmountType::Amount,
                                    AmountType::Percent:
                                      EmplJnlLine2.VALIDATE(Amount,Amount);
                                    AmountType::Quantity:
                                      EmplJnlLine2.VALIDATE(Quantity,Amount);
                                  END;

                                  EmplJnlLine2."Source Code" := SourceCodeSetup."Employee Journal";
                                  IF DescriptionText <> '' THEN
                                    EmplJnlLine.Description := DescriptionText;
                                  EmplJnlLine2.INSERT;
                                  LineNo := LineNo + 10000;
                                END;

               ReqFilterFields=No.,Org. Unit Code,Last Name }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1210016;2;Field     ;
                  CaptionML=[ENU=Posting Date;
                             RUS=Дата учета];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               PostingDateOnAfterValidate;
                             END;
                              }

      { 1210022;2;Field     ;
                  CaptionML=[ENU=Period Code;
                             RUS=Код периода];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PeriodCode;
                  TableRelation="Payroll Period" }

      { 1210019;2;Field     ;
                  CaptionML=[ENU=Document Date;
                             RUS=Дата документа];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentDate }

      { 1210021;2;Field     ;
                  CaptionML=[ENU=Document No.;
                             RUS=Номер документа];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentNo }

      { 1210000;2;Field     ;
                  CaptionML=[ENU=Element Code;
                             RUS=Код элемента];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ElementCode;
                  TableRelation="Payroll Element" }

      { 1210004;2;Field     ;
                  CaptionML=[ENU=Amount Type;
                             RUS=Тип суммы];
                  OptionCaptionML=[ENU=Amount,Percent,Quantity;
                                   RUS=Сумма,Процент,Количество];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=AmountType }

      { 1210007;2;Field     ;
                  CaptionML=[ENU=Amount;
                             RUS=Сумма];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Amount }

      { 1210011;2;Field     ;
                  CaptionML=[ENU=Start Date;
                             RUS=Дата начала];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=StartDate }

      { 1210013;2;Field     ;
                  CaptionML=[ENU=End Date;
                             RUS=Дата окончания];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=EndDate }

      { 1210024;2;Field     ;
                  CaptionML=[ENU=Description;
                             RUS=Описание];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DescriptionText }

      { 1210014;2;Field     ;
                  CaptionML=[ENU=Clear Lines;
                             RUS=Очистить строки];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ClearLine }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      EmplJnlLine@1210001 : Record 17382;
      EmplJnlLine2@1210014 : Record 17382;
      SourceCodeSetup@1210017 : Record 242;
      PayrollPeriod@1210022 : Record 17426;
      Window@1210004 : Dialog;
      ElementType@1210011 : 'Salary,Bonus,Deduction';
      ElementCode@1210000 : Code[20];
      ClearLine@1210009 : Boolean;
      Amount@1210008 : Decimal;
      StartDate@1210007 : Date;
      EndDate@1210006 : Date;
      PostingDate@1210018 : Date;
      DocumentDate@1210019 : Date;
      DocumentNo@1210020 : Code[20];
      LineNo@1210005 : Integer;
      AmountType@1210002 : 'Amount,Percent,Quantity';
      Text000@1210013 : TextConst 'ENU=Please enter %1.;RUS=Введите %1.';
      Text001@1210010 : TextConst 'ENU=Employee No.   #1####################;RUS=Код сотрудника     #1####################';
      PeriodCode@1210021 : Code[10];
      DescriptionText@1210023 : Text[50];

    PROCEDURE SetJnlLine@1210000(NewEmplJnlLine@1210000 : Record 17382);
    BEGIN
      EmplJnlLine := NewEmplJnlLine;
    END;

    PROCEDURE Initialize@1210002(NewElementType@1210005 : Option;NewElementCode@1210000 : Code[20];NewAmountType@1210001 : Option;NewAmount@1210002 : Decimal;NewStartDate@1210003 : Date;NewEndDate@1210004 : Date);
    BEGIN
      ElementType := NewElementType;
      ElementCode := NewElementCode;
      AmountType := NewAmountType;
      Amount := NewAmount;
      StartDate := NewStartDate;
      EndDate := NewEndDate;
    END;

    LOCAL PROCEDURE PostingDateOnAfterValidate@19003005();
    BEGIN
      IF PostingDate <> 0D THEN
        PeriodCode := PayrollPeriod.PeriodByDate(PostingDate);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

