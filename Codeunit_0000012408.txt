OBJECT Codeunit 12408 XML-Excel Reports Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=15.09.15;
    Time=12:00:00;
    Version List=NAVRU9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      RepPathNotFoundTxt@1210000 : TextConst 'ENU=Reporting Path %1 not found.;RUS=Не найден каталог для отчетности %1';
      XMLReport@1210001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";

    PROCEDURE AddSubNode@1210007(VAR ParentNode@1210002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR ChildNode@1210001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Tag@1210000 : Text[260]);
    VAR
      XMLElement@1210003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
    BEGIN
      XMLElement := XMLReport.CreateElement(Tag);
      ChildNode := ParentNode.AppendChild(XMLElement);
    END;

    PROCEDURE AddAttributeNode@1210005(VAR Node@1210003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR Attr@1210002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlAttribute";Name@1210001 : Text[250];Value@1210000 : Variant);
    BEGIN
      Attr := XMLReport.CreateAttribute(Name);
      Attr.Value := Value;

      Attr := Node.Attributes.SetNamedItem(Attr);
      Attr.Value := Value;
    END;

    PROCEDURE AddAttributeDate@1210004(VAR Node@1210003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR Attr@1210002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlAttribute";Name@1210001 : Text[260];Date@1210000 : Date);
    VAR
      Value@1210004 : Variant;
    BEGIN
      Attr := XMLReport.CreateAttribute(Name);
      Value := FORMAT(Date,10,'<Year4>-<Month,2>-<Day,2>');
      Attr.Value := Value;
      Attr := Node.Attributes.SetNamedItem(Attr);
    END;

    PROCEDURE CreateXMLDoc@1000000002(VAR XMLDoc@1210005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";TxtEncoding@1210001 : Text[60];VAR RootNode@1101489000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";TxtRootTagName@1210000 : Text[100]);
    VAR
      ProcInstr@1101489001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlProcessingInstruction";
    BEGIN
      XMLReport := XMLDoc;
      IF ISNULL(XMLReport) THEN
        XMLReport := XMLReport.XmlDocument;

      ProcInstr := XMLReport.CreateProcessingInstruction('xml',' version="1.0" encoding="' + TxtEncoding + '"');
      XMLReport.AppendChild(ProcInstr);

      RootNode := XMLReport.CreateElement(TxtRootTagName);
      XMLReport.AppendChild(RootNode);
    END;

    PROCEDURE AddAttribute@1000000000(VAR Node@1101489002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";TxtName@1101489001 : Text[250];VarValue@1101489000 : Variant);
    VAR
      Attribute@1101489003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlAttribute";
      DtDateTime@1101489004 : DateTime;
    BEGIN
      IF VarValue.ISDATE THEN BEGIN
        Attribute := Node.OwnerDocument.CreateAttribute(TxtName);
        AddAttributeDate(Node,Attribute,TxtName,VarValue);
      END ELSE
        IF EVALUATE(DtDateTime,FORMAT(VarValue)) AND
           (FORMAT(DtDateTime) = FORMAT(VarValue))
        THEN
          AddAttributeDateTime(Node,TxtName,DtDateTime)
        ELSE BEGIN
          Attribute := Node.OwnerDocument.CreateAttribute(TxtName);
          Attribute.Value := FORMAT(VarValue);
          Attribute := Node.Attributes.SetNamedItem(Attribute);
          CLEAR(Attribute);
        END;
    END;

    PROCEDURE AddAttributeDateTime@1101489000(VAR Node@1101489002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";TxtName@1101489001 : Text[250];DtValue@1101489000 : DateTime);
    VAR
      Attribute@1101489003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlAttribute";
      VarValue@1170000000 : Variant;
    BEGIN
      Attribute := Node.OwnerDocument.CreateAttribute(TxtName);
      VarValue :=
        FORMAT(DtValue,20,'<Year4>-<Month,2>-<Day,2>T <Hours24,2>:<Minutes,2>:<Seconds,2>');
      Attribute.Value := VarValue;
      Attribute := Node.Attributes.SetNamedItem(Attribute);
      Attribute.Value := VarValue;
      CLEAR(Attribute);
    END;

    PROCEDURE SaveXMLDocWithEncoding@1210000(OutStr@1210003 : OutStream;XmlDoc@1210004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";EncodingName@1210005 : Text);
    VAR
      XmlWriterSettings@1210002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlWriterSettings";
      XmlWriter@1210001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlWriter";
      Encoding@1210000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding";
    BEGIN
      XmlWriterSettings := XmlWriterSettings.XmlWriterSettings;
      XmlWriterSettings.Indent := TRUE;
      XmlWriterSettings.Encoding := Encoding.GetEncoding(EncodingName);

      XmlWriter := XmlWriter.Create(OutStr,XmlWriterSettings);
      XmlDoc.WriteTo(XmlWriter);
      XmlWriter.Close;
    END;

    BEGIN
    END.
  }
}

