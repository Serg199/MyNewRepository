OBJECT Table 5065 Interaction Log Entry
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               InteractionCommentLine@1000 : Record 5123;
               Attachment@1003 : Record 5062;
               CampaignMgt@1001 : Codeunit 7030;
             BEGIN
               InteractionCommentLine.SETRANGE("Entry No.","Entry No.");
               InteractionCommentLine.DELETEALL;

               CampaignMgt.DeleteContfromTargetGr(Rec);
               IF Attachment.GET("Attachment No.") THEN
                 Attachment.RemoveAttachment(FALSE);
             END;

    CaptionML=[ENU=Interaction Log Entry;
               RUS=Журнал взаимодействия];
    LookupPageID=Page5076;
    DrillDownPageID=Page5076;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              RUS=Номер операции] }
    { 2   ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   CaptionML=[ENU=Contact No.;
                                                              RUS=Код контакта] }
    { 3   ;   ;Contact Company No. ;Code20        ;TableRelation=Contact WHERE (Type=CONST(Company));
                                                   CaptionML=[ENU=Contact Company No.;
                                                              RUS=Номер контакта организации] }
    { 4   ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              RUS=Дата] }
    { 5   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 6   ;   ;Information Flow    ;Option        ;CaptionML=[ENU=Information Flow;
                                                              RUS=Поток информации];
                                                   OptionCaptionML=[ENU=" ,Outbound,Inbound";
                                                                    RUS=" ,Исходящие,Входящие"];
                                                   OptionString=[ ,Outbound,Inbound] }
    { 7   ;   ;Initiated By        ;Option        ;CaptionML=[ENU=Initiated By;
                                                              RUS=Кем начато];
                                                   OptionCaptionML=[ENU=" ,Us,Them";
                                                                    RUS=" ,Нами,Ими"];
                                                   OptionString=[ ,Us,Them] }
    { 8   ;   ;Attachment No.      ;Integer       ;TableRelation=Attachment;
                                                   CaptionML=[ENU=Attachment No.;
                                                              RUS=Номер вложения] }
    { 9   ;   ;Cost (LCY)          ;Decimal       ;CaptionML=[ENU=Cost (LCY);
                                                              RUS=Себестоимость (руб.)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 10  ;   ;Duration (Min.)     ;Decimal       ;CaptionML=[ENU=Duration (Min.);
                                                              RUS=Длительность (мин.)];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 11  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              RUS=Код пользователя] }
    { 12  ;   ;Interaction Group Code;Code10      ;TableRelation="Interaction Group";
                                                   CaptionML=[ENU=Interaction Group Code;
                                                              RUS=Код группы взаимодействия] }
    { 13  ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=[ENU=Interaction Template Code;
                                                              RUS=Код шаблона взаимодействия] }
    { 14  ;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   CaptionML=[ENU=Campaign No.;
                                                              RUS=Код кампании] }
    { 15  ;   ;Campaign Entry No.  ;Integer       ;TableRelation="Campaign Entry" WHERE (Campaign No.=FIELD(Campaign No.));
                                                   CaptionML=[ENU=Campaign Entry No.;
                                                              RUS=Номер операции кампании] }
    { 16  ;   ;Campaign Response   ;Boolean       ;CaptionML=[ENU=Campaign Response;
                                                              RUS=Отклик кампании] }
    { 17  ;   ;Campaign Target     ;Boolean       ;CaptionML=[ENU=Campaign Target;
                                                              RUS=Цель кампании] }
    { 18  ;   ;Segment No.         ;Code20        ;CaptionML=[ENU=Segment No.;
                                                              RUS=Номер сегмента] }
    { 19  ;   ;Evaluation          ;Option        ;CaptionML=[ENU=Evaluation;
                                                              RUS=Оценка];
                                                   OptionCaptionML=[ENU=" ,Very Positive,Positive,Neutral,Negative,Very Negative";
                                                                    RUS=" ,Отлично,Хорошо,Нейтрально,Плохо,Очень плохо"];
                                                   OptionString=[ ,Very Positive,Positive,Neutral,Negative,Very Negative];
                                                   Editable=No }
    { 20  ;   ;Time of Interaction ;Time          ;CaptionML=[ENU=Time of Interaction;
                                                              RUS=Время взаимодействия] }
    { 21  ;   ;Attempt Failed      ;Boolean       ;CaptionML=[ENU=Attempt Failed;
                                                              RUS=Попытка неудачна] }
    { 23  ;   ;To-do No.           ;Code20        ;TableRelation=To-do;
                                                   CaptionML=[ENU=To-do No.;
                                                              RUS=Номер задачи] }
    { 24  ;   ;Salesperson Code    ;Code10        ;TableRelation=Salesperson/Purchaser;
                                                   CaptionML=[ENU=Salesperson Code;
                                                              RUS=Код менеджера по продажам] }
    { 25  ;   ;Delivery Status     ;Option        ;CaptionML=[ENU=Delivery Status;
                                                              RUS=Статус доставки];
                                                   OptionCaptionML=[ENU=" ,In Progress,Error";
                                                                    RUS=" ,В работе,Ошибка"];
                                                   OptionString=[ ,In Progress,Error] }
    { 26  ;   ;Canceled            ;Boolean       ;CaptionML=[ENU=Canceled;
                                                              RUS=Отменено] }
    { 27  ;   ;Correspondence Type ;Option        ;CaptionML=[ENU=Correspondence Type;
                                                              RUS=Тип корреспонденции];
                                                   OptionCaptionML=[ENU=" ,Hard Copy,Email,Fax";
                                                                    RUS=" ,Распечатанная копия,Электронная почта,Факс"];
                                                   OptionString=[ ,Hard Copy,Email,Fax] }
    { 28  ;   ;Contact Alt. Address Code;Code10   ;TableRelation="Contact Alt. Address".Code WHERE (Contact No.=FIELD(Contact No.));
                                                   CaptionML=[ENU=Contact Alt. Address Code;
                                                              RUS=Код альт. адр. контакта] }
    { 29  ;   ;Logged Segment Entry No.;Integer   ;TableRelation="Logged Segment";
                                                   CaptionML=[ENU=Logged Segment Entry No.;
                                                              RUS=Номер операции зарегистр. сегмента] }
    { 30  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              RUS=Тип документа];
                                                   OptionCaptionML=[ENU=" ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet,Sales Return Order,Sales Finance Charge Memo,Sales Return Receipt,Purch. Return Shipment,Purch. Return Ord. Cnfrmn.,Service Contract,Service Contract Quote,Service Quote";
                                                                    RUS=" ,Предл. по продаже,Общ. заказ на прод.,Подтв. заказ на прод.,Счет прод.,Извещен. об отгр. прод.,Кр.-нота прод.,Отчет по прод.,Напомин. по прод.,Создание серв. заказа,Учет серв. заказа,Предл. по покуп.,Общ. заказ на покуп.,Заказ на покупку,Счет покупки,Прих. накладная,Кр.-нота покупки,Титульный лист,Возврат продажи,Процент-нота прод.,Возвр. прих. накл. прод.,Возвр. расх. накладная покуп.,Подтв. возврат покуп.,Серв. контракт,Предл. по серв. контр.,Серв. предложение"];
                                                   OptionString=[ ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet,Sales Return Order,Sales Finance Charge Memo,Sales Return Receipt,Purch. Return Shipment,Purch. Return Ord. Cnfrmn.,Service Contract,Service Contract Quote,Service Quote] }
    { 31  ;   ;Document No.        ;Code20        ;TestTableRelation=No;
                                                   CaptionML=[ENU=Document No.;
                                                              RUS=Номер документа] }
    { 32  ;   ;Version No.         ;Integer       ;CaptionML=[ENU=Version No.;
                                                              RUS=Номер версии] }
    { 33  ;   ;Doc. No. Occurrence ;Integer       ;CaptionML=[ENU=Doc. No. Occurrence;
                                                              RUS=Вхождения номера документа] }
    { 34  ;   ;Contact Via         ;Text80        ;CaptionML=[ENU=Contact Via;
                                                              RUS=Контакт через] }
    { 35  ;   ;Send Word Docs. as Attmt.;Boolean  ;CaptionML=[ENU=Send Word Docs. as Attmt.;
                                                              RUS=Отправить прикр. документ Word] }
    { 36  ;   ;Interaction Language Code;Code10   ;TableRelation=Language;
                                                   CaptionML=[ENU=Interaction Language Code;
                                                              RUS=Код языка взаимодействия] }
    { 37  ;   ;E-Mail Logged       ;Boolean       ;CaptionML=[ENU=Email Logged;
                                                              RUS=Адрес эл. почты зарегистрирован] }
    { 38  ;   ;Subject             ;Text50        ;CaptionML=[ENU=Subject;
                                                              RUS=Тема] }
    { 39  ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              RUS=Имя контакта];
                                                   Editable=No }
    { 40  ;   ;Contact Company Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Company No.),
                                                                                          Type=CONST(Company)));
                                                   CaptionML=[ENU=Contact Company Name;
                                                              RUS=Название организации контакта];
                                                   Editable=No }
    { 43  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Inter. Log Entry Comment Line" WHERE (Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Comment;
                                                              RUS=Комментарий];
                                                   Editable=No }
    { 44  ;   ;Opportunity No.     ;Code20        ;TableRelation=Opportunity;
                                                   CaptionML=[ENU=Opportunity No.;
                                                              RUS=Код возможности] }
    { 45  ;   ;Postponed           ;Boolean       ;CaptionML=[ENU=Postponed;
                                                              RUS=Отложен] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Contact Company No.,Contact No.,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Contact Company No.,Date,Contact No.,Canceled,Initiated By,Attempt Failed,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.);
                                                   MaintainSIFTIndex=No }
    {    ;Interaction Group Code,Date              }
    {    ;Interaction Group Code,Canceled,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Interaction Template Code,Date           }
    {    ;Interaction Template Code,Canceled,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Canceled,Campaign No.,Campaign Entry No.,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Campaign No.,Campaign Entry No.,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Salesperson Code,Date,Postponed         ;SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Canceled,Salesperson Code,Date,Postponed;SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Logged Segment Entry No.,Postponed       }
    {    ;Attachment No.                           }
    {    ;To-do No.,Date                           }
    {    ;Contact No.,Correspondence Type,E-Mail Logged,Subject,Postponed }
    {    ;Campaign No.,Campaign Target             }
    {    ;Campaign No.,Contact Company No.,Campaign Target,Postponed }
    {    ;Opportunity No.,Date                     }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Description,Contact No.,Date   }
    { 2   ;Brick               ;Salesperson Code,Description,Date,Contact Name,Contact Company Name }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 is marked %3.\;RUS=%1 %2 помечен %3.\';
      Text001@1001 : TextConst 'ENU=Do you wish to remove the checkmark?;RUS=Удалить пометку?';
      Text002@1002 : TextConst 'ENU=Do you wish to mark %1 %2 as %3?;RUS=Пометить %1 %2 как %3?';
      Text003@1003 : TextConst 'ENU=It is not possible to view sales statements after they have been printed.;RUS=Невозможно просмотреть состояние отчетов о продажах после того, как они были напечатаны.';
      Text004@1004 : TextConst 'ENU=It is not possible to show cover sheets after they have been printed.;RUS=Невозможно показать титульные листы после того, как они были напечатаны.';
      Text005@1005 : TextConst 'ENU=Do you wish to remove the checkmark from the selected %1 lines?;RUS=Удалить пометку в выбранных строках %1?';
      Text006@1006 : TextConst 'ENU=Do you wish to mark the selected %1 lines as %2?;RUS=Пометить выбранные строки %1 как %2?';
      Text009@1007 : TextConst 'ENU=Do you want to remove Attachment?;RUS=Удалить вложение?';
      Text010@1008 : TextConst 'ENU=Do you want to remove unique Attachments for the selected lines?;RUS=Удалить вложения для выбранных строк?';
      Text011@1009 : TextConst 'ENU=Very Positive,Positive,Neutral,Negative,Very Negative;RUS=Отлично,Хорошо,Нейтрально,Плохо,Очень плохо';

    PROCEDURE AssignNewOpportunity@15();
    VAR
      Opportunity@1000 : Record 5092;
    BEGIN
      TESTFIELD(Canceled,FALSE);
      TESTFIELD("Opportunity No.",'');
      Opportunity.CreateFromInteractionLogEntry(Rec);
      "Opportunity No." := Opportunity."No.";
      MODIFY;
    END;

    PROCEDURE CanCreateOpportunity@13() : Boolean;
    BEGIN
      EXIT(NOT Canceled AND ("Opportunity No." = ''));
    END;

    PROCEDURE CreateInteraction@10();
    VAR
      TempSegLine@1000 : TEMPORARY Record 5077;
    BEGIN
      TempSegLine.CreateInteractionFromIntLogEnt(Rec);
    END;

    PROCEDURE CreateTodo@8();
    VAR
      TempTodo@1000 : TEMPORARY Record 5080;
    BEGIN
      TempTodo.CreateToDoFromInteractLogEntry(Rec)
    END;

    PROCEDURE OpenAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
      SegLine@1001 : Record 5077;
      IStream@1003 : InStream;
      EmailMessageUrl@1002 : Text;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");

      IF Attachment."Storage Type" <> Attachment."Storage Type"::"Exchange Storage" THEN BEGIN
        SegLine."Contact No." := "Contact No.";
        SegLine."Salesperson Code" := "Salesperson Code";
        SegLine."Contact Alt. Address Code" := "Contact Alt. Address Code";
        SegLine.Date := Date;
        SegLine."Campaign No." := "Campaign No.";
        SegLine."Segment No." := "Segment No.";
        SegLine."Line No." := "Entry No.";
        SegLine.Description := Description;
        SegLine.Subject := Subject;
        SegLine."Language Code" := "Interaction Language Code";
        Attachment.ShowAttachment(SegLine,FORMAT("Entry No.") + ' ' + Description,FALSE);
      END ELSE BEGIN
        Attachment.CALCFIELDS("Email Message Url");
        IF Attachment."Email Message Url".HASVALUE THEN BEGIN
          Attachment."Email Message Url".CREATEINSTREAM(IStream);
          IStream.READ(EmailMessageUrl);
          HYPERLINK(EmailMessageUrl);
        END ELSE
          Attachment.DisplayInOutlook;
      END;
    END;

    PROCEDURE ToggleCanceledCheckmark@7();
    VAR
      ErrorTxt@1004 : Text[80];
      MasterCanceledCheckmark@1001 : Boolean;
      RemoveUniqueAttachment@1000 : Boolean;
    BEGIN
      IF FIND('-') THEN BEGIN
        IF ConfirmToggleCanceledCheckmark(COUNT,ErrorTxt) THEN BEGIN
          MasterCanceledCheckmark := NOT Canceled;
          IF FindUniqueAttachment AND MasterCanceledCheckmark THEN
            RemoveUniqueAttachment := CONFIRM(ErrorTxt,FALSE);
          SETCURRENTKEY("Entry No.");
          IF FIND('-') THEN
            REPEAT
              SetCanceledCheckmark(MasterCanceledCheckmark,RemoveUniqueAttachment);
            UNTIL NEXT = 0
        END;
      END
    END;

    PROCEDURE SetCanceledCheckmark@2(CanceledCheckmark@1003 : Boolean;RemoveUniqueAttachment@1004 : Boolean);
    VAR
      CampaignEntry@1000 : Record 5072;
      LoggedSeg@1001 : Record 5075;
      Attachment@1002 : Record 5062;
    BEGIN
      IF Canceled AND NOT CanceledCheckmark THEN BEGIN
        IF "Logged Segment Entry No." <> 0 THEN BEGIN
          LoggedSeg.GET("Logged Segment Entry No.");
          LoggedSeg.TESTFIELD(Canceled,FALSE);
        END;
        IF "Campaign Entry No." <> 0 THEN BEGIN
          CampaignEntry.GET("Campaign Entry No.");
          CampaignEntry.TESTFIELD(Canceled,FALSE);
        END;
      END;

      IF NOT Canceled AND CanceledCheckmark THEN
        IF UniqueAttachment AND RemoveUniqueAttachment THEN BEGIN
          IF Attachment.GET("Attachment No.") THEN
            Attachment.RemoveAttachment(FALSE);
          "Attachment No." := 0;
        END;

      Canceled := CanceledCheckmark;
      MODIFY;
    END;

    LOCAL PROCEDURE ConfirmToggleCanceledCheckmark@3(NumberOfSelectedLines@1000 : Integer;VAR ErrorTxt@1001 : Text[80]) : Boolean;
    BEGIN
      IF NumberOfSelectedLines = 1 THEN BEGIN
        ErrorTxt := Text009;
        IF Canceled THEN
          EXIT(CONFIRM(
              Text000 +
              Text001,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));

        EXIT(CONFIRM(
            Text002,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));
      END;
      ErrorTxt := Text010;
      IF Canceled THEN
        EXIT(CONFIRM(
            Text005,TRUE,TABLECAPTION));

      EXIT(CONFIRM(
          Text006,TRUE,TABLECAPTION,FIELDCAPTION(Canceled)));
    END;

    PROCEDURE UniqueAttachment@4() IsUnique@1000 : Boolean;
    VAR
      InteractLogEntry@1001 : Record 5065;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        InteractLogEntry.SETCURRENTKEY("Attachment No.");
        InteractLogEntry.SETRANGE("Attachment No.","Attachment No.");
        InteractLogEntry.SETFILTER("Entry No.",'<>%1',"Entry No.");
        IsUnique := NOT InteractLogEntry.FINDFIRST;
      END;
    END;

    LOCAL PROCEDURE FindUniqueAttachment@6() IsUnique@1000 : Boolean;
    BEGIN
      IF FIND('-') THEN
        REPEAT
          IsUnique := UniqueAttachment;
        UNTIL (NEXT = 0) OR IsUnique
    END;

    PROCEDURE ShowDocument@1();
    VAR
      SalesHeader@1000 : Record 36;
      SalesHeaderArchive@1011 : Record 5107;
      SalesInvHeader@1001 : Record 112;
      SalesShptHeader@1002 : Record 110;
      SalesCrMemoHeader@1003 : Record 114;
      IssuedReminderHeader@1004 : Record 297;
      PurchHeader@1005 : Record 38;
      PurchHeaderArchive@1012 : Record 5109;
      PurchInvHeader@1006 : Record 122;
      PurchRcptHeader@1007 : Record 120;
      PurchCrMemoHeader@1008 : Record 124;
      ServHeader@1009 : Record 5900;
      ReturnRcptHeader@1013 : Record 6660;
      IssuedFinChargeMemoHeader@1015 : Record 304;
      ReturnReceiptHeader@1014 : Record 6660;
      ReturnShipmentHeader@1016 : Record 6650;
      ServiceContractHeader@1017 : Record 5965;
    BEGIN
      CASE "Document Type" OF
        "Document Type"::"Sales Qte.":
          IF "Version No." <> 0 THEN BEGIN
            SalesHeaderArchive.GET(
              SalesHeaderArchive."Document Type"::Quote,"Document No.",
              "Doc. No. Occurrence","Version No.");
            SalesHeaderArchive.SETRANGE("Document Type",SalesHeaderArchive."Document Type"::Quote);
            SalesHeaderArchive.SETRANGE("No.","Document No.");
            SalesHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Sales Quote Archive",SalesHeaderArchive);
          END ELSE BEGIN
            SalesHeader.GET(SalesHeader."Document Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Sales Quote",SalesHeader);
          END;
        "Document Type"::"Sales Blnkt. Ord":
          BEGIN
            SalesHeader.GET(SalesHeader."Document Type"::"Blanket Order","Document No.");
            PAGE.RUN(PAGE::"Blanket Sales Order",SalesHeader);
          END;
        "Document Type"::"Sales Ord. Cnfrmn.":
          IF "Version No." <> 0 THEN BEGIN
            SalesHeaderArchive.GET(
              SalesHeaderArchive."Document Type"::Order,"Document No.",
              "Doc. No. Occurrence","Version No.");
            SalesHeaderArchive.SETRANGE("Document Type",SalesHeaderArchive."Document Type"::Order);
            SalesHeaderArchive.SETRANGE("No.","Document No.");
            SalesHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Sales Order Archive",SalesHeaderArchive);
          END ELSE BEGIN
            SalesHeader.GET(SalesHeader."Document Type"::Order,"Document No.");
            PAGE.RUN(PAGE::"Sales Order",SalesHeader);
          END;
        "Document Type"::"Sales Inv.":
          BEGIN
            SalesInvHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Sales Invoice",SalesInvHeader);
          END;
        "Document Type"::"Sales Shpt. Note":
          BEGIN
            SalesShptHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Sales Shipment",SalesShptHeader);
          END;
        "Document Type"::"Sales Cr. Memo":
          BEGIN
            SalesCrMemoHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Sales Credit Memo",SalesCrMemoHeader);
          END;
        "Document Type"::"Sales Stmnt.":
          ERROR(Text003);
        "Document Type"::"Sales Rmdr.":
          BEGIN
            IssuedReminderHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Issued Reminder",IssuedReminderHeader);
          END;
        "Document Type"::"Serv. Ord. Create":
          BEGIN
            ServHeader.GET(ServHeader."Document Type"::Order,"Document No.");
            PAGE.RUN(PAGE::"Service Order",ServHeader)
          END;
        "Document Type"::"Purch.Qte.":
          IF "Version No." <> 0 THEN BEGIN
            PurchHeaderArchive.GET(
              PurchHeaderArchive."Document Type"::Quote,"Document No.",
              "Doc. No. Occurrence","Version No.");
            PurchHeaderArchive.SETRANGE("Document Type",PurchHeaderArchive."Document Type"::Quote);
            PurchHeaderArchive.SETRANGE("No.","Document No.");
            PurchHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Purchase Quote Archive",PurchHeaderArchive);
          END ELSE BEGIN
            PurchHeader.GET(PurchHeader."Document Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Purchase Quote",PurchHeader);
          END;
        "Document Type"::"Purch. Blnkt. Ord.":
          BEGIN
            PurchHeader.GET(PurchHeader."Document Type"::"Blanket Order","Document No.");
            PAGE.RUN(PAGE::"Blanket Purchase Order",PurchHeader);
          END;
        "Document Type"::"Purch. Ord.":
          IF "Version No." <> 0 THEN BEGIN
            PurchHeaderArchive.GET(
              PurchHeaderArchive."Document Type"::Order,"Document No.",
              "Doc. No. Occurrence","Version No.");
            PurchHeaderArchive.SETRANGE("Document Type",PurchHeaderArchive."Document Type"::Order);
            PurchHeaderArchive.SETRANGE("No.","Document No.");
            PurchHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Purchase Order Archive",PurchHeaderArchive);
          END ELSE BEGIN
            PurchHeader.GET(PurchHeader."Document Type"::Order,"Document No.");
            PAGE.RUN(PAGE::"Purchase Order",PurchHeader);
          END;
        "Document Type"::"Purch. Inv.":
          BEGIN
            PurchInvHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Purchase Invoice",PurchInvHeader);
          END;
        "Document Type"::"Purch. Rcpt.":
          BEGIN
            PurchRcptHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Purchase Receipt",PurchRcptHeader);
          END;
        "Document Type"::"Purch. Cr. Memo":
          BEGIN
            PurchCrMemoHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Purchase Credit Memo",PurchCrMemoHeader);
          END;
        "Document Type"::"Cover Sheet":
          ERROR(Text004);
        "Document Type"::"Sales Return Order":
          IF SalesHeader.GET(SalesHeader."Document Type"::"Return Order","Document No.") THEN
            PAGE.RUN(PAGE::"Sales Return Order",SalesHeader)
          ELSE BEGIN
            ReturnRcptHeader.SETRANGE("Return Order No.","Document No.");
            PAGE.RUN(PAGE::"Posted Return Receipt",ReturnRcptHeader);
          END;
        "Document Type"::"Sales Finance Charge Memo":
          BEGIN
            IssuedFinChargeMemoHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Issued Finance Charge Memo",IssuedFinChargeMemoHeader);
          END;
        "Document Type"::"Sales Return Receipt":
          BEGIN
            ReturnReceiptHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Return Receipt",ReturnReceiptHeader);
          END;
        "Document Type"::"Purch. Return Shipment":
          BEGIN
            ReturnShipmentHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Return Shipment",ReturnShipmentHeader);
          END;
        "Document Type"::"Purch. Return Ord. Cnfrmn.":
          IF PurchHeader.GET(PurchHeader."Document Type"::"Return Order","Document No.") THEN
            PAGE.RUN(PAGE::"Purchase Return Order",PurchHeader)
          ELSE BEGIN
            ReturnShipmentHeader.SETRANGE("Return Order No.","Document No.");
            PAGE.RUN(PAGE::"Posted Return Shipment",ReturnShipmentHeader);
          END;
        "Document Type"::"Service Contract":
          BEGIN
            ServiceContractHeader.GET(ServiceContractHeader."Contract Type"::Contract,"Document No.");
            PAGE.RUN(PAGE::"Service Contract",ServiceContractHeader);
          END;
        "Document Type"::"Service Contract Quote":
          BEGIN
            ServiceContractHeader.GET(ServiceContractHeader."Contract Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Service Contract Quote",ServiceContractHeader);
          END;
        "Document Type"::"Service Quote":
          BEGIN
            ServHeader.GET(ServHeader."Document Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Service Quote",ServHeader);
          END;
      END;
    END;

    PROCEDURE EvaluateInteraction@9();
    VAR
      Selected@1000 : Integer;
    BEGIN
      IF FIND('-') THEN BEGIN
        Selected := DIALOG.STRMENU(Text011);
        IF Selected <> 0 THEN
          REPEAT
            Evaluation := Selected;
            MODIFY;
          UNTIL NEXT = 0
      END;
    END;

    PROCEDURE ResumeInteraction@11();
    VAR
      TempSegLine@1000 : TEMPORARY Record 5077;
      Cont@1006 : Record 5050;
      Salesperson@1005 : Record 13;
      Campaign@1004 : Record 5071;
      Todo@1003 : Record 5080;
      Opportunity@1002 : Record 5092;
      SegManagement@1001 : Codeunit 5051;
    BEGIN
      TempSegLine.DELETEALL;
      TempSegLine.INIT;
      SegManagement.CopyFieldsFromInteractLogEntry(TempSegLine,Rec);
      TempSegLine.VALIDATE(Date,WORKDATE);

      IF Todo.GET(GETFILTER("To-do No.")) THEN
        TempSegLine.SETRANGE("To-do No.",Todo."No.");
      IF Cont.GET(GETFILTER("Contact Company No.")) THEN
        TempSegLine.SETRANGE("Contact Company No.",Cont."No.");
      IF Cont.GET(GETFILTER("Contact No.")) THEN
        TempSegLine.SETRANGE("Contact No.",Cont."No.");
      IF Salesperson.GET(GETFILTER("Salesperson Code")) THEN
        TempSegLine.SETRANGE("Salesperson Code",Salesperson.Code);
      IF Campaign.GET(GETFILTER("Campaign No.")) THEN
        TempSegLine.SETRANGE("Campaign No.",Campaign."No.");
      IF Opportunity.GET(GETFILTER("Opportunity No.")) THEN
        TempSegLine.SETRANGE("Opportunity No.",Opportunity."No.");

      TempSegLine.StartWizard;
    END;

    BEGIN
    END.
  }
}

