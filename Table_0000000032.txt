OBJECT Table 32 Item Ledger Entry
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00,NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Ledger Entry;
               RUS=Книга операций по товарам];
    LookupPageID=Page38;
    DrillDownPageID=Page38;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              RUS=Номер операции] }
    { 2   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              RUS=Код товара] }
    { 3   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              RUS=Дата учета] }
    { 4   ;   ;Entry Type          ;Option        ;CaptionML=[ENU=Entry Type;
                                                              RUS=Тип операции];
                                                   OptionCaptionML=[ENU=Purchase,Sale,Positive Adjmt.,Negative Adjmt.,Transfer,Consumption,Output, ,Assembly Consumption,Assembly Output;
                                                                    RUS=Покупка,Продажа,Приход,Расход,Перемещение,Потребление,Выход,,Потребление при сборке,Выход при сборке];
                                                   OptionString=Purchase,Sale,Positive Adjmt.,Negative Adjmt.,Transfer,Consumption,Output, ,Assembly Consumption,Assembly Output }
    { 5   ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Customer)) Customer
                                                                 ELSE IF (Source Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Source Type=CONST(Item)) Item
                                                                 ELSE IF (Source Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   CaptionML=[ENU=Source No.;
                                                              RUS=Номер источника] }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              RUS=Номер документа] }
    { 7   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 8   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              RUS=Код склада] }
    { 12  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              RUS=Кол-во];
                                                   DecimalPlaces=0:5 }
    { 13  ;   ;Remaining Quantity  ;Decimal       ;CaptionML=[ENU=Remaining Quantity;
                                                              RUS=Остаток];
                                                   DecimalPlaces=0:5 }
    { 14  ;   ;Invoiced Quantity   ;Decimal       ;CaptionML=[ENU=Invoiced Quantity;
                                                              RUS=Кол-во по выст. счетам];
                                                   DecimalPlaces=0:5 }
    { 28  ;   ;Applies-to Entry    ;Integer       ;CaptionML=[ENU=Applies-to Entry;
                                                              RUS=Примен. к операции] }
    { 29  ;   ;Open                ;Boolean       ;CaptionML=[ENU=Open;
                                                              RUS=Открыто] }
    { 33  ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              RUS=Код глобального измерения 1];
                                                   CaptionClass='1,1,1' }
    { 34  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              RUS=Код глобального измерения 2];
                                                   CaptionClass='1,1,2' }
    { 36  ;   ;Positive            ;Boolean       ;CaptionML=[ENU=Positive;
                                                              RUS=Положит.] }
    { 41  ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              RUS=Тип источника];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Item,Fixed Asset";
                                                                    RUS=" ,Клиент,Поставщик,Товар,ОС"];
                                                   OptionString=[ ,Customer,Vendor,Item,Fixed Asset] }
    { 47  ;   ;Drop Shipment       ;Boolean       ;AccessByPermission=TableData 223=R;
                                                   CaptionML=[ENU=Drop Shipment;
                                                              RUS=Прямая поставка] }
    { 50  ;   ;Transaction Type    ;Code10        ;TableRelation="Transaction Type";
                                                   CaptionML=[ENU=Transaction Type;
                                                              RUS=Тип транзакции] }
    { 51  ;   ;Transport Method    ;Code10        ;TableRelation="Transport Method";
                                                   CaptionML=[ENU=Transport Method;
                                                              RUS=Метод перевозки] }
    { 52  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              RUS=Код страны/региона] }
    { 59  ;   ;Entry/Exit Point    ;Code10        ;TableRelation="Entry/Exit Point";
                                                   CaptionML=[ENU=Entry/Exit Point;
                                                              RUS=Пункт ввоза-вывоза] }
    { 60  ;   ;Document Date       ;Date          ;CaptionML=[ENU=Document Date;
                                                              RUS=Дата документа] }
    { 61  ;   ;External Document No.;Code35       ;CaptionML=[ENU=External Document No.;
                                                              RUS=Номер внешнего документа] }
    { 62  ;   ;Area                ;Code10        ;TableRelation=Area;
                                                   CaptionML=[ENU=Area;
                                                              RUS=Область] }
    { 63  ;   ;Transaction Specification;Code10   ;TableRelation="Transaction Specification";
                                                   CaptionML=[ENU=Transaction Specification;
                                                              RUS=Спецификация транзакции] }
    { 64  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              RUS=Серия номеров] }
    { 70  ;   ;Reserved Quantity   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Reservation Entry"."Quantity (Base)" WHERE (Source ID=CONST(),
                                                                                                                Source Ref. No.=FIELD(Entry No.),
                                                                                                                Source Type=CONST(32),
                                                                                                                Source Subtype=CONST(0),
                                                                                                                Source Batch Name=CONST(),
                                                                                                                Source Prod. Order Line=CONST(0),
                                                                                                                Reservation Status=CONST(Reservation)));
                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=[ENU=Reserved Quantity;
                                                              RUS=Зарезервированное кол-во];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 79  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              RUS=Тип документа];
                                                   OptionCaptionML=[ENU=" ,Sales Shipment,Sales Invoice,Sales Return Receipt,Sales Credit Memo,Purchase Receipt,Purchase Invoice,Purchase Return Shipment,Purchase Credit Memo,Transfer Shipment,Transfer Receipt,Service Shipment,Service Invoice,Service Credit Memo,Posted Assembly,Posted Disassembly,,,,,Item Receipt,Item Shipment,Direct Transfer";
                                                                    RUS=" ,Расх. накладная,Счет продажи,Возвр. прих. накладная продажи,Кредит-нота продажи,Прих. накладная,Счет покупки,возвр. расх. накладная покупки,Кредит-нота покупки,Расх. накл. на перемещение,Прих. накл. на перемещение,Сервисная расх. накладная,Сервисный счет,Сервисная кредит-нота,Разнесенная сборка,Разнесенный демонтаж,,,,,Акт оприходования товаров,Акт списания товаров,Прямое перемещение"];
                                                   OptionString=[ ,Sales Shipment,Sales Invoice,Sales Return Receipt,Sales Credit Memo,Purchase Receipt,Purchase Invoice,Purchase Return Shipment,Purchase Credit Memo,Transfer Shipment,Transfer Receipt,Service Shipment,Service Invoice,Service Credit Memo,Posted Assembly,Posted Disassembly,,,,,Item Receipt,Item Shipment,Direct Transfer] }
    { 80  ;   ;Document Line No.   ;Integer       ;CaptionML=[ENU=Document Line No.;
                                                              RUS=Номер строки документа] }
    { 90  ;   ;Order Type          ;Option        ;CaptionML=[ENU=Order Type;
                                                              RUS=Тип заказа];
                                                   OptionCaptionML=[ENU=" ,Production,Transfer,Service,Assembly";
                                                                    RUS=" ,Производство,Перемещение,Сервис,Сборка"];
                                                   OptionString=[ ,Production,Transfer,Service,Assembly];
                                                   Editable=No }
    { 91  ;   ;Order No.           ;Code20        ;CaptionML=[ENU=Order No.;
                                                              RUS=Номер заказа];
                                                   Editable=No }
    { 92  ;   ;Order Line No.      ;Integer       ;CaptionML=[ENU=Order Line No.;
                                                              RUS=Номер строки заказа];
                                                   Editable=No }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              RUS=Код набора измерений];
                                                   Editable=No }
    { 904 ;   ;Assemble to Order   ;Boolean       ;AccessByPermission=TableData 90=R;
                                                   CaptionML=[ENU=Assemble to Order;
                                                              RUS=Сборка для заказа] }
    { 1000;   ;Job No.             ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Job No.;
                                                              RUS=Код работы] }
    { 1001;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE (Job No.=FIELD(Job No.));
                                                   CaptionML=[ENU=Job Task No.;
                                                              RUS=Номер рабочего задания] }
    { 1002;   ;Job Purchase        ;Boolean       ;CaptionML=[ENU=Job Purchase;
                                                              RUS=Покупка работы] }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              RUS=Код варианта] }
    { 5404;   ;Qty. per Unit of Measure;Decimal   ;CaptionML=[ENU=Qty. per Unit of Measure;
                                                              RUS=Кол-во в единице измерения];
                                                   DecimalPlaces=0:5 }
    { 5407;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              RUS=Код единицы измерения] }
    { 5408;   ;Derived from Blanket Order;Boolean ;CaptionML=[ENU=Derived from Blanket Order;
                                                              RUS=Выделено из общего заказа] }
    { 5700;   ;Cross-Reference No. ;Code20        ;CaptionML=[ENU=Cross-Reference No.;
                                                              RUS=Номер перекрестной ссылки] }
    { 5701;   ;Originally Ordered No.;Code20      ;TableRelation=Item;
                                                   AccessByPermission=TableData 5715=R;
                                                   CaptionML=[ENU=Originally Ordered No.;
                                                              RUS=Первоначальный заказанный номер] }
    { 5702;   ;Originally Ordered Var. Code;Code10;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Originally Ordered No.));
                                                   AccessByPermission=TableData 5715=R;
                                                   CaptionML=[ENU=Originally Ordered Var. Code;
                                                              RUS=Первоначальный заказанный код варианта] }
    { 5703;   ;Out-of-Stock Substitution;Boolean  ;CaptionML=[ENU=Out-of-Stock Substitution;
                                                              RUS=Замена из-за отсутствия] }
    { 5704;   ;Item Category Code  ;Code20        ;TableRelation="Item Category";
                                                   CaptionML=[ENU=Item Category Code;
                                                              RUS=Код товарной категории] }
    { 5705;   ;Nonstock            ;Boolean       ;CaptionML=[ENU=Nonstock;
                                                              RUS=Нескладируемый] }
    { 5706;   ;Purchasing Code     ;Code10        ;TableRelation=Purchasing;
                                                   CaptionML=[ENU=Purchasing Code;
                                                              RUS=Код закупки] }
    { 5707;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE (Item Category Code=FIELD(Item Category Code));
                                                   CaptionML=[ENU=Product Group Code;
                                                              RUS=Код товарной группы] }
    { 5800;   ;Completely Invoiced ;Boolean       ;CaptionML=[ENU=Completely Invoiced;
                                                              RUS=Счет выставлен полностью] }
    { 5801;   ;Last Invoice Date   ;Date          ;CaptionML=[ENU=Last Invoice Date;
                                                              RUS=Дата последнего счета] }
    { 5802;   ;Applied Entry to Adjust;Boolean    ;CaptionML=[ENU=Applied Entry to Adjust;
                                                              RUS=Операция скорректирована] }
    { 5803;   ;Cost Amount (Expected);Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Cost Amount (Expected)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Cost Amount (Expected);
                                                              RUS=Сумма себестоимости (ожид.)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5804;   ;Cost Amount (Actual);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Cost Amount (Actual)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Cost Amount (Actual);
                                                              RUS=Сумма себестоимости (факт.)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5805;   ;Cost Amount (Non-Invtbl.);Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Cost Amount (Non-Invtbl.)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Cost Amount (Non-Invtbl.);
                                                              RUS=Сумма себестоимости (непроизводственная)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5806;   ;Cost Amount (Expected) (ACY);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Cost Amount (Expected) (ACY)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Cost Amount (Expected) (ACY);
                                                              RUS=Сумма себестоимости (ожид.) (ДОВ)];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 5807;   ;Cost Amount (Actual) (ACY);Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Cost Amount (Actual) (ACY)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Cost Amount (Actual) (ACY);
                                                              RUS=Сумма себестоимости (факт.) (ДОВ)];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 5808;   ;Cost Amount (Non-Invtbl.)(ACY);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Cost Amount (Non-Invtbl.)(ACY)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Cost Amount (Non-Invtbl.)(ACY);
                                                              RUS=Сумма себестоимости (непроизводственная) (ДОВ)];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 5813;   ;Purchase Amount (Expected);Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Purchase Amount (Expected)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Purchase Amount (Expected);
                                                              RUS=Сумма покупки (ожид.)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5814;   ;Purchase Amount (Actual);Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Purchase Amount (Actual)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Purchase Amount (Actual);
                                                              RUS=Сумма покупки (факт.)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5815;   ;Sales Amount (Expected);Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Sales Amount (Expected)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Sales Amount (Expected);
                                                              RUS=Сумма продаж (ожид.)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5816;   ;Sales Amount (Actual);Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Sales Amount (Actual)" WHERE (Item Ledger Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Sales Amount (Actual);
                                                              RUS=Сумма продаж (факт.)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5817;   ;Correction          ;Boolean       ;CaptionML=[ENU=Correction;
                                                              RUS=Корректировка] }
    { 5818;   ;Shipped Qty. Not Returned;Decimal  ;AccessByPermission=TableData 36=R;
                                                   CaptionML=[ENU=Shipped Qty. Not Returned;
                                                              RUS=Невозвращенное отгруженное кол-во];
                                                   DecimalPlaces=0:5 }
    { 5833;   ;Prod. Order Comp. Line No.;Integer ;AccessByPermission=TableData 5405=R;
                                                   CaptionML=[ENU=Prod. Order Comp. Line No.;
                                                              RUS=Номер строки произв. заказа компонента] }
    { 6500;   ;Serial No.          ;Code20        ;OnLookup=BEGIN
                                                              ItemTrackingMgt.LookupLotSerialNoInfo("Item No.","Variant Code",0,"Serial No.");
                                                            END;

                                                   CaptionML=[ENU=Serial No.;
                                                              RUS=Серийный номер] }
    { 6501;   ;Lot No.             ;Code20        ;OnLookup=BEGIN
                                                              ItemTrackingMgt.LookupLotSerialNoInfo("Item No.","Variant Code",1,"Lot No.");
                                                            END;

                                                   CaptionML=[ENU=Lot No.;
                                                              RUS=Номер партии] }
    { 6502;   ;Warranty Date       ;Date          ;CaptionML=[ENU=Warranty Date;
                                                              RUS=Дата гарантии] }
    { 6503;   ;Expiration Date     ;Date          ;CaptionML=[ENU=Expiration Date;
                                                              RUS=Годен до] }
    { 6510;   ;Item Tracking       ;Option        ;CaptionML=[ENU=Item Tracking;
                                                              RUS=Трассировка товаров];
                                                   OptionCaptionML=[ENU=None,Lot No.,Lot and Serial No.,Serial No.,CD No.,Lot and CD No.,Serial and CD No.,Lot and Serial and CD No.;
                                                                    RUS=Нет,Номер партии,Номер партии и серийный номер,Серийный номер,Номер ДТ,Номер партии и ДТ,Серийный номер и номер ДТ,Номер партии плюс серийный номер и номер ДТ];
                                                   OptionString=None,Lot No.,Lot and Serial No.,Serial No.,CD No.,Lot and CD No.,Serial and CD No.,Lot and Serial and CD No.;
                                                   Editable=No }
    { 6602;   ;Return Reason Code  ;Code10        ;TableRelation="Return Reason";
                                                   CaptionML=[ENU=Return Reason Code;
                                                              RUS=Код причины возврата] }
    { 12420;  ;CD No.              ;Code30        ;OnLookup=BEGIN
                                                              ItemTrackingMgt.LookupLotSerialNoInfo("Item No.","Variant Code",2,"CD No.");
                                                            END;

                                                   CaptionML=[ENU=CD No.;
                                                              RUS=Номер ДТ] }
    { 12450;  ;FA No.              ;Code20        ;TableRelation="Fixed Asset";
                                                   CaptionML=[ENU=FA No.;
                                                              RUS=Номер ОС] }
    { 12451;  ;FA Entry No.        ;Integer       ;TableRelation="FA Ledger Entry" WHERE (Entry No.=FIELD(FA Entry No.));
                                                   CaptionML=[ENU=FA Entry No.;
                                                              RUS=Номер операции ОС] }
    { 12452;  ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   CaptionML=[ENU=Depreciation Book Code;
                                                              RUS=Код книги амортизации] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Item No.                                ;SumIndexFields=Invoiced Quantity }
    {    ;Item No.,Posting Date                    }
    {    ;Item No.,Entry Type,Variant Code,Drop Shipment,Location Code,Posting Date;
                                                   SumIndexFields=Quantity,Invoiced Quantity }
    {    ;Source Type,Source No.,Item No.,Variant Code,Posting Date;
                                                   SumIndexFields=Quantity }
    {    ;Item No.,Open,Variant Code,Positive,Location Code,Posting Date;
                                                   SumIndexFields=Quantity,Remaining Quantity }
    {    ;Item No.,Open,Variant Code,Positive,Location Code,Posting Date,Expiration Date,Lot No.,Serial No.,CD No.;
                                                   SumIndexFields=Quantity,Remaining Quantity }
    {    ;Country/Region Code,Entry Type,Posting Date }
    {    ;Document No.,Document Type,Document Line No. }
    { No ;Item No.,Entry Type,Variant Code,Drop Shipment,Global Dimension 1 Code,Global Dimension 2 Code,Location Code,Posting Date;
                                                   SumIndexFields=Quantity,Invoiced Quantity }
    { No ;Source Type,Source No.,Global Dimension 1 Code,Global Dimension 2 Code,Item No.,Variant Code,Posting Date;
                                                   SumIndexFields=Quantity }
    {    ;Order Type,Order No.,Order Line No.,Entry Type,Prod. Order Comp. Line No.;
                                                   SumIndexFields=Quantity;
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Applied Entry to Adjust         }
    {    ;Item No.,Positive,Location Code,Variant Code }
    { No ;Entry Type,Nonstock,Item No.,Posting Date }
    {    ;Item No.,Location Code,Open,Variant Code,Unit of Measure Code,Lot No.,Serial No.,CD No.;
                                                   SumIndexFields=Remaining Quantity }
    {    ;Item No.,Open,Variant Code,Positive,Lot No.,Serial No.,CD No.;
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Open,Variant Code,Location Code,Item Tracking,Lot No.,Serial No.,CD No.;
                                                   SumIndexFields=Remaining Quantity;
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    { No ;Lot No.                                  }
    { No ;Serial No.                               }
    {    ;CD No.                                   }
    {    ;Document No.,Posting Date                }
    {    ;Item No.,Entry Type,Variant Code,CD No.,Location Code;
                                                   SumIndexFields=Quantity }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Description,Item No.,Posting Date,Entry Type,Document No. }
  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      ReservEntry@1001 : Record 337;
      ReservEngineMgt@1002 : Codeunit 99000831;
      ReserveItemLedgEntry@1003 : Codeunit 99000841;
      ItemTrackingMgt@1006 : Codeunit 6500;
      GLSetupRead@1005 : Boolean;
      IsNotOnInventoryErr@1004 : TextConst 'ENU=You have insufficient quantity of Item %1 on inventory.;RUS=В запасах имеется недостаточное количество номенклатуры %1.';

    LOCAL PROCEDURE GetCurrencyCode@1() : Code[10];
    BEGIN
      IF NOT GLSetupRead THEN BEGIN
        GLSetup.GET;
        GLSetupRead := TRUE;
      END;
      EXIT(GLSetup."Additional Reporting Currency");
    END;

    PROCEDURE ShowReservationEntries@2(Modal@1000 : Boolean);
    BEGIN
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,TRUE);
      ReserveItemLedgEntry.FilterReservFor(ReservEntry,Rec);
      IF Modal THEN
        PAGE.RUNMODAL(PAGE::"Reservation Entries",ReservEntry)
      ELSE
        PAGE.RUN(PAGE::"Reservation Entries",ReservEntry);
    END;

    PROCEDURE SetAppliedEntryToAdjust@66(AppliedEntryToAdjust@1001 : Boolean);
    BEGIN
      IF "Applied Entry to Adjust" <> AppliedEntryToAdjust THEN BEGIN
        "Applied Entry to Adjust" := AppliedEntryToAdjust;
        MODIFY;
      END;
    END;

    PROCEDURE SetAvgTransCompletelyInvoiced@80() : Boolean;
    VAR
      ItemApplnEntry@1005 : Record 339;
      InbndItemLedgEntry@1004 : Record 32;
      CompletelyInvoiced@1002 : Boolean;
    BEGIN
      IF "Entry Type" <> "Entry Type"::Transfer THEN
        EXIT(FALSE);

      ItemApplnEntry.SETCURRENTKEY("Item Ledger Entry No.");
      ItemApplnEntry.SETRANGE("Item Ledger Entry No.","Entry No.");
      ItemApplnEntry.FIND('-');
      IF NOT "Completely Invoiced" THEN BEGIN
        CompletelyInvoiced := TRUE;
        REPEAT
          InbndItemLedgEntry.GET(ItemApplnEntry."Inbound Item Entry No.");
          IF NOT InbndItemLedgEntry."Completely Invoiced" THEN
            CompletelyInvoiced := FALSE;
        UNTIL ItemApplnEntry.NEXT = 0;

        IF CompletelyInvoiced THEN BEGIN
          SetCompletelyInvoiced;
          EXIT(TRUE);
        END;
      END;
      EXIT(FALSE);
    END;

    PROCEDURE SetCompletelyInvoiced@3();
    BEGIN
      IF NOT "Completely Invoiced" THEN BEGIN
        "Completely Invoiced" := TRUE;
        MODIFY;
      END;
    END;

    PROCEDURE AppliedEntryToAdjustExists@40(ItemNo@1001 : Code[20]) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Item No.","Applied Entry to Adjust");
      SETRANGE("Item No.",ItemNo);
      SETRANGE("Applied Entry to Adjust",TRUE);
      EXIT(FIND('-'));
    END;

    PROCEDURE IsOutbndConsump@57() : Boolean;
    BEGIN
      EXIT(("Entry Type" = "Entry Type"::Consumption) AND NOT Positive);
    END;

    PROCEDURE IsExactCostReversingPurchase@59() : Boolean;
    BEGIN
      EXIT(
        ("Applies-to Entry" <> 0) AND
        ("Entry Type" = "Entry Type"::Purchase) AND
        ("Invoiced Quantity" < 0));
    END;

    PROCEDURE IsExactCostReversingOutput@8() : Boolean;
    BEGIN
      EXIT(
        ("Applies-to Entry" <> 0) AND
        ("Entry Type" IN ["Entry Type"::Output,"Entry Type"::"Assembly Output"]) AND
        ("Invoiced Quantity" < 0));
    END;

    PROCEDURE UpdateItemTracking@5();
    VAR
      ItemTrackingMgt@1000 : Codeunit 6500;
    BEGIN
      "Item Tracking" := ItemTrackingMgt.ItemTrackingOption("Lot No.","Serial No.","CD No.");
    END;

    PROCEDURE GetUnitCostLCY@4() : Decimal;
    BEGIN
      IF Quantity = 0 THEN
        EXIT("Cost Amount (Actual)");

      EXIT(ROUND("Cost Amount (Actual)" / Quantity,0.00001));
    END;

    PROCEDURE FilterLinesWithItemToPlan@69(VAR Item@1000 : Record 27;NetChange@1001 : Boolean);
    BEGIN
      RESET;
      SETCURRENTKEY("Item No.",Open,"Variant Code",Positive,"Location Code","Posting Date");
      SETRANGE("Item No.",Item."No.");
      SETRANGE(Open,TRUE);
      SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      SETFILTER("Global Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      SETFILTER("Global Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
      IF NetChange THEN
        SETFILTER("Posting Date",Item.GETFILTER("Date Filter"));
    END;

    PROCEDURE FindLinesWithItemToPlan@7(VAR Item@1000 : Record 27;NetChange@1001 : Boolean) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item,NetChange);
      EXIT(FIND('-'));
    END;

    PROCEDURE LinesWithItemToPlanExist@67(VAR Item@1000 : Record 27;NetChange@1001 : Boolean) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item,NetChange);
      EXIT(NOT ISEMPTY);
    END;

    PROCEDURE IsOutbndSale@5888() : Boolean;
    BEGIN
      EXIT(("Entry Type" = "Entry Type"::Sale) AND NOT Positive);
    END;

    PROCEDURE ShowDimensions@6();
    VAR
      DimMgt@1000 : Codeunit 408;
    BEGIN
      DimMgt.ShowDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2',TABLECAPTION,"Entry No."));
    END;

    PROCEDURE CalculateRemQuantity@10(ItemLedgEntryNo@1000 : Integer;PostingDate@1001 : Date) : Decimal;
    VAR
      ItemApplnEntry@1002 : Record 339;
      RemQty@1003 : Decimal;
    BEGIN
      ItemApplnEntry.SETCURRENTKEY("Inbound Item Entry No.");
      ItemApplnEntry.SETRANGE("Inbound Item Entry No.",ItemLedgEntryNo);
      RemQty := 0;
      IF ItemApplnEntry.FINDSET THEN
        REPEAT
          IF ItemApplnEntry."Posting Date" <= PostingDate THEN
            RemQty += ItemApplnEntry.Quantity;
        UNTIL ItemApplnEntry.NEXT = 0;
      EXIT(RemQty);
    END;

    PROCEDURE VerifyOnInventory@9();
    VAR
      Item@1000 : Record 27;
    BEGIN
      IF NOT Open THEN
        EXIT;
      IF Quantity >= 0 THEN
        EXIT;
      CASE "Entry Type" OF
        "Entry Type"::Consumption,"Entry Type"::"Assembly Consumption","Entry Type"::Transfer:
          ERROR(IsNotOnInventoryErr,"Item No.");
        ELSE BEGIN
          Item.GET("Item No.");
          IF Item.PreventNegativeInventory THEN
            ERROR(IsNotOnInventoryErr,"Item No.");
        END;
      END;
    END;

    PROCEDURE CalculateRemInventoryValue@12(ItemLedgEntryNo@1000 : Integer;ItemLedgEntryQty@1004 : Decimal;RemQty@1005 : Decimal;IncludeExpectedCost@1001 : Boolean;PostingDate@1006 : Date) : Decimal;
    VAR
      ValueEntry@1002 : Record 5802;
      AdjustedCost@1003 : Decimal;
      TotalQty@1007 : Decimal;
    BEGIN
      ValueEntry.SETCURRENTKEY("Item Ledger Entry No.");
      ValueEntry.SETRANGE("Item Ledger Entry No.",ItemLedgEntryNo);
      ValueEntry.SETFILTER("Valuation Date",'<=%1',PostingDate);
      IF NOT IncludeExpectedCost THEN
        ValueEntry.SETRANGE("Expected Cost",FALSE);
      IF ValueEntry.FINDSET THEN
        REPEAT
          IF ValueEntry."Entry Type" = ValueEntry."Entry Type"::Revaluation THEN
            TotalQty := ValueEntry."Valued Quantity"
          ELSE
            TotalQty := ItemLedgEntryQty;
          IF ValueEntry."Entry Type" <> ValueEntry."Entry Type"::Rounding THEN
            IF IncludeExpectedCost THEN
              AdjustedCost += RemQty / TotalQty * (ValueEntry."Cost Amount (Actual)" + ValueEntry."Cost Amount (Expected)")
            ELSE
              AdjustedCost += RemQty / TotalQty * ValueEntry."Cost Amount (Actual)";
        UNTIL ValueEntry.NEXT = 0;
      EXIT(AdjustedCost);
    END;

    PROCEDURE TrackingExists@11() : Boolean;
    BEGIN
      EXIT(("Serial No." <> '') OR ("Lot No." <> '') OR ("CD No." <> ''));
    END;

    PROCEDURE IsRedStorno@1210002() RedStorno : Boolean;
    VAR
      ValueEntry@1210000 : Record 5802;
    BEGIN
      ValueEntry.SETCURRENTKEY("Item Ledger Entry No.","Entry Type");
      ValueEntry.SETRANGE("Item Ledger Entry No.","Entry No.");
      IF ValueEntry.FINDFIRST THEN
        EXIT(ValueEntry."Red Storno");

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

