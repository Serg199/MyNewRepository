OBJECT Codeunit 17352 Person\Vendor Update
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVRU6.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 23=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Vendor@1000 : Record 23;
      HRSetup@1210000 : Record 5218;

    PROCEDURE PersonToVendor@1(OldPerson@1210001 : Record 17350;Person@1210000 : Record 17350);
    BEGIN
      IF (Person."Vendor No." <> '') AND
         ((OldPerson."Vendor No." <> Person."Vendor No.") OR
          (OldPerson."First Name" <> Person."First Name") OR
          (OldPerson."Middle Name" <> Person."Middle Name") OR
          (OldPerson."Last Name" <> Person."Last Name"))
      THEN
        VendorUpdate(Person)
      ELSE
        EXIT;
    END;

    PROCEDURE VendorUpdate@4(Person@1210000 : Record 17350);
    BEGIN
      Vendor.GET(Person."Vendor No.");
      Vendor.Name := COPYSTR(Person."Full Name",1,MAXSTRLEN(Vendor.Name));
      Vendor.MODIFY
    END;

    PROCEDURE CreateVendor@1210009(VAR Person@1210001 : Record 17350);
    VAR
      NewVendor@1210000 : Record 23;
    BEGIN
      HRSetup.GET;
      HRSetup.TESTFIELD("Person Vendor No. Series");

      Person.TESTFIELD("Vendor No.",'');

      NewVendor.INIT;
      NewVendor."No. Series" := HRSetup."Person Vendor No. Series";
      NewVendor.INSERT(TRUE);
      NewVendor.Name := COPYSTR(Person."Full Name",1,MAXSTRLEN(NewVendor.Name));
      NewVendor."Vendor Type" := NewVendor."Vendor Type"::Person;
      NewVendor."Vendor Posting Group" := HRSetup."Person Vendor Posting Group";
      NewVendor."Gen. Bus. Posting Group" := HRSetup."Pers. Vend.Gen.Bus. Posting Gr";
      NewVendor."VAT Bus. Posting Group" := HRSetup."Pers. Vend.VAT Bus. Posting Gr";
      NewVendor."VAT Registration No." := Person."VAT Registration No.";
      NewVendor.MODIFY;

      Person."Vendor No." := NewVendor."No.";
      Person.MODIFY;
    END;

    BEGIN
    END.
  }
}

