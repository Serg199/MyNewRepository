OBJECT Table 17429 Payroll Calendar Setup
{
  OBJECT-PROPERTIES
  {
    Date=09.09.14;
    Time=12:00:00;
    Version List=NAVRU8.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Calendar Code;
    CaptionML=[ENU=Payroll Calendar Setup;
               RUS=Настройка зарплатного календаря];
  }
  FIELDS
  {
    { 1   ;   ;Calendar Code       ;Code10        ;TableRelation="Payroll Calendar";
                                                   CaptionML=[ENU=Calendar Code;
                                                              RUS=Код календаря];
                                                   Editable=No }
    { 2   ;   ;Year                ;Integer       ;OnValidate=BEGIN
                                                                VALIDATE("Period No.");
                                                              END;

                                                   CaptionML=[ENU=Year;
                                                              RUS=Год];
                                                   MinValue=0;
                                                   MaxValue=9999;
                                                   BlankZero=Yes }
    { 3   ;   ;Period Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Period Type" <> xRec."Period Type" THEN BEGIN
                                                                  "Period No." := 0;
                                                                  "Period Name" := '';
                                                                  "Week Day" := 0;
                                                                  Description := '';
                                                                  Nonworking := FALSE;
                                                                  "Work Hours" := 0;
                                                                  "Night Hours" := 0;
                                                                  "Starting Time" := 0T;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Period Type;
                                                              RUS=Тип периода];
                                                   OptionCaptionML=[ENU=" ,Week,Month,Shift";
                                                                    RUS=" ,Неделя,Месяц,Смена"];
                                                   OptionString=[ ,Week,Month,Shift] }
    { 4   ;   ;Period No.          ;Integer       ;OnValidate=BEGIN
                                                                CASE "Period Type" OF
                                                                  "Period Type"::Week:
                                                                    IF "Period No." > 0 THEN
                                                                      IF "Period No." <= 53 THEN
                                                                        "Period Name" := FORMAT("Period No.")
                                                                      ELSE
                                                                        ERROR(Text000,FIELDCAPTION("Period No."),52)
                                                                    ELSE
                                                                      "Period Name" := '';
                                                                  "Period Type"::Month:
                                                                    CASE "Period No." OF
                                                                      0:
                                                                        "Period Name" := '';
                                                                      1..12:
                                                                        "Period Name" := LocMgt.GetMonthName(DMY2DATE(1,"Period No.",2000),FALSE);
                                                                      ELSE
                                                                        ERROR(Text000,FIELDCAPTION("Period No."),12);
                                                                    END;
                                                                END;
                                                                VALIDATE("Week Day");
                                                              END;

                                                   CaptionML=[ENU=Period No.;
                                                              RUS=Номер периода];
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 5   ;   ;Period Name         ;Text30        ;CaptionML=[ENU=Period Name;
                                                              RUS=Название периода];
                                                   Editable=No }
    { 6   ;   ;Day No.             ;Integer       ;OnValidate=BEGIN
                                                                CASE "Period Type" OF
                                                                  "Period Type"::Week:
                                                                    CASE "Day No." OF
                                                                      0:
                                                                        Description := '';
                                                                      1..7:
                                                                        BEGIN
                                                                          "Week Day" := "Day No.";
                                                                          Description := FORMAT("Week Day");
                                                                        END
                                                                      ELSE
                                                                        ERROR(Text002,FORMAT("Period Type"::Week));
                                                                    END;
                                                                  "Period Type"::Month:
                                                                    BEGIN
                                                                      IF Year <> 0 THEN
                                                                        Date := DMY2DATE("Day No.","Period No.",Year)
                                                                      ELSE
                                                                        CASE "Period No." OF
                                                                          1,3,5,7,8,10,12:
                                                                            IF "Day No." > 31 THEN
                                                                              ERROR(Text002,FORMAT("Period Type"::Month));
                                                                          2:
                                                                            IF "Day No." > 29 THEN
                                                                              ERROR(Text002,FORMAT("Period Type"::Month));
                                                                          4,6,9,11:
                                                                            IF "Day No." > 30 THEN
                                                                              ERROR(Text002,FORMAT("Period Type"::Month));
                                                                        END;
                                                                      Description := '';
                                                                      IF ("Day No." <> 0) AND ("Period No." > 0) AND ("Period No." <= 12) THEN
                                                                        Description := FORMAT("Day No.") + ' ' +
                                                                          LocMgt.GetMonthName(DMY2DATE(1,"Period No.",2000),TRUE);
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Day No.;
                                                              RUS=Номер дня];
                                                   MinValue=1 }
    { 7   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 8   ;   ;Nonworking          ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF Nonworking THEN BEGIN
                                                                  "Work Hours" := 0;
                                                                  "Starting Time" := 0T;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Nonworking;
                                                              RUS=Нерабочий] }
    { 9   ;   ;Starting Time       ;Time          ;CaptionML=[ENU=Starting Time;
                                                              RUS=Время начала];
                                                   BlankNumbers=BlankZero }
    { 10  ;   ;Work Hours          ;Decimal       ;OnValidate=BEGIN
                                                                IF "Work Hours" > 0 THEN
                                                                  Nonworking := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Work Hours;
                                                              RUS=Рабочие часы];
                                                   MinValue=0;
                                                   MaxValue=24;
                                                   BlankZero=Yes }
    { 11  ;   ;Week Day            ;Option        ;OnValidate=BEGIN
                                                                UpdateDayName;
                                                              END;

                                                   CaptionML=[ENU=Week Day;
                                                              RUS=День недели];
                                                   OptionCaptionML=[ENU=" ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday";
                                                                    RUS=" ,Понедельник,Вторник,Среда,Четверг,Пятница,Суббота,Воскресенье"];
                                                   OptionString=[ ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday] }
    { 12  ;   ;Day Status          ;Option        ;CaptionML=[ENU=Day Status;
                                                              RUS=Статус дня];
                                                   OptionCaptionML=[ENU=" ,Weekend,Holiday";
                                                                    RUS=" ,Выходной,Праздник"];
                                                   OptionString=[ ,Weekend,Holiday] }
    { 13  ;   ;Time Activity Code  ;Code10        ;TableRelation="Time Activity";
                                                   CaptionML=[ENU=Time Activity Code;
                                                              RUS=Код временной активности] }
    { 14  ;   ;Night Hours         ;Decimal       ;OnValidate=BEGIN
                                                                GetCalendar;
                                                                IF PayrollCalendar."Shift Days" = 0 THEN
                                                                  ERROR(Text001,FIELDCAPTION("Night Hours"));
                                                              END;

                                                   CaptionML=[ENU=Night Hours;
                                                              RUS=Ночные часы] }
  }
  KEYS
  {
    {    ;Calendar Code,Year,Period Type,Period No.,Day No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210004 : TextConst 'ENU=%1 can not be greater than %2.;RUS=%1 не может превышать %2.';
      Text001@1210001 : TextConst 'ENU=You can enter %1 in shift calendar only.;RUS=%1 можно ввести только в календарь смен.';
      Text002@1210002 : TextConst 'ENU=Wrong day of the %1.;RUS=Неверный день %1.';
      PayrollCalendar@1210000 : Record 17428;
      LocMgt@1210003 : Codeunit 12400;
      Date@1210005 : Date;

    PROCEDURE UpdateDayName@1();
    BEGIN
      CASE "Period Type" OF
        "Period Type"::Month:
          "Week Day" := "Week Day"::" ";
        "Period Type"::Week:
          "Week Day" := "Day No.";
      END;
    END;

    PROCEDURE GetCalendar@1210003();
    BEGIN
      TESTFIELD("Calendar Code");
      IF "Calendar Code" <> PayrollCalendar.Code THEN
        PayrollCalendar.GET("Calendar Code");
    END;

    PROCEDURE GetMaxShiftDay@1210000(CalendarCode@1210000 : Code[10]) : Integer;
    VAR
      PayrollCalendarSetup@1210001 : Record 17429;
      MaxShiftDay@1210002 : Integer;
    BEGIN
      PayrollCalendarSetup.SETRANGE("Calendar Code",CalendarCode);

      MaxShiftDay := 0;
      IF PayrollCalendarSetup.FINDSET THEN
        REPEAT
          IF PayrollCalendarSetup."Day No." > MaxShiftDay THEN
            MaxShiftDay := PayrollCalendarSetup."Day No.";
        UNTIL PayrollCalendarSetup.NEXT = 0;

      EXIT(MaxShiftDay);
    END;

    BEGIN
    END.
  }
}

