OBJECT Table 843 Cash Flow Setup
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1261=rimd;
    CaptionML=[ENU=Cash Flow Setup;
               RUS=Настройка движения денежных средств];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=[ENU=Primary Key;
                                                              RUS=Первичный ключ] }
    { 2   ;   ;Cash Flow Forecast No. Series;Code10;
                                                   TableRelation="No. Series";
                                                   CaptionML=[ENU=Cash Flow Forecast No. Series;
                                                              RUS=Серия номеров прогноза движения денежных средств] }
    { 3   ;   ;Receivables CF Account No.;Code20  ;TableRelation="Cash Flow Account";
                                                   OnValidate=BEGIN
                                                                CheckAccType("Receivables CF Account No.");
                                                              END;

                                                   CaptionML=[ENU=Receivables CF Account No.;
                                                              RUS=Номер счета движения денежных средств по расчетам с клиентами] }
    { 4   ;   ;Payables CF Account No.;Code20     ;TableRelation="Cash Flow Account";
                                                   OnValidate=BEGIN
                                                                CheckAccType("Payables CF Account No.");
                                                              END;

                                                   CaptionML=[ENU=Payables CF Account No.;
                                                              RUS=Номер счета движения денежных средств по расчетам с поставщиками] }
    { 5   ;   ;Sales Order CF Account No.;Code20  ;TableRelation="Cash Flow Account";
                                                   OnValidate=BEGIN
                                                                CheckAccType("Sales Order CF Account No.");
                                                              END;

                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=[ENU=Sales Order CF Account No.;
                                                              RUS=Номер счета движения денежных средств по заказам на продажу] }
    { 6   ;   ;Purch. Order CF Account No.;Code20 ;TableRelation="Cash Flow Account";
                                                   OnValidate=BEGIN
                                                                CheckAccType("Purch. Order CF Account No.");
                                                              END;

                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=[ENU=Purch. Order CF Account No.;
                                                              RUS=Номер счета движения денежных средств по заказам на покупку] }
    { 8   ;   ;FA Budget CF Account No.;Code20    ;TableRelation="Cash Flow Account";
                                                   OnValidate=BEGIN
                                                                CheckAccType("FA Budget CF Account No.");
                                                              END;

                                                   AccessByPermission=TableData 5600=R;
                                                   CaptionML=[ENU=FA Budget CF Account No.;
                                                              RUS=Номер счета движения денежных средств по бюджету ОС] }
    { 9   ;   ;FA Disposal CF Account No.;Code20  ;TableRelation="Cash Flow Account";
                                                   OnValidate=BEGIN
                                                                CheckAccType("FA Disposal CF Account No.");
                                                              END;

                                                   AccessByPermission=TableData 5600=R;
                                                   CaptionML=[ENU=FA Disposal CF Account No.;
                                                              RUS=Номер счета движения денежных средств по выбытию ОС] }
    { 10  ;   ;Service CF Account No.;Code20      ;TableRelation="Cash Flow Account";
                                                   OnValidate=BEGIN
                                                                CheckAccType("Service CF Account No.");
                                                              END;

                                                   AccessByPermission=TableData 5900=R;
                                                   CaptionML=[ENU=Service CF Account No.;
                                                              RUS=Номер счета движения денежных средств по сервисному обслуживанию] }
    { 11  ;   ;CF No. on Chart in Role Center;Code20;
                                                   OnValidate=BEGIN
                                                                IF NOT ConfirmedChartRoleCenterCFNo("CF No. on Chart in Role Center") THEN
                                                                  "CF No. on Chart in Role Center" := xRec."CF No. on Chart in Role Center";
                                                              END;

                                                   CaptionML=[ENU=CF No. on Chart in Role Center;
                                                              RUS=Номер движения денежных средств на диаграмме в ролевом центре] }
    { 12  ;   ;Job CF Account No.  ;Code20        ;TableRelation="Cash Flow Account";
                                                   OnValidate=BEGIN
                                                                CheckAccType("Job CF Account No.");
                                                              END;

                                                   AccessByPermission=TableData 1003=R;
                                                   CaptionML=[ENU=Job CF Account No.;
                                                              RUS=Номер счета движения денежных средств по работе] }
    { 13  ;   ;Automatic Update Frequency;Option  ;OnValidate=VAR
                                                                CashFlowManagement@1000 : Codeunit 841;
                                                              BEGIN
                                                                IF "Automatic Update Frequency" = xRec."Automatic Update Frequency" THEN
                                                                  EXIT;

                                                                CashFlowManagement.DeleteJobQueueEntries;
                                                                CashFlowManagement.CreateAndStartJobQueueEntry("Automatic Update Frequency");
                                                              END;

                                                   CaptionML=[ENU=Automatic Update Frequency;
                                                              RUS=Периодичность автоматических обновлений];
                                                   OptionCaptionML=[ENU=Never,Daily,Weekly;
                                                                    RUS=Никогда,Ежедневно,Еженедельно];
                                                   OptionString=Never,Daily,Weekly }
    { 14  ;   ;Tax CF Account No.  ;Code20        ;TableRelation="Cash Flow Account";
                                                   OnValidate=BEGIN
                                                                CheckAccType("Tax CF Account No.");
                                                              END;

                                                   AccessByPermission=TableData 254=R;
                                                   CaptionML=[ENU=Tax CF Account No.;
                                                              RUS=Номер счета движения денежных средств по налогу] }
    { 19  ;   ;Taxable Period      ;Option        ;InitValue=Quarterly;
                                                   CaptionML=[ENU=Taxable Period;
                                                              RUS=Налогооблагаемый период];
                                                   OptionCaptionML=[ENU=Monthly,Quarterly,Accounting Period,Yearly;
                                                                    RUS=Ежемесячно,Ежеквартально,Учетный период,Ежегодно];
                                                   OptionString=Monthly,Quarterly,Accounting Period,Yearly }
    { 20  ;   ;Tax Payment Window  ;DateFormula   ;CaptionML=[ENU=Tax Payment Window;
                                                              RUS=Окно налоговых платежей] }
    { 21  ;   ;Tax Bal. Account Type;Option       ;OnValidate=BEGIN
                                                                EmptyTaxBalAccountIfTypeChanged(xRec."Tax Bal. Account Type");
                                                              END;

                                                   CaptionML=[ENU=Tax Bal. Account Type;
                                                              RUS=Тип баланс. счета налога];
                                                   OptionCaptionML=[ENU=" ,Vendor,G/L Account";
                                                                    RUS=" ,Поставщик,Счет ГК"];
                                                   OptionString=[ ,Vendor,G/L Account] }
    { 22  ;   ;Tax Bal. Account No.;Code20        ;TableRelation=IF (Tax Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Tax Bal. Account Type=CONST(Vendor)) Vendor;
                                                   CaptionML=[ENU=Tax Bal. Account No.;
                                                              RUS=Номер баланс. счета налога] }
    { 23  ;   ;API Key             ;Text250       ;OnValidate=BEGIN
                                                                IF NOT ISNULLGUID("Service Pass API Key ID") THEN
                                                                  EnableEncryption;
                                                                SaveUserDefinedAPIKey("API Key");
                                                              END;

                                                   CaptionML=[ENU=API Key;
                                                              RUS=Ключ API] }
    { 24  ;   ;API URL             ;Text250       ;CaptionML=[ENU=API URL;
                                                              RUS=URL-адрес API] }
    { 25  ;   ;Variance %          ;Integer       ;InitValue=35;
                                                   CaptionML=[ENU=Variance %;
                                                              RUS=Отклонение %];
                                                   MinValue=1;
                                                   MaxValue=100 }
    { 26  ;   ;Historical Periods  ;Integer       ;InitValue=24;
                                                   CaptionML=[ENU=Historical Periods;
                                                              RUS=Исторические периоды];
                                                   MinValue=5;
                                                   MaxValue=24 }
    { 27  ;   ;Horizon             ;Integer       ;InitValue=4;
                                                   CaptionML=[ENU=Horizon;
                                                              RUS=Горизонт];
                                                   MinValue=3;
                                                   MaxValue=24 }
    { 28  ;   ;Period Type         ;Option        ;InitValue=Month;
                                                   CaptionML=[ENU=Period Type;
                                                              RUS=Тип периода];
                                                   OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year;
                                                                    RUS=День,Неделя,Месяц,Квартал,Год];
                                                   OptionString=Day,Week,Month,Quarter,Year }
    { 29  ;   ;TimeOut             ;Integer       ;InitValue=120;
                                                   CaptionML=[ENU=TimeOut;
                                                              RUS=Время ожидания];
                                                   MinValue=1 }
    { 30  ;   ;Service Pass API Key ID;GUID       ;TableRelation="Service Password".Key;
                                                   CaptionML=[ENU=Service Pass API Key ID;
                                                              RUS=Код ключа API пароля службы];
                                                   Description=The Key for retrieving the API Key from the Service Password table. }
    { 31  ;   ;Cortana Intelligence Enabled;Boolean;
                                                   InitValue=No;
                                                   CaptionML=[ENU=Cortana Intelligence Enabled;
                                                              RUS=Cortana Intelligence включено] }
    { 32  ;   ;Show Cortana Notification;Boolean  ;InitValue=Yes;
                                                   CaptionML=[ENU=Show Cortana Notification;
                                                              RUS=Показать уведомление Cortana] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst '@@@=Cash Flow <No.> <Description> is shown in the chart on the Role Center.;ENU=Cash Flow Forecast %1 %2 is shown in the chart on the Role Center. Do you want to show this Cash Flow Forecast instead?;RUS=Прогноз движения денежных средств %1 %2 отображается на диаграмме в ролевом центре. Отобразить вместо него этот прогноз движения денежных средств?';
      AzureKeyVaultManagement@1001 : Codeunit 2200;

    LOCAL PROCEDURE CheckAccType@1(Code@1000 : Code[20]);
    VAR
      CFAccount@1001 : Record 841;
    BEGIN
      IF Code <> '' THEN BEGIN
        CFAccount.GET(Code);
        CFAccount.TESTFIELD("Account Type",CFAccount."Account Type"::Entry);
      END;
    END;

    PROCEDURE SetChartRoleCenterCFNo@11(CashFlowNo@1000 : Code[20]);
    BEGIN
      GET;
      "CF No. on Chart in Role Center" := CashFlowNo;
      MODIFY;
    END;

    PROCEDURE GetChartRoleCenterCFNo@2() : Code[20];
    BEGIN
      GET;
      EXIT("CF No. on Chart in Role Center");
    END;

    LOCAL PROCEDURE ConfirmedChartRoleCenterCFNo@3(NewCashFlowNo@1000 : Code[20]) : Boolean;
    VAR
      CashFlowForecast@1001 : Record 840;
    BEGIN
      IF NewCashFlowNo = '' THEN
        EXIT(TRUE);

      IF NOT (xRec."CF No. on Chart in Role Center" IN ['',NewCashFlowNo]) THEN BEGIN
        CashFlowForecast.GET(xRec."CF No. on Chart in Role Center");
        EXIT(CONFIRM(STRSUBSTNO(Text001,CashFlowForecast."No.",CashFlowForecast.Description),TRUE));
      END;
      EXIT(TRUE);
    END;

    PROCEDURE GetTaxPaymentDueDate@4(ReferenceDate@1000 : Date) : Date;
    VAR
      EndOfTaxPeriod@1002 : Date;
    BEGIN
      GET;
      EndOfTaxPeriod := CalculateTaxableDate(ReferenceDate,TRUE);
      EXIT(CALCDATE("Tax Payment Window",EndOfTaxPeriod));
    END;

    PROCEDURE GetTaxPeriodStartEndDates@8(TaxDueDate@1000 : Date;VAR StartDate@1001 : Date;VAR EndDate@1002 : Date);
    BEGIN
      GET;
      EndDate := GetTaxPeriodEndDate(TaxDueDate);
      StartDate := CalculateTaxableDate(EndDate,FALSE);
    END;

    PROCEDURE GetTaxPaymentStartDate@5(TaxDueDate@1000 : Date) : Date;
    BEGIN
      GET;
      EXIT(CALCDATE('<1D>',GetTaxPeriodEndDate(TaxDueDate)));
    END;

    LOCAL PROCEDURE GetTaxPeriodEndDate@7(TaxDueDate@1000 : Date) : Date;
    BEGIN
      GET;
      EXIT(CALCDATE(STRSUBSTNO('<%1>',ReverseDateFormulaAsText),TaxDueDate));
    END;

    PROCEDURE GetCurrentPeriodStartDate@12() : Date;
    BEGIN
      GET;
      EXIT(CalculateTaxableDate(WORKDATE,FALSE));
    END;

    PROCEDURE UpdateTaxPaymentInfo@29(NewTaxablePeriod@1002 : Option;NewPaymentWindow@1000 : DateFormula;NewTaxBalAccountType@1004 : Option;NewTaxBalAccountNum@1001 : Code[20]);
    VAR
      Modified@1003 : Boolean;
    BEGIN
      GET;
      IF "Taxable Period" <> NewTaxablePeriod THEN BEGIN
        "Taxable Period" := NewTaxablePeriod;
        Modified := TRUE;
      END;

      IF "Tax Payment Window" <> NewPaymentWindow THEN BEGIN
        "Tax Payment Window" := NewPaymentWindow;
        Modified := TRUE;
      END;

      IF "Tax Bal. Account Type" <> NewTaxBalAccountType THEN BEGIN
        "Tax Bal. Account Type" := NewTaxBalAccountType;
        Modified := TRUE;
      END;

      IF "Tax Bal. Account No." <> NewTaxBalAccountNum THEN BEGIN
        "Tax Bal. Account No." := NewTaxBalAccountNum;
        Modified := TRUE;
      END;

      IF Modified THEN
        MODIFY;
    END;

    LOCAL PROCEDURE CalculateTaxableDate@6(ReferenceDate@1000 : Date;FindLast@1001 : Boolean) Result : Date;
    VAR
      AccountingPeriod@1003 : Record 50;
    BEGIN
      CASE "Taxable Period" OF
        "Taxable Period"::Monthly:
          IF FindLast THEN
            Result := CALCDATE('<CM>',ReferenceDate)
          ELSE
            Result := CALCDATE('<-CM>',ReferenceDate);
        "Taxable Period"::Quarterly:
          IF FindLast THEN
            Result := CALCDATE('<CQ>',ReferenceDate)
          ELSE
            Result := CALCDATE('<-CQ>',ReferenceDate);
        "Taxable Period"::"Accounting Period":
          IF FindLast THEN BEGIN
            // The end of the current accounting period is the start of the next acc. period - 1 day
            AccountingPeriod.SETFILTER("Starting Date",'>%1',ReferenceDate);
            AccountingPeriod.FINDFIRST;
            Result := AccountingPeriod."Starting Date" - 1;
          END ELSE BEGIN
            // The end of the current accounting period is the start of the next acc. period - 1 day
            AccountingPeriod.SETFILTER("Starting Date",'<=%1',ReferenceDate);
            AccountingPeriod.FINDFIRST;
            Result := AccountingPeriod."Starting Date";
          END;
        "Taxable Period"::Yearly:
          IF FindLast THEN
            Result := CALCDATE('<CY>',ReferenceDate)
          ELSE
            Result := CALCDATE('<-CY>',ReferenceDate);
      END;
    END;

    LOCAL PROCEDURE ReverseDateFormulaAsText@15() Result : Text;
    VAR
      TempChar@1000 : Char;
    BEGIN
      Result := FORMAT("Tax Payment Window");
      IF Result = '' THEN
        EXIT('');

      IF NOT (COPYSTR(Result,1,1) IN ['+','-']) THEN
        Result := '+' + Result;

      TempChar := '#';
      Result := ReplaceCharInString(Result,'+',TempChar);
      Result := ReplaceCharInString(Result,'-','+');
      Result := ReplaceCharInString(Result,TempChar,'-');
    END;

    LOCAL PROCEDURE ReplaceCharInString@16(StringToReplace@1000 : Text;OldChar@1001 : Char;NewChar@1002 : Char) Result : Text;
    VAR
      Index@1005 : Integer;
      FirstPart@1003 : Text;
      LastPart@1004 : Text;
    BEGIN
      Index := STRPOS(StringToReplace,FORMAT(OldChar));
      Result := StringToReplace;
      WHILE Index > 0 DO BEGIN
        IF Index > 1 THEN
          FirstPart := COPYSTR(Result,1,Index - 1);
        IF Index < STRLEN(Result) THEN
          LastPart := COPYSTR(Result,Index + 1);
        Result := FirstPart + FORMAT(NewChar) + LastPart;
        Index := STRPOS(Result,FORMAT(OldChar));
      END;
    END;

    PROCEDURE HasValidTaxAccountInfo@9() : Boolean;
    BEGIN
      EXIT("Tax Bal. Account Type" <> "Tax Bal. Account Type"::" ");
    END;

    PROCEDURE EmptyTaxBalAccountIfTypeChanged@10(OldTypeValue@1000 : Option);
    BEGIN
      IF "Tax Bal. Account Type" <> OldTypeValue THEN
        "Tax Bal. Account No." := '';
    END;

    PROCEDURE SaveUserDefinedAPIKey@14(APIKeyValue@1000 : Text[250]);
    VAR
      ServicePassword@1002 : Record 1261;
    BEGIN
      IF ISNULLGUID("Service Pass API Key ID") OR NOT ServicePassword.GET("Service Pass API Key ID") THEN BEGIN
        ServicePassword.SavePassword(APIKeyValue);
        ServicePassword.INSERT(TRUE);
        "Service Pass API Key ID" := ServicePassword.Key;
      END ELSE BEGIN
        ServicePassword.SavePassword(APIKeyValue);
        ServicePassword.MODIFY;
      END;
    END;

    PROCEDURE GetMLCredentials@13(VAR APIURL@1003 : Text[250];VAR APIKey@1002 : Text[200];VAR LimitValue@1001 : Decimal) : Boolean;
    VAR
      ServicePassword@1000 : Record 1261;
      PermissionManager@1004 : Codeunit 9002;
    BEGIN
      // user-defined credentials
      IF IsAPIUserDefined THEN BEGIN
        ServicePassword.GET("Service Pass API Key ID");
        APIKey := COPYSTR(ServicePassword.GetPassword,1,200);
        IF (APIKey = '') OR ("API URL" = '') THEN
          EXIT(FALSE);
        APIURL := "API URL";
        EXIT(TRUE);
      END;

      // if credentials not user-defined retrieve it from Azure Key Vault
      IF PermissionManager.SoftwareAsAService THEN
        EXIT(RetrieveSaaSMLCredentials(APIURL,APIKey,LimitValue));
    END;

    LOCAL PROCEDURE RetrieveSaaSMLCredentials@55(VAR APIURL@1003 : Text[250];VAR APIKey@1002 : Text[200];VAR LimitValue@1001 : Decimal) : Boolean;
    BEGIN
      IF NOT AzureKeyVaultManagement.GetMachineLearningCredentials(APIURL,APIKey,LimitValue) THEN
        EXIT(FALSE);
      APIURL := APIURL + '/execute?api-version=2.0&details=true';
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE EnableEncryption@56();
    VAR
      EncryptionManagement@1000 : Codeunit 1266;
    BEGIN
      IF NOT EncryptionManagement.IsEncryptionEnabled THEN
        EncryptionManagement.EnableEncryption;
    END;

    PROCEDURE GetUserDefinedAPIKey@18() : Text[200];
    VAR
      ServicePassword@1000 : Record 1261;
    BEGIN
      // user-defined credentials
      IF NOT ISNULLGUID("Service Pass API Key ID") THEN BEGIN
        ServicePassword.GET("Service Pass API Key ID");
        EXIT(COPYSTR(ServicePassword.GetPassword,1,200));
      END;
    END;

    PROCEDURE IsAPIUserDefined@99() : Boolean;
    BEGIN
      EXIT(NOT (ISNULLGUID("Service Pass API Key ID") OR ("API URL" = '')));
    END;

    BEGIN
    END.
  }
}

