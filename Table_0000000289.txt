OBJECT Table 289 Payment Method
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    CaptionML=[ENU=Payment Method;
               RUS=Способ платежа];
    LookupPageID=Page427;
    DrillDownPageID=Page427;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              RUS=Код];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 3   ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                "Bal. Account No." := '';
                                                              END;

                                                   CaptionML=[ENU=Bal. Account Type;
                                                              RUS=Тип баланс. счета];
                                                   OptionCaptionML=[ENU=G/L Account,Bank Account;
                                                                    RUS=Счет ГК,Банковский счет];
                                                   OptionString=G/L Account,Bank Account }
    { 4   ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account No." <> '' THEN
                                                                  TESTFIELD("Direct Debit",FALSE);
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"G/L Account" THEN
                                                                  CheckGLAcc("Bal. Account No.");
                                                              END;

                                                   CaptionML=[ENU=Bal. Account No.;
                                                              RUS=Номер баланс. счета] }
    { 6   ;   ;Direct Debit        ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT "Direct Debit" THEN
                                                                  "Direct Debit Pmt. Terms Code" := '';
                                                                IF "Direct Debit" THEN
                                                                  TESTFIELD("Bal. Account No.",'');
                                                              END;

                                                   CaptionML=[ENU=Direct Debit;
                                                              RUS=Прямое дебетование] }
    { 7   ;   ;Direct Debit Pmt. Terms Code;Code10;TableRelation="Payment Terms";
                                                   OnValidate=BEGIN
                                                                IF "Direct Debit Pmt. Terms Code" <> '' THEN
                                                                  TESTFIELD("Direct Debit",TRUE);
                                                              END;

                                                   CaptionML=[ENU=Direct Debit Pmt. Terms Code;
                                                              RUS=Код условий оплаты прямого дебетования] }
    { 8   ;   ;Pmt. Export Line Definition;Code20 ;OnLookup=VAR
                                                              DataExchLineDef@1000 : Record 1227;
                                                              TempDataExchLineDef@1001 : TEMPORARY Record 1227;
                                                              DataExchDef@1002 : Record 1222;
                                                            BEGIN
                                                              DataExchLineDef.SETFILTER(Code,'<>%1','');
                                                              IF DataExchLineDef.FINDSET THEN BEGIN
                                                                REPEAT
                                                                  DataExchDef.GET(DataExchLineDef."Data Exch. Def Code");
                                                                  IF DataExchDef.Type = DataExchDef.Type::"Payment Export" THEN BEGIN
                                                                    TempDataExchLineDef.INIT;
                                                                    TempDataExchLineDef.Code := DataExchLineDef.Code;
                                                                    TempDataExchLineDef.Name := DataExchLineDef.Name;
                                                                    IF TempDataExchLineDef.INSERT THEN;
                                                                  END;
                                                                UNTIL DataExchLineDef.NEXT = 0;
                                                                IF PAGE.RUNMODAL(PAGE::"Pmt. Export Line Definitions",TempDataExchLineDef) = ACTION::LookupOK THEN
                                                                  "Pmt. Export Line Definition" := TempDataExchLineDef.Code;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Pmt. Export Line Definition;
                                                              RUS=Определение строки экспорта платежа] }
    { 9   ;   ;Bank Data Conversion Pmt. Type;Text50;
                                                   TableRelation="Bank Data Conversion Pmt. Type";
                                                   CaptionML=[ENU=Bank Data Conversion Pmt. Type;
                                                              RUS=Тип платежа - преобразование данных банка] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Code,Description,Bal. Account Type       }
  }
  CODE
  {

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

