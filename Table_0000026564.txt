OBJECT Table 26564 Statutory Report Data Value
{
  OBJECT-PROPERTIES
  {
    Date=14.02.08;
    Time=12:00:00;
    Version List=NAVRU5.00.01.03;
  }
  PROPERTIES
  {
    OnDelete=VAR
               StatReportDataChangeLog@1210000 : Record 26565;
             BEGIN
               TestReportDataStatus;

               StatReportDataChangeLog.SETRANGE("Report Data No.","Report Data No.");
               StatReportDataChangeLog.SETRANGE("Report Code","Report Code");
               StatReportDataChangeLog.SETRANGE("Table Code","Table Code");
               StatReportDataChangeLog.SETRANGE("Excel Sheet Name","Excel Sheet Name");
               StatReportDataChangeLog.SETRANGE("Row No.","Row No.");
               StatReportDataChangeLog.SETRANGE("Column No.","Column No.");
               StatReportDataChangeLog.DELETEALL;
             END;

    CaptionML=[ENU=Statutory Report Data Value;
               RUS=Значение данных регламентного отчета];
  }
  FIELDS
  {
    { 1   ;   ;Report Data No.     ;Code20        ;TableRelation="Statutory Report Data Header";
                                                   CaptionML=[ENU=Report Data No.;
                                                              RUS=Номер данных отчета] }
    { 2   ;   ;Row No.             ;Integer       ;CaptionML=[ENU=Row No.;
                                                              RUS=Номер ряда] }
    { 3   ;   ;Column No.          ;Integer       ;CaptionML=[ENU=Column No.;
                                                              RUS=Номер столбца] }
    { 6   ;   ;Value               ;Text150       ;OnValidate=VAR
                                                                StatReportRequisite@1210001 : Record 26559;
                                                              BEGIN
                                                                TestReportDataStatus;

                                                                IF Value <> xRec.Value THEN
                                                                  AddChangeHistoryEntry;

                                                                StatReportRequisite.SETCURRENTKEY("Report Code","Table Code","Row Link No.","Column Link No.");
                                                                StatReportRequisite.SETRANGE("Report Code","Report Code");
                                                                StatReportRequisite.SETRANGE("Table Code","Table Code");
                                                                StatReportRequisite.SETRANGE("Row Link No.","Row No.");
                                                                StatReportRequisite.SETRANGE("Column Link No.","Column No.");
                                                                IF StatReportRequisite.FINDFIRST THEN
                                                                  StatReportRequisite.ValidateValueDataType(Value);
                                                              END;

                                                   CaptionML=[ENU=Value;
                                                              RUS=Значение] }
    { 7   ;   ;Report Code         ;Code20        ;TableRelation="Statutory Report";
                                                   CaptionML=[ENU=Report Code;
                                                              RUS=Код отчета] }
    { 8   ;   ;Table Code          ;Code20        ;TableRelation="Statutory Report Table".Code WHERE (Report Code=FIELD(Report Code));
                                                   CaptionML=[ENU=Table Code;
                                                              RUS=Код таблицы] }
    { 11  ;   ;Excel Sheet Name    ;Text30        ;TableRelation="Stat. Report Excel Sheet"."Sheet Name" WHERE (Report Code=FIELD(Report Code),
                                                                                                                Table Code=FIELD(Table Code),
                                                                                                                Report Data No.=FIELD(Report Data No.));
                                                   CaptionML=[ENU=Excel Sheet Name;
                                                              RUS=Имя листа Excel] }
  }
  KEYS
  {
    {    ;Report Data No.,Report Code,Table Code,Excel Sheet Name,Row No.,Column No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      StatutoryReportDataHeader@1210000 : Record 26563;

    PROCEDURE AddChangeHistoryEntry@1470000();
    VAR
      StatReportDataChangeLog@1470000 : Record 26565;
      VersionNo@1470001 : Integer;
    BEGIN
      StatReportDataChangeLog.SETRANGE("Report Data No.","Report Data No.");
      StatReportDataChangeLog.SETRANGE("Report Code","Report Code");
      StatReportDataChangeLog.SETRANGE("Table Code","Table Code");
      StatReportDataChangeLog.SETRANGE("Excel Sheet Name","Excel Sheet Name");
      StatReportDataChangeLog.SETRANGE("Row No.","Row No.");
      StatReportDataChangeLog.SETRANGE("Column No.","Column No.");
      IF StatReportDataChangeLog.FINDLAST THEN;
      VersionNo := StatReportDataChangeLog."Version No." + 1;

      StatReportDataChangeLog.INIT;
      StatReportDataChangeLog."Report Data No." := "Report Data No.";
      StatReportDataChangeLog."Report Code" := "Report Code";
      StatReportDataChangeLog."Table Code" := "Table Code";
      StatReportDataChangeLog."Excel Sheet Name" := "Excel Sheet Name";
      StatReportDataChangeLog."Row No." := "Row No.";
      StatReportDataChangeLog."Column No." := "Column No.";
      StatReportDataChangeLog."Version No." := VersionNo;
      StatReportDataChangeLog."New Value" := Value;
      StatReportDataChangeLog."Old Value" := xRec.Value;

      StatReportDataChangeLog.INSERT(TRUE);
    END;

    PROCEDURE TestReportDataStatus@1210000();
    BEGIN
      StatutoryReportDataHeader.GET("Report Data No.");
      StatutoryReportDataHeader.TESTFIELD(Status,StatutoryReportDataHeader.Status::Open);
    END;

    PROCEDURE AddValue@1210001(ReportDataNo@1210001 : Code[20];ReportCode@1210000 : Code[20];TableCode@1210002 : Code[20];SheetName@1210003 : Text[30];RowNo@1210004 : Integer;ColumnNo@1210005 : Integer;CellValue@1210006 : Text[150]);
    BEGIN
      "Report Data No." := ReportDataNo;
      "Report Code" := ReportCode;
      "Row No." := RowNo;
      "Column No." := ColumnNo;
      "Table Code" := TableCode;
      "Excel Sheet Name" := SheetName;
      IF FIND THEN BEGIN
        Value := CellValue;
        MODIFY;
      END ELSE BEGIN
        Value := CellValue;
        INSERT;
      END;
    END;

    BEGIN
    END.
  }
}

