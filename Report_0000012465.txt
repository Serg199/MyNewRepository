OBJECT Report 12465 Combine Customer/Vendor
{
  OBJECT-PROPERTIES
  {
    Date=23.01.17;
    Time=12:00:00;
    Version List=NAVRU10.00.00.15052;
  }
  PROPERTIES
  {
    TransactionType=Update;
    CaptionML=[ENU=Combine Customer/Vendor;
               RUS=Объединить клиента/поставщика];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 3182;    ;DataItem;                    ;
               DataItemTable=Table23;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               IF (OldVendor = '') OR (NewVendor = '') THEN
                                 EXIT
                               ELSE BEGIN
                                 IF NOT CONFIRM(Text001) THEN
                                   EXIT;
                                 Window.OPEN('#1################################');
                                 Vendor.SETRANGE("No.",OldVendor);
                               END;
                             END;

               OnAfterGetRecord=VAR
                                  Vend@1210000 : Record 23;
                                BEGIN
                                  IF (OldVendor <> '') AND (NewVendor <> '') THEN
                                    WITH Vendor DO BEGIN
                                      Vend.GET(NewVendor);
                                      TmpVendor.INIT;
                                      TmpVendor.TRANSFERFIELDS(Vend,FALSE);
                                      Vend.DELETE;
                                      Window.UPDATE(1,Text002);
                                      IF VendAgrmt.GET(OldVendor,'') THEN
                                        VendAgrmt.DELETE;
                                      IF RENAME(NewVendor) THEN BEGIN
                                        TRANSFERFIELDS(TmpVendor,FALSE);
                                        MODIFY;
                                        Window.UPDATE(1,Text003);
                                      END ELSE BEGIN
                                        TmpVendor.INSERT;
                                        Window.UPDATE(1,Text004)
                                      END;
                                      SLEEP(200);
                                    END;
                                END;
                                 }

    { 6836;    ;DataItem;                    ;
               DataItemTable=Table18;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               IF (OldCustomer = '') OR (NewCustomer = '') THEN
                                 EXIT
                               ELSE BEGIN
                                 IF NOT CONFIRM(Text001) THEN
                                   EXIT;
                                 Window.OPEN('#1################################');
                                 Customer.SETRANGE("No.",OldCustomer);
                               END
                             END;

               OnAfterGetRecord=VAR
                                  Cust@1210000 : Record 18;
                                BEGIN
                                  IF (OldCustomer <> '') AND (NewCustomer <> '') THEN
                                    WITH Customer DO BEGIN
                                      Cust.GET(NewCustomer);
                                      TmpCustomer.INIT;
                                      TmpCustomer.TRANSFERFIELDS(Cust,FALSE);
                                      Cust.DELETE;
                                      Window.UPDATE(1,Text002);
                                      IF CustAgrmt.GET(OldCustomer,'') THEN
                                        CustAgrmt.DELETE;
                                      IF RENAME(NewCustomer) THEN BEGIN
                                        TRANSFERFIELDS(TmpCustomer,FALSE);
                                        MODIFY;
                                        Window.UPDATE(1,Text003);
                                      END ELSE BEGIN
                                        TmpCustomer.INSERT;
                                        Window.UPDATE(1,Text004)
                                      END;
                                      SLEEP(200);
                                    END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1210004;2;Field     ;
                  Name=Type;
                  CaptionML=[ENU=Type;
                             RUS=Тип];
                  OptionCaptionML=[ENU=Vendor,Customer;
                                   RUS=Поставщик,Клиент];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Type;
                  OnValidate=BEGIN
                               UpdateForm(TRUE);
                             END;
                              }

      { 1210000;2;Field     ;
                  Name=OldCustomer;
                  Lookup=Yes;
                  CaptionML=[ENU=Old Customer No.;
                             RUS=Код прежнего клиента];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=OldCustomer;
                  TableRelation=Customer.No.;
                  OnValidate=BEGIN
                               IF Type <> Type::Customer THEN
                                 ERROR(Text005,FORMAT(Type));
                               Customer.GET(OldCustomer);
                               OldName := Customer.Name;
                             END;
                              }

      { 1210008;2;Field     ;
                  Name=OldVendor;
                  Lookup=Yes;
                  CaptionML=[ENU=Old Vendor No.;
                             RUS=Код прежнего поставщика];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=OldVendor;
                  TableRelation=Vendor.No.;
                  OnValidate=BEGIN
                               IF Type <> Type::Vendor THEN
                                 ERROR(Text005,FORMAT(Type));

                               Vendor.GET(OldVendor);
                               OldName := Vendor.Name;
                             END;
                              }

      { 1210006;2;Field     ;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=OldName;
                  Enabled=TRUE;
                  Editable=FALSE }

      { 1210002;2;Field     ;
                  Name=NewCustomer;
                  Lookup=Yes;
                  CaptionML=[ENU=New Customer No.;
                             RUS=Код нового клиента];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=NewCustomer;
                  TableRelation=Customer;
                  OnValidate=BEGIN
                               IF Type <> Type::Customer THEN
                                 ERROR(Text005,FORMAT(Type));

                               Customer.GET(NewCustomer);
                               NewName := Customer.Name;
                             END;
                              }

      { 1210009;2;Field     ;
                  Name=NewVendor;
                  Lookup=Yes;
                  CaptionML=[ENU=New Vendor No.;
                             RUS=Код нового поставщика];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=NewVendor;
                  TableRelation=Vendor.No.;
                  OnValidate=BEGIN
                               IF Type <> Type::Vendor THEN
                                 ERROR(Text005,FORMAT(Type));

                               Vendor.GET(NewVendor);
                               NewName := Vendor.Name;
                             END;
                              }

      { 1210007;2;Field     ;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=NewName;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TmpCustomer@1210005 : Record 18;
      TmpVendor@1210004 : Record 23;
      CustAgrmt@1210016 : Record 14902;
      VendAgrmt@1210015 : Record 14901;
      OldCustomer@1210000 : Code[20];
      NewCustomer@1210001 : Code[20];
      OldName@1210002 : Text[50];
      NewName@1210003 : Text[50];
      Window@1210006 : Dialog;
      OldVendor@1210010 : Code[20];
      NewVendor@1210009 : Code[20];
      Type@1210007 : 'Vendor,Customer';
      Text001@1210008 : TextConst 'ENU=Are you sure?;RUS=Вы уверены?';
      Text002@1210011 : TextConst 'ENU=Change links process are going;RUS=Идет процесс замены ссылок';
      Text003@1210012 : TextConst 'ENU=Change links process completed;RUS=Процесс замены ссылок завершен';
      Text004@1210013 : TextConst 'ENU=Change links process failed;RUS=Процесс замены ссылок не выполнен';
      Text005@1210014 : TextConst 'ENU=Type must not be %1;RUS=Тип не может быть %1';

    PROCEDURE ChangeCustomer@1210000(VAR Rec@1210000 : Record 18);
    BEGIN
      Type := Type::Customer;
      UpdateForm(FALSE);
      OldCustomer := Rec."No.";
      OldName := Rec.Name;
      Customer.GET(Rec."No.");
    END;

    PROCEDURE ChangeVendor@1210001(VAR Rec@1210000 : Record 23);
    BEGIN
      Type := Type::Vendor;
      UpdateForm(FALSE);
      OldVendor := Rec."No.";
      OldName := Rec.Name;
      Vendor.GET(Rec."No.");
    END;

    LOCAL PROCEDURE UpdateForm@1210002(TypeEnable@1210000 : Boolean);
    BEGIN
      OldName := '';
      NewName := '';
      OldVendor := '';
      OldCustomer := '';
      NewVendor := '';
      NewCustomer := '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

