OBJECT Codeunit 17300 TaxDiffJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    Permissions=TableData 17303=imd,
                TableData 17304=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1210004 : TextConst 'ENU=Do you want to post the journals?;RUS=Учесть журналы?';
      Text001@1210003 : TextConst 'ENU=The journals were successfully posted.;RUS=Журналы успешно учтены.';
      Text002@1210002 : TextConst 'ENU="It was not possible to post all of the journals. ";RUS="Не удалось учесть все журналы. "';
      Text003@1210001 : TextConst 'ENU=The journals that were not successfully posted are now marked.;RUS=Помечены журналы, которые не были успешно учтены.';
      Text004@1210007 : TextConst 'ENU=Do you want to post the journal lines?;RUS=Учесть строки журнала?';
      Text005@1210006 : TextConst 'ENU=There is nothing to post.;RUS=Здесь нечего учитывать.';
      Text006@1210005 : TextConst 'ENU=The journal lines were successfully posted.;RUS=Строки журнала успешно учтены.';
      Text1000@1000 : TextConst 'ENU=%1 journal;RUS=Журнал %1';
      Text1001@1001 : TextConst 'ENU=DEFAULT;RUS=СТАНДАРТ';
      Text1002@1002 : TextConst 'ENU=Default Journal;RUS=Журнал по умолчанию';
      LastTaxDiffJnlLine@1003 : Record 17305;
      OpenFromBatch@1210000 : Boolean;

    PROCEDURE TemplateSelection@1(PageID@1210000 : Integer;PageTemplate@1000 : 'General';VAR TaxDiffJnlLine@1470000 : Record 17305;VAR JnlSelected@1470001 : Boolean);
    VAR
      TaxDiffJnlTemplate@1001 : Record 17303;
    BEGIN
      JnlSelected := TRUE;

      TaxDiffJnlTemplate.RESET;
      IF NOT OpenFromBatch THEN
        TaxDiffJnlTemplate.SETRANGE("Page ID",PageID);
      TaxDiffJnlTemplate.SETRANGE(Type,PageTemplate);

      CASE TaxDiffJnlTemplate.COUNT OF
        0:
          BEGIN
            TaxDiffJnlTemplate.INIT;
            TaxDiffJnlTemplate.Type := PageTemplate;
            TaxDiffJnlTemplate.Name := FORMAT(TaxDiffJnlTemplate.Type,MAXSTRLEN(TaxDiffJnlTemplate.Name));
            TaxDiffJnlTemplate.Description := STRSUBSTNO(Text1000,TaxDiffJnlTemplate.Type);
            TaxDiffJnlTemplate.VALIDATE(Type);
            TaxDiffJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          TaxDiffJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,TaxDiffJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        TaxDiffJnlLine.FILTERGROUP := 2;
        TaxDiffJnlLine.SETRANGE("Journal Template Name",TaxDiffJnlTemplate.Name);
        TaxDiffJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          TaxDiffJnlLine."Journal Template Name" := '';
          PAGE.RUN(TaxDiffJnlTemplate."Page ID",TaxDiffJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@1210000(VAR TaxDiffJnlBatch@1000 : Record 17304);
    VAR
      TaxDiffJnlLine@1002 : Record 17305;
      JnlSelected@1001 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      TaxDiffJnlLine."Journal Batch Name" := TaxDiffJnlBatch.Name;
      TemplateSelection(0,0,TaxDiffJnlLine,JnlSelected);
    END;

    PROCEDURE OpenJnl@2(VAR CurrentJnlBatchName@1000 : Code[10];VAR TaxDiffJnlLine@1001 : Record 17305);
    BEGIN
      CheckTemplateName(TaxDiffJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      TaxDiffJnlLine.FILTERGROUP := 2;
      TaxDiffJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      TaxDiffJnlLine.FILTERGROUP := 0;
    END;

    LOCAL PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      TaxDiffJnlBatch@1002 : Record 17304;
    BEGIN
      TaxDiffJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT TaxDiffJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT TaxDiffJnlBatch.FINDFIRST THEN BEGIN
          TaxDiffJnlBatch.INIT;
          TaxDiffJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          TaxDiffJnlBatch.SetupNewBatch;
          TaxDiffJnlBatch.Name := Text1001;
          TaxDiffJnlBatch.Description := Text1002;
          TaxDiffJnlBatch.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := TaxDiffJnlBatch.Name;
      END;
    END;

    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR TaxDiffJnlLine@1001 : Record 17305);
    VAR
      TaxDiffJnlBatch@1002 : Record 17304;
    BEGIN
      TaxDiffJnlBatch.GET(TaxDiffJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@5(CurrentJnlBatchName@1000 : Code[10];VAR TaxDiffJnlLine@1001 : Record 17305);
    BEGIN
      TaxDiffJnlLine.FILTERGROUP := 2;
      TaxDiffJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      TaxDiffJnlLine.FILTERGROUP := 0;
      IF TaxDiffJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@6(VAR CurrentJnlBatchName@1000 : Code[10];VAR TaxDiffJnlLine@1001 : Record 17305);
    VAR
      TaxDiffJnlBatch@1002 : Record 17304;
    BEGIN
      COMMIT;
      TaxDiffJnlBatch."Journal Template Name" := TaxDiffJnlLine.GETRANGEMAX("Journal Template Name");
      TaxDiffJnlBatch.Name := TaxDiffJnlLine.GETRANGEMAX("Journal Batch Name");
      TaxDiffJnlBatch.FILTERGROUP := 2;
      TaxDiffJnlBatch.SETRANGE("Journal Template Name",TaxDiffJnlBatch."Journal Template Name");
      TaxDiffJnlBatch.FILTERGROUP := 0;
      IF PAGE.RUNMODAL(0,TaxDiffJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := TaxDiffJnlBatch.Name;
        SetName(CurrentJnlBatchName,TaxDiffJnlLine);
      END;
    END;

    PROCEDURE GetAccounts@7(VAR TaxDiffJnlLine@1000 : Record 17305;VAR TaxDiffName@1001 : Text[50];VAR SourceName@1002 : Text[50]);
    VAR
      TaxDiff@1003 : Record 17300;
      FE@1004 : Record 5600;
    BEGIN
      IF TaxDiffJnlLine."Tax Diff. Code" <> LastTaxDiffJnlLine."Tax Diff. Code" THEN BEGIN
        TaxDiffName := '';
        IF TaxDiffJnlLine."Tax Diff. Code" <> '' THEN
          IF TaxDiff.GET(TaxDiffJnlLine."Tax Diff. Code") THEN
            TaxDiffName := COPYSTR(TaxDiff.Description,1,MAXSTRLEN(TaxDiffName));
      END;

      IF (TaxDiffJnlLine."Source Type" <> LastTaxDiffJnlLine."Source Type") OR
         (TaxDiffJnlLine."Source No." <> LastTaxDiffJnlLine."Source No.")
      THEN BEGIN
        SourceName := '';
        IF TaxDiffJnlLine."Source No." <> '' THEN
          CASE TaxDiffJnlLine."Source Type" OF
            TaxDiffJnlLine."Source Type"::"Future Expense":
              IF FE.GET(TaxDiffJnlLine."Source No.") THEN
                SourceName := FE.Description;
          END;
      END;

      LastTaxDiffJnlLine := TaxDiffJnlLine;
    END;

    PROCEDURE JnlBatchPost@8(VAR Rec@1000 : Record 17304);
    VAR
      TaxDiffJnlBatch@1001 : Record 17304;
      TaxDiffJnlLine@1002 : Record 17305;
      TaxDiffPostJnlBatch@1003 : Codeunit 17302;
      JnlWithErrors@1004 : Boolean;
    BEGIN
      TaxDiffJnlBatch.COPY(Rec);
      WITH TaxDiffJnlBatch DO BEGIN
        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        FIND('-');
        REPEAT
          TaxDiffJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
          TaxDiffJnlLine.SETRANGE("Journal Batch Name",Name);
          CLEAR(TaxDiffPostJnlBatch);
          IF TaxDiffPostJnlBatch.RUN(TaxDiffJnlLine) THEN
            MARK(FALSE)
          ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP(0);
          Name := '';
        END;
      END;

      Rec.COPY(TaxDiffJnlBatch);
      COMMIT;
    END;

    PROCEDURE JnlPost@9(VAR Rec@1000 : Record 17305);
    VAR
      TaxDiffJnlLine@1001 : Record 17305;
      TaxDiffPostJnlBatch@1002 : Codeunit 17302;
    BEGIN
      TaxDiffJnlLine.COPY(Rec);
      WITH TaxDiffJnlLine DO BEGIN
        IF NOT CONFIRM(Text004,FALSE) THEN
          EXIT;

        TaxDiffPostJnlBatch.RUN(TaxDiffJnlLine);

        IF "Line No." = 0 THEN
          MESSAGE(Text005)
        ELSE
          MESSAGE(Text006);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 10000;
        END;
      END;
      Rec.COPY(TaxDiffJnlLine);
      COMMIT;
    END;

    PROCEDURE JnlShowEntries@10(TaxDiffJnlLine@1000 : Record 17305);
    VAR
      TaxDiffEntry@1001 : Record 17306;
    BEGIN
      WITH TaxDiffJnlLine DO
        CASE "Source Type" OF
          "Source Type"::"Future Expense":
            BEGIN
              TaxDiffEntry.RESET;
              TaxDiffEntry.SETRANGE("Tax Diff. Code","Tax Diff. Code");
              TaxDiffEntry.SETRANGE("Source Type","Source Type");
              TaxDiffEntry.SETRANGE("Source No.","Source No.");
              PAGE.RUN(0,TaxDiffEntry);
            END;
          ELSE BEGIN
            TaxDiffEntry.RESET;
            TaxDiffEntry.SETRANGE("Tax Diff. Code","Tax Diff. Code");
            PAGE.RUN(0,TaxDiffEntry);
          END;
        END;
    END;

    BEGIN
    END.
  }
}

