OBJECT Table 5330 CRM Connection Setup
{
  OBJECT-PROPERTIES
  {
    Date=23.02.17;
    Time=12:00:00;
    Version List=NAVW110.00.00.15601;
  }
  PROPERTIES
  {
    Permissions=TableData 1261=rimd;
    CaptionML=[ENU=CRM Connection Setup;
               RUS=Настройка подключения к CRM];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code20        ;CaptionML=[ENU=Primary Key;
                                                              RUS=Первичный ключ] }
    { 2   ;   ;Server Address      ;Text250       ;OnValidate=BEGIN
                                                                CRMIntegrationManagement.CheckModifyCRMConnectionURL("Server Address");

                                                                IF STRPOS("Server Address",'https') > 0 THEN
                                                                  "Authentication Type" := "Authentication Type"::Office365
                                                                ELSE
                                                                  "Authentication Type" := "Authentication Type"::AD;
                                                                IF "User Name" <> '' THEN
                                                                  UpdateConnectionString;
                                                              END;

                                                   CaptionML=[ENU=Dynamics CRM URL;
                                                              RUS=URL-адрес Dynamics CRM] }
    { 3   ;   ;User Name           ;Text250       ;OnValidate=BEGIN
                                                                "User Name" := DELCHR("User Name",'<>');
                                                                CheckUserName;
                                                                UpdateDomainName;
                                                                UpdateConnectionString;
                                                              END;

                                                   CaptionML=[ENU=User Name;
                                                              RUS=Имя пользователя] }
    { 4   ;   ;User Password Key   ;GUID          ;CaptionML=[ENU=User Password Key;
                                                              RUS=Ключ пароля пользователя] }
    { 60  ;   ;Is Enabled          ;Boolean       ;OnValidate=BEGIN
                                                                EnableCRMConnection;
                                                                UpdateIsEnabledState;
                                                                RefreshDataFromCRM;
                                                              END;

                                                   CaptionML=[ENU=Is Enabled;
                                                              RUS=Включено] }
    { 61  ;   ;Is User Mapping Required;Boolean   ;OnValidate=BEGIN
                                                                UpdateAllConnectionRegistrations;
                                                                UpdateIsEnabledState;
                                                              END;

                                                   CaptionML=[ENU=Dynamics NAV Users Must Map to Dynamics CRM Users;
                                                              RUS=Пользователи Dynamics NAV должны быть сопоставлены с пользователями Dynamics CRM] }
    { 62  ;   ;Is User Mapped To CRM User;Boolean ;CaptionML=[ENU=Is User Mapped To CRM User;
                                                              RUS=Пользователь сопоставлен пользователю CRM] }
    { 63  ;   ;CRM Version         ;Text30        ;CaptionML=[ENU=CRM Version;
                                                              RUS=Версия CRM] }
    { 66  ;   ;Is S.Order Integration Enabled;Boolean;
                                                   CaptionML=[ENU=Is S.Order Integration Enabled;
                                                              RUS=Интеграция с заказом на продажу включена] }
    { 67  ;   ;Is CRM Solution Installed;Boolean  ;CaptionML=[ENU=Is CRM Solution Installed;
                                                              RUS=Решение CRM установлено] }
    { 68  ;   ;Is Enabled For User ;Boolean       ;CaptionML=[ENU=Is Enabled For User;
                                                              RUS=Включено для пользователя] }
    { 69  ;   ;Dynamics NAV URL    ;Text250       ;OnValidate=BEGIN
                                                                CRMIntegrationManagement.SetCRMNAVConnectionUrl("Dynamics NAV URL");
                                                              END;

                                                   CaptionML=[ENU=Dynamics NAV URL;
                                                              RUS=URL-адрес Dynamics NAV] }
    { 70  ;   ;Dynamics NAV OData URL;Text250     ;OnValidate=BEGIN
                                                                CRMIntegrationManagement.SetCRMNAVODataUrlCredentials(
                                                                  "Dynamics NAV OData URL","Dynamics NAV OData Username","Dynamics NAV OData Accesskey");
                                                              END;

                                                   CaptionML=[ENU=Dynamics NAV OData URL;
                                                              RUS=URL-адрес OData Dynamics NAV] }
    { 71  ;   ;Dynamics NAV OData Username;Text250;OnValidate=VAR
                                                                User@1000 : Record 2000000120;
                                                              BEGIN
                                                                User.SETRANGE("User Name","Dynamics NAV OData Username");
                                                                IF User.FINDFIRST THEN
                                                                  UpdateODataUsernameAccesskey(User)
                                                                ELSE BEGIN
                                                                  "Dynamics NAV OData Username" := '';
                                                                  "Dynamics NAV OData Accesskey" := '';
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              User@1000 : Record 2000000120;
                                                            BEGIN
                                                              IF PAGE.RUNMODAL(PAGE::Users,User) = ACTION::LookupOK THEN
                                                                UpdateODataUsernameAccesskey(User);
                                                            END;

                                                   CaptionML=[ENU=Dynamics NAV OData Username;
                                                              RUS=Имя пользователя OData Dynamics NAV] }
    { 72  ;   ;Dynamics NAV OData Accesskey;Text250;
                                                   CaptionML=[ENU=Dynamics NAV OData Accesskey;
                                                              RUS=Ключ доступа OData Dynamics NAV] }
    { 75  ;   ;Default CRM Price List ID;GUID     ;CaptionML=[ENU=Default CRM Price List ID;
                                                              RUS=ИД прайс-листа CRM по умолчанию] }
    { 135 ;   ;Authentication Type ;Option        ;OnValidate=BEGIN
                                                                IF xRec."Authentication Type" <> "Authentication Type" THEN
                                                                  VALIDATE("Is User Mapping Required",FALSE);
                                                                CASE "Authentication Type" OF
                                                                  "Authentication Type"::Office365:
                                                                    Domain := '';
                                                                  "Authentication Type"::AD:
                                                                    UpdateDomainName;
                                                                END;
                                                                UpdateConnectionString;
                                                              END;

                                                   CaptionML=[ENU=Authentication Type;
                                                              RUS=Authentication Type];
                                                   OptionCaptionML=[ENU=Office365,AD,IFD,OAuth;
                                                                    RUS=Office365,AD,IFD,OAuth];
                                                   OptionString=Office365,AD,IFD,OAuth }
    { 136 ;   ;Connection String   ;Text250       ;OnValidate=BEGIN
                                                                IF STRPOS("Connection String",MissingPasswordTok) = 0 THEN
                                                                  ERROR(ConnectionStringPwdPlaceHolderMissingErr);
                                                              END;

                                                   CaptionML=[ENU=Connection String;
                                                              RUS=Connection String] }
    { 137 ;   ;Domain              ;Text250       ;CaptionML=[ENU=Domain;
                                                              RUS=Domain];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CRMIntegrationManagement@1055 : Codeunit 5330;
      AllowNewUncoupledItemsToBeSynchronizedQst@1012 : TextConst '@@@="%1 = Page 5338 Caption";ENU=Full Synchronization completed.\See the %1 window for details.\\Do you want to allow all scheduled synchronization jobs to create new records?;RUS=Полная синхронизация завершена.\Подробности см. в окне %1.\\Разрешить всем запланированным заданиям синхронизации создавать новые записи?';
      CantRegisterDisabledConnectionErr@1001 : TextConst 'ENU=A disabled connection cannot be registered.;RUS=Отключенное подключение нельзя зарегистрировать.';
      ConnectionErr@1009 : TextConst '@@@=%1 Error message from the provider (.NET exception message);ENU=The connection setup cannot be validated. Verify the settings and try again.\Detailed error description: %1.;RUS=Нельзя проверить настройку подключения. Проверьте параметры и повторите попытку.\Подробное описание ошибки: %1.';
      ConnectionStringFormatTok@1000 : TextConst '@@@={Locked};ENU="Url=%1; UserName=%2; Password=%3; %4";RUS="Url=%1; UserName=%2; Password=%3; %4"';
      ConnectionWithCallerIdStringFormatTok@1002 : TextConst '@@@={Locked};ENU="Url=%1; UserName=%2; Password=%3; CallerID=%4; %5";RUS="Url=%1; UserName=%2; Password=%3; CallerID=%4; %5"';
      ConnectionSuccessMsg@1008 : TextConst 'ENU=The connection test was successful. The settings are valid.;RUS=Проверка подключения пройдена успешно. Настройки действительны.';
      ConnectionSuccessNotEnabledForCurrentUserMsg@1007 : TextConst '@@@="%1 = Current User ID, %2 - product name";ENU=The connection test was successful. The settings are valid.\\However, because the %2 Users Must Map to Dynamics CRM Users field is set, Dynamics CRM integration is not enabled for %1.;RUS=Проверка подключения пройдена успешно. Параметры действительны.\\Но поскольку задано поле "Пользователи %2 должны быть сопоставлены с пользователями Dynamics CRM", интеграция с Dynamics CRM не включена для %1.';
      CannotResolveUserFromConnectionSetupErr@1013 : TextConst 'ENU=The Dynamics CRM user that is specified in the CRM connection setup does not exist.;RUS=Пользователь Dynamics CRM, указанный в настройках подключения к CRM, не существует.';
      DetailsMissingErr@1006 : TextConst 'ENU=A Dynamics CRM URL, user name and password are required to enable a connection.;RUS=Для обеспечения подключения необходимы URL-адрес Dynamics CRM, имя пользователя и пароль.';
      UnableToRetrieveCrmVersionErr@1004 : TextConst 'ENU=Unable to retrieve the Dynamics CRM version.;RUS=Не удалось получить версию Dynamics CRM.';
      MissingUsernameTok@1003 : TextConst '@@@={Locked};ENU={USER};RUS={USER}';
      MissingPasswordTok@1005 : TextConst '@@@={Locked};ENU={PASSWORD};RUS={PASSWORD}';
      ProcessDialogMapTitleMsg@1011 : TextConst '@@@=@1 Progress dialog map no.;ENU=Synchronizing @1;RUS=Синхронизация @1';
      ThereIsNoCRMUserErr@1010 : TextConst '@@@="%1 = User Email address";ENU=Cannot find a Dynamics CRM user with the email address %1, so the mapping of the user accounts is disabled. Specify the email address of a Dynamics CRM user in the Authentication Email field, and then enable the mapping again.;RUS=Не удается найти пользователя Dynamics CRM с адресом электронной почты %1, поэтому сопоставление учетных записей пользователей отключено. Укажите адрес электронной почты пользователя Dynamics CRM в поле "Адрес проверки подлинности", а затем снова включите сопоставление.';
      ThereIsNoADCRMUserErr@1017 : TextConst '@@@="%1 = User Name";ENU=Cannot find a Dynamics CRM user %1, so the mapping of the user accounts is disabled. Enable the mapping only if user names match Dynamics CRM user names.;RUS=Cannot find a Dynamics CRM user %1, so the mapping of the user accounts is disabled. Enable the mapping only if user names match Dynamics CRM user names.';
      CannotConnectCRMErr@1014 : TextConst '@@@=%1 - email of the user;ENU=The system is unable to connect to Microsoft Dynamics CRM. Verify credentials of the connection for the user name %1.;RUS=The system is unable to connect to Microsoft Dynamics CRM. Verify credentials of the connection for the user name %1.';
      UserNameMustIncludeDomainErr@1020 : TextConst 'ENU=The user name must include the domain when the authentication type is set to Active Directory.;RUS=The user name must include the domain when the authentication type is set to Active Directory.';
      UserNameMustBeEmailErr@1021 : TextConst 'ENU=The user name must be a valid email address when the authentication type is set to Office 365.;RUS=The user name must be a valid email address when the authentication type is set to Office 365.';
      ConnectionStringPwdPlaceHolderMissingErr@1028 : TextConst 'ENU=The connection string must include the password placeholder {PASSWORD}.;RUS=The connection string must include the password placeholder {PASSWORD}.';

    PROCEDURE CountCRMJobQueueEntries@30(VAR ActiveJobs@1000 : Integer;VAR TotalJobs@1001 : Integer);
    VAR
      JobQueueEntry@1002 : Record 472;
    BEGIN
      IF NOT "Is Enabled" THEN BEGIN
        TotalJobs := 0;
        ActiveJobs := 0;
      END ELSE BEGIN
        IF "Is CRM Solution Installed" THEN
          JobQueueEntry.SETFILTER("Object ID to Run",'%1|%2',CODEUNIT::"Integration Synch. Job Runner",CODEUNIT::"CRM Statistics Job")
        ELSE
          JobQueueEntry.SETRANGE("Object ID to Run",CODEUNIT::"Integration Synch. Job Runner");
        TotalJobs := JobQueueEntry.COUNT;

        JobQueueEntry.SETFILTER(Status,'%1|%2',JobQueueEntry.Status::Ready,JobQueueEntry.Status::"In Process");
        ActiveJobs := JobQueueEntry.COUNT;
      END;
    END;

    PROCEDURE HasPassword@8() : Boolean;
    BEGIN
      EXIT(GetPassword <> '');
    END;

    PROCEDURE SetPassword@1(PasswordText@1000 : Text);
    VAR
      ServicePassword@1002 : Record 1261;
    BEGIN
      IF ISNULLGUID("User Password Key") OR NOT ServicePassword.GET("User Password Key") THEN BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.INSERT(TRUE);
        "User Password Key" := ServicePassword.Key;
      END ELSE BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.MODIFY;
      END;
    END;

    PROCEDURE UpdateAllConnectionRegistrations@4();
    BEGIN
      UNREGISTERTABLECONNECTION(TABLECONNECTIONTYPE::CRM,GETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::CRM));

      UnregisterConnection;
      IF "Is Enabled" THEN
        RegisterUserConnection;
    END;

    PROCEDURE UpdateIsEnabledState@34();
    BEGIN
      "Is User Mapped To CRM User" := IsCurrentUserMappedToCrmSystemUser;
      "Is Enabled For User" :=
        "Is Enabled" AND
        ((NOT "Is User Mapping Required") OR ("Is User Mapping Required" AND "Is User Mapped To CRM User"));
    END;

    LOCAL PROCEDURE UpdateODataUsernameAccesskey@43(User@1000 : Record 2000000120);
    VAR
      IdentityManagement@1001 : Codeunit 9801;
    BEGIN
      "Dynamics NAV OData Username" := User."User Name";
      "Dynamics NAV OData Accesskey" := IdentityManagement.GetWebServicesKey(User."User Security ID");

      CRMIntegrationManagement.SetCRMNAVODataUrlCredentials(
        "Dynamics NAV OData URL","Dynamics NAV OData Username","Dynamics NAV OData Accesskey");
    END;

    PROCEDURE RegisterConnection@17();
    BEGIN
      RegisterConnectionWithName("Primary Key");
    END;

    PROCEDURE RegisterConnectionWithName@12(ConnectionName@1000 : Text);
    BEGIN
      REGISTERTABLECONNECTION(TABLECONNECTIONTYPE::CRM,ConnectionName,GetConnectionStringWithPassword);
      SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::CRM,GetDefaultCRMConnection(ConnectionName));
    END;

    PROCEDURE UnregisterConnection@5() : Boolean;
    BEGIN
      EXIT(UnregisterConnectionWithName("Primary Key"));
    END;

    [TryFunction]
    PROCEDURE UnregisterConnectionWithName@20(ConnectionName@1000 : Text);
    BEGIN
      UNREGISTERTABLECONNECTION(TABLECONNECTIONTYPE::CRM,ConnectionName);
    END;

    PROCEDURE ConstructConnectionString@3() : Text;
    VAR
      Password@1000 : Text;
    BEGIN
      IF "User Name" = '' THEN
        Password := MissingPasswordTok
      ELSE
        Password := GetPassword;
      EXIT(STRSUBSTNO(ConnectionStringFormatTok,"Server Address",GetUserName,Password,CrmAuthenticationType));
    END;

    LOCAL PROCEDURE ConstructConnectionStringWithCalledID@16(CallerID@1000 : Text) : Text;
    BEGIN
      EXIT(STRSUBSTNO(ConnectionWithCallerIdStringFormatTok,"Server Address","User Name",GetPassword,CallerID,CrmAuthenticationType));
    END;

    LOCAL PROCEDURE GetConnectionStringWithPassword@53() ConnectionString : Text;
    VAR
      PasswordPlaceHolderPos@1000 : Integer;
    BEGIN
      IF "Connection String" = '' THEN
        UpdateConnectionString;
      IF "User Name" = '' THEN
        EXIT("Connection String");
      PasswordPlaceHolderPos := STRPOS("Connection String",MissingPasswordTok);
      ConnectionString :=
        COPYSTR("Connection String",1,PasswordPlaceHolderPos - 1) + GetPassword +
        COPYSTR("Connection String",PasswordPlaceHolderPos + STRLEN(MissingPasswordTok));
    END;

    PROCEDURE RegisterUserConnection@6();
    VAR
      SyncUser@1000 : Record 2000000120;
      CallerID@1001 : GUID;
    BEGIN
      RegisterConnection;
      SyncUser."User Name" := COPYSTR("User Name",1,MAXSTRLEN(SyncUser."User Name"));
      SyncUser."Authentication Email" := "User Name";
      IF NOT TryGetSystemUserId(SyncUser,CallerID) THEN BEGIN
        UnregisterConnection;
        ERROR(CannotConnectCRMErr,"User Name");
      END;
      RegisterAuthUserConnection;
    END;

    LOCAL PROCEDURE RegisterAuthUserConnection@42();
    VAR
      User@1001 : Record 2000000120;
      CallerID@1000 : GUID;
    BEGIN
      IF GetUser(User) THEN BEGIN
        IF NOT TryGetSystemUserId(User,CallerID) THEN BEGIN
          UnregisterConnection;
          ERROR(CannotConnectCRMErr,User."Authentication Email");
        END;
        IF NOT ISNULLGUID(CallerID) THEN BEGIN
          UnregisterConnection;
          RegisterConnectionWithCallerID(CallerID);
        END ELSE
          IF "Is User Mapping Required" THEN BEGIN
            UnregisterConnection;
            CASE "Authentication Type" OF
              "Authentication Type"::Office365,"Authentication Type"::OAuth:
                ERROR(ThereIsNoCRMUserErr,User."Authentication Email");
              "Authentication Type"::AD,"Authentication Type"::IFD:
                ERROR(ThereIsNoADCRMUserErr,User."User Name");
            END;
          END;
      END;
    END;

    LOCAL PROCEDURE RegisterConnectionWithCallerID@11(CallerID@1000 : Text);
    BEGIN
      IF "Is Enabled" THEN BEGIN
        REGISTERTABLECONNECTION(TABLECONNECTIONTYPE::CRM,"Primary Key",ConstructConnectionStringWithCalledID(CallerID));
        IF "Primary Key" = '' THEN
          SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::CRM,GetDefaultCRMConnection("Primary Key"));
      END ELSE
        ERROR(CantRegisterDisabledConnectionErr);
    END;

    PROCEDURE GetIntegrationUserID@7() IntegrationUserID : GUID;
    VAR
      CRMSystemuser@1001 : Record 5340;
    BEGIN
      GET;
      TESTFIELD("Is Enabled");
      FilterCRMSystemUser(CRMSystemuser);
      IF CRMSystemuser.FINDFIRST THEN
        IntegrationUserID := CRMSystemuser.SystemUserId;
      IF ISNULLGUID(IntegrationUserID) THEN
        ERROR(CannotResolveUserFromConnectionSetupErr);
    END;

    LOCAL PROCEDURE GetPassword@2() : Text;
    VAR
      ServicePassword@1000 : Record 1261;
    BEGIN
      IF NOT ISNULLGUID("User Password Key") THEN
        IF ServicePassword.GET("User Password Key") THEN
          EXIT(ServicePassword.GetPassword);
    END;

    LOCAL PROCEDURE GetUser@51(VAR User@1000 : Record 2000000120) : Boolean;
    BEGIN
      IF User.GET(DATABASE.USERSECURITYID) THEN
        EXIT(TRUE);
      User.RESET;
      User.SETRANGE("Windows Security ID",SID);
      EXIT(User.FINDFIRST);
    END;

    LOCAL PROCEDURE GetUserName@60() UserName : Text;
    BEGIN
      IF "User Name" = '' THEN
        UserName := MissingUsernameTok
      ELSE
        UserName := COPYSTR("User Name",STRPOS("User Name",'\') + 1);
    END;

    PROCEDURE PerformTestConnection@19();
    BEGIN
      VerifyTestConnection;

      IF "Is User Mapping Required" AND "Is Enabled" THEN
        IF NOT IsCurrentUserMappedToCrmSystemUser THEN BEGIN
          MESSAGE(ConnectionSuccessNotEnabledForCurrentUserMsg,USERID,PRODUCTNAME.SHORT);
          EXIT;
        END;

      MESSAGE(ConnectionSuccessMsg);
    END;

    PROCEDURE VerifyTestConnection@21() : Boolean;
    BEGIN
      IF ("Server Address" = '') OR ("User Name" = '') OR NOT HasPassword THEN
        ERROR(DetailsMissingErr);

      CRMIntegrationManagement.ClearState;

      IF NOT TestConnection THEN
        ERROR(ConnectionErr,CRMIntegrationManagement.GetLastErrorMessage);

      EXIT(TRUE);
    END;

    PROCEDURE TestConnection@9() Success : Boolean;
    VAR
      TestConnectionName@1000 : Text;
    BEGIN
      TestConnectionName := FORMAT(CREATEGUID);
      UnregisterConnectionWithName(TestConnectionName);
      RegisterConnectionWithName(TestConnectionName);
      SETDEFAULTTABLECONNECTION(
        TABLECONNECTIONTYPE::CRM,GetDefaultCRMConnection(TestConnectionName),TRUE);
      Success := TryReadSystemUsers;

      UnregisterConnectionWithName(TestConnectionName);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryReadSystemUsers@14();
    VAR
      CRMSystemuser@1000 : Record 5340;
    BEGIN
      CRMSystemuser.FINDFIRST;
    END;

    LOCAL PROCEDURE CreateOrganizationService@10(VAR service@1000 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.IOrganizationService");
    VAR
      crmConnection@1002 : DotNet "'Microsoft.Xrm.Tooling.Connector, Version=2.2.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Tooling.Connector.CrmServiceClient";
    BEGIN
      crmConnection := crmConnection.CrmServiceClient(ConstructConnectionString);
      IF crmConnection.IsReady THEN BEGIN
        IF NOT ISNULL(crmConnection.OrganizationWebProxyClient) THEN
          service := crmConnection.OrganizationWebProxyClient
        ELSE
          service := crmConnection.OrganizationServiceProxy;
        EXIT;
      END;

      IF NOT ISNULL(crmConnection.LastCrmException) THEN
        ERROR(crmConnection.LastCrmException.Message);
      ERROR(crmConnection.LastCrmError);
    END;

    [TryFunction]
    LOCAL PROCEDURE GetCrmVersion@15(VAR Version@1003 : Text);
    VAR
      service@1000 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.IOrganizationService";
      request@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.OrganizationRequest";
      response@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.OrganizationResponse";
    BEGIN
      IF NOT DoReadCRMData THEN
        EXIT;

      Version := '';
      CreateOrganizationService(service);
      request := request.OrganizationRequest;
      request.RequestName := 'RetrieveVersion';
      response := service.Execute(request);
      IF NOT response.Results.TryGetValue('Version',Version) THEN
        ERROR(UnableToRetrieveCrmVersionErr);
    END;

    PROCEDURE IsVersionValid@33() : Boolean;
    VAR
      Version@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Version";
    BEGIN
      IF "CRM Version" <> '' THEN
        IF Version.TryParse("CRM Version",Version) THEN
          EXIT((Version.Major > 6) AND NOT ((Version.Major = 7) AND (Version.Minor = 1)));
      EXIT(FALSE);
    END;

    PROCEDURE IsCurrentUserMappedToCrmSystemUser@13() : Boolean;
    VAR
      User@1000 : Record 2000000120;
      CRMSystemUserId@1002 : GUID;
    BEGIN
      IF GetUser(User) THEN
        IF TryGetSystemUserId(User,CRMSystemUserId) THEN
          EXIT(NOT ISNULLGUID(CRMSystemUserId));
    END;

    [TryFunction]
    LOCAL PROCEDURE TryGetSystemUserId@18(User@1003 : Record 2000000120;VAR SystemUserId@1001 : GUID);
    VAR
      CRMSystemuser@1002 : Record 5340;
    BEGIN
      // Returns FALSE if CRMSystemuser.FINDFIRST throws an exception, e.g. due to wrong credentials;
      // Returns TRUE regardless of CRMSystemuser.FINDFIRST result,
      // further check of ISNULLGUID(SystemUserId) is required to identify if the user exists
      CLEAR(SystemUserId);
      IF "Is Enabled" THEN
        IF "Is User Mapping Required" THEN BEGIN
          CASE "Authentication Type" OF
            "Authentication Type"::AD,"Authentication Type"::IFD:
              CRMSystemuser.SETRANGE(DomainName,User."User Name");
            "Authentication Type"::Office365,"Authentication Type"::OAuth:
              CRMSystemuser.SETRANGE(InternalEMailAddress,User."Authentication Email");
          END;
          IF CRMSystemuser.FINDFIRST THEN
            SystemUserId := CRMSystemuser.SystemUserId;
        END;
    END;

    PROCEDURE UpdateFromWizard@23(VAR SourceCRMConnectionSetup@1001 : Record 5330;PasswordText@1002 : Text);
    BEGIN
      IF NOT GET THEN BEGIN
        INIT;
        INSERT;
      END;
      VALIDATE("Server Address",SourceCRMConnectionSetup."Server Address");
      VALIDATE("User Name",SourceCRMConnectionSetup."User Name");
      SetPassword(PasswordText);
      MODIFY(TRUE);
    END;

    LOCAL PROCEDURE EnableCRMConnection@24();
    BEGIN
      IF "Is Enabled" = xRec."Is Enabled" THEN
        EXIT;

      IF NOT UnregisterConnection THEN
        CLEARLASTERROR;

      IF "Is Enabled" THEN BEGIN
        VerifyTestConnection;
        RegisterUserConnection;
        EnableIntegrationTables;
      END ELSE BEGIN
        "CRM Version" := '';
        "Is S.Order Integration Enabled" := FALSE;
        "Is CRM Solution Installed" := FALSE;
      END;
    END;

    LOCAL PROCEDURE EnableIntegrationTables@27();
    VAR
      IntegrationTableMapping@1003 : Record 5335;
      IntegrationRecord@1002 : Record 5151;
      IntegrationManagement@1001 : Codeunit 5150;
      CRMSetupDefaults@1000 : Codeunit 5334;
    BEGIN
      IF IntegrationRecord.ISEMPTY THEN
        IntegrationManagement.SetupIntegrationTables;
      IntegrationManagement.SetConnectorIsEnabledForSession(TRUE);
      IF IntegrationTableMapping.ISEMPTY THEN BEGIN
        MODIFY; // Job Queue to read "Is Enabled"
        COMMIT;
        CRMSetupDefaults.ResetConfiguration(Rec);
      END;
    END;

    PROCEDURE EnableCRMConnectionFromWizard@25();
    VAR
      CRMSystemuser@1001 : Record 5340;
    BEGIN
      GET;
      "Is User Mapping Required" := FALSE;
      VALIDATE("Is Enabled",TRUE);
      MODIFY(TRUE);

      FilterCRMSystemUser(CRMSystemuser);
      CRMSystemuser.FINDFIRST;
      IF (CRMSystemuser.InviteStatusCode <> CRMSystemuser.InviteStatusCode::InvitationAccepted) OR
         (NOT CRMSystemuser.IsIntegrationUser)
      THEN BEGIN
        CRMSystemuser.InviteStatusCode := CRMSystemuser.InviteStatusCode::InvitationAccepted;
        CRMSystemuser.IsIntegrationUser := TRUE;
        CRMSystemuser.MODIFY(TRUE);
      END;
    END;

    PROCEDURE SetCRMSOPEnabled@37();
    BEGIN
      SetCRMSOPEnabledWithCredentials('',CREATEGUID);
    END;

    PROCEDURE SetCRMSOPEnabledWithCredentials@36(AdminEmail@1003 : Text[250];AdminPassKey@1004 : GUID);
    VAR
      CRMOrganization@1000 : Record 5363;
      TempCRMConnectionSetup@1001 : TEMPORARY Record 5330;
      ServicePassword@1005 : Record 1261;
      ConnectionName@1002 : Text;
    BEGIN
      CreateTempAdminConnection(TempCRMConnectionSetup);
      IF (AdminEmail <> '') AND (NOT ISNULLGUID(AdminPassKey)) THEN BEGIN
        ServicePassword.GET(AdminPassKey);
        TempCRMConnectionSetup.SetPassword(ServicePassword.GetPassword);
        TempCRMConnectionSetup.VALIDATE("User Name",AdminEmail);
      END;
      ConnectionName := FORMAT(CREATEGUID);
      TempCRMConnectionSetup.RegisterConnectionWithName(ConnectionName);
      SETDEFAULTTABLECONNECTION(
        TABLECONNECTIONTYPE::CRM,GetDefaultCRMConnection(ConnectionName),TRUE);

      CRMOrganization.FINDFIRST;
      IF NOT CRMOrganization.IsSOPIntegrationEnabled THEN BEGIN
        CRMOrganization.IsSOPIntegrationEnabled := TRUE;
        CRMOrganization.MODIFY(TRUE);
      END;

      TempCRMConnectionSetup.UnregisterConnectionWithName(ConnectionName);
    END;

    PROCEDURE SetUserAsIntegrationUser@39();
    VAR
      CRMSystemuser@1000 : Record 5340;
      TempCRMConnectionSetup@1001 : TEMPORARY Record 5330;
      ConnectionName@1002 : Text;
    BEGIN
      CreateTempAdminConnection(TempCRMConnectionSetup);
      ConnectionName := FORMAT(CREATEGUID);
      TempCRMConnectionSetup.RegisterConnectionWithName(ConnectionName);
      SETDEFAULTTABLECONNECTION(
        TABLECONNECTIONTYPE::CRM,GetDefaultCRMConnection(ConnectionName),TRUE);
      FilterCRMSystemUser(CRMSystemuser);
      CRMSystemuser.FINDFIRST;

      IF (CRMSystemuser.InviteStatusCode <> CRMSystemuser.InviteStatusCode::InvitationAccepted) OR
         (NOT CRMSystemuser.IsIntegrationUser)
      THEN BEGIN
        CRMSystemuser.InviteStatusCode := CRMSystemuser.InviteStatusCode::InvitationAccepted;
        CRMSystemuser.IsIntegrationUser := TRUE;
        CRMSystemuser.MODIFY(TRUE);
      END;

      TempCRMConnectionSetup.UnregisterConnectionWithName(ConnectionName);
    END;

    LOCAL PROCEDURE CreateTempAdminConnection@40(VAR CRMConnectionSetup@1000 : Record 5330);
    BEGIN
      CreateTempNoDelegateConnection(CRMConnectionSetup);
      CLEAR(CRMConnectionSetup."User Password Key");
      CRMConnectionSetup.VALIDATE("User Name",'');
    END;

    LOCAL PROCEDURE CreateTempNoDelegateConnection@41(VAR CRMConnectionSetup@1000 : Record 5330);
    BEGIN
      CRMConnectionSetup.INIT;
      CRMConnectionSetup.TRANSFERFIELDS(Rec);
      CRMConnectionSetup."Primary Key" := COPYSTR('TEMP' + "Primary Key",1,MAXSTRLEN(CRMConnectionSetup."Primary Key"));
      CRMConnectionSetup."Is Enabled" := TRUE;
      CRMConnectionSetup."Is User Mapping Required" := FALSE;
    END;

    PROCEDURE RefreshDataFromCRM@31();
    VAR
      TempCRMConnectionSetup@1000 : TEMPORARY Record 5330;
      ConnectionName@1001 : Text;
    BEGIN
      IF "Is Enabled" THEN BEGIN
        IF "Is User Mapping Required" THEN BEGIN
          CreateTempNoDelegateConnection(TempCRMConnectionSetup);
          ConnectionName := FORMAT(CREATEGUID);
          TempCRMConnectionSetup.RegisterConnectionWithName(ConnectionName);
          SETDEFAULTTABLECONNECTION(
            TABLECONNECTIONTYPE::CRM,GetDefaultCRMConnection(ConnectionName),TRUE);

          "Is User Mapped To CRM User" := IsCurrentUserMappedToCrmSystemUser;
        END;

        "Is CRM Solution Installed" := CRMIntegrationManagement.IsCRMSolutionInstalled;
        RefreshFromCRMConnectionInformation;
        IF TryRefreshCRMSettings THEN;

        IF ConnectionName <> '' THEN
          TempCRMConnectionSetup.UnregisterConnectionWithName(ConnectionName);
      END;
    END;

    LOCAL PROCEDURE RefreshFromCRMConnectionInformation@29();
    VAR
      CRMNAVConnection@1001 : Record 5368;
    BEGIN
      IF "Is CRM Solution Installed" THEN
        IF CRMNAVConnection.FINDFIRST THEN BEGIN
          "Dynamics NAV URL" := CRMNAVConnection."Dynamics NAV URL";
          "Dynamics NAV OData URL" := CRMNAVConnection."Dynamics NAV OData URL";
          "Dynamics NAV OData Username" := CRMNAVConnection."Dynamics NAV OData Username";
          "Dynamics NAV OData Accesskey" := CRMNAVConnection."Dynamics NAV OData Accesskey";
        END;
    END;

    [TryFunction]
    LOCAL PROCEDURE TryRefreshCRMSettings@26();
    VAR
      CRMOrganization@1001 : Record 5363;
    BEGIN
      GetCrmVersion("CRM Version");
      VALIDATE("CRM Version");

      IF CRMOrganization.FINDFIRST THEN
        "Is S.Order Integration Enabled" := CRMOrganization.IsSOPIntegrationEnabled
      ELSE
        "Is S.Order Integration Enabled" := FALSE;
    END;

    PROCEDURE PerformWebClientUrlReset@32();
    VAR
      TempCRMConnectionSetup@1000 : TEMPORARY Record 5330;
      CRMSetupDefaults@1002 : Codeunit 5334;
      ConnectionName@1001 : Text;
    BEGIN
      CreateTempNoDelegateConnection(TempCRMConnectionSetup);
      ConnectionName := FORMAT(CREATEGUID);
      TempCRMConnectionSetup.RegisterConnectionWithName(ConnectionName);
      SETDEFAULTTABLECONNECTION(
        TABLECONNECTIONTYPE::CRM,GetDefaultCRMConnection(ConnectionName),TRUE);

      CRMSetupDefaults.ResetCRMNAVConnectionData;

      TempCRMConnectionSetup.UnregisterConnectionWithName(ConnectionName);

      RefreshDataFromCRM;
    END;

    PROCEDURE PerformInitialSynch@38();
    VAR
      JobQueueEntry@1000 : Record 472;
      IntegrationTableMapping@1001 : Record 5335;
      TempActiveQueuesNameValueBuffer@1002 : TEMPORARY Record 823;
      TempCoupledOnlyMappingsNameValueBuffer@1003 : TEMPORARY Record 823;
      IntegrationSynchJobList@1004 : Page 5338;
    BEGIN
      JobQueueEntry.SETRANGE("Object ID to Run",CODEUNIT::"Integration Synch. Job Runner");
      JobQueueEntry.SETRANGE(Status,JobQueueEntry.Status::Ready);
      IF JobQueueEntry.FINDSET THEN
        REPEAT
          TempActiveQueuesNameValueBuffer.AddNewEntry(JobQueueEntry.ID,'');
        UNTIL JobQueueEntry.NEXT = 0;
      JobQueueEntry.MODIFYALL(Status,JobQueueEntry.Status::"On Hold",TRUE);

      IntegrationTableMapping.SETRANGE("Parent Name",'''''');
      IntegrationTableMapping.SETRANGE("Synch. Only Coupled Records",TRUE);
      IF IntegrationTableMapping.FINDSET THEN
        REPEAT
          TempCoupledOnlyMappingsNameValueBuffer.AddNewEntry(IntegrationTableMapping.Name,'');
        UNTIL IntegrationTableMapping.NEXT = 0;
      IntegrationTableMapping.MODIFYALL("Synch. Only Coupled Records",FALSE,TRUE);

      SynchronizeNow(TRUE);

      IF NOT CONFIRM(STRSUBSTNO(AllowNewUncoupledItemsToBeSynchronizedQst,IntegrationSynchJobList.CAPTION)) THEN
        IF TempCoupledOnlyMappingsNameValueBuffer.FINDSET THEN
          REPEAT
            IntegrationTableMapping.GET(TempCoupledOnlyMappingsNameValueBuffer.Name);
            IntegrationTableMapping."Synch. Only Coupled Records" := TRUE;
            IntegrationTableMapping.MODIFY(TRUE);
          UNTIL TempCoupledOnlyMappingsNameValueBuffer.NEXT = 0;

      IF TempActiveQueuesNameValueBuffer.FINDSET THEN
        REPEAT
          JobQueueEntry.GET(TempActiveQueuesNameValueBuffer.Name);
          JobQueueEntry.SetStatus(JobQueueEntry.Status::Ready);
        UNTIL TempActiveQueuesNameValueBuffer.NEXT = 0;
    END;

    PROCEDURE SynchronizeNow@44(DoFullSynch@1000 : Boolean);
    VAR
      IntegrationTableMapping@1006 : Record 5335;
      TempNameValueBuffer@1005 : TEMPORARY Record 823;
      CRMSetupDefaults@1004 : Codeunit 5334;
      ProgressWindow@1003 : Dialog;
      MappingCount@1002 : Integer;
      CurrentMappingIndex@1001 : Integer;
    BEGIN
      CRMSetupDefaults.GetPrioritizedMappingList(TempNameValueBuffer);
      TempNameValueBuffer.ASCENDING(TRUE);
      TempNameValueBuffer.FINDSET;

      CurrentMappingIndex := 0;
      MappingCount := TempNameValueBuffer.COUNT;
      ProgressWindow.OPEN(ProcessDialogMapTitleMsg,CurrentMappingIndex);
      REPEAT
        CurrentMappingIndex := CurrentMappingIndex + 1;
        ProgressWindow.UPDATE(1,ROUND(CurrentMappingIndex / MappingCount * 10000,1));
        IF IntegrationTableMapping.GET(TempNameValueBuffer.Value) THEN
          IntegrationTableMapping.SynchronizeNow(DoFullSynch);
      UNTIL TempNameValueBuffer.NEXT = 0;
      ProgressWindow.CLOSE;
    END;

    PROCEDURE DoReadCRMData@45() : Boolean;
    VAR
      SkipReading@1000 : Boolean;
    BEGIN
      OnReadingCRMData(SkipReading);
      EXIT(NOT SkipReading);
    END;

    [Integration]
    LOCAL PROCEDURE OnReadingCRMData@22(VAR SkipReading@1000 : Boolean);
    BEGIN
    END;

    LOCAL PROCEDURE GetDefaultCRMConnection@46(ConnectionName@1000 : Text) : Text;
    BEGIN
      OnGetDefaultCRMConnection(ConnectionName);
      EXIT(ConnectionName);
    END;

    [Integration]
    LOCAL PROCEDURE OnGetDefaultCRMConnection@48(VAR ConnectionName@1000 : Text);
    BEGIN
    END;

    LOCAL PROCEDURE CrmAuthenticationType@49() : Text;
    BEGIN
      CASE "Authentication Type" OF
        "Authentication Type"::Office365:
          EXIT('AuthType=Office365;');
        "Authentication Type"::AD:
          EXIT('AuthType=AD;' + GetDomain);
        "Authentication Type"::IFD:
          EXIT('AuthType=IFD;' + GetDomain + 'HomeRealmUri= ;');
        "Authentication Type"::OAuth:
          EXIT('AuthType=OAuth;' + 'AppId= ;' + 'RedirectUri= ;' + 'TokenCacheStorePath= ;' + 'LoginPrompt=Auto;');
      END;
    END;

    PROCEDURE UpdateConnectionString@50();
    BEGIN
      VALIDATE(
        "Connection String",
        STRSUBSTNO(
          ConnectionStringFormatTok,"Server Address",GetUserName,MissingPasswordTok,CrmAuthenticationType));
    END;

    LOCAL PROCEDURE UpdateDomainName@54();
    BEGIN
      IF "User Name" <> '' THEN
        IF STRPOS("User Name",'\') > 0 THEN
          VALIDATE(Domain,COPYSTR("User Name",1,STRPOS("User Name",'\') - 1))
        ELSE
          Domain := '';
    END;

    LOCAL PROCEDURE CheckUserName@47();
    BEGIN
      IF "User Name" <> '' THEN
        CASE "Authentication Type" OF
          "Authentication Type"::AD:
            IF STRPOS("User Name",'\') = 0 THEN
              ERROR(UserNameMustIncludeDomainErr);
          "Authentication Type"::Office365:
            IF STRPOS("User Name",'@') = 0 THEN
              ERROR(UserNameMustBeEmailErr);
        END;
    END;

    LOCAL PROCEDURE GetDomain@68() : Text;
    BEGIN
      IF Domain <> '' THEN
        EXIT(STRSUBSTNO('Domain=%1;',Domain));
    END;

    LOCAL PROCEDURE FilterCRMSystemUser@57(VAR CRMSystemuser@1000 : Record 5340);
    BEGIN
      CASE "Authentication Type" OF
        "Authentication Type"::Office365,"Authentication Type"::OAuth:
          CRMSystemuser.SETRANGE(InternalEMailAddress,"User Name");
        "Authentication Type"::AD,"Authentication Type"::IFD:
          CRMSystemuser.SETRANGE(DomainName,"User Name");
      END;
    END;

    BEGIN
    END.
  }
}

