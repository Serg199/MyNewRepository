OBJECT Report 12402 Cash Outgoing Order
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Cash Outgoing Order;
               RUS=Расходный кассовый ордер];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CompanyInfo.GET;
                  EmployeeDirector.GET(CompanyInfo."Director No.");

                  CashOrderReportHelper.InitOutgoingReportTmpl;
                END;

    OnPostReport=BEGIN
                   IF FileName <> '' THEN
                     CashOrderReportHelper.ExportDataFile(FileName)
                   ELSE
                     CashOrderReportHelper.ExportData;
                 END;

  }
  DATASET
  {
    { 7024;    ;DataItem;                    ;
               DataItemTable=Table81;
               DataItemTableView=SORTING(Journal Template Name,Journal Batch Name,Line No.);
               OnAfterGetRecord=BEGIN
                                  CurrencyText := CurrencyTxt;

                                  TESTFIELD("Bal. Account Type","Bal. Account Type"::"Bank Account");
                                  TESTFIELD("Bal. Account No.");
                                  TESTFIELD("Bank Payment Type");

                                  GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
                                  GenJnlBatch.TESTFIELD("No. Series",'');
                                  GenJnlBatch.TESTFIELD("Posting No. Series",'');

                                  BankAcc.GET("Bal. Account No.");
                                  BankAcc.TESTFIELD("Account Type",BankAcc."Account Type"::"Cash Account");

                                  IF BankAcc."Currency Code" <> '' THEN BEGIN
                                    TESTFIELD("Currency Code",BankAcc."Currency Code");
                                    Currency.GET("Gen. Journal Line"."Currency Code");
                                    CurrencyText := Currency.Description;
                                  END;

                                  CashierFullName := BankAcc.Contact;

                                  WrittenAmount := LocMgt.Amount2Text("Currency Code","Debit Amount");
                                  IF "Debit Amount" < 0 THEN
                                    WrittenAmount := MinusTxt + WrittenAmount;

                                  IF NOT "Check Printed" THEN
                                    IF NOT PrintTest THEN BEGIN
                                      IF (("Account Type" = "Account Type"::"Bank Account") OR
                                          ("Bal. Account Type" = "Bal. Account Type"::"Bank Account")) AND
                                         ((Amount < 0) OR (BankAcc."Account Type" = BankAcc."Account Type"::"Cash Account"))
                                      THEN
                                        TESTFIELD("Bank Payment Type");
                                      IF "Bank Payment Type" = "Bank Payment Type"::"Manual Check" THEN BEGIN
                                        TESTFIELD("Document No.");
                                        IF "Posting No. Series" <> '' THEN
                                          IF NOT CONFIRM(PrintQst,
                                               FALSE,FIELDCAPTION("Posting No. Series"),FIELDCAPTION("Document No."))
                                          THEN
                                            CurrReport.QUIT;
                                        "External Document No." := "Document No.";
                                        MODIFY;
                                      END ELSE BEGIN
                                        IF "Posting No. Series" <> '' THEN
                                          FIELDERROR("Posting No. Series",
                                            STRSUBSTNO(CannotBeUsedErr,
                                              FIELDCAPTION("Bank Payment Type"),"Bank Payment Type"));

                                        BankAcc.TESTFIELD("Credit Cash Order No. Series");

                                        IF "Document No." = '' THEN
                                          "Document No." := NoSeriesMgt.GetNextNo(BankAcc."Credit Cash Order No. Series",0D,TRUE)
                                        ELSE BEGIN
                                          LastNo := NoSeriesMgt.GetNextNo(BankAcc."Credit Cash Order No. Series",0D,FALSE);
                                          CLEAR(NoSeriesMgt);
                                          IF (DELCHR("Document No.",'<>','0123456789') <> DELCHR(LastNo,'<>','0123456789')) OR
                                             ("Document No." > LastNo)
                                          THEN
                                            ERROR(WrongDocumentNoErr,"Document No.",LastNo);
                                          IF "Document No." = LastNo THEN
                                            NoSeriesMgt.GetNextNo(BankAcc."Credit Cash Order No. Series",0D,TRUE);
                                        END;
                                        TESTFIELD("Document No.");
                                        "Check Printed" := TRUE;
                                        IF "External Document No." = '' THEN
                                          "External Document No." := "Document No.";

                                        CheckLedgEntry.INIT;
                                        CheckLedgEntry."Bank Account No." := "Bal. Account No.";
                                        CheckLedgEntry."Posting Date" := "Posting Date";
                                        CheckLedgEntry."Check Date" := "Posting Date";
                                        CheckLedgEntry."Document Type" := "Document Type";
                                        CheckLedgEntry."Document No." := "Document No.";
                                        CheckLedgEntry.Description := Description;
                                        CheckLedgEntry."Check No." := "Document No.";
                                        CheckLedgEntry."Payment Purpose" := "Payment Purpose";
                                        CheckLedgEntry."Cash Order Including" := "Cash Order Including";
                                        CheckLedgEntry."Cash Order Supplement" := "Cash Order Supplement";
                                        CheckLedgEntry.Amount := -"Debit Amount";
                                        CheckLedgEntry."Credit Amount" := "Debit Amount";
                                        CheckLedgEntry.Positive := FALSE;
                                        CheckLedgEntry."Bal. Account Type" := "Account Type";
                                        CheckLedgEntry."Bal. Account No." := "Account No.";
                                        CheckLedgEntry."External Document No." := "External Document No.";
                                        CheckLedgEntry."Entry Status" := CheckLedgEntry."Entry Status"::Printed;
                                        CheckLedgEntry."Bank Payment Type" := "Bank Payment Type";
                                        CheckManagement.InsertCheck(CheckLedgEntry,RECORDID);
                                        MODIFY;
                                      END;
                                    END;

                                  BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
                                  CreditAccNo := BankAccPostingGr."G/L Bank Account No.";

                                  CASE "Account Type" OF
                                    "Account Type"::Customer:
                                      BEGIN
                                        CustPostingGr.GET("Posting Group");
                                        BalAccNo := CustPostingGr."Receivables Account";
                                        IF Prepayment THEN
                                          BalAccNo := CustPostingGr."Prepayment Account";
                                      END;
                                    "Account Type"::Vendor:
                                      BEGIN
                                        VendPostingGr.GET("Posting Group");
                                        BalAccNo := VendPostingGr."Payables Account";
                                        IF Prepayment THEN
                                          BalAccNo := VendPostingGr."Prepayment Account";
                                      END;
                                    "Account Type"::"Bank Account":
                                      BEGIN
                                        BankAcc2.GET("Account No.");
                                        BankAccPostingGr.GET(BankAcc2."Bank Acc. Posting Group");
                                        BalAccNo := BankAccPostingGr."G/L Bank Account No.";
                                      END;
                                    "Account Type"::"G/L Account":
                                      BEGIN
                                        BalAccNo := "Account No.";
                                        "Account No." := '';
                                      END;
                                  END;

                                  PaymentCheckNo :=
                                    LocMgt.DigitalPartCode("Document No.");

                                  FillBody("Gen. Journal Line");
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Test Print;
                             RUS=Тестовая печать];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PrintTest }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PrintQst@1210001 : TextConst 'ENU=If %1 is not empty then it will be changed during posting\\Do you want to continue printing?;RUS=Если значение %1 не пустое, оно будет изменено во время учета.\\Продолжить печать?';
      CannotBeUsedErr@1210002 : TextConst '@@@=Parameter 1 - field caption, parameter 2 - bank payment type;ENU=Cannot be used if %1 %2.;RUS=Не может быть использовано, если %1 %2.';
      CompanyInfo@1210004 : Record 79;
      BankAcc@1210005 : Record 270;
      CheckLedgEntry@1210006 : Record 272;
      CustPostingGr@1210007 : Record 92;
      VendPostingGr@1210008 : Record 93;
      BankAccPostingGr@1210009 : Record 277;
      GenJnlBatch@1210003 : Record 232;
      BankAcc2@1210000 : Record 270;
      Currency@1210012 : Record 4;
      EmployeeDirector@1170000001 : Record 5200;
      CheckManagement@1210010 : Codeunit 367;
      LocMgt@1210011 : Codeunit 12400;
      NoSeriesMgt@1210021 : Codeunit 396;
      LocRepMgt@1210027 : Codeunit 12401;
      CashOrderReportHelper@1210024 : Codeunit 14936;
      FileName@1210016 : Text;
      WrittenAmount@1210013 : Text[250];
      BalAccNo@1210014 : Code[10];
      PrintTest@1210015 : Boolean;
      CreditAccNo@1210017 : Code[20];
      PaymentCheckNo@1210018 : Code[20];
      CashierFullName@1210019 : Text[50];
      LastNo@1210022 : Code[20];
      WrongDocumentNoErr@1210023 : TextConst 'ENU=The number of the document %1 does not match the input mask %2 or greater than the last number.;RUS=Номер документа %1 не соответствует входной маске %2 или превышает последний номер.';
      CurrencyTxt@1210025 : TextConst 'ENU=Rub., kop.;RUS=Руб., коп.';
      CurrencyText@1210026 : Text[30];
      MinusTxt@1470000 : TextConst 'ENU=(minus);RUS=(минус)';

    PROCEDURE TransferReportValues@1210006(VAR ReportValues@1210004 : ARRAY [18] OF Text;GenGnlLine@1210000 : Record 81);
    BEGIN
      ReportValues[1] := LocRepMgt.GetCompanyName;
      ReportValues[2] := CompanyInfo."OKPO Code";
      ReportValues[3] := PaymentCheckNo;
      ReportValues[4] := LocMgt.FormatDate(GenGnlLine."Posting Date");
      ReportValues[5] := CurrencyText;
      ReportValues[6] := BalAccNo;
      ReportValues[7] := CreditAccNo;
      ReportValues[8] := LocRepMgt.FormatReportValue(GenGnlLine."Debit Amount",2);
      ReportValues[9] := GenGnlLine."Reason Code";
      ReportValues[10] := GenGnlLine.Description;
      ReportValues[11] := GenGnlLine."Payment Purpose";
      ReportValues[12] := WrittenAmount;
      ReportValues[13] := GenGnlLine."Cash Order Supplement";
      ReportValues[14] := CompanyInfo."Director Name";
      ReportValues[15] := CompanyInfo."Accountant Name";
      ReportValues[16] := GenGnlLine."Cash Order Including";
      ReportValues[17] := CashierFullName;
      ReportValues[18] := EmployeeDirector."Job Title";
    END;

    LOCAL PROCEDURE FillBody@1210003(GenGnlLine@1210000 : Record 81);
    VAR
      ReportValues@1210001 : ARRAY [18] OF Text;
    BEGIN
      TransferReportValues(ReportValues,GenGnlLine);
      CashOrderReportHelper.FillBodyOutgoing(ReportValues);
    END;

    PROCEDURE SetFileNameSilent@1210000(NewFileName@1210001 : Text);
    BEGIN
      FileName := NewFileName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

