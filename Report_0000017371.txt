OBJECT Report 17371 Create Positions from Budget
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Positions from Budget;
               RUS=Создать позиции из бюджета];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   IF RemPositionRate < 0 THEN
                     ERROR(Text14701);

                   IF CONFIRM(Text14700,FALSE,PositionNumber,PositionRate,Position."No.") THEN BEGIN
                     WHILE PositionNumber > 0 DO BEGIN
                       Position2.INIT;
                       Position2.TRANSFERFIELDS(Position,FALSE);

                       Position2."No." := '';
                       Position2.Status := Position2.Status::Planned;
                       Position2.VALIDATE("Budgeted Position",FALSE);
                       Position2.VALIDATE("Budgeted Position No.",Position."No.");
                       Position2.VALIDATE("No.",PositionNo);
                       Position2.Status := Position2.Status::Planned;
                       Position2.VALIDATE(Rate,PositionRate);
                       Position2."Created By User" := USERID;
                       Position2."Creation Date" := TODAY;
                       Position2."Approved By User" := '';
                       Position2."Approval Date" := 0D;
                       Position2."Closed By User" := '';
                       Position2."Closing Date" := 0D;
                       Position2.INSERT(TRUE);
                       Position2.CopyContractTerms;

                       PositionNumber := PositionNumber - 1;
                       Position.Rate := Position.Rate - Position2.Rate;
                     END;
                     Position.MODIFY;
                   END;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   Position.TESTFIELD("No.");
                   Position.TESTFIELD(Rate);
                   Position.TESTFIELD(Status,Position.Status::Approved);
                   PositionRate := 1;
                   UpdateRate;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1210003;2;Field     ;
                  CaptionML=[ENU=Available Position Rate;
                             RUS=Доступная ставка позиции];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Position.Rate;
                  Editable=FALSE }

      { 1210000;2;Field     ;
                  CaptionML=[ENU=Number of Positions;
                             RUS=Количество позиций];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PositionNumber;
                  OnValidate=BEGIN
                               UpdateRate;
                             END;
                              }

      { 1210005;2;Field     ;
                  CaptionML=[ENU=Position Rate;
                             RUS=Ставка позиции];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PositionRate;
                  OnValidate=BEGIN
                               UpdateRate;
                             END;
                              }

      { 1210006;2;Field     ;
                  CaptionML=[ENU=Remaining Position Rate;
                             RUS=Оставшаяся ставка позиции];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=RemPositionRate;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Position@1210001 : Record 17370;
      Position2@1210002 : Record 17370;
      PositionNumber@1210000 : Integer;
      PositionRate@1210006 : Decimal;
      PositionNo@1210003 : Code[20];
      Text14700@1210004 : TextConst 'ENU=%1 position(s) with rate %2 will be created from budget position %3. Continue?;RUS=Из бюджетной позиции %3 будет создано позиций: %1 со ставкой %2. Продолжить?';
      RemPositionRate@1210007 : Decimal;
      Text14701@1210008 : TextConst 'ENU=Remaining position rate cannot be negative.;RUS=Оставшаяся ставка позиции не может быть отрицательной';

    PROCEDURE Set@1210000(NewPosition@1210000 : Record 17370);
    BEGIN
      Position := NewPosition;
      Position.GET(Position."No.");
      Position.TESTFIELD("Budgeted Position",TRUE);
    END;

    PROCEDURE UpdateRate@1210001();
    BEGIN
      RemPositionRate :=
        Position.Rate - PositionNumber * PositionRate;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

