OBJECT Table 1701 Deferral Header
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Schedule Description;
    OnDelete=VAR
               DeferralLine@1000 : Record 1702;
             BEGIN
               // If the user deletes the header, all associated lines should also be deleted
               DeferralLine.SETRANGE("Deferral Doc. Type","Deferral Doc. Type");
               DeferralLine.SETRANGE("Gen. Jnl. Template Name","Gen. Jnl. Template Name");
               DeferralLine.SETRANGE("Gen. Jnl. Batch Name","Gen. Jnl. Batch Name");
               DeferralLine.SETRANGE("Document Type","Document Type");
               DeferralLine.SETRANGE("Document No.","Document No.");
               DeferralLine.SETRANGE("Line No.","Line No.");
               DeferralLine.DELETEALL;
             END;

    CaptionML=[ENU=Deferral Header;
               RUS=Заголовок РБП];
  }
  FIELDS
  {
    { 1   ;   ;Deferral Doc. Type  ;Option        ;CaptionML=[ENU=Deferral Doc. Type;
                                                              RUS=Тип документа РБП];
                                                   OptionCaptionML=[ENU=Purchase,Sales,G/L;
                                                                    RUS=Покупка,Продажа,ГК];
                                                   OptionString=Purchase,Sales,G/L }
    { 2   ;   ;Gen. Jnl. Template Name;Code10     ;CaptionML=[ENU=Gen. Jnl. Template Name;
                                                              RUS=Название шаблона фин. журнала] }
    { 3   ;   ;Gen. Jnl. Batch Name;Code10        ;CaptionML=[ENU=Gen. Jnl. Batch Name;
                                                              RUS=Код раздела фин. журнала] }
    { 4   ;   ;Document Type       ;Integer       ;CaptionML=[ENU=Document Type;
                                                              RUS=Тип документа] }
    { 5   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              RUS=Номер документа] }
    { 6   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              RUS=Номер строки] }
    { 7   ;   ;Deferral Code       ;Code10        ;CaptionML=[ENU=Deferral Code;
                                                              RUS=Код РБП];
                                                   NotBlank=Yes }
    { 8   ;   ;Amount to Defer     ;Decimal       ;OnValidate=BEGIN
                                                                IF "Initial Amount to Defer" < 0 THEN BEGIN// Negative amount
                                                                  IF "Amount to Defer" < "Initial Amount to Defer" THEN
                                                                    ERROR(AmountToDeferErr);
                                                                  IF "Amount to Defer" > 0 THEN
                                                                    ERROR(AmountToDeferErr)
                                                                END;

                                                                IF "Initial Amount to Defer" >= 0 THEN BEGIN// Positive amount
                                                                  IF "Amount to Defer" > "Initial Amount to Defer" THEN
                                                                    ERROR(AmountToDeferErr);
                                                                  IF "Amount to Defer" < 0 THEN
                                                                    ERROR(AmountToDeferErr);
                                                                END;

                                                                IF "Amount to Defer" = 0 THEN
                                                                  ERROR(ZeroAmountToDeferErr);
                                                              END;

                                                   CaptionML=[ENU=Amount to Defer;
                                                              RUS=Сумма, относимая на РБП];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 9   ;   ;Amount to Defer (LCY);Decimal      ;CaptionML=[ENU=Amount to Defer (LCY);
                                                              RUS=Сумма, относимая на РБП (руб)];
                                                   AutoFormatType=1 }
    { 10  ;   ;Calc. Method        ;Option        ;CaptionML=[ENU=Calc. Method;
                                                              RUS=Метод расчета];
                                                   OptionCaptionML=[ENU=Straight-Line,Equal per Period,Days per Period,User-Defined;
                                                                    RUS=Линейный,Равномерно по периодам,Дни по периодам,Определяется пользователем];
                                                   OptionString=Straight-Line,Equal per Period,Days per Period,User-Defined }
    { 11  ;   ;Start Date          ;Date          ;OnValidate=VAR
                                                                AccountingPeriod@1000 : Record 50;
                                                              BEGIN
                                                                IF GenJnlCheckLine.DateNotAllowed("Start Date") THEN
                                                                  ERROR(InvalidPostingDateErr,"Start Date");

                                                                AccountingPeriod.SETFILTER("Starting Date",'>=%1',"Start Date");
                                                                IF AccountingPeriod.ISEMPTY THEN
                                                                  ERROR(DeferSchedOutOfBoundsErr);
                                                              END;

                                                   CaptionML=[ENU=Start Date;
                                                              RUS=Дата начала] }
    { 12  ;   ;No. of Periods      ;Integer       ;OnValidate=BEGIN
                                                                IF "No. of Periods" < 1 THEN
                                                                  ERROR(NumberofPeriodsErr);
                                                              END;

                                                   CaptionML=[ENU=No. of Periods;
                                                              RUS=Число периодов];
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 13  ;   ;Schedule Description;Text50        ;CaptionML=[ENU=Schedule Description;
                                                              RUS=Описание расписания] }
    { 14  ;   ;Initial Amount to Defer;Decimal    ;CaptionML=[ENU=Initial Amount to Defer;
                                                              RUS=Начальная сумма, относимая на РБП] }
    { 15  ;   ;Currency Code       ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency Code;
                                                              RUS=Код валюты] }
    { 20  ;   ;Schedule Line Total ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Deferral Line".Amount WHERE (Deferral Doc. Type=FIELD(Deferral Doc. Type),
                                                                                                 Gen. Jnl. Template Name=FIELD(Gen. Jnl. Template Name),
                                                                                                 Gen. Jnl. Batch Name=FIELD(Gen. Jnl. Batch Name),
                                                                                                 Document Type=FIELD(Document Type),
                                                                                                 Document No.=FIELD(Document No.),
                                                                                                 Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Schedule Line Total;
                                                              RUS=Итог по строке отчета] }
  }
  KEYS
  {
    {    ;Deferral Doc. Type,Gen. Jnl. Template Name,Gen. Jnl. Batch Name,Document Type,Document No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AmountToDeferErr@1000 : TextConst 'ENU=The deferred amount cannot be greater than the document line amount.;RUS=Сумма, отнесенная на РБП, не может быть большей, чем сумма строки документа.';
      GenJnlCheckLine@1001 : Codeunit 11;
      InvalidPostingDateErr@1004 : TextConst '@@@="%1=The date passed in for the posting date.";ENU=%1 is not within the range of posting dates for your company.;RUS=%1 не входит в диапазон дат учета для вашей компании.';
      DeferSchedOutOfBoundsErr@1003 : TextConst 'ENU=The deferral schedule falls outside the accounting periods that have been set up for the company.;RUS=План РБП выходит за пределы настроенных для компании учетных периодов.';
      SelectionMsg@1005 : TextConst 'ENU=You must specify a deferral code for this line before you can view the deferral schedule.;RUS=Необходимо указать код РБП для этой строки, прежде чем можно будет просматривать план РБП.';
      DeferralUtilities@1006 : Codeunit 1720;
      NumberofPeriodsErr@1007 : TextConst 'ENU=You must specify one or more periods.;RUS=Необходимо указать один или несколько периодов.';
      ZeroAmountToDeferErr@1008 : TextConst 'ENU=The Amount to Defer cannot be 0.;RUS=Сумма, относимая на РБП, не может равняться 0.';

    PROCEDURE CalculateSchedule@1() : Boolean;
    VAR
      DeferralDescription@1000 : Text[50];
    BEGIN
      IF "Deferral Code" = '' THEN BEGIN
        MESSAGE(SelectionMsg);
        EXIT(FALSE);
      END;
      DeferralDescription := "Schedule Description";
      DeferralUtilities.CreateDeferralSchedule("Deferral Code","Deferral Doc. Type","Gen. Jnl. Template Name",
        "Gen. Jnl. Batch Name","Document Type","Document No.","Line No.","Amount to Defer",
        "Calc. Method","Start Date","No. of Periods",FALSE,DeferralDescription,FALSE,"Currency Code");
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

