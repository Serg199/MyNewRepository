OBJECT Table 325 VAT Posting Setup
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00,NAVRU10.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "VAT %" = 0 THEN
                 "VAT %" := GetVATPtc;
             END;

    CaptionML=[ENU=VAT Posting Setup;
               RUS=Настройка учета НДС];
    LookupPageID=Page472;
    DrillDownPageID=Page472;
  }
  FIELDS
  {
    { 1   ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=VAT Bus. Posting Group;
                                                              RUS=НДС бизнес-группа] }
    { 2   ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              RUS=НДС товарная группа] }
    { 3   ;   ;VAT Calculation Type;Option        ;CaptionML=[ENU=VAT Calculation Type;
                                                              RUS=Тип расчета НДС];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                                    RUS=Обычный НДС,Обратный НДС,Полный НДС,Налог с продаж];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax }
    { 4   ;   ;VAT %               ;Decimal       ;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("VAT %"));
                                                                CheckVATIdentifier;
                                                                IF "VAT %" <> 0 THEN
                                                                  TESTFIELD("VAT Exempt",FALSE);
                                                                IF ("VAT %" = 0) AND ("Unrealized VAT Type" > 0) THEN
                                                                  FIELDERROR("Unrealized VAT Type");
                                                              END;

                                                   CaptionML=[ENU=VAT %;
                                                              RUS=НДС (%)];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 5   ;   ;Unrealized VAT Type ;Option        ;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Unrealized VAT Type"));

                                                                IF "Unrealized VAT Type" > 0 THEN BEGIN
                                                                  GLSetup.GET;
                                                                  IF NOT GLSetup."Unrealized VAT" AND NOT GLSetup."Prepayment Unrealized VAT" THEN
                                                                    GLSetup.TESTFIELD("Unrealized VAT",TRUE);
                                                                  TESTFIELD("VAT %");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Unrealized VAT Type;
                                                              RUS=Тип нереал. НДС];
                                                   OptionCaptionML=[ENU=" ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)";
                                                                    RUS=" ,Процент,Первый,Последний,Первый оплаченный,Последний оплаченный"];
                                                   OptionString=[ ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)] }
    { 6   ;   ;Adjust for Payment Discount;Boolean;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Adjust for Payment Discount"));

                                                                IF "Adjust for Payment Discount" THEN BEGIN
                                                                  GLSetup.GET;
                                                                  GLSetup.TESTFIELD("Adjust for Payment Disc.",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Adjust for Payment Discount;
                                                              RUS=Корр. для скидки оплаты] }
    { 7   ;   ;Sales VAT Account   ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Sales VAT Account"));

                                                                CheckGLAcc("Sales VAT Account");
                                                              END;

                                                   CaptionML=[ENU=Sales VAT Account;
                                                              RUS=Счет НДС продаж] }
    { 8   ;   ;Sales VAT Unreal. Account;Code20   ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Sales VAT Unreal. Account"));

                                                                CheckGLAcc("Sales VAT Unreal. Account");
                                                              END;

                                                   CaptionML=[ENU=Sales VAT Unreal. Account;
                                                              RUS=Нереал. счет НДС продаж] }
    { 9   ;   ;Purchase VAT Account;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Purchase VAT Account"));

                                                                CheckGLAcc("Purchase VAT Account");
                                                              END;

                                                   CaptionML=[ENU=Purchase VAT Account;
                                                              RUS=Счет НДС покупки] }
    { 10  ;   ;Purch. VAT Unreal. Account;Code20  ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Purch. VAT Unreal. Account"));

                                                                CheckGLAcc("Purch. VAT Unreal. Account");
                                                              END;

                                                   CaptionML=[ENU=Purch. VAT Unreal. Account;
                                                              RUS=Счет ГК нереал. НДС покупки] }
    { 11  ;   ;Reverse Chrg. VAT Acc.;Code20      ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Reverse Chrg. VAT Acc."));

                                                                CheckGLAcc("Reverse Chrg. VAT Acc.");
                                                              END;

                                                   CaptionML=[ENU=Reverse Chrg. VAT Acc.;
                                                              RUS=Счет ГК обратного НДС] }
    { 12  ;   ;Reverse Chrg. VAT Unreal. Acc.;Code20;
                                                   TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Reverse Chrg. VAT Unreal. Acc."));

                                                                CheckGLAcc("Reverse Chrg. VAT Unreal. Acc.");
                                                              END;

                                                   CaptionML=[ENU=Reverse Chrg. VAT Unreal. Acc.;
                                                              RUS=Счет ГК обратного нереал. НДС] }
    { 13  ;   ;VAT Identifier      ;Code10        ;OnValidate=BEGIN
                                                                "VAT %" := GetVATPtc;
                                                              END;

                                                   CaptionML=[ENU=VAT Identifier;
                                                              RUS=Идентификатор НДС] }
    { 14  ;   ;EU Service          ;Boolean       ;CaptionML=[ENU=EU Service;
                                                              RUS=Сервис ЕС] }
    { 15  ;   ;VAT Clause Code     ;Code10        ;TableRelation="VAT Clause";
                                                   OnValidate=BEGIN
                                                                CheckVATClauseCode;
                                                              END;

                                                   CaptionML=[ENU=VAT Clause Code;
                                                              RUS=Код Условия НДС] }
    { 16  ;   ;Certificate of Supply Required;Boolean;
                                                   CaptionML=[ENU=Certificate of Supply Required;
                                                              RUS=Требуется сертификат поставки] }
    { 17  ;   ;Tax Category        ;Code10        ;CaptionML=[ENU=Tax Category;
                                                              RUS=Налоговая категория] }
    { 12400;  ;Tax Invoice Amount Type;Option     ;CaptionML=[ENU=Tax Invoice Amount Type;
                                                              RUS=Тип суммы для счета-фактуры];
                                                   OptionCaptionML=[ENU=VAT,Excise,Sales Tax;
                                                                    RUS=НДС,Акциз,Налог с продаж];
                                                   OptionString=VAT,Excise,Sales Tax;
                                                   Editable=No }
    { 12402;  ;Not Include into VAT Ledger;Option ;CaptionML=[ENU=Not Include into VAT Ledger;
                                                              RUS=Не включать в книгу НДС];
                                                   OptionCaptionML=[ENU=" ,Purchases,Sales,Purchases & Sales";
                                                                    RUS=" ,Покупки,Продажи,Покупки и продажи"];
                                                   OptionString=[ ,Purchases,Sales,Purchases & Sales] }
    { 12403;  ;Trans. VAT Type     ;Option        ;CaptionML=[ENU=Trans. VAT Type;
                                                              RUS=Тип транз. НДС];
                                                   OptionCaptionML=[ENU=" ,Amount + Tax,Amount & Tax";
                                                                    RUS=" ,Сумма + налог,Сумма и налог"];
                                                   OptionString=[ ,Amount + Tax,Amount & Tax] }
    { 12404;  ;Trans. VAT Account  ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Trans. VAT Account");
                                                              END;

                                                   CaptionML=[ENU=Trans. VAT Account;
                                                              RUS=НДС транз. счет ГК] }
    { 12408;  ;VAT Settlement Template;Code10     ;TableRelation="Gen. Journal Template" WHERE (Type=CONST(VAT Settlement));
                                                   CaptionML=[ENU=VAT Settlement Template;
                                                              RUS=Шаблон журнала зачета НДС] }
    { 12409;  ;VAT Settlement Batch;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE (Template Type=CONST(VAT Settlement),
                                                                                                  Journal Template Name=FIELD(VAT Settlement Template));
                                                   CaptionML=[ENU=VAT Settlement Batch;
                                                              RUS=Раздел журнала зачета НДС] }
    { 12412;  ;VAT Exempt          ;Boolean       ;OnValidate=BEGIN
                                                                IF "VAT Exempt" THEN
                                                                  TESTFIELD("VAT %",0);
                                                              END;

                                                   CaptionML=[ENU=VAT Exempt;
                                                              RUS=Освоб. от НДС] }
    { 12413;  ;Manual VAT Settlement;Boolean      ;OnValidate=BEGIN
                                                                IF ("Unrealized VAT Type" = "Unrealized VAT Type"::" ") AND "Manual VAT Settlement" THEN
                                                                  ERROR('');
                                                              END;

                                                   CaptionML=[ENU=Manual VAT Settlement;
                                                              RUS=Зачет НДС вручную] }
    { 12414;  ;Write-Off VAT Account;Code20       ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Write-Off VAT Account;
                                                              RUS=Счет списания НДС] }
    { 12415;  ;VAT Charge No.      ;Code20        ;TableRelation="Item Charge";
                                                   CaptionML=[ENU=VAT Charge No.;
                                                              RUS=Код издержки НДС] }
    { 12418;  ;VAT Reinstatement Template;Code10  ;TableRelation="Gen. Journal Template" WHERE (Type=CONST(VAT Reinstatement));
                                                   CaptionML=[ENU=VAT Reinstatement Template;
                                                              RUS=Шаблон восстановления НДС] }
    { 12419;  ;VAT Reinstatement Batch;Code10     ;TableRelation="Gen. Journal Batch".Name WHERE (Template Type=CONST(VAT Reinstatement),
                                                                                                  Journal Template Name=FIELD(VAT Reinstatement Template));
                                                   CaptionML=[ENU=VAT Reinstatement Batch;
                                                              RUS=Пакет восстановления НДС] }
  }
  KEYS
  {
    {    ;VAT Bus. Posting Group,VAT Prod. Posting Group;
                                                   Clustered=Yes }
    {    ;VAT Prod. Posting Group,VAT Bus. Posting Group }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be entered on the tax jurisdiction line when %2 is %3.;RUS=%1 должно быть введено в строке юрисдикции, если %2 равно %3.';
      Text001@1001 : TextConst 'ENU="%1 = %2 has already been used for %3 = %4 in %5 for %6 = %7 and %8 = %9.";RUS="%1 = %2 уже было использовано для %3 = %4 в %5 для %6 = %7 и %8 = %9."';
      GLSetup@1002 : Record 98;
      DuplicateEntryErr@1003 : TextConst 'ENU=Another entry with the same %1 in the same %2 has a different %3 assigned. Use the same %3 or remove it.;RUS=У другой операции с тем же %1 в той же %2 назначен другой %3. Воспользуйтесь тем же %3 или удалите его.';
      Text12400@1210002 : TextConst 'ENU=%1 must be %2.;RUS=%1 должно быть %2.';
      Text12401@1210001 : TextConst 'ENU=Old VAT rate,New VAT rate;RUS=Старая ставка НДС,Новая ставка НДС';
      Text12402@1210000 : TextConst 'ENU=There is no other tax defined for this line.;RUS=Для этой строки не определен другой налог.';
      VATSetup@1210003 : Record 325;
      VATSetup2@1210004 : Record 325;
      VATType@1210005 : '0,OldTax,NewTax';

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
      END;
    END;

    LOCAL PROCEDURE TestNotSalesTax@1(FromFieldName@1000 : Text[100]);
    BEGIN
      IF "VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax" THEN
        ERROR(
          Text000,
          FromFieldName,FIELDCAPTION("VAT Calculation Type"),
          "VAT Calculation Type");
    END;

    LOCAL PROCEDURE CheckVATIdentifier@14();
    VAR
      VATPostingSetup@1000 : Record 325;
    BEGIN
      VATPostingSetup.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
      VATPostingSetup.SETFILTER("VAT Prod. Posting Group",'<>%1',"VAT Prod. Posting Group");
      VATPostingSetup.SETFILTER("VAT %",'<>%1',"VAT %");
      VATPostingSetup.SETRANGE("VAT Identifier","VAT Identifier");
      IF VATPostingSetup.FINDFIRST THEN
        ERROR(
          Text001,
          FIELDCAPTION("VAT Identifier"),VATPostingSetup."VAT Identifier",
          FIELDCAPTION("VAT %"),VATPostingSetup."VAT %",TABLECAPTION,
          FIELDCAPTION("VAT Bus. Posting Group"),VATPostingSetup."VAT Bus. Posting Group",
          FIELDCAPTION("VAT Prod. Posting Group"),VATPostingSetup."VAT Prod. Posting Group");
    END;

    LOCAL PROCEDURE CheckVATClauseCode@11();
    VAR
      VATPostingSetup@1000 : Record 325;
    BEGIN
      IF "VAT Clause Code" = '' THEN
        EXIT;
      VATPostingSetup.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
      VATPostingSetup.SETFILTER("VAT Prod. Posting Group",'<>%1',"VAT Prod. Posting Group");
      VATPostingSetup.SETRANGE("VAT Identifier","VAT Identifier");
      VATPostingSetup.SETFILTER("VAT Clause Code",'<>%1',"VAT Clause Code");
      IF NOT VATPostingSetup.ISEMPTY THEN
        ERROR(
          DuplicateEntryErr,
          FIELDCAPTION("VAT Identifier"),
          FIELDCAPTION("VAT Bus. Posting Group"),
          FIELDCAPTION("VAT Clause Code"));
    END;

    LOCAL PROCEDURE GetVATPtc@3() : Decimal;
    VAR
      VATPostingSetup@1000 : Record 325;
    BEGIN
      VATPostingSetup.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
      VATPostingSetup.SETFILTER("VAT Prod. Posting Group",'<>%1',"VAT Prod. Posting Group");
      VATPostingSetup.SETRANGE("VAT Identifier","VAT Identifier");
      IF NOT VATPostingSetup.FINDFIRST THEN
        VATPostingSetup."VAT %" := "VAT %";
      EXIT(VATPostingSetup."VAT %");
    END;

    PROCEDURE GetSalesAccount@4(Unrealized@1000 : Boolean) : Code[20];
    BEGIN
      IF Unrealized THEN BEGIN
        TESTFIELD("Sales VAT Unreal. Account");
        EXIT("Sales VAT Unreal. Account");
      END;
      TESTFIELD("Sales VAT Account");
      EXIT("Sales VAT Account");
    END;

    PROCEDURE GetPurchAccount@5(Unrealized@1000 : Boolean) : Code[20];
    BEGIN
      IF Unrealized THEN BEGIN
        TESTFIELD("Purch. VAT Unreal. Account");
        EXIT("Purch. VAT Unreal. Account");
      END;
      TESTFIELD("Purchase VAT Account");
      EXIT("Purchase VAT Account");
    END;

    PROCEDURE GetRevChargeAccount@6(Unrealized@1000 : Boolean) : Code[20];
    BEGIN
      IF Unrealized THEN BEGIN
        TESTFIELD("Reverse Chrg. VAT Unreal. Acc.");
        EXIT("Reverse Chrg. VAT Unreal. Acc.");
      END;
      TESTFIELD("Reverse Chrg. VAT Acc.");
      EXIT("Reverse Chrg. VAT Acc.");
    END;

    BEGIN
    END.
  }
}

