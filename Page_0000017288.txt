OBJECT Page 17288 Tax Reg G/L Corr. Dim. Filters
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Tax Reg G/L Corr. Dim. Filters;
               RUS=Фильтры изм. по корресп. для налог. регистра];
    SourceTable=Table17244;
    PageType=Worksheet;
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210000;1;Group     ;
                GroupType=Repeater }

    { 1210001;2;Field     ;
                ToolTipML=[ENU=Specifies the filter group associated with this filter.;
                           RUS=Определяет группу фильтров, связанную с этим фильтром.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Filter Group" }

    { 1210003;2;Field     ;
                ToolTipML=[ENU=Specifies the dimension code associated with this filter.;
                           RUS=Определяет код измерения, связанный с этим фильтром.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Dimension Code";
                OnLookup=BEGIN
                           EXIT(LookupDimension(Text));
                         END;
                          }

    { 1210005;2;Field     ;
                ToolTipML=[ENU=Specifies the dimension value filter associated with this filter.;
                           RUS=Определяет фильтр значения измерения, связанный с этим фильтром.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Dimension Value Filter";
                OnLookup=VAR
                           DimensionValue@1210001 : Record 349;
                           DimensionValueList@1210000 : Page 560;
                         BEGIN
                           DimensionValue.FILTERGROUP(2);
                           DimensionValue.SETRANGE("Dimension Code","Dimension Code");
                           DimensionValue.FILTERGROUP(0);
                           DimensionValueList.SETTABLEVIEW(DimensionValue);
                           DimensionValueList.LOOKUPMODE(TRUE);
                           IF NOT (DimensionValueList.RUNMODAL = ACTION::LookupOK) THEN
                             EXIT(FALSE);

                           Text := DimensionValueList.GetSelectionFilter;
                           EXIT(TRUE);
                         END;
                          }

    { 1210007;2;Field     ;
                ToolTipML=[ENU=Specifies the dimension name associated with this filter.;
                           RUS=Определяет название измерения, связанное с этим фильтром.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Dimension Name" }

  }
  CODE
  {
    VAR
      GLSetup@1210001 : Record 98;
      GLCorrAnalysisView@1210002 : Record 14940;
      TaxRegisterName@1210000 : Record 17200;

    PROCEDURE LookupDimension@1210000(VAR Text@1210002 : Text[1024]) : Boolean;
    VAR
      Dimension@1210001 : Record 348;
      DimCodeFilter@1210000 : Code[250];
    BEGIN
      TaxRegisterName.GET("Section Code","Tax Register No.");
      IF TaxRegisterName."G/L Corr. Analysis View Code" <> '' THEN BEGIN
        GLCorrAnalysisView.GET(TaxRegisterName."G/L Corr. Analysis View Code");
        CASE "Filter Group" OF
          "Filter Group"::Debit:
            BEGIN
              AddValue2Fiter(GLCorrAnalysisView."Debit Dimension 1 Code",DimCodeFilter);
              AddValue2Fiter(GLCorrAnalysisView."Debit Dimension 2 Code",DimCodeFilter);
              AddValue2Fiter(GLCorrAnalysisView."Debit Dimension 3 Code",DimCodeFilter);
            END;
          "Filter Group"::Credit:
            BEGIN
              AddValue2Fiter(GLCorrAnalysisView."Credit Dimension 1 Code",DimCodeFilter);
              AddValue2Fiter(GLCorrAnalysisView."Credit Dimension 2 Code",DimCodeFilter);
              AddValue2Fiter(GLCorrAnalysisView."Credit Dimension 3 Code",DimCodeFilter);
            END;
        END;
      END ELSE BEGIN
        GLSetup.GET;
        AddValue2Fiter(GLSetup."Global Dimension 1 Code",DimCodeFilter);
        AddValue2Fiter(GLSetup."Global Dimension 2 Code",DimCodeFilter);
      END;

      Dimension.FILTERGROUP(2);
      IF DimCodeFilter <> '' THEN
        Dimension.SETFILTER(Code,DimCodeFilter);
      Dimension.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,Dimension) = ACTION::LookupOK THEN BEGIN
        Text := Dimension.Code;
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE AddValue2Fiter@1210003(DimCode@1210000 : Code[20];VAR DimCodeFilter@1210001 : Code[250]);
    BEGIN
      IF DimCode <> '' THEN BEGIN
        IF DimCodeFilter = '' THEN
          DimCodeFilter := DimCode
        ELSE
          DimCodeFilter := DimCodeFilter + '|' + DimCode;
      END;
    END;

    BEGIN
    END.
  }
}

