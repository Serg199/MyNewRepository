OBJECT Page 9500 Debugger
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Debugger;
               RUS=Отладчик];
    LinksAllowed=No;
    SourceTable=Table2000000110;
    DataCaptionExpr=DataCaption;
    PageType=Card;
    RefreshOnActivate=Yes;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Code Tracking,Running Code,Breakpoints,Show;
                                RUS=Создать, Обработать, Отчет, Отслеживание кода, Выполнение кода, Точки останова, Показать];
    ShowFilter=No;
    OnInit=BEGIN
             BreakOnError := TRUE;
             BreakpointHit := DEBUGGER.ISBREAKPOINTHIT;
             BreakEnabled := NOT BreakpointHit;
           END;

    OnOpenPage=VAR
                 DebuggedSession@1000 : Record 2000000110;
               BEGIN
                 StartTime := CURRENTDATETIME;
                 FinishTime := StartTime;
                 DebuggerManagement.GetDebuggedSession(DebuggedSession);
                 IF DebuggedSession."Session ID" = 0 THEN
                   DEBUGGER.ACTIVATE
                 ELSE BEGIN
                   DEBUGGER.ATTACH(DebuggedSession."Session ID");
                   SetAttachedSession := TRUE;
                 END;

                 IF UserPersonalization.GET(USERSECURITYID) THEN BEGIN
                   BreakOnError := UserPersonalization."Debugger Break On Error";
                   BreakOnRecordChanges := UserPersonalization."Debugger Break On Rec Changes";
                   SkipCodeunit1 := UserPersonalization."Debugger Skip System Triggers";
                 END;

                 IF BreakOnError THEN
                   DEBUGGER.BREAKONERROR(TRUE);
                 IF BreakOnRecordChanges THEN
                   DEBUGGER.BREAKONRECORDCHANGES(TRUE);
                 IF SkipCodeunit1 THEN
                   DEBUGGER.SKIPSYSTEMTRIGGERS(TRUE);

                 DebuggerManagement.ResetActionState;
               END;

    OnClosePage=BEGIN
                  IF DEBUGGER.DEACTIVATE THEN;
                  SetAttachedSession := FALSE;
                END;

    OnFindRecord=VAR
                   DebuggedSession@1003 : Record 2000000110;
                   IsBreakOnErrorMessageNew@1001 : Boolean;
                   BreakOnErrorMessage@1002 : Text;
                 BEGIN
                   IF NOT DEBUGGER.ISACTIVE AND (Which = '=') THEN
                     MESSAGE(Text007Msg);

                   IF NOT DEBUGGER.ISACTIVE THEN BEGIN
                     CurrPage.CLOSE;
                     EXIT(FALSE);
                   END;

                   BreakpointHit := DEBUGGER.ISBREAKPOINTHIT;

                   IF BreakpointHit THEN BEGIN
                     BreakOnErrorMessage := DebuggerManagement.GetLastErrorMessage(IsBreakOnErrorMessageNew);

                     IF IsBreakOnErrorMessageNew AND (BreakOnErrorMessage <> '') THEN
                       MESSAGE(STRSUBSTNO(Text002Msg,BreakOnErrorMessage));

                     ShowLastErrorEnabled := (BreakOnErrorMessage <> '') OR (DEBUGGER.GETLASTERRORTEXT <> '');

                     BreakEnabled := FALSE;
                     IF NOT SetAttachedSession THEN BEGIN
                       DebuggedSession."Session ID" := DEBUGGER.DEBUGGEDSESSIONID;
                       DebuggerManagement.SetDebuggedSession(DebuggedSession);
                       SetAttachedSession := TRUE;
                     END;
                   END ELSE BEGIN
                     IsBreakOnErrorMessageNew := FALSE;
                     ShowLastErrorEnabled := FALSE;
                     DataCaption := Text004Cap;
                   END;

                   EXIT(TRUE);
                 END;

    OnAfterGetRecord=BEGIN
                       IF BreakpointHit THEN BEGIN
                         CurrPage.Callstack.PAGE.GetCurrentCallstack(DebuggerCallstack);
                         WITH DebuggerCallstack DO BEGIN
                           IF ID <> 0 THEN
                             DataCaption := STRSUBSTNO(Text003Cap,"Object Type","Object ID","Object Name")
                           ELSE
                             DataCaption := Text004Cap;
                         END;
                         FinishTime := CURRENTDATETIME;
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 4       ;    ;ActionContainer;
                      Name=Debugger Actions;
                      CaptionML=[ENU=Debugger Actions;
                                 RUS=Действия отладчика];
                      ActionContainerType=ActionItems }
      { 25      ;1   ;Separator  }
      { 26      ;1   ;ActionGroup;
                      Name=Code Tracking;
                      CaptionML=[ENU=Code Tracking;
                                 RUS=Отслеживание кода] }
      { 8       ;2   ;Action    ;
                      Name=Step Into;
                      ShortCutKey=F11;
                      CaptionML=[ENU=Step Into;
                                 RUS=Шаг внутрь];
                      ToolTipML=[ENU=Execute the current statement. If the statement contains a function call, then execute the function and break at the first statement inside the function.;
                                 RUS=Выполнение текущей инструкции. Если инструкция содержит вызов функции, выполнение функции и останов на первой инструкции внутри функции.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=StepInto;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 StartTime := CURRENTDATETIME;
                                 WaitingForBreak;
                                 DebuggerManagement.SetCodeTrackingAction;
                                 DEBUGGER.STEPINTO;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 7       ;2   ;Action    ;
                      Name=Step Over;
                      ShortCutKey=F10;
                      CaptionML=[ENU=Step Over;
                                 RUS=Перешагивание];
                      ToolTipML=[ENU=Execute the current statement. If the statement contains a function call, then execute the function and break at the first statement outside the function.;
                                 RUS=Выполнение текущей инструкции. Если инструкция содержит вызов функции, выполнение функции и останов на первой инструкции вне функции.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=StepOver;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 StartTime := CURRENTDATETIME;
                                 WaitingForBreak;
                                 DebuggerManagement.SetCodeTrackingAction;
                                 DEBUGGER.STEPOVER;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 9       ;2   ;Action    ;
                      Name=Step Out;
                      ShortCutKey=Shift+F11;
                      CaptionML=[ENU=Step Out;
                                 RUS=Шаг наружу];
                      ToolTipML=[ENU=Execute the remaining statements in the current function and break at the next statement in the calling function.;
                                 RUS=Выполнение оставшихся инструкций в текущей функции и останов на следующей инструкции в вызывающей функции.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=StepOut;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 StartTime := CURRENTDATETIME;
                                 WaitingForBreak;
                                 DebuggerManagement.SetCodeTrackingAction;
                                 DEBUGGER.STEPOUT;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 11      ;1   ;Separator  }
      { 27      ;1   ;ActionGroup;
                      Name=Running Code;
                      CaptionML=[ENU=Running Code;
                                 RUS=Выполнение кода] }
      { 12      ;2   ;Action    ;
                      Name=Continue;
                      ShortCutKey=F5;
                      CaptionML=[ENU=Continue;
                                 RUS=Продолжить];
                      ToolTipML=[ENU=Continue until the next break.;
                                 RUS=Продолжение выполнения до следующего останова.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=Continue;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 StartTime := CURRENTDATETIME;
                                 WaitingForBreak;
                                 DebuggerManagement.SetRunningCodeAction;
                                 DEBUGGER.CONTINUE;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 31      ;2   ;Action    ;
                      Name=Break;
                      ShortCutKey=Shift+Ctrl+B;
                      CaptionML=[ENU=Break;
                                 RUS=Останов];
                      ToolTipML=[ENU=Break at the next statement.;
                                 RUS=Останов на следующей инструкции.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakEnabled;
                      PromotedIsBig=Yes;
                      Image=Pause;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 BreakEnabled := FALSE;
                                 DebuggerManagement.SetRunningCodeAction;
                                 DEBUGGER."BREAK";
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 15      ;2   ;Action    ;
                      Name=Stop;
                      ShortCutKey=Shift+F5;
                      CaptionML=[ENU=Stop;
                                 RUS=Остановить];
                      ToolTipML=[ENU=Stop the current activity in the session being debugged while continuing to debug the session.;
                                 RUS=Останов текущего действия в отлаживаемом сеансе с продолжением отладки сеанса.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=Stop;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 WaitingForBreak;
                                 DebuggerManagement.SetRunningCodeAction;
                                 DEBUGGER.STOP;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 21      ;1   ;Separator  }
      { 28      ;1   ;ActionGroup;
                      Name=Breakpoints Group;
                      CaptionML=[ENU=Breakpoints;
                                 RUS=Контрольные точки] }
      { 14      ;2   ;Action    ;
                      Name=Toggle;
                      ShortCutKey=F9;
                      CaptionML=[ENU=Toggle;
                                 RUS=Переключить];
                      ToolTipML=[ENU=Toggle a breakpoint at the current line.;
                                 RUS=Переключение точки останова в текущей строке.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ToggleBreakpoint;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 CurrPage.CodeViewer.PAGE.ToggleBreakpoint;
                               END;
                                }
      { 13      ;2   ;Action    ;
                      Name=Set/Clear Condition;
                      ShortCutKey=Shift+F9;
                      CaptionML=[ENU=Set/Clear Condition;
                                 RUS=Установить/очистить условие];
                      ToolTipML=[ENU=Set or clear a breakpoint condition at the current line.;
                                 RUS=Установка или очистка условия точки останова в текущей строке.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ConditionalBreakpoint;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 CurrPage.CodeViewer.PAGE.SetBreakpointCondition;
                               END;
                                }
      { 19      ;2   ;Action    ;
                      Name=Disable All;
                      CaptionML=[ENU=Disable All;
                                 RUS=Выключить все];
                      ToolTipML=[ENU=Disable all breakpoints in the breakpoint list. You can edit the list by using the Breakpoints action.;
                                 RUS=Отключение всех точек останова в списке точек останова. Список можно редактировать с помощью операции "Точки останова".];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Image=DisableAllBreakpoints;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 DebuggerBreakpoint@1001 : Record 2000000100;
                               BEGIN
                                 DebuggerBreakpoint.MODIFYALL(Enabled,FALSE,TRUE);
                               END;
                                }
      { 20      ;2   ;Action    ;
                      Name=Breakpoints;
                      CaptionML=[ENU=Breakpoints;
                                 RUS=Контрольные точки];
                      ToolTipML=[ENU=Edit the breakpoint list for all objects.;
                                 RUS=Изменение списка точек останова для всех объектов.];
                      ApplicationArea=#All;
                      RunObject=Page 9505;
                      Promoted=Yes;
                      Image=BreakpointsList;
                      PromotedCategory=Category6 }
      { 35      ;2   ;Action    ;
                      Name=Break Rules;
                      CaptionML=[ENU=Break Rules;
                                 RUS=Правила останова];
                      ToolTipML=[ENU=Change settings for break rules. The debugger breaks code execution for certain configurable rules as well as for breakpoints.;
                                 RUS=Изменение параметров правил останова. Отладчик останавливает выполнение кода по некоторым настраиваемым правилам, а также в точках останова.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=BreakRulesList;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 DebuggerBreakRulesPage@1000 : Page 9509;
                               BEGIN
                                 DebuggerBreakRulesPage.SetBreakOnError(BreakOnError);
                                 DebuggerBreakRulesPage.SetBreakOnRecordChanges(BreakOnRecordChanges);
                                 DebuggerBreakRulesPage.SetSkipCodeunit1(SkipCodeunit1);

                                 IF DebuggerBreakRulesPage.RUNMODAL = ACTION::OK THEN BEGIN
                                   BreakOnError := DebuggerBreakRulesPage.GetBreakOnError;
                                   DEBUGGER.BREAKONERROR(BreakOnError);
                                   BreakOnRecordChanges := DebuggerBreakRulesPage.GetBreakOnRecordChanges;
                                   DEBUGGER.BREAKONRECORDCHANGES(BreakOnRecordChanges);
                                   SkipCodeunit1 := DebuggerBreakRulesPage.GetSkipCodeunit1;
                                   DEBUGGER.SKIPSYSTEMTRIGGERS(SkipCodeunit1);

                                   SaveConfiguration;
                                 END;
                               END;
                                }
      { 22      ;1   ;Separator  }
      { 33      ;1   ;ActionGroup;
                      Name=Show;
                      CaptionML=[ENU=Show;
                                 RUS=Показать];
                      Image=View }
      { 6       ;2   ;Action    ;
                      Name=Variables;
                      ShortCutKey=Shift+Ctrl+V;
                      CaptionML=[ENU=Variables;
                                 RUS=Переменные];
                      ToolTipML=[ENU=View the list of variables in the current scope.;
                                 RUS=Просмотр списка переменных в текущей области.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=VariableList;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 DebuggerCallstack@1001 : Record 2000000101;
                                 DebuggerVariable@1003 : Record 2000000102;
                               BEGIN
                                 CurrPage.Callstack.PAGE.GetCurrentCallstack(DebuggerCallstack);

                                 DebuggerVariable.FILTERGROUP(2);
                                 DebuggerVariable.SETRANGE("Call Stack ID",DebuggerCallstack.ID);
                                 DebuggerVariable.FILTERGROUP(0);

                                 PAGE.RUNMODAL(PAGE::"Debugger Variable List",DebuggerVariable);
                               END;
                                }
      { 36      ;2   ;Action    ;
                      Name=LastErrorMessage;
                      CaptionML=[ENU=Last Error;
                                 RUS=Последняя ошибка];
                      ToolTipML=[ENU=View the last error message shown by the session being debugged.;
                                 RUS=Просмотр последнего сообщения об ошибке, показанного в отлаживаемом сеансе.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=ShowLastErrorEnabled;
                      PromotedIsBig=Yes;
                      Image=PrevErrorMessage;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 DebuggerManagement@1003 : Codeunit 9500;
                                 LastErrorMessage@1001 : Text;
                                 IsLastErrorMessageNew@1002 : Boolean;
                               BEGIN
                                 LastErrorMessage := DebuggerManagement.GetLastErrorMessage(IsLastErrorMessageNew);

                                 IF LastErrorMessage = '' THEN
                                   LastErrorMessage := DEBUGGER.GETLASTERRORTEXT;

                                 MESSAGE(STRSUBSTNO(Text005Msg,LastErrorMessage));
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Part      ;
                Name=CodeViewer;
                CaptionML=[ENU=Code;
                           RUS=Код];
                ApplicationArea=#All;
                SubPageLink=Object Type=FIELD(Object Type),
                            Object ID=FIELD(Object ID),
                            Line No.=FIELD(Line No.),
                            ID=FIELD(ID);
                PagePartID=Page9504;
                ProviderID=10 }

    { 16  ;1   ;Field     ;
                CaptionML=[ENU=Duration;
                           RUS=Длительность];
                ApplicationArea=#All;
                SourceExpr=FinishTime - StartTime }

    { 3   ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 5   ;1   ;Part      ;
                Name=Watches;
                CaptionML=[ENU=Watches;
                           RUS=Контрольные значения];
                ApplicationArea=#All;
                SubPageLink=Call Stack ID=FIELD(ID);
                PagePartID=Page9503;
                ProviderID=10 }

    { 10  ;1   ;Part      ;
                Name=Callstack;
                CaptionML=[ENU=Call Stack;
                           RUS=Стек вызовов];
                ApplicationArea=#All;
                PagePartID=Page9502 }

  }
  CODE
  {
    VAR
      DebuggerCallstack@1005 : Record 2000000101;
      UserPersonalization@1009 : Record 2000000073;
      DebuggerManagement@1012 : Codeunit 9500;
      BreakEnabled@1004 : Boolean INDATASET;
      BreakpointHit@1002 : Boolean INDATASET;
      BreakOnError@1000 : Boolean INDATASET;
      Text002Msg@1007 : TextConst '@@@=Message shown when Break On Error occurs. Include the original error message.;ENU=Break On Error Message:\ \%1;RUS=Останов при выводе сообщения об ошибке:\ \%1';
      BreakOnRecordChanges@1003 : Boolean INDATASET;
      SkipCodeunit1@1010 : Boolean;
      DataCaption@1001 : Text[100];
      ShowLastErrorEnabled@1006 : Boolean INDATASET;
      Text003Cap@1011 : TextConst '@@@=DataCaption when debugger is broken in application code. Example: Codeunit 1:  Application Management;ENU=%1 %2 : %3;RUS=%1 %2 : %3';
      Text004Cap@1013 : TextConst '@@@=DataCaption when waiting for break;ENU=Waiting for break...;RUS=Ожидание останова...';
      SetAttachedSession@1008 : Boolean;
      Text005Msg@1014 : TextConst 'ENU=Last Error Message:\ \%1;RUS=Последнее сообщение об ошибке:\ \%1';
      Text007Msg@1016 : TextConst 'ENU=The session that was being debugged has closed. The Debugger Page will close.;RUS=Сеанс, отладка которого производилась, закрыт. Страница отладчика закрывается.';
      StartTime@1015 : DateTime;
      FinishTime@1017 : DateTime;

    LOCAL PROCEDURE SaveConfiguration@1();
    BEGIN
      IF UserPersonalization.GET(USERSECURITYID) THEN BEGIN
        UserPersonalization."Debugger Break On Error" := BreakOnError;
        UserPersonalization."Debugger Break On Rec Changes" := BreakOnRecordChanges;
        UserPersonalization."Debugger Skip System Triggers" := SkipCodeunit1;
        UserPersonalization.MODIFY;
      END;
    END;

    LOCAL PROCEDURE WaitingForBreak@3();
    BEGIN
      BreakEnabled := TRUE;
      CurrPage.Callstack.PAGE.ResetCallstackToTop;
    END;

    BEGIN
    END.
  }
}

