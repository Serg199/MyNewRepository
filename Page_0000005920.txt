OBJECT Page 5920 Service Document Log
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Service Document Log;
               RUS=Журнал сервисных документов];
    SourceTable=Table5912;
    DataCaptionExpr=GetCaptionHeader;
    SourceTableView=SORTING(Change Date,Change Time)
                    ORDER(Descending);
    PageType=List;
    RefreshOnActivate=Yes;
    OnInit=BEGIN
             DocumentNoVisible := TRUE;
             DocumentTypeVisible := TRUE;
           END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 16      ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 RUS=Ф&ункции];
                      Image=Action }
      { 17      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=&Delete Service Document Log;
                                 RUS=&Удалить журнал сервисных документов];
                      Image=Delete;
                      OnAction=VAR
                                 ServOrderLog@1002 : Record 5912;
                                 DeleteServOrderLog@1000 : Report 6002;
                               BEGIN
                                 ServOrderLog.SETRANGE("Document Type","Document Type");
                                 ServOrderLog.SETRANGE("Document No.","Document No.");
                                 DeleteServOrderLog.SETTABLEVIEW(ServOrderLog);
                                 DeleteServOrderLog.RUNMODAL;

                                 IF DeleteServOrderLog.GetOnPostReportStatus THEN BEGIN
                                   ServOrderLog.RESET;
                                   DeleteServOrderLog.GetServDocLog(ServOrderLog);
                                   COPYFILTERS(ServOrderLog);
                                   DELETEALL;
                                   RESET;
                                   SETCURRENTKEY("Change Date","Change Time");
                                   ASCENDING(FALSE);
                                 END;
                               END;
                                }
      { 18      ;1   ;Action    ;
                      CaptionML=[ENU=&Show;
                                 RUS=&Показать];
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ServShptHeader@1001 : Record 5990;
                                 ServInvHeader@1002 : Record 5992;
                                 ServCrMemoHeader@1003 : Record 5994;
                                 PageManagement@1000 : Codeunit 700;
                                 isError@1004 : Boolean;
                               BEGIN
                                 IF "Document Type" IN
                                    ["Document Type"::Order,"Document Type"::Quote,
                                     "Document Type"::Invoice,"Document Type"::"Credit Memo"]
                                 THEN
                                   IF ServOrderHeaderRec.GET("Document Type","Document No.") THEN BEGIN
                                     isError := FALSE;
                                     PageManagement.PageRun(ServOrderHeaderRec);
                                   END ELSE
                                     isError := TRUE
                                 ELSE BEGIN // posted documents
                                   isError := TRUE;
                                   CASE "Document Type" OF
                                     "Document Type"::Shipment:
                                       IF ServShptHeader.GET("Document No.") THEN BEGIN
                                         isError := FALSE;
                                         PAGE.RUN(PAGE::"Posted Service Shipment",ServShptHeader);
                                       END;
                                     "Document Type"::"Posted Invoice":
                                       IF ServInvHeader.GET("Document No.") THEN BEGIN
                                         isError := FALSE;
                                         PAGE.RUN(PAGE::"Posted Service Invoice",ServInvHeader);
                                       END;
                                     "Document Type"::"Posted Credit Memo":
                                       IF ServCrMemoHeader.GET("Document No.") THEN BEGIN
                                         isError := FALSE;
                                         PAGE.RUN(PAGE::"Posted Service Credit Memo",ServCrMemoHeader);
                                       END;
                                   END;
                                 END;
                                 IF isError THEN
                                   ERROR(Text001,"Document Type","Document No.");
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Indicates the type of the service document that underwent changes.;
                           RUS=Показывает тип сервисного документа, претерпевшего изменения.];
                SourceExpr="Document Type";
                Visible=DocumentTypeVisible }

    { 29  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the service document that has undergone changes.;
                           RUS=Определяет номер сервисного документа, который претерпел изменения.];
                SourceExpr="Document No.";
                Visible=DocumentNoVisible }

    { 14  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the service item line, if the event is linked to a service item line.;
                           RUS=Определяет номер строки сервисного товара, если событие связано с какой-либо строкой сервисного товара.];
                SourceExpr="Service Item Line No.";
                Visible=FALSE }

    { 31  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number assigned to this entry.;
                           RUS=Определяет номер, присвоенный этой операции.];
                SourceExpr="Entry No.";
                Visible=FALSE }

    { 33  ;2   ;Field     ;
                CaptionML=[ENU=Description;
                           RUS=Описание];
                ToolTipML=[ENU=Specifies the description of the event that occurred to a particular service document.;
                           RUS=Определяет описание события, связанного с определенным сервисным документом.];
                SourceExpr=ServLogMgt.ServOrderEventDescription("Event No.") }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the contents of the modified field after the event takes place.;
                           RUS=Определяет значение измененного поля, после того как событие произошло.];
                SourceExpr=After }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the contents of the modified field before the event takes place.;
                           RUS=Определяет значение измененного поля, до того как событие произошло.];
                SourceExpr=Before }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the date of the event.;
                           RUS=Определяет дату события.];
                SourceExpr="Change Date" }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the time of the event.;
                           RUS=Определяет время события.];
                SourceExpr="Change Time" }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the ID of the user who performed the changes.;
                           RUS=Определяет код пользователя, выполнившего изменения.];
                SourceExpr="User ID" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ServOrderHeaderRec@1001 : Record 5900;
      ServLogMgt@1000 : Codeunit 5906;
      Text001@1003 : TextConst '@@@=Service Order 2001 does not exist.;ENU=Service %1 %2 does not exist.;RUS=Служба %1 %2 не существует.';
      DocumentTypeVisible@19012068 : Boolean INDATASET;
      DocumentNoVisible@19042234 : Boolean INDATASET;

    LOCAL PROCEDURE GetCaptionHeader@1() : Text[250];
    VAR
      ServHeader@1000 : Record 5900;
    BEGIN
      IF GETFILTER("Document No.") <> '' THEN BEGIN
        DocumentTypeVisible := FALSE;
        DocumentNoVisible := FALSE;
        IF ServHeader.GET("Document Type","Document No.") THEN
          EXIT(FORMAT("Document Type") + ' ' + "Document No." + ' ' + ServHeader.Description);

        EXIT(FORMAT("Document Type") + ' ' + "Document No.");
      END;

      DocumentTypeVisible := TRUE;
      DocumentNoVisible := TRUE;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

