OBJECT Codeunit 50080 FI Jnl.-Post
{
  OBJECT-PROPERTIES
  {
    Date=26.12.16;
    Time=13:09:28;
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    TableNo=50220;
    OnRun=BEGIN
            FIJnlLine.COPY(Rec);
            Code;
            COPY(FIJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines?;RUS=Вы хотите учесть строки журнала?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;RUS=Здесь нечего учитывать.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;RUS=Строки журнала были успешно учтены.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;RUS=Строки журнала были успешно учтены. Вы сейчас в журнале %1.';
      FIJnlTemplate@1005 : Record 50218;
      FIJnlLine@1006 : Record 50220;
      FIJnlPostBatch@1007 : Codeunit 50079;
      TempJnlBatchName@1008 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH FIJnlLine DO BEGIN
        FIJnlTemplate.GET("Journal Template Name");

        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        FIJnlPostBatch.RUN(FIJnlLine);

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP := 0;
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    {
      NC NCS-2360 ORM Created
    }
    END.
  }
}

