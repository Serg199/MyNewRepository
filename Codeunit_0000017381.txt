OBJECT Codeunit 17381 Employee Journal - Check Line
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    TableNo=17382;
    OnRun=VAR
            TableID@1210002 : ARRAY [10] OF Integer;
            No@1210001 : ARRAY [10] OF Code[20];
          BEGIN
            IF EmptyLine THEN
              EXIT;

            TESTFIELD("Employee No.");
            TESTFIELD("Posting Date");
            IF "Post Action" = "Post Action"::Add THEN
              TESTFIELD("Starting Date");
            IF "Post Action" = "Post Action"::Close THEN
              TESTFIELD("Ending Date");
            TESTFIELD("Contract No.");
            TESTFIELD("Posting Group");
            TESTFIELD("Calendar Code");
            TESTFIELD("Payroll Calc Group");

            IF ("Element Code" = '') AND ("Time Activity Code" = '') THEN
              ERROR(Text002,FIELDCAPTION("Element Code"),FIELDCAPTION("Time Activity Code"));

            IF "Post Action" <> "Post Action"::Add THEN
              TESTFIELD("Applies-to Entry");

            IF "Post Action" = "Post Action"::Add THEN BEGIN
              PayrollElement.GET("Element Code");
              IF PayrollElement."Amount Mandatory" THEN
                TESTFIELD(Amount);
              IF PayrollElement."Quantity Mandatory" THEN
                TESTFIELD(Quantity);
              IF PayrollElement."Bonus Type" <> 0 THEN BEGIN
                TESTFIELD("Wage Period From");
                TESTFIELD("Wage Period To");
              END;
              IF PayrollElement.IsAECalc THEN BEGIN
                TESTFIELD("Document Type");
                TESTFIELD("AE Period From");
                TESTFIELD("AE Period To");
              END;
            END;

            PayrollPeriod.GET("Period Code");

            IF ("Time Activity Code" <> '') AND (NOT Terminated) THEN BEGIN
              TimesheetStatus.GET("Period Code","Employee No.");
              TimesheetStatus.TESTFIELD(Status,TimesheetStatus.Status::Open);
            END;

            IF "Posting Date" <> NORMALDATE("Posting Date") THEN
              FIELDERROR("Posting Date",Text000);

            IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
              IF USERID <> '' THEN
                IF UserSetup.GET(USERID) THEN BEGIN
                  AllowPostingFrom := UserSetup."Allow Posting From";
                  AllowPostingTo := UserSetup."Allow Posting To";
                END;
              IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
                GLSetup.GET;
                AllowPostingFrom := GLSetup."Allow Posting From";
                AllowPostingTo := GLSetup."Allow Posting To";
              END;
              IF AllowPostingTo = 0D THEN
                AllowPostingTo := 31129999D;
            END;
            IF ("Posting Date" < AllowPostingFrom) OR ("Posting Date" > AllowPostingTo) THEN
              FIELDERROR("Posting Date",Text001);

            IF "Document Date" <> 0D THEN
              IF "Document Date" <> NORMALDATE("Document Date") THEN
                FIELDERROR("Document Date",Text000);

            IF NOT DimMgt.CheckDimIDComb("Dimension Set ID") THEN
              ERROR(
                Text003,
                TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
                DimMgt.GetDimCombErr);

            TableID[1] := DATABASE::Employee;
            No[1] := "Employee No.";
            IF NOT DimMgt.CheckDimValuePosting(TableID,No,"Dimension Set ID") THEN
              IF "Line No." <> 0 THEN
                ERROR(
                  Text004,
                  TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
                  DimMgt.GetDimValuePostingErr)
              ELSE
                ERROR(DimMgt.GetDimValuePostingErr);
          END;

  }
  CODE
  {
    VAR
      GLSetup@1210004 : Record 98;
      UserSetup@1210003 : Record 91;
      PayrollPeriod@1210009 : Record 17426;
      TimesheetStatus@1210005 : Record 17440;
      PayrollElement@1210013 : Record 17400;
      DimMgt@1210010 : Codeunit 408;
      AllowPostingFrom@1210001 : Date;
      AllowPostingTo@1210000 : Date;
      Text000@1210008 : TextConst 'ENU=cannot be a closing date;RUS=не может быть датой закрытия';
      Text001@1210007 : TextConst 'ENU=is not within your range of allowed posting dates;RUS=находится за пределами разрешенного диапазона дат учета';
      Text002@1210002 : TextConst 'ENU=%1 or %2 should be entered;RUS=Должно быть введено %1 или %2';
      Text003@1210011 : TextConst 'ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5;RUS=Комбинация измерений, использованная в %1 %2, %3, %4, заблокирована. %5';
      Text004@1210012 : TextConst 'ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5;RUS=Измерение, использованное в %1 %2, %3, %4, вызвало ошибку. %5';

    BEGIN
    END.
  }
}

