OBJECT Page 522 View Applied Entries
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    Permissions=TableData 339=rimd;
    CaptionML=[ENU=View Applied Entries;
               RUS=Просмотреть примененные операции];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=Yes;
    SourceTable=Table32;
    DataCaptionExpr=CaptionExpr;
    PageType=Worksheet;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             RemoveAppButtonVisible := TRUE;
           END;

    OnOpenPage=BEGIN
                 CurrPage.LOOKUPMODE := NOT ShowApplied;
                 RemoveAppButtonVisible := ShowApplied;
                 Show;
               END;

    OnFindRecord=BEGIN
                   EXIT(FIND(Which));
                 END;

    OnAfterGetRecord=BEGIN
                       GetApplQty;
                     END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 36      ;1   ;ActionGroup;
                      CaptionML=[ENU=Ent&ry;
                                 RUS=Оп&ерация];
                      Image=Entry }
      { 37      ;2   ;Action    ;
                      AccessByPermission=TableData 348=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=[ENU=Dimensions;
                                 RUS=Измерения];
                      ToolTipML=[ENU=View or edit dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                                 RUS=Просмотр или изменение измерений, например области, проекта или отдела, которые можно назначать документам покупки и продажи для распределения себестоимости и анализа истории транзакций.];
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                               END;
                                }
      { 48      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[ENU=&Value Entries;
                                 RUS=Операции &стоимости];
                      RunObject=Page 5802;
                      RunPageView=SORTING(Item Ledger Entry No.);
                      RunPageLink=Item Ledger Entry No.=FIELD(Entry No.);
                      Image=ValueLedger }
      { 38      ;2   ;Action    ;
                      AccessByPermission=TableData 27=R;
                      CaptionML=[ENU=Reservation Entries;
                                 RUS=Операции резервирования];
                      Image=ReservationLedger;
                      OnAction=BEGIN
                                 ShowReservationEntries(TRUE);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1       ;1   ;Action    ;
                      Name=RemoveAppButton;
                      CaptionML=[ENU=Re&move Application;
                                 RUS=Уд&аление применения];
                      Promoted=Yes;
                      Visible=RemoveAppButtonVisible;
                      Image=Cancel;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 UnapplyRec;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the entry's posting date.;
                           RUS=Определяет дату учета операции.];
                SourceExpr="Posting Date" }

    { 5   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies which type of transaction that the entry is created from.;
                           RUS=Определяет тип транзакции, из которой создана операция.];
                SourceExpr="Entry Type" }

    { 7   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies what type of document was posted to create the item ledger entry.;
                           RUS=Определяет тип документа, учтенного для создания операции книги товаров.];
                SourceExpr="Document Type" }

    { 9   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the document number on the entry. The document is the voucher that the entry was based on, for example, a receipt.;
                           RUS=Определяет номер документа в операции. Документом является ваучер, на котором основана операция, например приходная накладная.];
                SourceExpr="Document No." }

    { 11  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the line on the posted document that corresponds to the item ledger entry.;
                           RUS=Определяет номер строки в учтенном документе, которая соответствует операции книги товаров.];
                SourceExpr="Document Line No.";
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the item in the entry.;
                           RUS=Определяет номер товара в операции.];
                SourceExpr="Item No.";
                Visible=FALSE }

    { 19  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the variant code for the items.;
                           RUS=Определяет код варианта товаров.];
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 21  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies a serial number if the posted item carries such a number.;
                           RUS=Определяет серийный номер, если на учтенном товаре указан такой номер.];
                SourceExpr="Serial No.";
                Visible=FALSE }

    { 23  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies a lot number if the posted item carries such a number.;
                           RUS=Определяет номер партии, если на учтенном товаре указан такой номер.];
                SourceExpr="Lot No.";
                Visible=FALSE }

    { 25  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies a description of the entry.;
                           RUS=Определяет описание операции.];
                SourceExpr=Description;
                Visible=FALSE }

    { 27  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the dimension value code that the entry is linked to.;
                           RUS=Определяет код значения измерения, с которым связана операция.];
                SourceExpr="Global Dimension 1 Code";
                Visible=FALSE }

    { 29  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the dimension value code that the entry is linked to.;
                           RUS=Определяет код значения измерения, с которым связана операция.];
                SourceExpr="Global Dimension 2 Code";
                Visible=FALSE }

    { 31  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the location that the entry is linked to.;
                           RUS=Определяет код склада, с которым связана эта операция.];
                SourceExpr="Location Code";
                Visible=FALSE }

    { 33  ;2   ;Field     ;
                CaptionML=[ENU=Applied Quantity;
                           RUS=Примененное количество];
                ToolTipML=[ENU=Specifies the quantity of the item ledger entry linked to an inventory decrease, or increase, as appropriate.;
                           RUS=Определяет количество операции книги товаров, которая была связана соответственно с уменьшением или увеличением запасов.];
                DecimalPlaces=0:5;
                SourceExpr=ApplQty }

    { 35  ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           RUS=Кол-во];
                ToolTipML=[ENU=Specifies the quantity of the item ledger entry.;
                           RUS=Определяет количество операции книги товаров.];
                DecimalPlaces=0:5;
                SourceExpr=Qty }

    { 40  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the adjusted cost, in LCY, of the quantity posting.;
                           RUS=Определяет скорректированную себестоимость учитываемого количества в рублях.];
                SourceExpr="Cost Amount (Actual)" }

    { 42  ;2   ;Field     ;
                CaptionML=[ENU=Unit Cost(LCY);
                           RUS=Себестоимость единицы (руб.)];
                ToolTipML=[ENU=Specifies the unit cost of the item in the item ledger entry.;
                           RUS=Определяет себестоимость единицы товара в операции книги товаров.];
                SourceExpr=GetUnitCostLCY;
                Visible=FALSE }

    { 44  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies how many units of the item on the line have been invoiced.;
                           RUS=Определяет, на какое количество единиц товара из строки были выставлены счета.];
                SourceExpr="Invoiced Quantity";
                Visible=TRUE }

    { 46  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies how many units of the item on the line have been reserved.;
                           RUS=Определяет, какое количество единиц товара из строки было зарезервировано.];
                SourceExpr="Reserved Quantity" }

    { 49  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity that remains in inventory in the Quantity field if the entry is an increase (a purchase or positive adjustment).;
                           RUS=Определяет количество, остающееся в запасах в поле "Кол-во", если операция является операцией увеличения (покупка или положительная корректировка).];
                SourceExpr="Remaining Quantity";
                Visible=TRUE }

    { 1102601000;2;Field  ;
                CaptionML=[ENU=Quantity Available for Cost Applications;
                           RUS=Доступное количество для применения себестоимости];
                DecimalPlaces=0:5;
                SourceExpr=CostAvailable(Rec) }

    { 51  ;2   ;Field     ;
                CaptionML=[ENU=Available for Quantity Application;
                           RUS=Доступное количество для применения];
                DecimalPlaces=0:5;
                SourceExpr=QuantityAvailable(Rec) }

    { 53  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity for this item ledger entry that was shipped and has not yet been returned.;
                           RUS=Определяет количество для этой операции книги товаров, которое было отгружено и еще не было возвращено.];
                SourceExpr="Shipped Qty. Not Returned" }

    { 55  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies whether the entry has been fully applied to.;
                           RUS=Определяет, полностью ли применена операция.];
                SourceExpr=Open }

    { 57  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity per item unit of measure.;
                           RUS=Определяет количество на единицу измерения товара.];
                SourceExpr="Qty. per Unit of Measure";
                Visible=FALSE }

    { 59  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies whether the items on the line have been shipped directly to the customer.;
                           RUS=Определяет, были ли товары этой строки отгружены клиенту в виде прямой поставки.];
                SourceExpr="Drop Shipment";
                Visible=FALSE }

    { 61  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the item entry number that was applied to when the entry was posted, if an already-posted document was designated to be applied.;
                           RUS=Определяет номер товарной операции, примененный при учете операции, если для уже учтенного документа было задано применение к нему товарной операции.];
                SourceExpr="Applies-to Entry";
                Visible=FALSE }

    { 63  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies whether there is one or more applied entries, which need to be adjusted.;
                           RUS=Определяет, есть ли примененные операции, которые необходимо скорректировать.];
                SourceExpr="Applied Entry to Adjust";
                Visible=FALSE }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies which type of order that the entry was created in.;
                           RUS=Определяет тип заказа, в котором создана операция.];
                SourceExpr="Order Type" }

    { 65  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the order that created the entry.;
                           RUS=Определяет номер заказа, на основании которого создана операция.];
                SourceExpr="Order No.";
                Visible=FALSE }

    { 67  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the entry number for the entry.;
                           RUS=Определяет номер операции для операции.];
                SourceExpr="Entry No." }

  }
  CODE
  {
    VAR
      RecordToShow@1008 : Record 32;
      TempItemLedgEntry@1001 : TEMPORARY Record 32;
      Apply@1003 : Codeunit 22;
      ShowApplied@1002 : Boolean;
      ShowQuantity@1007 : Boolean;
      MaxToApply@1004 : Decimal;
      ApplQty@1005 : Decimal;
      Qty@1006 : Decimal;
      TotalApplied@1015 : Decimal;
      Text001@1000 : TextConst 'ENU=Applied Entries;RUS=Примененные операции';
      Text002@1009 : TextConst 'ENU=Unapplied Entries;RUS=Непримененные операции';
      RemoveAppButtonVisible@19026324 : Boolean INDATASET;

    PROCEDURE SetRecordToShow@7(VAR RecordToSet@1000 : Record 32;VAR ApplyCodeunit@1001 : Codeunit 22;newShowApplied@1002 : Boolean);
    BEGIN
      RecordToShow.COPY(RecordToSet);
      Apply := ApplyCodeunit;
      ShowApplied := newShowApplied;
    END;

    LOCAL PROCEDURE Show@8();
    VAR
      ItemLedgEntry@1000 : Record 32;
      Apprec@1102601000 : Record 339;
    BEGIN
      WITH ItemLedgEntry DO BEGIN
        GET(RecordToShow."Entry No.");
        ShowQuantity := NOT (("Entry Type" IN ["Entry Type"::Sale,"Entry Type"::Consumption,"Entry Type"::Output]) AND Positive);

        MaxToApply := 0;
        IF NOT ShowQuantity THEN
          MaxToApply := Quantity + Apprec.Returned("Entry No.");
      END;
      SetMyView(RecordToShow,ShowApplied,ShowQuantity,MaxToApply);
    END;

    LOCAL PROCEDURE SetMyView@22(ItemLedgEntry@1000 : Record 32;ShowApplied@1002 : Boolean;ShowQuantity@1004 : Boolean;MaxToApply@1005 : Decimal);
    BEGIN
      InitView;
      CASE ShowQuantity OF
        TRUE:
          CASE ShowApplied OF
            TRUE:
              ShowQuantityApplied(ItemLedgEntry);
            FALSE:
              BEGIN
                ShowQuantityOpen(ItemLedgEntry);
                ShowCostOpen(ItemLedgEntry,MaxToApply);
              END;
          END;
        FALSE:
          CASE ShowApplied OF
            TRUE:
              ShowCostApplied(ItemLedgEntry);
            FALSE:
              ShowCostOpen(ItemLedgEntry,MaxToApply);
          END;
      END;

      IF TempItemLedgEntry.FINDSET THEN
        REPEAT
          Rec := TempItemLedgEntry;
          INSERT;
        UNTIL TempItemLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE InitView@23();
    BEGIN
      DELETEALL;
      TempItemLedgEntry.RESET;
      TempItemLedgEntry.DELETEALL;
    END;

    LOCAL PROCEDURE ShowQuantityApplied@20(ItemLedgEntry@1000 : Record 32);
    VAR
      ItemApplnEntry@1002 : Record 339;
    BEGIN
      InitApplied;
      WITH ItemLedgEntry DO
        IF Positive THEN BEGIN
          ItemApplnEntry.RESET;
          ItemApplnEntry.SETCURRENTKEY("Inbound Item Entry No.","Outbound Item Entry No.","Cost Application");
          ItemApplnEntry.SETRANGE("Inbound Item Entry No.","Entry No.");
          ItemApplnEntry.SETFILTER("Outbound Item Entry No.",'<>%1&<>%2',"Entry No.",0);
          IF ItemApplnEntry.FIND('-') THEN
            REPEAT
              InsertTempEntry(ItemApplnEntry."Outbound Item Entry No.",ItemApplnEntry.Quantity,TRUE);
            UNTIL ItemApplnEntry.NEXT = 0;
        END ELSE BEGIN
          ItemApplnEntry.RESET;
          ItemApplnEntry.SETCURRENTKEY("Outbound Item Entry No.","Item Ledger Entry No.","Cost Application");
          ItemApplnEntry.SETRANGE("Outbound Item Entry No.","Entry No.");
          ItemApplnEntry.SETRANGE("Item Ledger Entry No.","Entry No.");
          IF ItemApplnEntry.FIND('-') THEN
            REPEAT
              InsertTempEntry(ItemApplnEntry."Inbound Item Entry No.",-ItemApplnEntry.Quantity,TRUE);
            UNTIL ItemApplnEntry.NEXT = 0;
        END;
    END;

    LOCAL PROCEDURE ShowQuantityOpen@19(ItemLedgEntry@1000 : Record 32);
    VAR
      ItemApplnEntry@1002 : Record 339;
      ItemLedgEntry2@1001 : Record 32;
    BEGIN
      WITH ItemLedgEntry DO
        IF "Remaining Quantity" <> 0 THEN BEGIN
          ItemLedgEntry2.SETCURRENTKEY("Item No.",Open,"Variant Code",Positive,"Location Code","Posting Date");
          ItemLedgEntry2.SETRANGE("Item No.","Item No.");
          ItemLedgEntry2.SETRANGE("Location Code" ,"Location Code");
          ItemLedgEntry2.SETRANGE(Positive,NOT Positive);
          ItemLedgEntry2.SETRANGE(Open,TRUE);
          IF ItemLedgEntry2.FIND('-') THEN
            REPEAT
              IF (QuantityAvailable(ItemLedgEntry2) <> 0) AND
                 NOT ItemApplnEntry.ExistsBetween("Entry No.",ItemLedgEntry2."Entry No.")
              THEN
                InsertTempEntry(ItemLedgEntry2."Entry No.",0,TRUE);
            UNTIL ItemLedgEntry2.NEXT = 0;
        END;
    END;

    LOCAL PROCEDURE ShowCostApplied@18(ItemLedgEntry@1000 : Record 32);
    VAR
      ItemApplnEntry@1002 : Record 339;
    BEGIN
      InitApplied;
      WITH ItemLedgEntry DO
        IF Positive THEN BEGIN
          ItemApplnEntry.RESET;
          ItemApplnEntry.SETCURRENTKEY("Inbound Item Entry No.","Outbound Item Entry No.","Cost Application");
          ItemApplnEntry.SETRANGE("Inbound Item Entry No.","Entry No.");
          ItemApplnEntry.SETFILTER("Item Ledger Entry No.",'<>%1',"Entry No.");
          ItemApplnEntry.SETFILTER("Outbound Item Entry No.",'<>%1',0);
          ItemApplnEntry.SETRANGE("Cost Application",TRUE); // we want to show even average cost application
          IF ItemApplnEntry.FIND('-') THEN
            REPEAT
              InsertTempEntry(ItemApplnEntry."Outbound Item Entry No.",ItemApplnEntry.Quantity,FALSE);
            UNTIL ItemApplnEntry.NEXT = 0;
        END ELSE BEGIN
          ItemApplnEntry.RESET;
          ItemApplnEntry.SETCURRENTKEY("Outbound Item Entry No.","Item Ledger Entry No.","Cost Application");
          ItemApplnEntry.SETRANGE("Outbound Item Entry No.","Entry No.");
          ItemApplnEntry.SETFILTER("Item Ledger Entry No.",'<>%1',"Entry No.");
          ItemApplnEntry.SETRANGE("Cost Application",TRUE); // we want to show even average cost application
          IF ItemApplnEntry.FIND('-') THEN
            REPEAT
              InsertTempEntry(ItemApplnEntry."Inbound Item Entry No.",-ItemApplnEntry.Quantity,FALSE);
            UNTIL ItemApplnEntry.NEXT = 0;
        END;
    END;

    LOCAL PROCEDURE ShowCostOpen@17(ItemLedgEntry@1000 : Record 32;MaxToApply@1003 : Decimal);
    VAR
      ItemApplnEntry@1002 : Record 339;
      ItemLedgEntry2@1001 : Record 32;
    BEGIN
      WITH ItemLedgEntry DO BEGIN
        ItemLedgEntry2.SETCURRENTKEY("Item No.",Positive,"Location Code","Variant Code");
        ItemLedgEntry2.SETRANGE("Item No.","Item No.");
        ItemLedgEntry2.SETRANGE("Location Code","Location Code");
        ItemLedgEntry2.SETRANGE(Positive,NOT Positive);
        ItemLedgEntry2.SETFILTER("Shipped Qty. Not Returned",'<%1&>=%2',0,-MaxToApply);
        IF (MaxToApply <> 0) AND Positive THEN
          ItemLedgEntry2.SETFILTER("Shipped Qty. Not Returned",'<=%1',-MaxToApply);
        IF ItemLedgEntry2.FIND('-') THEN
          REPEAT
            IF (CostAvailable(ItemLedgEntry2) <> 0) AND
               NOT ItemApplnEntry.ExistsBetween("Entry No.",ItemLedgEntry2."Entry No.")
            THEN
              InsertTempEntry(ItemLedgEntry2."Entry No.",0,TRUE);
          UNTIL ItemLedgEntry2.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE InsertTempEntry@21(EntryNo@1000 : Integer;AppliedQty@1001 : Decimal;ShowQuantity@1003 : Boolean);
    VAR
      ItemLedgEntry@1002 : Record 32;
    BEGIN
      ItemLedgEntry.GET(EntryNo);

      IF ShowQuantity THEN
        IF AppliedQty * ItemLedgEntry.Quantity < 0 THEN
          EXIT;

      IF NOT TempItemLedgEntry.GET(EntryNo) THEN BEGIN
        TempItemLedgEntry.RESET;
        TempItemLedgEntry := ItemLedgEntry;
        TempItemLedgEntry.CALCFIELDS("Reserved Quantity");
        TempItemLedgEntry.Quantity := AppliedQty;
        TempItemLedgEntry.INSERT;
      END ELSE BEGIN
        TempItemLedgEntry.Quantity := TempItemLedgEntry.Quantity + AppliedQty;
        TempItemLedgEntry.MODIFY;
      END;

      TotalApplied := TotalApplied + AppliedQty;
    END;

    LOCAL PROCEDURE InitApplied@13();
    BEGIN
      CLEAR(TotalApplied);
    END;

    LOCAL PROCEDURE RemoveApplications@1(Inbound@1000 : Integer;OutBound@1001 : Integer);
    VAR
      Application@1003 : Record 339;
    BEGIN
      Application.SETCURRENTKEY("Inbound Item Entry No.","Outbound Item Entry No.");
      Application.SETRANGE("Inbound Item Entry No.",Inbound);
      Application.SETRANGE("Outbound Item Entry No.",OutBound);
      IF Application.FINDSET THEN
        REPEAT
          Apply.UnApply(Application);
          Apply.LogUnapply(Application);
        UNTIL Application.NEXT = 0;
    END;

    LOCAL PROCEDURE UnapplyRec@3();
    VAR
      Applyrec@1001 : Record 32;
      AppliedItemLedgEntry@1002 : Record 32;
    BEGIN
      Applyrec.GET(RecordToShow."Entry No.");
      CurrPage.SETSELECTIONFILTER(TempItemLedgEntry);
      IF TempItemLedgEntry.FINDSET THEN
        REPEAT
          AppliedItemLedgEntry.GET(TempItemLedgEntry."Entry No.");
          IF AppliedItemLedgEntry."Entry No." <> 0 THEN BEGIN
            IF Applyrec.Positive THEN
              RemoveApplications(Applyrec."Entry No.",AppliedItemLedgEntry."Entry No.")
            ELSE
              RemoveApplications(AppliedItemLedgEntry."Entry No.",Applyrec."Entry No.");
          END;
        UNTIL TempItemLedgEntry.NEXT = 0;

      BlockItem(Applyrec."Item No.");
      Show;
    END;

    PROCEDURE ApplyRec@5();
    VAR
      Applyrec@1000 : Record 32;
      AppliedItemLedgEntry@1002 : Record 32;
    BEGIN
      Applyrec.GET(RecordToShow."Entry No.");
      CurrPage.SETSELECTIONFILTER(TempItemLedgEntry);
      IF TempItemLedgEntry.FINDSET THEN
        REPEAT
          AppliedItemLedgEntry.GET(TempItemLedgEntry."Entry No.");
          IF AppliedItemLedgEntry."Entry No." <> 0 THEN BEGIN
            Apply.ReApply(Applyrec,AppliedItemLedgEntry."Entry No.");
            Apply.LogApply(Applyrec,AppliedItemLedgEntry);
          END;
        UNTIL TempItemLedgEntry.NEXT = 0;

      IF Applyrec.Positive THEN
        RemoveDuplicateApplication(Applyrec."Entry No.");

      Show;
    END;

    LOCAL PROCEDURE RemoveDuplicateApplication@4(ItemLedgerEntryNo@1000 : Integer);
    VAR
      ItemApplicationEntry@1001 : Record 339;
    BEGIN
      WITH ItemApplicationEntry DO BEGIN
        SETCURRENTKEY("Inbound Item Entry No.","Item Ledger Entry No.","Outbound Item Entry No.","Cost Application");
        SETRANGE("Inbound Item Entry No.",ItemLedgerEntryNo);
        SETRANGE("Item Ledger Entry No.",ItemLedgerEntryNo);
        SETFILTER("Outbound Item Entry No.",'<>0');
        IF NOT ISEMPTY THEN BEGIN
          SETRANGE("Outbound Item Entry No.",0);
          DELETEALL;
        END
      END;
    END;

    LOCAL PROCEDURE BlockItem@9(ItemNo@1000 : Code[20]);
    VAR
      Item@1001 : Record 27;
    BEGIN
      Item.GET(ItemNo);
      IF Item."Application Wksh. User ID" <> UPPERCASE(USERID) THEN
        Item.CheckBlockedByApplWorksheet;

      Item."Application Wksh. User ID" := USERID;
      Item.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE GetApplQty@10();
    VAR
      ItemLedgEntry@1000 : Record 32;
    BEGIN
      ItemLedgEntry.GET("Entry No.");
      ApplQty := Quantity;
      Qty := ItemLedgEntry.Quantity;
    END;

    LOCAL PROCEDURE QuantityAvailable@11(ILE@1000 : Record 32) : Decimal;
    BEGIN
      WITH ILE DO BEGIN
        CALCFIELDS("Reserved Quantity");
        EXIT("Remaining Quantity" - "Reserved Quantity");
      END;
    END;

    LOCAL PROCEDURE CostAvailable@1102601000(ILE@1000 : Record 32) : Decimal;
    VAR
      Apprec@1102601000 : Record 339;
    BEGIN
      WITH ILE DO BEGIN
        IF "Shipped Qty. Not Returned" <> 0 THEN
          EXIT(-"Shipped Qty. Not Returned");

        EXIT("Remaining Quantity" + Apprec.Returned("Entry No."));
      END;
    END;

    LOCAL PROCEDURE CaptionExpr@2() : Text[250];
    BEGIN
      IF ShowApplied THEN
        EXIT(Text001);

      EXIT(Text002);
    END;

    BEGIN
    END.
  }
}

