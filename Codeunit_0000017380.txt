OBJECT Codeunit 17380 EmployeeJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    Permissions=TableData 17380=imd,
                TableData 17381=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      OldEmployeeNo@1005 : Code[20];
      OpenFromBatch@1012 : Boolean;
      Text001@1210000 : TextConst 'ENU=%1 journal;RUS=журнал %1';
      Text003@1210001 : TextConst 'ENU=DEFAULT;RUS=СТАНДАРТ';
      Text004@1210002 : TextConst 'ENU=Default Journal;RUS=Журнал по умолчанию';

    PROCEDURE TemplateSelection@1(FormID@1005 : Integer;FormTemplate@1210000 : 'Salary,Vacation';VAR EmplJnlLine@1004 : Record 17382;VAR JnlSelected@1003 : Boolean);
    VAR
      EmplJnlTemplate@1002 : Record 17380;
    BEGIN
      JnlSelected := TRUE;

      EmplJnlTemplate.RESET;

      CASE EmplJnlTemplate.COUNT OF
        0:
          BEGIN
            EmplJnlTemplate.INIT;
            EmplJnlTemplate.Type := FormTemplate;
            EmplJnlTemplate.Name := DELCHR(FORMAT(EmplJnlTemplate.Type,MAXSTRLEN(EmplJnlTemplate.Name)));
            EmplJnlTemplate.Description := STRSUBSTNO(Text001,EmplJnlTemplate.Type);
            EmplJnlTemplate.VALIDATE(Type);
            EmplJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          EmplJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,EmplJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        EmplJnlLine.FILTERGROUP := 2;
        EmplJnlLine.SETRANGE("Journal Template Name",EmplJnlTemplate.Name);
        EmplJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          EmplJnlLine."Journal Template Name" := '';
          PAGE.RUN(EmplJnlTemplate."Page ID",EmplJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@10(VAR EmplJnlBatch@1000 : Record 17381);
    VAR
      EmplJnlLine@1002 : Record 17382;
      EmplJnlTemplate@1003 : Record 17380;
    BEGIN
      OpenFromBatch := TRUE;
      EmplJnlTemplate.GET(EmplJnlBatch."Journal Template Name");
      EmplJnlTemplate.TESTFIELD("Page ID");
      EmplJnlBatch.TESTFIELD(Name);

      EmplJnlLine.FILTERGROUP := 2;
      EmplJnlLine.SETRANGE("Journal Template Name",EmplJnlTemplate.Name);
      EmplJnlLine.FILTERGROUP := 0;

      EmplJnlLine."Journal Template Name" := '';
      EmplJnlLine."Journal Batch Name" := EmplJnlBatch.Name;
      PAGE.RUN(EmplJnlTemplate."Page ID",EmplJnlLine);
    END;

    PROCEDURE OpenJnl@2(VAR CurrentJnlBatchName@1000 : Code[10];VAR EmplJnlLine@1001 : Record 17382);
    BEGIN
      CheckTemplateName(EmplJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      EmplJnlLine.FILTERGROUP := 2;
      EmplJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      EmplJnlLine.FILTERGROUP := 0;
    END;

    PROCEDURE OpenJnlBatch@12(VAR EmplJnlBatch@1000 : Record 17381);
    VAR
      EmplJnlTemplate@1002 : Record 17380;
      JnlSelected@1004 : Boolean;
    BEGIN
      IF EmplJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;
      EmplJnlBatch.FILTERGROUP(2);
      IF EmplJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        EmplJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      EmplJnlBatch.FILTERGROUP(0);

      EmplJnlBatch.FIND('-');
      JnlSelected := TRUE;
      IF EmplJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EmplJnlTemplate.SETRANGE(Name,EmplJnlBatch.GETFILTER("Journal Template Name"));
      CASE EmplJnlTemplate.COUNT OF
        1:
          EmplJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,EmplJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      EmplJnlBatch.FILTERGROUP(2);
      EmplJnlBatch.SETRANGE("Journal Template Name",EmplJnlTemplate.Name);
      EmplJnlBatch.FILTERGROUP(0);
    END;

    PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      EmplJnlBatch@1002 : Record 17381;
    BEGIN
      EmplJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT EmplJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT EmplJnlBatch.FINDFIRST THEN BEGIN
          EmplJnlBatch.INIT;
          EmplJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          EmplJnlBatch.SetupNewBatch;
          EmplJnlBatch.Name := Text003;
          EmplJnlBatch.Description := Text004;
          EmplJnlBatch.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := EmplJnlBatch.Name
      END;
    END;

    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR EmplJnlLine@1001 : Record 17382);
    VAR
      EmplJnlBatch@1002 : Record 17381;
    BEGIN
      EmplJnlBatch.GET(EmplJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@5(CurrentJnlBatchName@1000 : Code[10];VAR EmplJnlLine@1001 : Record 17382);
    BEGIN
      EmplJnlLine.FILTERGROUP := 2;
      EmplJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      EmplJnlLine.FILTERGROUP := 0;
      IF EmplJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@6(VAR CurrentJnlBatchName@1000 : Code[10];VAR EmplJnlLine@1001 : Record 17382);
    VAR
      EmplJnlBatch@1002 : Record 17381;
    BEGIN
      COMMIT;
      EmplJnlBatch."Journal Template Name" := EmplJnlLine.GETRANGEMAX("Journal Template Name");
      EmplJnlBatch.Name := EmplJnlLine.GETRANGEMAX("Journal Batch Name");
      EmplJnlBatch.FILTERGROUP(2);
      EmplJnlBatch.SETRANGE("Journal Template Name",EmplJnlBatch."Journal Template Name");
      EmplJnlBatch.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,EmplJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := EmplJnlBatch.Name;
        SetName(CurrentJnlBatchName,EmplJnlLine);
      END;
    END;

    PROCEDURE GetEmployee@7(EmployeeNo@1000 : Code[20];VAR EmployeeName@1001 : Text[50]);
    VAR
      Employee@1002 : Record 5200;
    BEGIN
      IF EmployeeNo <> OldEmployeeNo THEN BEGIN
        EmployeeName := '';
        IF EmployeeNo <> '' THEN
          IF Employee.GET(EmployeeNo) THEN
            EmployeeName := Employee.GetFullName;
        OldEmployeeNo := EmployeeNo;
      END;
    END;

    PROCEDURE GetAccounts@1210000(EmplJnlLine@1000 : Record 17382;VAR EmployeeName@1001 : Text[100];VAR ElementName@1002 : Text[100]);
    VAR
      Employee@1210000 : Record 5200;
      PayrollElement@1210001 : Record 17400;
    BEGIN
      EmployeeName := '';
      IF Employee.GET(EmplJnlLine."Employee No.") THEN
        EmployeeName := Employee.GetFullNameOnDate(EmplJnlLine."Starting Date");

      ElementName := '';
      IF PayrollElement.GET(EmplJnlLine."Element Code") THEN
        ElementName := PayrollElement.Description;
    END;

    BEGIN
    END.
  }
}

