OBJECT Page 17208 Tax Register Term Lines
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Tax Register Term Lines;
               RUS=Строка термина налогового регистра];
    SourceTable=Table17205;
    PageType=List;
    AutoSplitKey=Yes;
    OnInit=BEGIN
             AmountTypeEditable := TRUE;
             AccountTypeEditable := TRUE;
             OperationEditable := TRUE;
             BalAccountNoEditable := TRUE;
           END;

    OnAfterGetRecord=BEGIN
                       SetEnable;
                     END;

    OnNewRecord=BEGIN
                  CALCFIELDS("Expression Type");
                  CASE "Expression Type" OF
                    "Expression Type"::"Plus/Minus":
                      Operation := Operation::"+";
                    "Expression Type"::"Multiply/Divide":
                      Operation := Operation::"*";
                    ELSE BEGIN
                      IF NOT (COUNT = 3) THEN
                        CurrPage.CLOSE;
                      Operation := Operation::Negative;
                      "Account Type" := "Account Type"::Term;
                    END;
                  END;
                END;

    OnDeleteRecord=BEGIN
                     CALCFIELDS("Expression Type");
                     IF "Expression Type" = "Expression Type"::Compare THEN BEGIN
                       IF NOT CONFIRM(Text001,FALSE) THEN
                         EXIT(FALSE);
                       DELETEALL;
                       CurrPage.CLOSE;
                     END;
                     EXIT(TRUE);
                   END;

    OnQueryClosePage=BEGIN
                       CALCFIELDS("Expression Type");
                       IF NOT ("Expression Type" = "Expression Type"::Compare) THEN
                         EXIT(TRUE);
                       IF NOT (COUNT = 3) THEN
                         EXIT(TRUE);
                       ExternReportFormula1.COPY(Rec);
                       ExternReportFormula1.FIND('-');
                       IF ExternReportFormula1."Account No." = '' THEN BEGIN
                         Rec := ExternReportFormula1;
                         CurrPage.UPDATE(FALSE);
                         ExternReportFormula1.TESTFIELD("Account No.");
                       END;
                       REPEAT
                         IF ExternReportFormula1."Bal. Account No." = '' THEN BEGIN
                           Rec := ExternReportFormula1;
                           CurrPage.UPDATE(FALSE);
                           ExternReportFormula1.TESTFIELD("Bal. Account No.");
                         END;
                       UNTIL ExternReportFormula1.NEXT = 0;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           SetEnable;
                         END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the operation associated with the tax register term line.;
                           RUS=Определяет операцию, связанную со строкой термина налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Operation;
                Editable=OperationEditable }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the account type associated with the tax register term line.;
                           RUS=Определяет тип счета, связанный со строкой термина налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Account Type";
                Editable=AccountTypeEditable;
                OnValidate=BEGIN
                             AccountTypeOnAfterValidate;
                           END;
                            }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the account number associated with the tax register term line.;
                           RUS=Определяет номер счета, связанный со строкой термина налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Account No.";
                OnValidate=BEGIN
                             AccountNoOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           CASE "Account Type" OF
                             "Account Type"::"G/L Account","Account Type"::"Net Change":
                               BEGIN
                                 GLAcc.RESET;
                                 IF "Bal. Account No." <> '' THEN
                                   IF STRPOS('|&<>',COPYSTR("Account No.",STRLEN("Account No."))) = 0 THEN BEGIN
                                     GLAcc.SETFILTER("No.","Account No.");
                                     IF GLAcc.FINDFIRST THEN ;
                                     GLAcc.SETRANGE("No.");
                                   END;
                                 IF ACTION::LookupOK = PAGE.RUNMODAL(0,GLAcc) THEN BEGIN
                                   Text := GLAcc."No.";
                                   EXIT(TRUE);
                                 END;
                               END;
                             "Account Type"::Term:
                               BEGIN
                                 TaxRegTermName.RESET;
                                 IF "Account No." <> '' THEN BEGIN
                                   TaxRegTermName.SETFILTER("Term Code","Account No.");
                                   IF TaxRegTermName.FINDFIRST THEN ;
                                   TaxRegTermName.SETRANGE("Term Code");
                                 END;
                                 IF ACTION::LookupOK = PAGE.RUNMODAL(0,TaxRegTermName) THEN BEGIN
                                   "Account No." := '';
                                   Text := TaxRegTermName."Term Code";
                                   EXIT(TRUE);
                                 END;
                               END;
                             "Account Type"::Norm:
                               BEGIN
                                 CALCFIELDS("Norm Jurisdiction Code");
                                 IF "Norm Jurisdiction Code" <> '' THEN BEGIN
                                   TaxRegNormGroup.RESET;
                                   TaxRegNormGroup.FILTERGROUP(2);
                                   TaxRegNormGroup.SETRANGE("Norm Jurisdiction Code","Norm Jurisdiction Code");
                                   TaxRegNormGroup.FILTERGROUP(0);
                                   TaxRegNormGroup.SETRANGE("Has Details",TRUE);
                                   IF TaxRegNormGroup.GET("Norm Jurisdiction Code",COPYSTR("Account No.",1,MAXSTRLEN(TaxRegNormGroup.Code))) THEN ;
                                   IF ACTION::LookupOK = PAGE.RUNMODAL(0,TaxRegNormGroup) THEN BEGIN
                                     "Account No." := '';
                                     Text := TaxRegNormGroup.Code;
                                     EXIT(TRUE);
                                   END;
                                 END;
                               END;
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the balance account number associated with the tax register term line.;
                           RUS=Определяет номер балансового счета, связанный со строкой термина налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Bal. Account No.";
                Editable=BalAccountNoEditable;
                OnLookup=BEGIN
                           IF "Account Type" = "Account Type"::"Net Change" THEN BEGIN
                             GLAcc.RESET;
                             IF "Bal. Account No." <> '' THEN
                               IF STRPOS('|&<>',COPYSTR("Bal. Account No.",STRLEN("Bal. Account No."))) = 0 THEN BEGIN
                                 GLAcc.SETFILTER("No.","Bal. Account No.");
                                 IF GLAcc.FINDFIRST THEN ;
                                 GLAcc.SETRANGE("No.");
                               END;
                             IF ACTION::LookupOK = PAGE.RUNMODAL(0,GLAcc) THEN BEGIN
                               Text := GLAcc."No.";
                               EXIT(TRUE);
                             END;
                           END;
                           CALCFIELDS("Expression Type");
                           IF "Expression Type" = "Expression Type"::Compare THEN BEGIN
                             TaxRegTermName.RESET;
                             IF "Bal. Account No." <> '' THEN BEGIN
                               TaxRegTermName.SETFILTER("Term Code","Bal. Account No.");
                               IF TaxRegTermName.FINDFIRST THEN ;
                               TaxRegTermName.SETRANGE("Term Code");
                             END;
                             IF ACTION::LookupOK = PAGE.RUNMODAL(0,TaxRegTermName) THEN BEGIN
                               "Bal. Account No." := '';
                               Text := TaxRegTermName."Term Code";
                               EXIT(TRUE);
                             END;
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the amount type associated with the tax register term line.;
                           RUS=Определяет тип суммы, связанный со строкой термина налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Amount Type";
                Editable=AmountTypeEditable }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the process sign associated with the tax register term line.;
                           RUS=Определяет знак обработки, связанный со строкой термина налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Process Sign" }

    { 14  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the process division by zero associated with the tax register term line.;
                           RUS=Определяет деление на ноль, связанное со строкой термина налогового регистра.];
                SourceExpr="Process Division by Zero";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Delete all lines?;RUS=Удалить все строки?';
      GLAcc@1002 : Record 15;
      ExternReportFormula1@1003 : Record 17205;
      TaxRegTermName@1004 : Record 17204;
      TaxRegNormGroup@1000000000 : Record 17221;
      BalAccountNoEditable@19037417 : Boolean INDATASET;
      OperationEditable@19015421 : Boolean INDATASET;
      AccountTypeEditable@19025853 : Boolean INDATASET;
      AmountTypeEditable@19052850 : Boolean INDATASET;

    LOCAL PROCEDURE SetEnable@1();
    BEGIN
      CALCFIELDS("Expression Type");
      IF "Expression Type" = "Expression Type"::Compare THEN BEGIN
        BalAccountNoEditable := TRUE;
        OperationEditable := FALSE;
        AccountTypeEditable := FALSE;
      END ELSE
        BalAccountNoEditable := "Account Type" = "Account Type"::"Net Change";

      AmountTypeEditable := "Account Type" IN ["Account Type"::"G/L Account","Account Type"::"Net Change"];
    END;

    LOCAL PROCEDURE AccountTypeOnAfterValidate@19013737();
    BEGIN
      SetEnable;
    END;

    LOCAL PROCEDURE AccountNoOnAfterValidate@19049580();
    BEGIN
      SetEnable;
      CurrPage.UPDATE(TRUE);
    END;

    BEGIN
    END.
  }
}

