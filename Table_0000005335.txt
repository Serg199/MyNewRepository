OBJECT Table 5335 Integration Table Mapping
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               CRMOptionMapping@1001 : Record 5334;
               IntegrationFieldMapping@1000 : Record 5336;
             BEGIN
               IntegrationFieldMapping.RESET;
               IntegrationFieldMapping.SETRANGE("Integration Table Mapping Name",Name);
               IntegrationFieldMapping.DELETEALL;

               CRMOptionMapping.RESET;
               CRMOptionMapping.SETRANGE("Table ID","Table ID");
               CRMOptionMapping.SETRANGE("Integration Table ID","Integration Table ID");
               CRMOptionMapping.SETRANGE("Integration Field ID","Integration Table UID Fld. No.");
               CRMOptionMapping.DELETEALL;
             END;

    CaptionML=[ENU=Integration Table Mapping;
               RUS=Сопоставление таблиц интеграции];
    LookupPageID=Page5335;
    DrillDownPageID=Page5335;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code20        ;CaptionML=[ENU=Name;
                                                              RUS=Название] }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table ID;
                                                              RUS=Код таблицы] }
    { 3   ;   ;Integration Table ID;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Integration Table ID;
                                                              RUS=Код таблицы интеграции] }
    { 4   ;   ;Synch. Codeunit ID  ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[ENU=Synch. Codeunit ID;
                                                              RUS=Код модуля Codeunit синхронизации] }
    { 5   ;   ;Integration Table UID Fld. No.;Integer;
                                                   OnValidate=VAR
                                                                Field@1000 : Record 2000000041;
                                                              BEGIN
                                                                Field.GET("Integration Table ID","Integration Table UID Fld. No.");
                                                                "Int. Table UID Field Type" := Field.Type;
                                                              END;

                                                   CaptionML=[ENU=Integration Table UID Fld. No.;
                                                              RUS=№ поля UID табл. интеграции];
                                                   Description=Integration Table Unique Identifier Field No. }
    { 6   ;   ;Int. Tbl. Modified On Fld. No.;Integer;
                                                   CaptionML=[ENU=Int. Tbl. Modified On Fld. No.;
                                                              RUS=Внутр. табл. изм. по № поля];
                                                   Description=Integration Table Modified On Field No. }
    { 7   ;   ;Int. Table UID Field Type;Integer  ;CaptionML=[ENU=Int. Table UID Field Type;
                                                              RUS=Тип поля кода пользователя таблицы интеграции];
                                                   Editable=No }
    { 8   ;   ;Table Config Template Code;Code10  ;TableRelation="Config. Template Header".Code WHERE (Table ID=FIELD(Table ID));
                                                   CaptionML=[ENU=Table Config Template Code;
                                                              RUS=Код шаблона конфигурации таблиц] }
    { 9   ;   ;Int. Tbl. Config Template Code;Code10;
                                                   TableRelation="Config. Template Header".Code WHERE (Table ID=FIELD(Integration Table ID));
                                                   CaptionML=[ENU=Int. Tbl. Config Template Code;
                                                              RUS=Код шаблона конфигурации внутр. табл.] }
    { 10  ;   ;Direction           ;Option        ;CaptionML=[ENU=Direction;
                                                              RUS=Направление];
                                                   OptionCaptionML=[ENU=Bidirectional,ToIntegrationTable,FromIntegrationTable;
                                                                    RUS=Двунаправленное,ВТаблицуИнтеграции,ИзТаблицыИнтеграции];
                                                   OptionString=Bidirectional,ToIntegrationTable,FromIntegrationTable }
    { 11  ;   ;Int. Tbl. Caption Prefix;Text30    ;CaptionML=[ENU=Int. Tbl. Caption Prefix;
                                                              RUS=Префикс заголовка внутр. табл.] }
    { 12  ;   ;Synch. Int. Tbl. Mod. On Fltr.;DateTime;
                                                   CaptionML=[ENU=Synch. Int. Tbl. Mod. On Fltr.;
                                                              RUS=Синхр. внутр. табл. изм. по флтр.];
                                                   Description=Scheduled synch. Integration Table Modified On Filter }
    { 13  ;   ;Synch. Modified On Filter;DateTime ;CaptionML=[ENU=Synch. Modified On Filter;
                                                              RUS=Синхронизировать измененные по фильтру];
                                                   Description=Scheduled synch. Modified On Filter }
    { 14  ;   ;Table Filter        ;BLOB          ;CaptionML=[ENU=Table Filter;
                                                              RUS=Фильтр таблицы] }
    { 15  ;   ;Integration Table Filter;BLOB      ;CaptionML=[ENU=Integration Table Filter;
                                                              RUS=Фильтр таблицы интеграции] }
    { 16  ;   ;Synch. Only Coupled Records;Boolean;InitValue=Yes;
                                                   CaptionML=[ENU=Synch. Only Coupled Records;
                                                              RUS=Синхр. только связанные записи] }
    { 17  ;   ;Parent Name         ;Code20        ;CaptionML=[ENU=Parent Name;
                                                              RUS=Имя родителя] }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetExtendedIntegrationTableCaption@1() : Text;
    VAR
      TableMetadata@1000 : Record 2000000136;
    BEGIN
      IF NOT TableMetadata.GET("Integration Table ID") THEN
        EXIT('');
      IF "Int. Tbl. Caption Prefix" <> '' THEN
        EXIT(STRSUBSTNO('%1 %2',"Int. Tbl. Caption Prefix",TableMetadata.Caption));
      EXIT(TableMetadata.Caption);
    END;

    PROCEDURE SetTableFilter@5(Filter@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Table Filter".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(Filter);
    END;

    PROCEDURE GetTableFilter@6() Value : Text;
    VAR
      InStream@1000 : InStream;
    BEGIN
      CALCFIELDS("Table Filter");
      "Table Filter".CREATEINSTREAM(InStream);
      InStream.READ(Value);
    END;

    PROCEDURE SetIntegrationTableFilter@7(Filter@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Integration Table Filter".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(Filter);
    END;

    PROCEDURE GetIntegrationTableFilter@8() Value : Text;
    VAR
      InStream@1000 : InStream;
    BEGIN
      CALCFIELDS("Integration Table Filter");
      "Integration Table Filter".CREATEINSTREAM(InStream);
      InStream.READ(Value);
    END;

    PROCEDURE GetAllowedSyncDirection@93(TableNumber@1000 : Integer) : Integer;
    VAR
      IntegrationTableMapping@1001 : Record 5335;
    BEGIN
      IntegrationTableMapping.SETRANGE("Table ID",TableNumber);
      IF IntegrationTableMapping.FINDFIRST THEN
        EXIT(IntegrationTableMapping.Direction);

      IntegrationTableMapping.RESET;
      IntegrationTableMapping.SETRANGE("Integration Table ID",TableNumber);
      IF IntegrationTableMapping.FINDFIRST THEN
        EXIT(IntegrationTableMapping.Direction);

      EXIT(IntegrationTableMapping.Direction::Bidirectional);
    END;

    PROCEDURE SetIntTableModifiedOn@4(ModifiedOn@1000 : DateTime);
    BEGIN
      IF (ModifiedOn <> 0DT) AND (ModifiedOn > "Synch. Int. Tbl. Mod. On Fltr.") THEN BEGIN
        "Synch. Int. Tbl. Mod. On Fltr." := ModifiedOn;
        MODIFY(TRUE);
      END;
    END;

    PROCEDURE SetTableModifiedOn@9(ModifiedOn@1000 : DateTime);
    BEGIN
      IF (ModifiedOn <> 0DT) AND (ModifiedOn > "Synch. Modified On Filter") THEN BEGIN
        "Synch. Modified On Filter" := ModifiedOn;
        MODIFY(TRUE);
      END;
    END;

    PROCEDURE SynchronizeNow@2(ResetLastSynchModifiedOnDateTime@1000 : Boolean);
    BEGIN
      IF ResetLastSynchModifiedOnDateTime THEN BEGIN
        CLEAR("Synch. Modified On Filter");
        CLEAR("Synch. Int. Tbl. Mod. On Fltr.");
        MODIFY;
        COMMIT;
      END;
      CODEUNIT.RUN("Synch. Codeunit ID",Rec);
    END;

    PROCEDURE GetRecordRef@3(ID@1000 : Variant;VAR IntegrationRecordRef@1001 : RecordRef) : Boolean;
    VAR
      IDFieldRef@1002 : FieldRef;
      RecordID@1003 : RecordID;
    BEGIN
      IntegrationRecordRef.CLOSE;
      IF ID.ISGUID THEN BEGIN
        // Integratio
        IntegrationRecordRef.OPEN("Integration Table ID");
        IDFieldRef := IntegrationRecordRef.FIELD("Integration Table UID Fld. No.");
        IDFieldRef.SETFILTER(ID);
        IF IntegrationRecordRef.FINDFIRST THEN
          EXIT(TRUE);
      END ELSE
        IF ID.ISRECORDID THEN BEGIN
          RecordID := ID;
          IF RecordID.TABLENO <> "Table ID" THEN
            EXIT(FALSE);
          IntegrationRecordRef.GET(ID);
          EXIT(TRUE);
        END;

      EXIT(FALSE);
    END;

    PROCEDURE SetIntRecordRefFilter@10(VAR IntRecordRef@1000 : RecordRef);
    VAR
      IntFieldRef@1003 : FieldRef;
      TableFilter@1001 : Text;
    BEGIN
      TableFilter := GetIntegrationTableFilter;
      IF TableFilter <> '' THEN
        IntRecordRef.SETVIEW(TableFilter);

      IF "Synch. Int. Tbl. Mod. On Fltr." <> 0DT THEN BEGIN
        IntFieldRef := IntRecordRef.FIELD("Int. Tbl. Modified On Fld. No.");
        IntFieldRef.SETFILTER('>%1',"Synch. Int. Tbl. Mod. On Fltr.");
      END;
    END;

    PROCEDURE SetRecordRefFilter@11(VAR RecordRef@1000 : RecordRef);
    VAR
      TableFilter@1001 : Text;
    BEGIN
      TableFilter := GetTableFilter;
      IF TableFilter <> '' THEN
        RecordRef.SETVIEW(TableFilter);
    END;

    BEGIN
    END.
  }
}

