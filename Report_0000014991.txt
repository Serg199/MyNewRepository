OBJECT Report 14991 Posted FA Writeoff Act FA-4a
{
  OBJECT-PROPERTIES
  {
    Date=09.09.14;
    Time=12:00:00;
    Version List=NAVRU8.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Posted FA Writeoff Act FA-4a;
               RUS=Учт. акт списания ОС-4a];
    ProcessingOnly=Yes;
    OnInitReport=VAR
                   Employee@1470000 : Record 5200;
                 BEGIN
                   CompanyInfo.GET;
                   FASetup.GET;
                   IF Employee.GET(CompanyInfo."Director No.") THEN
                     DirectorPosition := Employee.GetJobTitleName;
                 END;

    OnPreReport=BEGIN
                  FA4Helper.InitReportTemplate(REPORT::"FA Writeoff Act FA-4a");
                END;

    OnPostReport=BEGIN
                   IF FileName = '' THEN
                     FA4Helper.ExportData
                   ELSE
                     FA4Helper.ExportDataFile(FileName);
                 END;

  }
  DATASET
  {
    { 3950;    ;DataItem;                    ;
               DataItemTable=Table12471;
               DataItemTableView=SORTING(Document Type,No.);
               OnAfterGetRecord=BEGIN
                                  FASetup.GET;

                                  CheckSignature(Chairman,Chairman."Employee Type"::Chairman);
                                  CheckSignature(Member1,Member1."Employee Type"::Member1);
                                  CheckSignature(Member2,Member2."Employee Type"::Member2);
                                END;

               OnPostDataItem=BEGIN
                                FA4Helper.SetDocSheet;
                              END;
                               }

    { 2746;1   ;DataItem;Part 3              ;
               DataItemTable=Table12472;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnPreDataItem=BEGIN
                               IsHeaderPrinted := FALSE;
                             END;

               OnAfterGetRecord=BEGIN
                                  FA.GET("FA No.");
                                  FA.TESTFIELD("Vehicle Model");

                                  FADepreciationBook.GET("FA No.","Depreciation Book Code");
                                  FADepreciationBook.CALCFIELDS("Acquisition Cost",Depreciation);
                                  FAPostingGroup.GET("FA Posting Group");

                                  GetFAComments(Reason,PostedFAComment.Type::Reason);
                                  GetFAComments(Conclusion,PostedFAComment.Type::Conclusion);
                                  GetFAComments(Appendix,PostedFAComment.Type::Appendix);
                                  GetFAComments(Result,PostedFAComment.Type::Result);
                                  GetFAComments(Characteristics,PostedFAComment.Type::Characteristics);

                                  FA4Helper.SetDocSheet;
                                  IF NOT IsHeaderPrinted THEN BEGIN
                                    FA4Helper.FillHeader2(
                                      StdRepMgt.GetEmpDepartment("FA Employee No."),
                                      FORMAT("Posted FA Doc. Header"."FA Posting Date"),FAPostingGroup."Acquisition Cost Account",
                                      DirectorPosition,"Posted FA Doc. Header"."No.",FORMAT("Posted FA Doc. Header"."Posting Date"),
                                      FA."Factory No.",FA."Vehicle Reg. No.",FA."Inventory Number",
                                      Description + ', ' + FA."Vehicle Model" +', ' + FA."Vehicle Type",
                                      "Posted FA Doc. Header"."Posting Description",StdRepMgt.GetEmpPosition("FA Employee No."),
                                      StdRepMgt.GetEmpName("FA Employee No."),"FA Employee No.");
                                    FA4Helper.FillStatePageHeader;
                                    IsHeaderPrinted := TRUE;
                                  END;

                                  FA4Helper.FillStateLine2(
                                    FA."Manufacturing Year",FORMAT(FADepreciationBook."Acquisition Date",0,'<Month,2>.<Year4>'),
                                    FORMAT(FADepreciationBook."G/L Acquisition Date"),FORMAT(FA."Is Vehicle"),
                                    FORMAT(FA."Vehicle Writeoff Date"),FORMAT(FA."Run after Release Date"),FORMAT(FA."Run after Renovation Date"),
                                    StdRepMgt.FormatReportValue(ABS(FADepreciationBook."Initial Acquisition Cost"),2),
                                    StdRepMgt.FormatReportValue(ABS(FADepreciationBook.Depreciation),2),
                                    StdRepMgt.FormatReportValue(FADepreciationBook."Book Value",2));

                                  FA4Helper.SetAssetSheet;
                                  FA4Helper.FillAssetHeader;
                                  FA4Helper.FillAssetLine2(
                                    FA."Vehicle Reg. No.",FA."Vehicle Engine No.",FA."Vehicle Chassis No.",
                                    StdRepMgt.FormatReportValue(FA."Vehicle Capacity",2),StdRepMgt.FormatReportValue(FA."Vehicle Passport Weight",2),
                                    '','','','','');
                                END;

               DataItemLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.) }

    { 8951;2   ;DataItem;                    ;
               DataItemTable=Table12474;
               DataItemTableView=SORTING(Item Type);
               OnAfterGetRecord=BEGIN
                                  FA4Helper.FillAssetLine2(
                                    '','','','','',
                                    Name,"Precious Metals Code",StdRepMgt.GetUoMDesc("Unit of Measure Code"),
                                    StdRepMgt.FormatReportValue(Quantity,2),StdRepMgt.FormatReportValue(Mass,2));
                                END;

               OnPostDataItem=BEGIN
                                FA4Helper.FillAssetFooter(
                                  Characteristics,Conclusion,Appendix[1],Appendix[2],
                                  Chairman."Employee Job Title",Chairman."Employee Name",
                                  Member1."Employee Job Title",Member1."Employee Name",
                                  Member2."Employee Job Title",Member2."Employee Name");
                              END;

               DataItemLinkReference=Part 3;
               DataItemLink=No.=FIELD(FA No.) }

    { 7655;2   ;DataItem;Part 4              ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               FA4Helper.SetExpSheet;
                               FA4Helper.FillExpHeader;
                             END;

               OnPostDataItem=BEGIN
                                FA4Helper.FillExpFooter;
                              END;
                               }

    { 2461;3   ;DataItem;                    ;
               DataItemTable=Table12452;
               DataItemTableView=SORTING(Document No.,Line No.);
               OnPreDataItem=BEGIN
                               ConsNo := '0';
                             END;

               OnAfterGetRecord=BEGIN
                                  ConsNo := INCSTR(ConsNo);

                                  FA4Helper.FillExpLine(
                                    ConsNo,"Item No.",Description,"Unit of Measure Code",
                                    StdRepMgt.FormatReportValue(Quantity,2),StdRepMgt.FormatReportValue("Unit Amount",2),
                                    StdRepMgt.FormatReportValue(Amount,2));
                                END;

               DataItemLinkReference=Part 3;
               DataItemLink=Document No.=FIELD(Item Receipt No.) }

    { 3672;2   ;DataItem;Part 5              ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1 }

    { 3435;2   ;DataItem;Footer              ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               FA4Helper.FillReportFooter(Result[1]);
                             END;
                              }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CompanyInfo@1210005 : Record 79;
      FASetup@1210006 : Record 5603;
      FADepreciationBook@1470003 : Record 5612;
      FAPostingGroup@1210008 : Record 5606;
      FA@1210001 : Record 5600;
      PostedFAComment@1470000 : Record 12479;
      Chairman@1470008 : Record 12421;
      Member1@1470007 : Record 12421;
      Member2@1470006 : Record 12421;
      DocSignMgt@1470001 : Codeunit 12420;
      StdRepMgt@1210007 : Codeunit 12401;
      FA4Helper@1210004 : Codeunit 14953;
      IsHeaderPrinted@1210003 : Boolean;
      Characteristics@1470004 : ARRAY [5] OF Text[80];
      Reason@1210002 : ARRAY [5] OF Text[80];
      Conclusion@1210052 : ARRAY [5] OF Text[80];
      Appendix@1210053 : ARRAY [5] OF Text[80];
      Result@1470002 : ARRAY [5] OF Text[80];
      ConsNo@1470009 : Code[10];
      DirectorPosition@1470005 : Text[80];
      FileName@1210000 : Text;

    PROCEDURE SetFileNameSilent@1210005(NewFileName@1210000 : Text);
    BEGIN
      FileName := NewFileName;
    END;

    PROCEDURE CheckSignature@1470002(VAR PostedDocSign@1470000 : Record 12421;EmpType@1470001 : Integer);
    BEGIN
      DocSignMgt.GetPostedDocSign(
        PostedDocSign,DATABASE::"Posted FA Doc. Header",
        "Posted FA Doc. Header"."Document Type","Posted FA Doc. Header"."No.",EmpType,FALSE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

