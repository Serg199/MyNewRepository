OBJECT Table 12477 FA Document Line
{
  OBJECT-PROPERTIES
  {
    Date=30.05.17;
    Time=18:13:53;
    Modified=Yes;
    Version List=NAVRU9.00,NOS-26;
  }
  PROPERTIES
  {
    CaptionML=[ENU=FA Document Line;
               RUS=Строка документа ОС];
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              RUS=Тип документа];
                                                   OptionCaptionML=[ENU=Writeoff,Release,Movement;
                                                                    RUS=Списание,Ввод в эксплуатацию,Перемещение];
                                                   OptionString=Writeoff,Release,Movement }
    { 2   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              RUS=Номер документа] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              RUS=Номер строки] }
    { 4   ;   ;FA No.              ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "FA No." = '' THEN
                                                                  EXIT;

                                                                GetFADocHeader;
                                                                "Posting Date" := FADocHeader."Posting Date";
                                                                "FA Posting Date" := FADocHeader."FA Posting Date";

                                                                GetFA("FA No.");
                                                                FA.TESTFIELD(Blocked,FALSE);
                                                                FA.TESTFIELD(Inactive,FALSE);
                                                                Description := FA.Description;
                                                                "FA Location Code" := FA."FA Location Code";
                                                                "FA Employee No." := FA."Responsible Employee";
                                                                Status := FA.Status;
                                                                Check(FADocHeader."FA Location Code",FADocHeader."New FA Location Code",FADocHeader."FA Employee No.");

                                                                SetDepreciationBook;
                                                                CalcQty;
                                                                IF FADeprBook.GET("FA No.","Depreciation Book Code") THEN
                                                                  "FA Posting Group" := FADeprBook."FA Posting Group";

                                                                "New FA No." := "FA No.";
                                                                IF FADeprBook.GET("New FA No.","New Depreciation Book Code") THEN
                                                                  "New FA Posting Group" := FADeprBook."FA Posting Group";

                                                                CreateDim(DATABASE::"Fixed Asset","FA No.");
                                                              END;

                                                   CaptionML=[ENU=FA No.;
                                                              RUS=Номер ОС] }
    { 5   ;   ;New FA No.          ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "New FA No." = '' THEN
                                                                  EXIT;

                                                                GetFA("New FA No.");
                                                                FA.TESTFIELD(Blocked,FALSE);
                                                                FA.TESTFIELD(Inactive,FALSE);
                                                              END;

                                                   CaptionML=[ENU=New FA No.;
                                                              RUS=Новый номер ОС] }
    { 6   ;   ;FA Posting Date     ;Date          ;CaptionML=[ENU=FA Posting Date;
                                                              RUS=Дата учета ОС] }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              RUS=Дата учета] }
    { 8   ;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   CaptionML=[ENU=Depreciation Book Code;
                                                              RUS=Код книги амортизации] }
    { 13  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 14  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                "Book Value" := Amount * "Value %" / 100;
                                                              END;

                                                   CaptionML=[ENU=Amount;
                                                              RUS=Сумма] }
    { 15  ;   ;Book Value          ;Decimal       ;CaptionML=[ENU=Book Value;
                                                              RUS=Балансовая стоимость] }
    { 16  ;   ;Value %             ;Decimal       ;CaptionML=[ENU=Value %;
                                                              RUS=Значение %] }
    { 18  ;   ;Quantity            ;Decimal       ;OnValidate=VAR
                                                                FADeprBook@1210000 : Record 5612;
                                                              BEGIN
                                                                FA.GET("FA No.");
                                                                FADeprBook.GET("FA No.","Depreciation Book Code");
                                                                FADeprBook.SETFILTER("FA Posting Date Filter",'<=%1',"FA Posting Date");
                                                                FADeprBook.CALCFIELDS(Quantity,"Book Value");
                                                                IF FA."Undepreciable FA" THEN BEGIN
                                                                  IF Quantity > FADeprBook.Quantity THEN
                                                                    TESTFIELD(Quantity,FADeprBook.Quantity);
                                                                  IF FADeprBook.Quantity > 0 THEN
                                                                    "Value %" := Quantity / FADeprBook.Quantity * 100;
                                                                END ELSE BEGIN
                                                                  Quantity := 1;
                                                                  "Value %" := 100;
                                                                END;
                                                                VALIDATE(Amount,FADeprBook."Book Value");
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              RUS=Кол-во] }
    { 24  ;   ;FA Posting Group    ;Code10        ;TableRelation="FA Posting Group";
                                                   CaptionML=[ENU=FA Posting Group;
                                                              RUS=Учетная группа ОС] }
    { 27  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                            END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              RUS=Код ярлыка измерения 1];
                                                   CaptionClass='1,2,1' }
    { 28  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                            END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              RUS=Код ярлыка измерения 2];
                                                   CaptionClass='1,2,2' }
    { 30  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              RUS=Статус];
                                                   OptionCaptionML=[ENU=" ,Montage,Operation,Maintenance,Repair";
                                                                    RUS=" ,Монтаж,Эксплуатация,Обслуживание,Ремонт"];
                                                   OptionString=[ ,Montage,Operation,Maintenance,Repair] }
    { 36  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              RUS=Код причины] }
    { 37  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=[ENU=Source Code;
                                                              RUS=Код источника] }
    { 44  ;   ;FA Location Code    ;Code10        ;TableRelation="FA Location";
                                                   OnValidate=VAR
                                                                FALocation@1470000 : Record 5609;
                                                              BEGIN
                                                                IF FALocation.GET("FA Location Code") THEN
                                                                  "FA Employee No." := FALocation."Employee No.";
                                                              END;

                                                   CaptionML=[ENU=FA Location Code;
                                                              RUS=Код местонахождения ОС] }
    { 45  ;   ;FA Employee No.     ;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=FA Employee No.;
                                                              RUS=Материал.-ответств. лицо] }
    { 46  ;   ;New Depreciation Book Code;Code10  ;TableRelation="Depreciation Book";
                                                   CaptionML=[ENU=New Depreciation Book Code;
                                                              RUS=Новый код книги амортизации] }
    { 47  ;   ;New FA Posting Group;Code10        ;TableRelation="FA Posting Group";
                                                   CaptionML=[ENU=New FA Posting Group;
                                                              RUS=Новая учетная группа ОС] }
    { 50  ;   ;Item Receipt No.    ;Code20        ;TableRelation="Item Document Header".No. WHERE (Document Type=CONST(Receipt));
                                                   CaptionML=[ENU=Item Receipt No.;
                                                              RUS=Номер акта оприходования товаров] }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              RUS=Код набора измерений];
                                                   Editable=No }
    { 50003;  ;FA Dimension Code   ;Code20        ;TableRelation=Dimension.Code;
                                                   OnValidate=VAR
                                                                DefDim@1000000000 : Record 352;
                                                              BEGIN
                                                                //NC OFA005 > DP
                                                                IF "FA Dimension Code"<>'' THEN
                                                                  IF DefDim.GET(5600,"New FA No.","FA Dimension Code") THEN
                                                                    "FA Dimension Value Code" := DefDim."Dimension Value Code";
                                                                //NC OFA005 < DP
                                                              END;

                                                   CaptionML=[ENU=FA Dimension Code;
                                                              RUS=ОС Измерение Код];
                                                   Description=NC OFA005 }
    { 50004;  ;FA Dimension Value Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(FA Dimension Code));
                                                   CaptionML=[ENU=FA Dimension Value Code;
                                                              RUS=ОС Измерение Значение Код];
                                                   Description=NC OFA005 }
    { 50005;  ;New FA Dimension Value Code;Code20 ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(FA Dimension Code));
                                                   CaptionML=[ENU=New FA Dimension Value Code;
                                                              RUS=Новый ОС Измерение Значение Код];
                                                   Description=NC OFA005 }
  }
  KEYS
  {
    {    ;Document Type,Document No.,Line No.     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FA@1000 : Record 5600;
      FASetup@1470002 : Record 5603;
      FADocHeader@1470001 : Record 12470;
      FADeprBook@1470000 : Record 5612;
      DimMgt@1210000 : Codeunit 408;

    PROCEDURE CreateDim@26(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      TableID@1007 : ARRAY [10] OF Integer;
      No@1008 : ARRAY [10] OF Code[20];
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      "Dimension Set ID" := DimMgt.GetDefaultDimID(
          TableID,No,"Source Code","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0);
    END;

    PROCEDURE ShowDimensions@1210001();
    BEGIN
      DimMgt.SetObjectFrom(Rec); // NC NOS-26 SPA
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2 %3',"Document Type","Document No.","Line No."));
      DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    PROCEDURE LookupShortcutDimCode@28(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      ValidateShortcutDimCode(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE ShowShortcutDimCode@27(VAR ShortcutDimCode@1000 : ARRAY [20] OF Code[20]);
    BEGIN
      DimMgt.GetShortcutDimensions("Dimension Set ID",ShortcutDimCode);
    END;

    PROCEDURE GetFADocHeader@1470002();
    BEGIN
      TESTFIELD("Document No.");
      IF ("Document Type" <> FADocHeader."Document Type") OR ("Document No." <> FADocHeader."No.") THEN
        FADocHeader.GET("Document Type","Document No.");
    END;

    LOCAL PROCEDURE GetFA@2(FANo@1470000 : Code[20]);
    BEGIN
      IF FA."No." <> FANo THEN
        FA.GET(FANo);
    END;

    PROCEDURE SetDepreciationBook@1470000();
    BEGIN
      FASetup.GET;
      CASE "Document Type" OF
        "Document Type"::Writeoff:
          BEGIN
            VALIDATE("Depreciation Book Code",FASetup."Release Depr. Book");
            "New Depreciation Book Code" := '';
          END;
        "Document Type"::Release:
          BEGIN
            VALIDATE("Depreciation Book Code",FASetup."Default Depr. Book");
            "New Depreciation Book Code" := FASetup."Release Depr. Book";
          END;
        "Document Type"::Movement:
          BEGIN
            VALIDATE("Depreciation Book Code",FASetup."Release Depr. Book");
            "New Depreciation Book Code" := FASetup."Release Depr. Book";
          END;
      END;
    END;

    PROCEDURE ShowComments@1470003();
    VAR
      FAComment@1210001 : Record 12478;
      FAComments@1470000 : Page 12495;
    BEGIN
      TESTFIELD("Document No.");
      TESTFIELD("Line No.");
      FAComment.SETRANGE("Document Type","Document Type");
      FAComment.SETRANGE("Document No.","Document No.");
      FAComment.SETRANGE("Document Line No.","Line No.");
      FAComments.SETTABLEVIEW(FAComment);
      FAComments.RUNMODAL;
    END;

    PROCEDURE CalcQty@1470005();
    BEGIN
      IF FADeprBook.GET("FA No.","Depreciation Book Code") THEN BEGIN
        FADeprBook.SETFILTER("FA Posting Date Filter",'<=%1',"FA Posting Date");
        FADeprBook.CALCFIELDS(Quantity,"Book Value");
        "Value %" := 100;
        VALIDATE(Amount,FADeprBook."Book Value");
        IF FA."Undepreciable FA" THEN
          Quantity := FADeprBook.Quantity
        ELSE
          Quantity := 1;
      END;
    END;

    PROCEDURE GetFAComments@1470004(VAR Comment@1470000 : ARRAY [5] OF Text[80];Type@1470001 : Integer);
    VAR
      FAComment@1470003 : Record 12478;
      Index@1210000 : Integer;
    BEGIN
      CLEAR(Comment);
      Index := 0;
      FAComment.RESET;
      FAComment.SETCURRENTKEY("Document Type","Document No.","Document Line No.",Type);
      FAComment.SETRANGE("Document Type","Document Type");
      FAComment.SETRANGE("Document No.","Document No.");
      FAComment.SETRANGE("Document Line No.","Line No.");
      FAComment.SETRANGE(Type,Type);
      IF FAComment.FINDSET THEN
        REPEAT
          Index += 1;
          Comment[Index] := FAComment.Comment
        UNTIL (FAComment.NEXT = 0) OR (Index = ARRAYLEN(Comment));
    END;

    PROCEDURE Check@1470006(FALocationCode@1470000 : Code[10];NewFALocationCode@1470001 : Code[10];FAEmployeeNo@1470002 : Code[20]);
    BEGIN
      GetFA("FA No.");
      IF Status > Status::Repair THEN
        FA.FIELDERROR(Status);

      CASE "Document Type" OF
        "Document Type"::Movement:
          BEGIN
            IF FALocationCode <> '' THEN
              FA.TESTFIELD("FA Location Code",FALocationCode);
            IF "FA Location Code" <> NewFALocationCode THEN
              VALIDATE("FA Location Code",NewFALocationCode);
          END;
        "Document Type"::Writeoff:
          BEGIN
            IF FALocationCode <> '' THEN
              FA.TESTFIELD("FA Location Code",FALocationCode);
            IF FAEmployeeNo <> '' THEN
              FA.TESTFIELD("Responsible Employee",FAEmployeeNo);
          END;
      END;
    END;

    BEGIN
    {
      NC NCS-2356 DP Changed dimension of array ShortcutDimCode 8->10 function ShowShortcutDimCode
      ----------------
      NC NOS-26 SPA Миграция на NAV2017
    }
    END.
  }
}

