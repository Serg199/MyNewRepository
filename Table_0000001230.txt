OBJECT Table 1230 SEPA Direct Debit Mandate
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    DataCaptionFields=ID,Customer Bank Account Code;
    OnInsert=BEGIN
               InsertNoSeries
             END;

    OnModify=BEGIN
               IF xRec.Blocked THEN
                 TESTFIELD(Blocked,FALSE);
             END;

    CaptionML=[ENU=SEPA Direct Debit Mandate;
               RUS=Поручение на прямое дебетование SEPA];
    LookupPageID=Page1230;
    DrillDownPageID=Page1230;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Code35        ;OnValidate=VAR
                                                                SalesSetup@1001 : Record 311;
                                                                NoSeriesMgt@1000 : Codeunit 396;
                                                              BEGIN
                                                                IF ID <> xRec.ID THEN BEGIN
                                                                  SalesSetup.GET;
                                                                  NoSeriesMgt.TestManual(SalesSetup."Direct Debit Mandate Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=ID;
                                                              RUS=Код] }
    { 2   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                IF (xRec."Customer No." <> '') AND ("Customer No." <> xRec."Customer No.") THEN BEGIN
                                                                  TESTFIELD("Date of Signature",0D);
                                                                  TESTFIELD("Debit Counter",0);
                                                                  "Customer Bank Account Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              RUS=Код клиента];
                                                   NotBlank=Yes }
    { 3   ;   ;Customer Bank Account Code;Code10  ;TableRelation="Customer Bank Account".Code WHERE (Customer No.=FIELD(Customer No.));
                                                   CaptionML=[ENU=Customer Bank Account Code;
                                                              RUS=Код банковского счета клиента];
                                                   NotBlank=Yes }
    { 4   ;   ;Valid From          ;Date          ;OnValidate=BEGIN
                                                                ValidateDates;
                                                              END;

                                                   CaptionML=[ENU=Valid From;
                                                              RUS=Действительно с] }
    { 5   ;   ;Valid To            ;Date          ;OnValidate=BEGIN
                                                                ValidateDates;
                                                              END;

                                                   CaptionML=[ENU=Valid To;
                                                              RUS=Действительно до] }
    { 6   ;   ;Date of Signature   ;Date          ;CaptionML=[ENU=Date of Signature;
                                                              RUS=Дата подписания];
                                                   NotBlank=Yes }
    { 7   ;   ;Type of Payment     ;Option        ;OnValidate=BEGIN
                                                                IF ("Type of Payment" = "Type of Payment"::OneOff) AND ("Debit Counter" > 1) THEN
                                                                  ERROR(MandateChangeErr);
                                                                "Expected Number of Debits" := 1;
                                                              END;

                                                   CaptionML=[ENU=Type of Payment;
                                                              RUS=Тип платежа];
                                                   OptionCaptionML=[ENU=OneOff,Recurrent;
                                                                    RUS=Единоразовый,Типовой];
                                                   OptionString=OneOff,Recurrent }
    { 8   ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              RUS=Заблокировано] }
    { 9   ;   ;Expected Number of Debits;Integer  ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                IF "Expected Number of Debits" < "Debit Counter" THEN
                                                                  ERROR(InvalidNumberOfDebitsTxt);
                                                                IF ("Type of Payment" = "Type of Payment"::OneOff) AND ("Expected Number of Debits" > 1) THEN
                                                                  ERROR(InvalidOneOffNumOfDebitsErr);

                                                                Closed := "Expected Number of Debits" <= "Debit Counter";
                                                              END;

                                                   CaptionML=[ENU=Expected Number of Debits;
                                                              RUS=Ожидаемое число списаний];
                                                   MinValue=1 }
    { 10  ;   ;Debit Counter       ;Integer       ;OnValidate=BEGIN
                                                                IF "Expected Number of Debits" < "Debit Counter" THEN BEGIN
                                                                  MESSAGE(InvalidNumberOfDebitsTxt);
                                                                  FIELDERROR("Debit Counter");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Debit Counter;
                                                              RUS=Счетчик списаний];
                                                   Editable=No }
    { 11  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              RUS=Серия номеров] }
    { 12  ;   ;Closed              ;Boolean       ;CaptionML=[ENU=Closed;
                                                              RUS=Закрыто];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Customer No.                             }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;ID,Customer Bank Account Code,Valid From,Valid To,Type of Payment }
  }
  CODE
  {
    VAR
      DateErr@1006 : TextConst 'ENU=The Valid To date must be after the Valid From date.;RUS=Дата "Действительно до" должна следовать за датой "Действительно с".';
      InvalidNumberOfDebitsTxt@1005 : TextConst 'ENU=The Debit Counter cannot be greater than the Number of Debits.;RUS=Счетчик списаний не может быть большим, чем Число списаний.';
      InvalidOneOffNumOfDebitsErr@1004 : TextConst 'ENU=The Number of Debits for OneOff Sequence Type cannot be greater than one.;RUS=Число списаний для типа последовательности "Единоразовый" не может быть большим одного.';
      MandateChangeErr@1003 : TextConst 'ENU=SequenceType cannot be set to OneOff, since the Mandate has already been used.;RUS=Тип последовательности не может быть установлен в значение Единоразовый, так как Поручение уже использовалось.';

    LOCAL PROCEDURE InsertNoSeries@1();
    VAR
      SalesSetup@1001 : Record 311;
      NoSeriesMgt@1000 : Codeunit 396;
      NewNo@1002 : Code[20];
    BEGIN
      IF ID = '' THEN BEGIN
        SalesSetup.GET;
        SalesSetup.TESTFIELD("Direct Debit Mandate Nos.");
        NoSeriesMgt.InitSeries(SalesSetup."Direct Debit Mandate Nos.",xRec."No. Series",0D,NewNo,"No. Series");
        ID := NewNo;
      END;
    END;

    PROCEDURE IsMandateActive@2(TransactionDate@1000 : Date) : Boolean;
    BEGIN
      IF ("Valid To" <> 0D) AND ("Valid To" < TransactionDate) OR ("Valid From" > TransactionDate) OR Blocked OR Closed THEN
        EXIT(FALSE);
      EXIT(TRUE)
    END;

    PROCEDURE GetDefaultMandate@58(CustomerNo@1001 : Code[20];DueDate@1002 : Date) : Code[35];
    VAR
      SEPADirectDebitMandate@1000 : Record 1230;
    BEGIN
      SEPADirectDebitMandate.SETRANGE("Customer No.",CustomerNo);
      SEPADirectDebitMandate.SETFILTER("Valid From",'%1|<=%2',0D,DueDate);
      SEPADirectDebitMandate.SETFILTER("Valid To",'%1|>=%2',0D,DueDate);
      SEPADirectDebitMandate.SETRANGE(Blocked,FALSE);
      SEPADirectDebitMandate.SETRANGE(Closed,FALSE);
      IF SEPADirectDebitMandate.FINDFIRST THEN
        EXIT(SEPADirectDebitMandate.ID);
      EXIT('');
    END;

    PROCEDURE UpdateCounter@5();
    BEGIN
      TESTFIELD(Blocked,FALSE);
      VALIDATE("Debit Counter","Debit Counter" + 1);
      Closed := "Debit Counter" >= "Expected Number of Debits";
      MODIFY;
    END;

    PROCEDURE GetSequenceType@4() : Integer;
    VAR
      DirectDebitCollectionEntry@1000 : Record 1208;
    BEGIN
      DirectDebitCollectionEntry.INIT;
      IF "Type of Payment" = "Type of Payment"::OneOff THEN
        EXIT(DirectDebitCollectionEntry."Sequence Type"::"One Off");
      IF "Debit Counter" = 0 THEN
        EXIT(DirectDebitCollectionEntry."Sequence Type"::First);
      IF "Debit Counter" >= "Expected Number of Debits" - 1 THEN
        EXIT(DirectDebitCollectionEntry."Sequence Type"::Last);
      EXIT(DirectDebitCollectionEntry."Sequence Type"::Recurring);
    END;

    PROCEDURE RollBackSequenceType@6();
    BEGIN
      IF "Debit Counter" <= 0 THEN
        EXIT;

      "Debit Counter" -= 1;
      Closed := "Debit Counter" >= "Expected Number of Debits";
      MODIFY;
    END;

    LOCAL PROCEDURE ValidateDates@3();
    BEGIN
      IF ("Valid To" <> 0D) AND ("Valid From" > "Valid To") THEN
        ERROR(DateErr);
    END;

    BEGIN
    END.
  }
}

