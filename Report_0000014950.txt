OBJECT Report 14950 Job Ticket
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job Ticket;
               RUS=Наряд-заказ];
    ProcessingOnly=Yes;
    OnPostReport=VAR
                   SalesHeader@1210003 : Record 36;
                   SalesReceivablesSetup@1210004 : Record 311;
                   DocumentTemplate@1210006 : Record 14919;
                   wrdApp@1210010 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.ApplicationClass" RUNONCLIENT;
                   wrdDoc@1210009 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.DocumentClass" RUNONCLIENT;
                   WordHelper@1210008 : DotNet "'Microsoft.Dynamics.Nav.Integration.Office, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Integration.Office.Word.WordHelper" RUNONCLIENT;
                   FileName@1210005 : Text[1024];
                   ReportSource@1210007 : 'UnpostedSales,SalesInvoice,SalesShipment';
                 BEGIN
                   IF NOT DefineReportSource(ReportSource,SalesHeader) THEN
                     EXIT;

                   IF ReportSource = ReportSource::UnpostedSales THEN
                     SalesHeader.TESTFIELD(Status,SalesHeader.Status::Released);

                   SalesReceivablesSetup.GET;
                   FileName := DocumentTemplate.OpenTemplate(SalesReceivablesSetup."Job Ticket Template Code");

                   wrdApp := wrdApp.ApplicationClass;
                   wrdDoc := WordHelper.CallOpen(wrdApp,FileName,FALSE,FALSE);

                   FillBookmarks(wrdDoc,SalesHeader,ReportSource);
                   wrdApp.Visible(TRUE);
                 END;

  }
  DATASET
  {
    { 6640;    ;DataItem;                    ;
               DataItemTable=Table36 }

    { 5581;    ;DataItem;                    ;
               DataItemTable=Table112 }

    { 3595;    ;DataItem;                    ;
               DataItemTable=Table110 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1210000; ;Container ;
                  Name=Controls;
                  ContainerType=ContentArea }

      { 1210004;1;Group     ;
                  GroupType=Group }

      { 1210001;2;Field     ;
                  Name=ItemDescription;
                  CaptionML=[ENU=Item Description;
                             RUS=Описание товара];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ItemDescription }

      { 1210002;2;Field     ;
                  Name=VehicleDescription;
                  CaptionML=[ENU=Vehicle Description;
                             RUS=Описание ТС];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=VehicleDescription }

      { 1210003;2;Field     ;
                  Name=VehicleRegistrationNo;
                  CaptionML=[ENU=Vehicle Registration No.;
                             RUS=Регистрационный номер ТС];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=VehicleDescriptionRegNo }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ItemDescription@1210001 : Text[1024];
      VehicleDescription@1210002 : Text[1024];
      VehicleDescriptionRegNo@1210003 : Text[1024];

    PROCEDURE FillBookmarks@1210002(VAR wrdDoc@1210007 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.DocumentClass" RUNONCLIENT;SalesHeader@1210002 : Record 36;ReportSource@1210005 : 'UnpostedSales,SalesInvoice,SalesShipment');
    VAR
      LocalReportManagement@1210006 : Codeunit 12401;
      Quantity@1210003 : Decimal;
      Weight@1210004 : Decimal;
      Volume@1210001 : Decimal;
    BEGIN
      FillBookmark(wrdDoc,'OrderDate',FORMAT(SalesHeader."Order Date"));
      FillBookmark(wrdDoc,'OrderNo',SalesHeader."No.");
      FillBookmark(wrdDoc,'Vendor',GetVendorInfo(SalesHeader));
      FillBookmark(wrdDoc,'ResponsiblePerson',GetResponsiblePersonInfo(SalesHeader,ReportSource));
      FillBookmark(wrdDoc,'ShippingAgent',GetShippingAgentName(SalesHeader));

      FillBookmark(wrdDoc,'ItemDescription',ItemDescription);
      GetLinesInfo(SalesHeader,Quantity,Weight,Volume,ReportSource);
      FillBookmark(wrdDoc,'Quantity',FORMAT(Quantity));
      FillBookmark(
        wrdDoc,'NetWeight',
        LocalReportManagement.FormatReportValue(Weight,2) + ',  ,' +
        LocalReportManagement.FormatReportValue(Volume,2));

      FillBookmark(wrdDoc,'VehicleDescription',VehicleDescription);
      FillBookmark(wrdDoc,'VehicleDescriptionRegNo',VehicleDescriptionRegNo);
    END;

    PROCEDURE FillBookmark@1210003(VAR wrdDoc@1210005 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.DocumentClass" RUNONCLIENT;BookmarkName@1210003 : Text[250];TextValue@1210001 : Text[1024]);
    VAR
      wrdBookmark@1210002 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.Bookmark" RUNONCLIENT;
      Enumerator@1210000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.IEnumerator" RUNONCLIENT;
    BEGIN
      Enumerator := wrdDoc.Bookmarks.GetEnumerator();
      WHILE Enumerator.MoveNext DO BEGIN
        wrdBookmark := Enumerator.Current;
        IF wrdBookmark.Name = BookmarkName THEN BEGIN
          wrdBookmark.Range.Text(TextValue);
          EXIT;
        END;
      END;
    END;

    PROCEDURE GetVendorInfo@1210006(SalesHeader@1210000 : Record 36) : Text[250];
    VAR
      LocalReportManagement@1210001 : Codeunit 12401;
    BEGIN
      IF SalesHeader."Consignor No." = '' THEN
        EXIT(LocalReportManagement.GetCompanyName + '. ' + LocalReportManagement.GetLegalAddress);
      EXIT(LocalReportManagement.GetConsignerInfo(SalesHeader."Consignor No.",SalesHeader."Responsibility Center"));
    END;

    PROCEDURE GetShippingAgentName@1210012(SalesHeader@1210000 : Record 36) : Text[250];
    VAR
      ShippingAgent@1210001 : Record 291;
    BEGIN
      IF ShippingAgent.GET(SalesHeader."Shipping Agent Code") THEN
        EXIT(ShippingAgent.Name);
    END;

    PROCEDURE GetResponsiblePersonInfo@1210018(SalesHeader@1210000 : Record 36;ReportSource@1210004 : 'UnpostedSales,SalesInvoice,SalesShipment') : Text[250];
    VAR
      Employee@1210003 : Record 5200;
      DocumentSignature@1210002 : Record 12420;
      PostedDocumentSignature@1210005 : Record 12421;
      LocalReportManagement@1210001 : Codeunit 12401;
    BEGIN
      CASE ReportSource OF
        ReportSource::UnpostedSales:
          BEGIN
            IF NOT DocumentSignature.GET(
                 DATABASE::"Sales Header",
                 SalesHeader."Document Type",
                 SalesHeader."No.",
                 DocumentSignature."Employee Type"::Responsible)
            THEN
              EXIT('');
            IF NOT Employee.GET(DocumentSignature."Employee No.") THEN
              EXIT('');
          END;
        ReportSource::SalesInvoice:
          BEGIN
            IF NOT PostedDocumentSignature.GET(
                 DATABASE::"Sales Invoice Header",
                 0,
                 SalesHeader."No.",
                 DocumentSignature."Employee Type"::Responsible)
            THEN
              EXIT('');
            IF NOT Employee.GET(PostedDocumentSignature."Employee No.") THEN
              EXIT('');
          END;
        ReportSource::SalesShipment:
          BEGIN
            IF NOT PostedDocumentSignature.GET(
                 DATABASE::"Sales Shipment Header",
                 0,
                 SalesHeader."No.",
                 DocumentSignature."Employee Type"::Responsible)
            THEN
              EXIT('');
            IF NOT Employee.GET(PostedDocumentSignature."Employee No.") THEN
              EXIT('');
          END;
      END;

      EXIT(
        LocalReportManagement.GetEmpName(Employee."No.") + ' ' +
        Employee."Phone No." + ' ' +
        Employee."Mobile Phone No.");
    END;

    PROCEDURE GetLinesInfo@1210021(SalesHeader@1210003 : Record 36;VAR Quantity@1210002 : Decimal;VAR Weight@1210001 : Decimal;VAR Volume@1210004 : Decimal;ReportSource@1210000 : 'UnpostedSales,SalesInvoice,SalesShipment');
    BEGIN
      CASE ReportSource OF
        ReportSource::UnpostedSales:
          GetSalesLinesInfo(SalesHeader,Quantity,Weight,Volume);
        ReportSource::SalesInvoice:
          GetInvoiceLinesInfo(SalesHeader,Quantity,Weight,Volume);
        ReportSource::SalesShipment:
          GetShipmentLinesInfo(SalesHeader,Quantity,Weight,Volume);
      END;
    END;

    PROCEDURE GetSalesLinesInfo@1210028(SalesHeader@1210000 : Record 36;VAR Quantity@1210001 : Decimal;VAR Weight@1210002 : Decimal;VAR Volume@1210004 : Decimal);
    VAR
      SalesLine@1210003 : Record 37;
    BEGIN
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");

      IF SalesLine.FINDSET THEN
        REPEAT
          Quantity += SalesLine."Qty. to Ship";
          Weight += SalesLine."Qty. to Ship" * SalesLine."Net Weight";
          Volume += SalesLine."Qty. to Ship" * SalesLine."Unit Volume";
        UNTIL SalesLine.NEXT = 0;
    END;

    PROCEDURE GetInvoiceLinesInfo@1210019(SalesHeader@1210002 : Record 36;VAR Quantity@1210001 : Decimal;VAR Weight@1210000 : Decimal;VAR Volume@1210004 : Decimal);
    VAR
      SalesInvoiceLine@1210003 : Record 113;
    BEGIN
      SalesInvoiceLine.SETRANGE("Document No.",SalesHeader."No.");

      IF SalesInvoiceLine.FINDSET THEN
        REPEAT
          Quantity += SalesInvoiceLine.Quantity;
          Weight += SalesInvoiceLine.Quantity * SalesInvoiceLine."Net Weight";
          Volume += SalesInvoiceLine.Quantity * SalesInvoiceLine."Unit Volume";
        UNTIL SalesInvoiceLine.NEXT = 0;
    END;

    PROCEDURE GetShipmentLinesInfo@1210020(SalesHeader@1210002 : Record 36;VAR Quantity@1210001 : Decimal;VAR Weight@1210000 : Decimal;VAR Volume@1210004 : Decimal);
    VAR
      SalesShipmentLine@1210003 : Record 111;
    BEGIN
      SalesShipmentLine.SETRANGE("Document No.",SalesHeader."No.");

      IF SalesShipmentLine.FINDSET THEN
        REPEAT
          Quantity += SalesShipmentLine.Quantity;
          Weight += SalesShipmentLine.Quantity * SalesShipmentLine."Net Weight";
          Volume += SalesShipmentLine.Quantity * SalesShipmentLine."Unit Volume";
        UNTIL SalesShipmentLine.NEXT = 0;
    END;

    PROCEDURE DefineReportSource@1210001(VAR ReportSource@1210000 : 'UnpostedSales,SalesInvoice,SalesShipment';VAR SalesHeader@1210002 : Record 36) : Boolean;
    BEGIN
      IF "Sales Header".GETFILTERS <> '' THEN BEGIN
        ReportSource := ReportSource::UnpostedSales;
        SalesHeader := "Sales Header";
      END ELSE
        IF "Sales Invoice Header".GETFILTERS <> '' THEN BEGIN
          ReportSource := ReportSource::SalesInvoice;
          SalesHeader.TRANSFERFIELDS("Sales Invoice Header");
        END ELSE
          IF "Sales Shipment Header".GETFILTERS <> '' THEN BEGIN
            ReportSource := ReportSource::SalesShipment;
            SalesHeader.TRANSFERFIELDS("Sales Shipment Header");
          END ELSE
            EXIT(FALSE);
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

