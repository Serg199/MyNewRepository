OBJECT Codeunit 9652 Edit RDLC Report Layout
{
  OBJECT-PROPERTIES
  {
    Date=23.01.17;
    Time=12:00:00;
    Version List=NAVW110.00.00.15052;
  }
  PROPERTIES
  {
    TableNo=9650;
    OnRun=BEGIN
            EditReportLayout(Rec);
          END;

  }
  CODE
  {
    VAR
      LoadDocQst@1000 : TextConst 'ENU=The report layout has been opened in SQL Report Builder.\\Edit the report layout in SQL Report Builder and save the changes. Then return to this message and choose Yes to import the changes or No to cancel the changes.\Do you want to import the changes?;RUS=Макет отчета открыт в построителе отчетов SQL.\\Измените макет отчета в построителе отчетов SQL и сохраните изменения. Затем вернитесь к этому сообщению и выберите вариант "Да", чтобы импортировать изменения, или вариант "Нет", чтобы отменить изменения.\Импортировать изменения?';
      NoReportBuilderPresentErr@1001 : TextConst 'ENU=Microsoft Report Builder is not installed on this computer.;RUS=Построитель отчетов Microsoft не установлен на этом компьютере.';

    LOCAL PROCEDURE EditReportLayout@4(VAR CustomReportLayout@1015 : Record 9650);
    VAR
      TempBlob@1009 : Record 99008535;
      FileMgt@1005 : Codeunit 419;
      Process@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Diagnostics.Process" RUNONCLIENT;
      FileName@1003 : Text;
      RBFileName@1000 : Text;
      LoadModifiedDoc@1002 : Boolean;
    BEGIN
      CustomReportLayout.CALCFIELDS(Layout);
      CustomReportLayout.TESTFIELD(Layout);

      RBFileName := GetReportBuilderExe;
      IF RBFileName = '' THEN
        ERROR(NoReportBuilderPresentErr);

      TempBlob.INIT;
      TempBlob.Blob := CustomReportLayout.Layout;

      FileName := FileMgt.BLOBExport(TempBlob,'report.rdl',FALSE);

      Process := Process.Start(RBFileName,'"' + FileName + '"');

      LoadModifiedDoc := CONFIRM(LoadDocQst);

      IF LoadModifiedDoc THEN BEGIN
        FileMgt.BLOBImport(TempBlob,FileName);
        CustomReportLayout.ImportLayoutBlob(TempBlob,'');
      END;

      IF NOT Process.HasExited THEN
        Process.CloseMainWindow;

      FileMgt.DeleteClientFile(FileName);
    END;

    LOCAL PROCEDURE GetReportBuilderExe@1() : Text;
    VAR
      Registry@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.Registry" RUNONCLIENT;
      FileVersion@1004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Diagnostics.FileVersionInfo";
      FileName@1002 : Text;
      length@1001 : Integer;
      offset@1003 : Integer;
    BEGIN
      FileName := Registry.GetValue('HKEY_CLASSES_ROOT\MSReportBuilder_ReportFile_32\shell\Open\command','','');
      length := STRPOS(UPPERCASE(FileName),'.EXE');
      // Strip leading quotes if any

      IF length > 0 THEN BEGIN
        length += 3;
        IF FileName[1] = '"' THEN BEGIN
          offset := 1;
          length -= 1;
        END;
        FileName := COPYSTR(FileName,offset + 1,length);
      END ELSE
        FileName := '';

      FileVersion := FileVersion.GetVersionInfo(FileName);
      IF FileVersion.ProductMajorPart >= 13 THEN BEGIN
        FileName :=
          Registry.GetValue('HKEY_LOCAL_MACHINE\SOFTWARE\WOW6432Node\Microsoft\Microsoft SQL Server\Report Builder 3.0','location','');
        IF FileName <> '' THEN BEGIN
          FileName := FileName + '\MSReportBuilder.exe';
          FileVersion := FileVersion.GetVersionInfo(FileName);
          IF FileVersion.ProductMajorPart >= 13 THEN
            FileName := '';
        END;
      END;
      EXIT(FileName);
    END;

    BEGIN
    END.
  }
}

