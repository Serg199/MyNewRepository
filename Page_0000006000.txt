OBJECT Page 6000 Dispatch Board
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Dispatch Board;
               RUS=Панель диспетчера];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table5900;
    SourceTableView=SORTING(Status,Response Date,Response Time,Priority);
    DataCaptionFields=Status;
    PageType=Worksheet;
    OnOpenPage=BEGIN
                 IF UserMgt.GetServiceFilter <> '' THEN BEGIN
                   FILTERGROUP(2);
                   SETRANGE("Responsibility Center",UserMgt.GetServiceFilter);
                   FILTERGROUP(0);
                 END;
                 SetAllFilters;

                 IF ISEMPTY THEN BEGIN
                   ServOrderFilter := '';
                   SetServOrderFilter;
                 END;
               END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 59      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Dispatch Board;
                                 RUS=Панель &диспетчера];
                      Image=ServiceMan }
      { 60      ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[ENU=&Show Document;
                                 RUS=&Показать документ];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PageManagement@1000 : Codeunit 700;
                               BEGIN
                                 PageManagement.PageRunModal(Rec);
                               END;
                                }
      { 8       ;1   ;ActionGroup;
                      CaptionML=[ENU=Pla&nning;
                                 RUS=Пла&нирование];
                      Image=Planning }
      { 9       ;2   ;Action    ;
                      CaptionML=[ENU=Resource &Allocations;
                                 RUS=&Распределение ресурсов];
                      RunObject=Page 6005;
                      RunPageView=SORTING(Status,Document Type,Document No.,Service Item Line No.,Allocation Date,Starting Time,Posted)
                                  WHERE(Status=FILTER(<>Canceled));
                      RunPageLink=Document Type=FIELD(Document Type),
                                  Document No.=FIELD(No.);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ResourcePlanning;
                      PromotedCategory=Process }
      { 14      ;2   ;Separator  }
      { 55      ;2   ;Action    ;
                      CaptionML=[ENU=Demand Overview;
                                 RUS=Обзор спроса];
                      Image=Forecast;
                      OnAction=VAR
                                 DemandOverview@1000 : Page 5830;
                               BEGIN
                                 DemandOverview.SetCalculationParameter(TRUE);
                                 DemandOverview.Initialize(0D,4,'','','');
                                 DemandOverview.RUNMODAL;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 47      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Print;
                                 RUS=&Печать];
                      Image=Print }
      { 48      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Print &Dispatch Board;
                                 RUS=Печать панели &диспетчера];
                      Image=Print;
                      OnAction=BEGIN
                                 REPORT.RUN(REPORT::"Dispatch Board",TRUE,TRUE,Rec);
                               END;
                                }
      { 49      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Print Service &Order;
                                 RUS=Печать сервисного &заказа];
                      Image=Print;
                      OnAction=BEGIN
                                 CLEAR(ServHeader);
                                 ServHeader.SETRANGE("Document Type","Document Type");
                                 ServHeader.SETRANGE("No.","No.");
                                 REPORT.RUN(REPORT::"Service Order",TRUE,TRUE,ServHeader);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 22  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           RUS=Общее] }

    { 24  ;2   ;Field     ;
                CaptionML=[ENU=Resource Filter;
                           RUS=Фильтр по ресурсу];
                ToolTipML=[ENU=Specifies the filter that displays an overview of documents with service item lines that a certain resource is allocated to.;
                           RUS=Определяет фильтр, который позволяет просматривать обзор документов со строками сервисных товаров, на которые распределен определенный ресурс.];
                SourceExpr=ResourceFilter;
                OnValidate=BEGIN
                             SetResourceFilter;
                             ResourceFilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           Res.RESET;
                           IF PAGE.RUNMODAL(0,Res) = ACTION::LookupOK THEN BEGIN
                             Text := Res."No.";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 39  ;2   ;Field     ;
                CaptionML=[ENU=Resource Group Filter;
                           RUS=Фильтр по группе ресурсов];
                ToolTipML=[ENU=Specifies the filter that displays an overview of documents with service item lines that a certain resource group is allocated to.;
                           RUS=Определяет фильтр, который позволяет просматривать обзор документов со строками сервисных товаров, на которые распределены ресурсы определенной группы.];
                SourceExpr=ResourceGroupFilter;
                OnValidate=BEGIN
                             SetResourceGroupFilter;
                             ResourceGroupFilterOnAfterVali;
                           END;

                OnLookup=BEGIN
                           ResourceGroup.RESET;
                           IF PAGE.RUNMODAL(0,ResourceGroup) = ACTION::LookupOK THEN BEGIN
                             Text := ResourceGroup."No.";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 42  ;2   ;Field     ;
                CaptionML=[ENU=Response Date Filter;
                           RUS=Фильтр по дате отклика];
                ToolTipML=[ENU=Specifies the filter that displays an overview of documents with the specified value in the Response Date field.;
                           RUS=Определяет фильтр, который позволяет получить обзор документов с указанным значением в поле "Дата отклика".];
                SourceExpr=RespDateFilter;
                OnValidate=BEGIN
                             SetRespDateFilter;
                             RespDateFilterOnAfterValidate;
                           END;
                            }

    { 30  ;2   ;Field     ;
                CaptionML=[ENU=Allocation Filter;
                           RUS=Фильтр по распределению];
                ToolTipML=[ENU=Specifies the filter that displays the overview of documents from their allocation analysis point of view.;
                           RUS=Определяет фильтр, который позволяет получить обзор документов с точки зрения анализа распределения ресурсов.];
                OptionCaptionML=[ENU=" ,No or Partial Allocation,Full Allocation,Reallocation Needed";
                                 RUS=" ,Нет или частичное распределение,Полное распределение,Требуется перераспределение"];
                SourceExpr=AllocationFilter;
                OnValidate=BEGIN
                             SetAllocFilter;
                             AllocationFilterOnAfterValidat;
                           END;
                            }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=Document Filter;
                           RUS=Фильтр по документу];
                ToolTipML=[ENU=Specifies the filter that displays the overview of the documents of the specified type.;
                           RUS=Определяет фильтр, который позволяет получить обзор документов выбранного типа.];
                OptionCaptionML=[ENU=Order,Quote,All;
                                 RUS=Заказ,Предложение,Все];
                SourceExpr=DocFilter;
                OnValidate=BEGIN
                             SetDocFilter;
                             DocFilterOnAfterValidate;
                           END;
                            }

    { 65  ;2   ;Field     ;
                CaptionML=[ENU=No. Filter;
                           RUS=Фильтр по номеру];
                ToolTipML=[ENU=Specifies the filter that is used to see the specified document.;
                           RUS=Определяет фильтр для просмотра конкретного документа.];
                SourceExpr=ServOrderFilter;
                OnValidate=BEGIN
                             SetServOrderFilter;
                             ServOrderFilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           ServHeader.RESET;
                           SetDocFilter2(ServHeader);
                           IF PAGE.RUNMODAL(0,ServHeader) = ACTION::LookupOK THEN BEGIN
                             Text := ServHeader."No.";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 79  ;2   ;Field     ;
                CaptionML=[ENU=Status Filter;
                           RUS=Фильтр по статусу];
                ToolTipML=[ENU=Specifies the filter that displays an overview of documents with a certain value in the Status field.;
                           RUS=Определяет фильтр, который позволяет получить обзор документов с определенным значением в поле "Статус".];
                OptionCaptionML=[ENU=" ,Pending,In Process,Finished,On Hold";
                                 RUS=" ,Ожидание,В работе,Завершено,На удержании"];
                SourceExpr=StatusFilter;
                OnValidate=BEGIN
                             SetStatusFilter;
                             StatusFilterOnAfterValidate;
                           END;
                            }

    { 45  ;2   ;Field     ;
                CaptionML=[ENU=Customer Filter;
                           RUS=Фильтр по клиенту];
                ToolTipML=[ENU=Specifies the filter that displays an overview of documents with a certain value in the Customer No. field.;
                           RUS=Определяет фильтр, который позволяет получить обзор документов с определенным значением в поле "Код клиента".];
                SourceExpr=CustomFilter;
                OnValidate=BEGIN
                             SetCustFilter;
                             CustomFilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           Cust.RESET;
                           IF PAGE.RUNMODAL(0,Cust) = ACTION::LookupOK THEN BEGIN
                             Text := Cust."No.";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 43  ;2   ;Field     ;
                CaptionML=[ENU=Contract Filter;
                           RUS=Фильтр по контракту];
                ToolTipML=[ENU=Specifies all billable prices for the job task.;
                           RUS=Определяет полные цены к оплате для рабочего задания.];
                SourceExpr=ContractFilter;
                OnValidate=BEGIN
                             SetContractFilter;
                             ContractFilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           ServiceContract.RESET;
                           ServiceContract.SETRANGE("Contract Type",ServiceContract."Contract Type"::Contract);
                           IF PAGE.RUNMODAL(0,ServiceContract) = ACTION::LookupOK THEN BEGIN
                             Text := ServiceContract."Contract No.";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 37  ;2   ;Field     ;
                CaptionML=[ENU=Zone Filter;
                           RUS=Фильтр по зоне];
                ToolTipML=[ENU=Specifies the filter that displays an overview of documents with a certain value in the Service Zone Code field.;
                           RUS=Определяет фильтр, который позволяет получить обзор документов с определенным значением в поле "Код сервисной зоны".];
                SourceExpr=ZoneFilter;
                OnValidate=BEGIN
                             SetZoneFilter;
                             ZoneFilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           ServiceZones.RESET;
                           IF PAGE.RUNMODAL(0,ServiceZones) = ACTION::LookupOK THEN BEGIN
                             Text := ServiceZones.Code;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the estimated date when work on the order should start, that is, when the service order status changes from Pending, to In Process.;
                           RUS=Определяет предполагаемую дату начала работы по сервисному заказу, то есть дату изменения статуса сервисного заказа с "Ожидает" на "В работе".];
                SourceExpr="Response Date" }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the estimated time when work on the order starts, that is, when the service order status changes from Pending, to In Process.;
                           RUS=Определяет предполагаемое время начала работы по сервисному заказу, то есть время изменения статуса сервисного заказа с "Ожидает" на "В работе".];
                SourceExpr="Response Time" }

    { 81  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the priority of the service order.;
                           RUS=Определяет приоритет сервисного заказа.];
                SourceExpr=Priority }

    { 15  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the type of the service document on the line.;
                           RUS=Определяет тип сервисного документа в строке.];
                SourceExpr="Document Type" }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the service document you are creating.;
                           RUS=Определяет номер создаваемого сервисного документа.];
                SourceExpr="No." }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies a short description of the service document, such as Order 2001.;
                           RUS=Определяет краткое описание сервисного документа, например "Заказ 2001".];
                SourceExpr=Description;
                Visible=FALSE }

    { 33  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the service order status, which reflects the repair or maintenance status of all service items on the service order.;
                           RUS=Определяет статус сервисного заказа, который отражает состояние ремонта или обслуживания всех сервисных товаров по этому сервисному заказу.];
                SourceExpr=Status }

    { 53  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the customer who owns the items in the service document.;
                           RUS=Определяет номер клиента, владеющего товарами из сервисного документа.];
                SourceExpr="Customer No." }

    { 36  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the name of the customer to whom the items on the document will be shipped.;
                           RUS=Определяет название клиента, которому будут отгружены товары по этому документу.];
                SourceExpr=Name }

    { 28  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the contract associated with the order.;
                           RUS=Определяет номер контракта, связанного с заказом.];
                SourceExpr="Contract No.";
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the service zone code of the customer's ship-to address in the service order.;
                           RUS=Определяет код сервисной зоны адреса получателя для клиента в сервисном заказе.];
                SourceExpr="Service Zone Code" }

    { 16  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of resource allocations to service items in this order.;
                           RUS=Определяет количество ресурсов, распределенной по сервисным товарам в этом заказе.];
                SourceExpr="No. of Allocations" }

    { 18  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the date when the service order was created.;
                           RUS=Определяет дату создания сервисного заказа.];
                SourceExpr="Order Date" }

    { 20  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the time when the service order was created.;
                           RUS=Определяет время создания сервисного заказа.];
                SourceExpr="Order Time" }

    { 40  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies that you must reallocate resources to at least one service item in this service order.;
                           RUS=Указывает, что необходимо перераспределить ресурсы по крайней мере на один сервисный товар в этом сервисном заказе.];
                SourceExpr="Reallocation Needed" }

    { 94  ;1   ;Group      }

    { 92  ;2   ;Field     ;
                Name=Description2;
                CaptionML=[ENU=Service Order Description;
                           RUS=Описание сервисного заказа];
                ToolTipML=[ENU=Specifies a short description of the service document, such as Order 2001.;
                           RUS=Определяет краткое описание сервисного документа, например "Заказ 2001".];
                SourceExpr=Description;
                Editable=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ServiceZones@1018 : Record 5957;
      Cust@1017 : Record 18;
      Res@1020 : Record 156;
      ResourceGroup@1023 : Record 152;
      ServHeader@1004 : Record 5900;
      ServiceContract@1019 : Record 5965;
      UserMgt@1005 : Codeunit 5700;
      DocFilter@1006 : 'Order,Quote,All';
      StatusFilter@1007 : ' ,Pending,In Process,Finished,On Hold';
      RespDateFilter@1010 : Text;
      ServOrderFilter@1009 : Text;
      CustomFilter@1013 : Text;
      ZoneFilter@1014 : Text;
      ContractFilter@1015 : Text;
      ResourceFilter@1016 : Text;
      ResourceGroupFilter@1022 : Text;
      AllocationFilter@1008 : ' ,No or Partial Allocation,Full Allocation,Reallocation Needed';

    PROCEDURE SetAllFilters@3();
    BEGIN
      SetDocFilter;
      SetStatusFilter;
      SetRespDateFilter;
      SetServOrderFilter;
      SetCustFilter;
      SetZoneFilter;
      SetContractFilter;
      SetResourceFilter;
      SetResourceGroupFilter;
      SetAllocFilter;
    END;

    PROCEDURE SetDocFilter@7();
    BEGIN
      FILTERGROUP(2);
      SetDocFilter2(Rec);
      FILTERGROUP(0);
    END;

    PROCEDURE SetDocFilter2@2(VAR ServHeader@1000 : Record 5900);
    BEGIN
      WITH ServHeader DO BEGIN
        FILTERGROUP(2);
        CASE DocFilter OF
          DocFilter::Order:
            SETRANGE("Document Type","Document Type"::Order);
          DocFilter::Quote:
            SETRANGE("Document Type","Document Type"::Quote);
          DocFilter::All:
            SETFILTER("Document Type",'%1|%2',"Document Type"::Order,"Document Type"::Quote);
        END;
        FILTERGROUP(0);
      END;
    END;

    PROCEDURE SetStatusFilter@8();
    BEGIN
      FILTERGROUP(2);
      CASE StatusFilter OF
        StatusFilter::" ":
          SETRANGE(Status);
        StatusFilter::Pending:
          SETRANGE(Status,Status::Pending);
        StatusFilter::"In Process":
          SETRANGE(Status,Status::"In Process");
        StatusFilter::Finished:
          SETRANGE(Status,Status::Finished);
        StatusFilter::"On Hold":
          SETRANGE(Status,Status::"On Hold");
      END;
      FILTERGROUP(0);
    END;

    PROCEDURE SetRespDateFilter@9();
    BEGIN
      FILTERGROUP(2);
      SETFILTER("Response Date",RespDateFilter);
      RespDateFilter := GETFILTER("Response Date");
      FILTERGROUP(0);
    END;

    PROCEDURE SetServOrderFilter@10();
    BEGIN
      FILTERGROUP(2);
      SETFILTER("No.",ServOrderFilter);
      ServOrderFilter := GETFILTER("No.");
      FILTERGROUP(0);
    END;

    PROCEDURE SetCustFilter@11();
    BEGIN
      FILTERGROUP(2);
      SETFILTER("Customer No.",CustomFilter);
      CustomFilter := GETFILTER("Customer No.");
      FILTERGROUP(0);
    END;

    PROCEDURE SetZoneFilter@12();
    BEGIN
      FILTERGROUP(2);
      SETFILTER("Service Zone Code",ZoneFilter);
      ZoneFilter := GETFILTER("Service Zone Code");
      FILTERGROUP(0);
    END;

    PROCEDURE SetContractFilter@13();
    BEGIN
      FILTERGROUP(2);
      SETFILTER("Contract No.",ContractFilter);
      ContractFilter := GETFILTER("Contract No.");
      FILTERGROUP(0);
    END;

    PROCEDURE SetResourceFilter@4();
    BEGIN
      FILTERGROUP(2);
      IF ResourceFilter <> '' THEN BEGIN
        SETFILTER("No. of Allocations",'>0');
        SETFILTER("Resource Filter",ResourceFilter);
        ResourceFilter := GETFILTER("Resource Filter");
      END ELSE BEGIN
        IF ResourceGroupFilter = '' THEN
          SETRANGE("No. of Allocations");
        SETRANGE("Resource Filter");
      END;
      FILTERGROUP(0);
    END;

    PROCEDURE SetResourceGroupFilter@6();
    BEGIN
      FILTERGROUP(2);
      IF ResourceGroupFilter <> '' THEN BEGIN
        SETFILTER("No. of Allocations",'>0');
        SETFILTER("Resource Group Filter",ResourceGroupFilter);
        ResourceGroupFilter := GETFILTER("Resource Group Filter");
      END ELSE BEGIN
        IF ResourceFilter = '' THEN
          SETRANGE("No. of Allocations");
        SETRANGE("Resource Group Filter");
      END;
      FILTERGROUP(0);
    END;

    PROCEDURE SetAllocFilter@1();
    BEGIN
      FILTERGROUP(2);
      CASE AllocationFilter OF
        AllocationFilter::" ":
          BEGIN
            SETRANGE("No. of Unallocated Items");
            SETRANGE("Reallocation Needed");
          END;
        AllocationFilter::"No or Partial Allocation":
          BEGIN
            SETFILTER("No. of Unallocated Items",'>0');
            SETRANGE("Reallocation Needed",FALSE);
          END;
        AllocationFilter::"Full Allocation":
          BEGIN
            SETRANGE("No. of Unallocated Items",0);
            SETRANGE("Reallocation Needed",FALSE);
          END;
        AllocationFilter::"Reallocation Needed":
          BEGIN
            SETRANGE("No. of Unallocated Items");
            SETRANGE("Reallocation Needed",TRUE);
          END;
      END;
      FILTERGROUP(0);
    END;

    LOCAL PROCEDURE RespDateFilterOnAfterValidate@19063229();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ServOrderFilterOnAfterValidate@19063213();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE StatusFilterOnAfterValidate@19021475();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ZoneFilterOnAfterValidate@19076430();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE CustomFilterOnAfterValidate@19018481();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ContractFilterOnAfterValidate@19030304();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ResourceFilterOnAfterValidate@19005532();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE DocFilterOnAfterValidate@19004301();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE AllocationFilterOnAfterValidat@19066130();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ResourceGroupFilterOnAfterVali@19056724();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

