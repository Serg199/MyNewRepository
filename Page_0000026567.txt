OBJECT Page 26567 Table Individ. Rqst. Subform
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Individual Requisites;
               RUS=Индивидуальные показатели];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table26556;
    PageType=ListPart;
    OnAfterGetRecord=BEGIN
                       IndRequisiteValue := '';
                       IF  StatutoryReportDataValue.GET(DataHeaderNo,ReportCode,TableCode,ExcelSheetName,"Line No.",0) THEN
                         IF StatReportDataChangeLog.ShouldValueBeDisplayed(
                              ShowOnlyChangedValues,
                              DataHeaderNo,
                              "Report Code",
                              TableCode,
                              ExcelSheetName,
                              "Line No.",
                              0)
                         THEN
                           IndRequisiteValue := StatutoryReportDataValue.Value;
                       DescriptionOnFormat;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210000;1;Group     ;
                GroupType=Repeater }

    { 1210001;2;Field     ;
                ToolTipML=[ENU=Specifies the description associated with the individual table requisite.;
                           RUS=Определяет описание, связанное с индивидуальным показателем таблицы.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                Style=Strong;
                StyleExpr=DescriptionEmphasize }

    { 1210008;2;Field     ;
                Name=RequisiteValue;
                CaptionML=[ENU=Value;
                           RUS=Значение];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=IndRequisiteValue;
                OnValidate=BEGIN
                             IF  StatutoryReportDataValue.GET(DataHeaderNo,ReportCode,TableCode,ExcelSheetName,"Line No.",0) THEN BEGIN
                               StatutoryReportDataValue.VALIDATE(Value,IndRequisiteValue);
                               StatutoryReportDataValue.MODIFY;
                             END ELSE BEGIN
                               StatutoryReportDataValue.INIT;
                               StatutoryReportDataValue."Report Data No." := DataHeaderNo;
                               StatutoryReportDataValue."Report Code" := ReportCode;
                               StatutoryReportDataValue."Table Code" := TableCode;
                               StatutoryReportDataValue."Excel Sheet Name" := ExcelSheetName;
                               StatutoryReportDataValue."Row No." := "Line No.";
                               StatutoryReportDataValue."Column No." := 0;
                               StatutoryReportDataValue.VALIDATE(Value,IndRequisiteValue);
                               StatutoryReportDataValue.INSERT;
                             END;
                           END;

                OnDrillDown=VAR
                              StatReportDataChangeLog@1210000 : Record 26565;
                            BEGIN
                              StatReportDataChangeLog.SETRANGE("Report Data No.",DataHeaderNo);
                              StatReportDataChangeLog.SETRANGE("Report Code",ReportCode);
                              StatReportDataChangeLog.SETRANGE("Table Code",TableCode);
                              StatReportDataChangeLog.SETRANGE("Row No.","Line No.");
                              StatReportDataChangeLog.SETRANGE("Column No.",0);
                              PAGE.RUNMODAL(PAGE::"Stat. Report Data Change Log",StatReportDataChangeLog);
                            END;
                             }

    { 1210003;2;Field     ;
                ToolTipML=[ENU=Specifies the row code associated with the individual table requisite.;
                           RUS=Определяет код строки, связанный с индивидуальным показателем таблицы.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Row Code" }

  }
  CODE
  {
    VAR
      StatutoryReportDataValue@1210005 : Record 26564;
      StatReportDataChangeLog@1210007 : Record 26565;
      ReportCode@1210003 : Code[20];
      TableCode@1210002 : Code[20];
      DataHeaderNo@1210001 : Code[20];
      ExcelSheetName@1210000 : Text[30];
      IndRequisiteValue@1210004 : Text[150];
      ShowOnlyChangedValues@1210006 : Boolean;
      DescriptionEmphasize@19010547 : Boolean INDATASET;

    PROCEDURE SetParameters@1210000(NewReportCode@1210002 : Code[20];NewDataHeaderNo@1210000 : Code[20];NewTableCode@1210003 : Code[20];NewExcelSheetName@1210001 : Text[30]);
    BEGIN
      ReportCode := NewReportCode;
      TableCode := NewTableCode;
      DataHeaderNo := NewDataHeaderNo;
      ExcelSheetName := NewExcelSheetName;
    END;

    PROCEDURE UpdateForm@1470001(NewReportCode@1210005 : Code[20];NewDataHeaderNo@1210003 : Code[20];NewTableCode@1210004 : Code[20];NewExcelSheetName@1210000 : Text[30];NewShowOnlyChangedValues@1210006 : Boolean);
    BEGIN
      ReportCode := NewReportCode;
      TableCode := NewTableCode;
      DataHeaderNo := NewDataHeaderNo;
      ExcelSheetName := NewExcelSheetName;

      FILTERGROUP(2);
      SETRANGE("Report Code",ReportCode);
      SETRANGE("Table Code",TableCode);
      FILTERGROUP(0);

      IF FINDFIRST THEN;
      CurrPage.UPDATE(FALSE);
      ShowOnlyChangedValues := NewShowOnlyChangedValues;
    END;

    LOCAL PROCEDURE DescriptionOnFormat@19023855();
    BEGIN
      DescriptionEmphasize := Bold;
    END;

    BEGIN
    END.
  }
}

