OBJECT Table 17207 Tax Register Section
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Status := Status::Blocked;
               VALIDATE("Page ID");
             END;

    OnDelete=BEGIN
               IF Status = Status::Closed THEN
                 ERROR(Text1009,Status);

               SectionReset;
               TaxReg.RESET;
               TaxReg.SETRANGE("Section Code",Code);
               TaxReg.DELETEALL(TRUE);

               TaxRegTerm.SETRANGE("Section Code",Code);
               TaxRegTerm.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Tax Register Section;
               RUS=Секция расчета налогового регистра];
    LookupPageID=Page17218;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              RUS=Код];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 6   ;   ;Page ID             ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Page));
                                                   OnValidate=BEGIN
                                                                IF "Page ID" = 0 THEN
                                                                  VALIDATE(Type);
                                                                CALCFIELDS("Page Name");
                                                              END;

                                                   CaptionML=[ENU=Page ID;
                                                              RUS=Код страницы] }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Standard:
                                                                    "Page ID" := PAGE::"Tax Register Accumulation";
                                                                  ELSE
                                                                    ERROR('');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              RUS=Тип];
                                                   OptionCaptionML=[ENU=Standard;
                                                                    RUS=Стандарт];
                                                   OptionString=Standard;
                                                   Editable=No }
    { 10  ;   ;Norm Jurisdiction Code;Code10      ;TableRelation="Tax Register Norm Jurisdiction";
                                                   OnValidate=BEGIN
                                                                IF "Norm Jurisdiction Code" <> xRec."Norm Jurisdiction Code" THEN BEGIN
                                                                  CheckChangeDeclaration;
                                                                  TaxRegTemplate.RESET;
                                                                  TaxRegTemplate.SETRANGE("Section Code",Code);
                                                                  TaxRegTemplate.SETRANGE("Norm Jurisdiction Code",xRec."Norm Jurisdiction Code");
                                                                  TaxRegTermFormula.RESET;
                                                                  TaxRegTermFormula.SETRANGE("Section Code",Code);
                                                                  TaxRegTermFormula.SETRANGE("Account Type",TaxRegTermFormula."Account Type"::Norm);
                                                                  TaxRegTermFormula.SETRANGE("Norm Jurisdiction Code",xRec."Norm Jurisdiction Code");
                                                                  IF TaxRegTermFormula.FINDFIRST OR TaxRegTemplate.FINDFIRST THEN
                                                                    IF CONFIRM(Text1013,TRUE,"Norm Jurisdiction Code",FIELDCAPTION("Norm Jurisdiction Code")) THEN BEGIN
                                                                      TaxRegTemplate.MODIFYALL("Norm Jurisdiction Code","Norm Jurisdiction Code",TRUE);
                                                                      TaxRegTermFormula.MODIFYALL("Norm Jurisdiction Code","Norm Jurisdiction Code",TRUE);
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Norm Jurisdiction Code;
                                                              RUS=Код нормы юрисдикции] }
    { 11  ;   ;Dimension 1 Code    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                CheckUseDimCode(1,xRec."Dimension 1 Code","Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Dimension 1 Code;
                                                              RUS=Код измерения 1] }
    { 12  ;   ;Dimension 2 Code    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                CheckUseDimCode(2,xRec."Dimension 2 Code","Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Dimension 2 Code;
                                                              RUS=Код измерения 2] }
    { 13  ;   ;Dimension 3 Code    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                CheckUseDimCode(3,xRec."Dimension 3 Code","Dimension 3 Code");
                                                              END;

                                                   CaptionML=[ENU=Dimension 3 Code;
                                                              RUS=Код измерения 3] }
    { 14  ;   ;Dimension 4 Code    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                CheckUseDimCode(4,xRec."Dimension 4 Code","Dimension 4 Code");
                                                              END;

                                                   CaptionML=[ENU=Dimension 4 Code;
                                                              RUS=Код измерения 4] }
    { 16  ;   ;Page Name           ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Page),
                                                                                                                Object ID=FIELD(Page ID)));
                                                   CaptionML=[ENU=Page Name;
                                                              RUS=Название страницы];
                                                   Editable=No }
    { 17  ;   ;Last Register No.   ;Code10        ;CaptionML=[ENU=Last Register No.;
                                                              RUS=Посл. номер регистра];
                                                   Editable=No }
    { 18  ;   ;Last Date Filter    ;Text30        ;CaptionML=[ENU=Last Date Filter;
                                                              RUS=Фильтр по последней дате];
                                                   Editable=No }
    { 21  ;   ;Debit Balance Point 1;DateFormula  ;OnValidate=BEGIN
                                                                CheckUseBlncePoint(xRec."Debit Balance Point 1","Debit Balance Point 1");
                                                              END;

                                                   CaptionML=[ENU=Debit Balance Point 1;
                                                              RUS=Дебетовое сальдо - точка 1] }
    { 22  ;   ;Debit Balance Point 2;DateFormula  ;OnValidate=BEGIN
                                                                CheckUseBlncePoint(xRec."Debit Balance Point 2","Debit Balance Point 2");
                                                              END;

                                                   CaptionML=[ENU=Debit Balance Point 2;
                                                              RUS=Дебетовое сальдо - точка 2] }
    { 23  ;   ;Debit Balance Point 3;DateFormula  ;OnValidate=BEGIN
                                                                CheckUseBlncePoint(xRec."Debit Balance Point 3","Debit Balance Point 3");
                                                              END;

                                                   CaptionML=[ENU=Debit Balance Point 3;
                                                              RUS=Дебетовое сальдо - точка 3] }
    { 24  ;   ;Credit Balance Point 1;DateFormula ;OnValidate=BEGIN
                                                                CheckUseBlncePoint(xRec."Credit Balance Point 1","Credit Balance Point 1");
                                                              END;

                                                   CaptionML=[ENU=Credit Balance Point 1;
                                                              RUS=Кредитовое сальдо - точка 1] }
    { 30  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Blocked);
                                                                IF NOT CONFIRM(Text1002,FALSE) THEN
                                                                  ERROR('');
                                                                IF "Starting Date" <> 0D THEN
                                                                  "Starting Date" := CALCDATE('<-CM>',"Starting Date");
                                                                SectionReset;
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              RUS=Дата начала] }
    { 31  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF "Ending Date" = xRec."Ending Date" THEN
                                                                  EXIT;
                                                                IF "Ending Date" <> 0D THEN BEGIN
                                                                  IF Status > Status::Blocked THEN
                                                                    ERROR(Text1005,Status);
                                                                  TESTFIELD("Starting Date");
                                                                  "Ending Date" := CALCDATE('<CM>',"Ending Date");
                                                                  IF ("Ending Date" <> xRec."Ending Date") AND ("Ending Date" < "Starting Date") THEN
                                                                    ERROR(Text1004,"Starting Date");
                                                                  IF ("Ending Date" <> xRec."Ending Date") AND ("Ending Date" < LastDateEntries) THEN
                                                                    ERROR(Text1004,LastDateEntries);
                                                                  IF CALCDATE('<CY>',"Ending Date") <> CALCDATE('<CY>',"Starting Date") THEN
                                                                    ERROR(Text1010);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              RUS=Дата окончания] }
    { 32  ;   ;Last Date Updated   ;Date          ;CaptionML=[ENU=Last Date Updated;
                                                              RUS=Дата посл. обновления];
                                                   Editable=No }
    { 33  ;   ;Status              ;Option        ;OnValidate=BEGIN
                                                                IF Status <> Status::Blocked THEN
                                                                  TESTFIELD("Starting Date");

                                                                CASE Status OF
                                                                  Status::Reporting:
                                                                    BEGIN
                                                                      IF NOT CONFIRM(Text1002,FALSE) THEN
                                                                        ERROR('');
                                                                      SectionReset;
                                                                    END;
                                                                  Status::Closed:
                                                                    BEGIN
                                                                      TESTFIELD("Ending Date");
                                                                      IF xRec.Status <> xRec.Status::Reporting THEN
                                                                        ERROR(Text1006,FIELDCAPTION(Status),Status);
                                                                      IF TaxRegMgt.GetNextAvailableBeginDate(Code,DATABASE::"Tax Register Accumulation",TRUE) <> 0D THEN
                                                                        ERROR(Text1006,FIELDCAPTION(Status),Status);
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              RUS=Статус];
                                                   OptionCaptionML=[ENU=Open,Reporting,Blocked,Closed;
                                                                    RUS=Открыт,Отчетность,Заблокирован,Закрыт];
                                                   OptionString=Open,Reporting,Blocked,Closed }
    { 34  ;   ;Absence GL Entries Date;Date       ;CaptionML=[ENU=Absence GL Entries Date;
                                                              RUS=Отсутствие операций ГК с даты];
                                                   Editable=No }
    { 35  ;   ;Absence CV Entries Date;Date       ;CaptionML=[ENU=Absence CV Entries Date;
                                                              RUS=Отсутствие операций задолженности с даты];
                                                   Editable=No }
    { 36  ;   ;Absence Item Entries Date;Date     ;CaptionML=[ENU=Absence Item Entries Date;
                                                              RUS=Отсутствие операций по товару с даты];
                                                   Editable=No }
    { 37  ;   ;Absence FA Entries Date;Date       ;CaptionML=[ENU=Absence FA Entries Date;
                                                              RUS=Отсутствие операций ОС с даты];
                                                   Editable=No }
    { 38  ;   ;Absence FE Entries Date;Date       ;CaptionML=[ENU=Absence FE Entries Date;
                                                              RUS=Отсутствие операций РБП с даты];
                                                   Editable=No }
    { 39  ;   ;Absence PR Entries Date;Date       ;CaptionML=[ENU=Absence PR Entries Date;
                                                              RUS=Отсутствие операций ЗП с даты];
                                                   Editable=No }
    { 40  ;   ;Last GL Entries Date;Date          ;OnValidate=BEGIN
                                                                "Last Date Updated" := TODAY;
                                                              END;

                                                   CaptionML=[ENU=Last GL Entries Date;
                                                              RUS=Посл. дата операций ГК];
                                                   Editable=No }
    { 41  ;   ;Last CV Entries Date;Date          ;OnValidate=BEGIN
                                                                "Last Date Updated" := TODAY;
                                                              END;

                                                   CaptionML=[ENU=Last CV Entries Date;
                                                              RUS=Посл. дата операций задолженности];
                                                   Editable=No }
    { 42  ;   ;Last Item Entries Date;Date        ;OnValidate=BEGIN
                                                                "Last Date Updated" := TODAY;
                                                              END;

                                                   CaptionML=[ENU=Last Item Entries Date;
                                                              RUS=Посл. дата операций по товару];
                                                   Editable=No }
    { 43  ;   ;Last FA Entries Date;Date          ;OnValidate=BEGIN
                                                                "Last Date Updated" := TODAY;
                                                              END;

                                                   CaptionML=[ENU=Last FA Entries Date;
                                                              RUS=Посл. дата операций ОС];
                                                   Editable=No }
    { 44  ;   ;Last FE Entries Date;Date          ;OnValidate=BEGIN
                                                                "Last Date Updated" := TODAY;
                                                              END;

                                                   CaptionML=[ENU=Last FE Entries Date;
                                                              RUS=Посл. дата операций РБП];
                                                   Editable=No }
    { 45  ;   ;Last PR Entries Date;Date          ;OnValidate=BEGIN
                                                                "Last Date Updated" := TODAY;
                                                              END;

                                                   CaptionML=[ENU=Last PR Entries Date;
                                                              RUS=Посл. дата операций ЗП];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description,Status                  }
  }
  CODE
  {
    VAR
      TaxReg@1000 : Record 17200;
      Text1001@1001 : TextConst 'ENU=You cannot use the same dimension twice in the same %1.;RUS=Одно измерение не может быть использовано дважды в одной %1.';
      Text1002@1002 : TextConst 'ENU=If you change the section declaration, the tax statement entries will be deleted.\You will have to update again.\\Do you want to change the section declaration?;RUS=При изменении объявления разделов операции налоговой отчетности будут удалены.\Потребуется повторное обновление.\\Изменить объявления разделов?';
      Text1004@1004 : TextConst 'ENU=Ending Date cannot be less then %1.;RUS=Дата окончания не может быть меньше %1.';
      Text1005@1005 : TextConst 'ENU=Ending Date cannot be changed if Status is %1.;RUS=Дату окончания невозможно изменить, если статус - %1.';
      Text1006@1006 : TextConst 'ENU=%1 cannot be set %2 if not ending build registers.;RUS=%1 невозможно установить равным %2, если не закончено формирование регистров.';
      Text1008@1008 : TextConst 'ENU=Value cannot change if %1 is %2.;RUS="Значение не может быть изменено, если %1 = %2."';
      Text1009@1009 : TextConst 'ENU=You cannot delete Tax Register Section because Status is %1.;RUS=Невозможно удалить секцию налогового регистра, так как статус - %1.';
      TaxRegTemplate@1010 : Record 17202;
      TaxRegTerm@1011 : Record 17204;
      TaxRegTermFormula@1210006 : Record 17205;
      TaxRegMgt@1012 : Codeunit 17201;
      Text1010@1013 : TextConst 'ENU=Starting Date and Ending Date are in different year.;RUS=Дата начала и дата окончания относятся к разным годам.';
      Text1011@1014 : TextConst 'ENU=Value must be negative.;RUS=Значение должно быть отрицательным.';
      FileMgt@1210005 : Codeunit 419;
      Text1013@1000000001 : TextConst 'ENU=Do you want to update the %2 field on the lines to reflect the new value of %1?;RUS=Обновить поле %2 в строках в соответствии с новым значением %1?';
      FileName@1210001 : Text[1024];
      Text1015@1210003 : TextConst 'ENU=Select a filename to export settings to.;RUS=Выберите файл для экспорта настроек.';

    LOCAL PROCEDURE CheckUseDimCode@1(FieldNumber@1000 : Integer;xRecDimCode@1001 : Code[20];DimCode@1002 : Code[20]);
    BEGIN
      IF xRecDimCode = DimCode THEN
        EXIT;
      IF Status <> Status::Open THEN
        ERROR(Text1008,FIELDCAPTION(Status),Status);

      IF DimCode <> '' THEN
        IF ((1 <> FieldNumber) AND (DimCode = "Dimension 1 Code")) OR
           ((2 <> FieldNumber) AND (DimCode = "Dimension 2 Code")) OR
           ((3 <> FieldNumber) AND (DimCode = "Dimension 3 Code")) OR
           ((4 <> FieldNumber) AND (DimCode = "Dimension 4 Code"))
        THEN
          ERROR(Text1001,TABLECAPTION);

      IF xRecDimCode <> '' THEN
        CheckChangeDeclaration;
    END;

    LOCAL PROCEDURE CheckUseBlncePoint@2(xRecBalancePint@1000 : DateFormula;BalancePint@1001 : DateFormula);
    BEGIN
      IF xRecBalancePint = BalancePint THEN
        EXIT;
      IF Status <> Status::Open THEN
        ERROR(Text1008,FIELDCAPTION(Status),Status);
      CheckChangeDeclaration;
      IF CALCDATE(BalancePint,TODAY) > TODAY THEN
        ERROR(Text1011);
    END;

    PROCEDURE SectionReset@3();
    VAR
      TaxRegGLCorrEntry@1000 : Record 17203;
      TaxRegAccumulation@1001 : Record 17208;
      TaxRegGLEntry@1002 : Record 17209;
      TaxRegCVEntry@1003 : Record 17210;
      TaxRegFAEntry@1004 : Record 17211;
      TaxRegItemEntry@1005 : Record 17212;
      TaxRegFEEntry@1006 : Record 17213;
      TaxRegPREntry@1007 : Record 17214;
    BEGIN
      TaxRegAccumulation.SETCURRENTKEY("Section Code");
      TaxRegAccumulation.SETRANGE("Section Code",Code);
      TaxRegAccumulation.DELETEALL(TRUE);

      TaxRegGLEntry.SETCURRENTKEY("Section Code");
      TaxRegGLEntry.SETRANGE("Section Code",Code);
      TaxRegGLEntry.DELETEALL(TRUE);

      TaxRegCVEntry.SETCURRENTKEY("Section Code");
      TaxRegCVEntry.SETRANGE("Section Code",Code);
      TaxRegCVEntry.DELETEALL(TRUE);

      TaxRegFAEntry.SETCURRENTKEY("Section Code");
      TaxRegFAEntry.SETRANGE("Section Code",Code);
      TaxRegFAEntry.DELETEALL(TRUE);

      TaxRegItemEntry.SETCURRENTKEY("Section Code");
      TaxRegItemEntry.SETRANGE("Section Code",Code);
      TaxRegItemEntry.DELETEALL(TRUE);

      TaxRegFEEntry.SETCURRENTKEY("Section Code");
      TaxRegFEEntry.SETRANGE("Section Code",Code);
      TaxRegFEEntry.DELETEALL(TRUE);

      TaxRegPREntry.SETCURRENTKEY("Section Code");
      TaxRegPREntry.SETRANGE("Section Code",Code);
      TaxRegPREntry.DELETEALL(TRUE);

      TaxRegGLCorrEntry.SETCURRENTKEY("Section Code");
      TaxRegGLCorrEntry.SETRANGE("Section Code",Code);
      TaxRegGLCorrEntry.DELETEALL(TRUE);

      "Last Date Updated" := 0D;
      "Absence GL Entries Date" := 0D;
      "Absence CV Entries Date" := 0D;
      "Absence Item Entries Date" := 0D;
      "Absence FA Entries Date" := 0D;
      "Absence FE Entries Date" := 0D;
      "Absence PR Entries Date" := 0D;
      "Last GL Entries Date" := 0D;
      "Last CV Entries Date" := 0D;
      "Last Item Entries Date" := 0D;
      "Last FA Entries Date" := 0D;
      "Last FE Entries Date" := 0D;
      "Last PR Entries Date" := 0D;
    END;

    PROCEDURE ValidateChangeDeclaration@4();
    BEGIN
      IF LastDateEntries <> 0D THEN BEGIN
        IF NOT CONFIRM(Text1002,FALSE) THEN
          ERROR('');
        SectionReset;
        Status := Status::Blocked;
        MODIFY;
      END;
    END;

    LOCAL PROCEDURE CheckChangeDeclaration@5();
    BEGIN
      IF LastDateEntries <> 0D THEN BEGIN
        IF NOT CONFIRM(Text1002,FALSE) THEN
          ERROR('');
        SectionReset;
      END;
    END;

    PROCEDURE LastDateEntries@6() LastDate : Date;
    BEGIN
      IF (LastDate = 0D) OR (LastDate > "Last GL Entries Date") AND ("Last GL Entries Date" <> 0D) THEN
        LastDate := "Last GL Entries Date";

      IF (LastDate = 0D) OR ((LastDate > "Last CV Entries Date") AND ("Last CV Entries Date" <> 0D)) THEN
        LastDate := "Last CV Entries Date";

      IF (LastDate = 0D) OR ((LastDate > "Last Item Entries Date") AND ("Last Item Entries Date" <> 0D)) THEN
        LastDate := "Last Item Entries Date";

      IF (LastDate = 0D) OR ((LastDate > "Last FA Entries Date") AND ("Last FA Entries Date" <> 0D)) THEN
        LastDate := "Last FA Entries Date";

      IF (LastDate = 0D) OR ((LastDate > "Last FE Entries Date") AND ("Last FE Entries Date" <> 0D)) THEN
        LastDate := "Last FE Entries Date";

      IF (LastDate = 0D) OR ((LastDate > "Last PR Entries Date") AND ("Last PR Entries Date" <> 0D)) THEN
        LastDate := "Last PR Entries Date";
    END;

    PROCEDURE ExportSettings@1210002(VAR TaxRegisterSection@1210000 : Record 17207);
    VAR
      TaxRegisterSetup@1210001 : XMLport 17200;
      OutputFile@1210002 : File;
      OutStr@1210003 : OutStream;
    BEGIN
      FileName := FileMgt.ServerTempFileName('xml');
      OutputFile.CREATE(FileName);
      OutputFile.CREATEOUTSTREAM(OutStr);
      TaxRegisterSetup.SETDESTINATION(OutStr);
      TaxRegisterSetup.SetData(TaxRegisterSection);
      TaxRegisterSetup.EXPORT;
      OutputFile.CLOSE;
      CLEAR(OutStr);
      DOWNLOAD(FileName,Text1015,'','',FileName);
    END;

    PROCEDURE ImportSettings@1210010(FileName2@1210000 : Text[250]);
    VAR
      TaxRegisterSetup@1210001 : XMLport 17200;
      InStr@1210003 : InStream;
      SettingsFile@1210002 : File;
    BEGIN
      SettingsFile.OPEN(FileName2);
      SettingsFile.CREATEINSTREAM(InStr);

      TaxRegisterSetup.SETSOURCE(InStr);
      TaxRegisterSetup.IMPORT;
      TaxRegisterSetup.ImportData;
      CLEAR(InStr);
      SettingsFile.CLOSE;
    END;

    PROCEDURE PromptImportSettings@1210000();
    BEGIN
      ImportSettings('');
    END;

    BEGIN
    END.
  }
}

