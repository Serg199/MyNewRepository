OBJECT Report 17420 Future Period Vacation Posting
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Future Period Vacation Posting;
               RUS=Учет отпуска будущего периода];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 7528;    ;DataItem;                    ;
               DataItemTable=Table5200;
               OnAfterGetRecord=BEGIN
                                  Person.GET("Person No.");
                                  Person.TESTFIELD("Vendor No.");
                                END;

               ReqFilterFields=No.,Org. Unit Code }

    { 3499;1   ;DataItem;                    ;
               DataItemTable=Table17418;
               DataItemTableView=SORTING(Employee No.,Period Code,Element Code)
                                 WHERE(Document Type=CONST(Vacation));
               OnPreDataItem=BEGIN
                               SETFILTER("Action Start Date",'%1..%2',PayrollPeriod."Starting Date",PayrollPeriod."Ending Date");
                               SETRANGE("Future Period Vacation Posted",FALSE);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF "Action Start Date" > "Posting Date" THEN BEGIN
                                    PayrollPostingGroup.GET("Posting Group");
                                    PayrollPostingGroup.TESTFIELD("Future Vacation G/L Acc. No.");
                                    PayrollPostingGroup.TESTFIELD("Account No.");

                                    GenJnlLine.INIT;
                                    GenJnlLine."Document No." := "Document No.";
                                    GenJnlLine.VALIDATE("Posting Date",PayrollPeriod."Ending Date");
                                    GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
                                    GenJnlLine.VALIDATE("Account No.",PayrollPostingGroup."Future Vacation G/L Acc. No.");
                                    CASE PayrollPostingGroup."Account Type" OF
                                      PayrollPostingGroup."Account Type"::"G/L Account":
                                        GenJnlLine."Bal. Account Type" := GenJnlLine."Bal. Account Type"::"G/L Account";
                                      PayrollPostingGroup."Account Type"::Vendor:
                                        GenJnlLine."Bal. Account Type" := GenJnlLine."Bal. Account Type"::Vendor;
                                    END;
                                    GenJnlLine.VALIDATE("Bal. Account No.",PayrollPostingGroup."Account No.");
                                    GenJnlLine.Description :=
                                      COPYSTR(
                                        STRSUBSTNO(
                                          Text002,
                                          FORMAT(PayrollPeriod."Ending Date",0,'<Month Text>'),
                                          Employee.GetNameInitials),
                                        1,
                                        MAXSTRLEN(GenJnlLine.Description));
                                    GenJnlLine.VALIDATE(Amount,"Payroll Amount");
                                    GenJnlLine."Payroll Ledger Entry No." := "Entry No.";
                                    GenJnlLine."Dimension Set ID" := "Dimension Set ID";

                                    GenJnlPostLine.RunWithCheck(GenJnlLine);

                                    "Future Period Vacation Posted" := TRUE;
                                    MODIFY;
                                  END;
                                END;

               DataItemLink=Employee No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   IF HiddenPeriodCode <> '' THEN
                     PeriodCode := HiddenPeriodCode
                   ELSE
                     PeriodCode := PeriodByDate(WORKDATE);
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1210000;2;Field     ;
                  CaptionML=[ENU=Payroll Period;
                             RUS=Зарплатный период];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PeriodCode;
                  TableRelation="Payroll Period";
                  OnValidate=BEGIN
                               PeriodCodeOnAfterValidate;
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Person@1210008 : Record 17350;
      GenJnlLine@1210002 : Record 81;
      PayrollPeriod@1210001 : Record 17426;
      PayrollPostingGroup@1210007 : Record 17401;
      GenJnlPostLine@1210003 : Codeunit 12;
      PeriodCode@1210000 : Code[10];
      Text002@1210009 : TextConst 'ENU=Vacation pay write-off for %1 %2;RUS=Списание отпускных для %1 %2';
      HiddenPeriodCode@1210010 : Code[10];

    PROCEDURE PeriodByDate@1210001(Date@1210000 : Date) : Code[10];
    VAR
      PayrollPeriod@1210001 : Record 17426;
    BEGIN
      PayrollPeriod.RESET;
      PayrollPeriod.SETFILTER("Ending Date",'%1..',Date);
      IF PayrollPeriod.FINDFIRST THEN
        IF PayrollPeriod."Starting Date" <= Date THEN
          EXIT(PayrollPeriod.Code);

      EXIT('');
    END;

    PROCEDURE SetPeriod@1210002(NewPeriodCode@1210000 : Code[10]);
    BEGIN
      HiddenPeriodCode := NewPeriodCode;
      PayrollPeriod.GET(NewPeriodCode);
    END;

    LOCAL PROCEDURE PeriodCodeOnAfterValidate@19067385();
    BEGIN
      PayrollPeriod.GET(PeriodCode);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

