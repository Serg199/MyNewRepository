OBJECT Codeunit 1233 SEPA DD-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=15.09.15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=1208;
    OnRun=BEGIN
            DeletePaymentFileErrors;
            CheckCollectionEntry(Rec);
          END;

  }
  CODE
  {
    VAR
      EuroCurrErr@1007 : TextConst 'ENU=Only transactions in euro (EUR) are allowed.;RUS=Допускаются только транзакции в евро (EUR).';
      FieldBlankErr@1006 : TextConst '@@@="%1=field name, %2=table name. Example: Name must have a value in Customer.";ENU=%1 must have a value in %2.;RUS=У %1 должно быть значение в %2.';
      FieldKeyBlankErr@1004 : TextConst '@@@="%1=field name, %2= table name, %3=key field value. Example: Name must have a value in Customer 10000.";ENU=%1 must have a value in %2 %3.;RUS=Поле %1 в %2 %3 должно иметь значение.';
      MustBeCustomerErr@1001 : TextConst 'ENU=The customer %1 does not exist.;RUS=Клиент %1 не существует.';
      MustBePositiveErr@1000 : TextConst 'ENU=The amount must be positive.;RUS=Сумма должна быть положительной.';
      NotActiveMandateErr@1010 : TextConst 'ENU=The mandate %1 is not active.;RUS=Поручение %1 неактивно.';
      PartnerTypeErr@1008 : TextConst '@@@="%1 = Partner Type; %2 = Company/Person; %3 = Company/Person.";ENU=The customer''s %1, %2, must be equal to the %1, %3, specified in the collection.;RUS=%1, %2 клиента должны совпадать с %1, %3, указанными в коллекции.';
      TransferDateErr@1002 : TextConst 'ENU=The earliest possible transfer date is today.;RUS=Самая ранняя возможная дата перевода - сегодня.';
      SelectedRecordTxt@1003 : TextConst 'ENU=the currently selected record;RUS=текущая выбранная запись';
      PartnerTypeBlankErr@1103 : TextConst '@@@=Partner Type must be filled.;ENU=%1 must be filled.;RUS=%1 должно быть заполнено.';

    LOCAL PROCEDURE CheckCollectionEntry@2(DirectDebitCollectionEntry@1000 : Record 1208);
    VAR
      Customer@1004 : Record 18;
      CustomerBankAccount@1003 : Record 287;
      DirectDebitCollection@1005 : Record 1207;
      GLSetup@1002 : Record 98;
      SEPADirectDebitMandate@1001 : Record 1230;
    BEGIN
      GLSetup.GET;
      WITH DirectDebitCollectionEntry DO BEGIN
        IF "Transfer Amount" <= 0 THEN
          InsertPaymentFileError(MustBePositiveErr);

        IF "Currency Code" <> GLSetup.GetCurrencyCode('EUR') THEN
          InsertPaymentFileError(EuroCurrErr);

        IF "Transfer Date" < TODAY THEN
          InsertPaymentFileError(TransferDateErr);

        IF NOT Customer.GET("Customer No.") THEN BEGIN
          InsertPaymentFileError(STRSUBSTNO(MustBeCustomerErr,"Customer No."));
          EXIT;
        END;

        IF Customer.Name = '' THEN
          AddFieldEmptyError(DirectDebitCollectionEntry,Customer.TABLECAPTION,Customer.FIELDCAPTION(Name),"Customer No.");

        DirectDebitCollection.GET("Direct Debit Collection No.");
        IF Customer."Partner Type" <> DirectDebitCollection."Partner Type" THEN
          InsertPaymentFileError(STRSUBSTNO(PartnerTypeErr,Customer.FIELDCAPTION("Partner Type"),Customer."Partner Type",
              DirectDebitCollection."Partner Type"));

        IF DirectDebitCollection."Partner Type" = DirectDebitCollection."Partner Type"::" " THEN
          InsertPaymentFileError(STRSUBSTNO(PartnerTypeBlankErr,DirectDebitCollection.FIELDCAPTION("Partner Type")));

        IF "Mandate ID" = '' THEN
          AddFieldEmptyError(DirectDebitCollectionEntry,SelectedRecordTxt,FIELDCAPTION("Mandate ID"),"Mandate ID")
        ELSE BEGIN
          SEPADirectDebitMandate.GET("Mandate ID");
          IF SEPADirectDebitMandate."Date of Signature" = 0D THEN
            AddFieldEmptyError(
              DirectDebitCollectionEntry,SEPADirectDebitMandate.TABLECAPTION,SEPADirectDebitMandate.FIELDCAPTION("Date of Signature"),
              "Mandate ID");
          IF NOT SEPADirectDebitMandate.IsMandateActive("Transfer Date") THEN
            InsertPaymentFileError(STRSUBSTNO(NotActiveMandateErr,"Mandate ID"));

          IF SEPADirectDebitMandate."Customer Bank Account Code" = '' THEN
            AddFieldEmptyError(
              DirectDebitCollectionEntry,SEPADirectDebitMandate.TABLECAPTION,
              SEPADirectDebitMandate.FIELDCAPTION("Customer Bank Account Code"),SEPADirectDebitMandate.ID)
          ELSE BEGIN
            CustomerBankAccount.GET(Customer."No.",SEPADirectDebitMandate."Customer Bank Account Code");
            IF CustomerBankAccount."SWIFT Code" = '' THEN
              AddFieldEmptyError(
                DirectDebitCollectionEntry,CustomerBankAccount.TABLECAPTION,CustomerBankAccount.FIELDCAPTION("SWIFT Code"),
                CustomerBankAccount.Code);
            IF CustomerBankAccount.IBAN = '' THEN
              AddFieldEmptyError(
                DirectDebitCollectionEntry,CustomerBankAccount.TABLECAPTION,CustomerBankAccount.FIELDCAPTION(IBAN),
                CustomerBankAccount.Code);
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE AddFieldEmptyError@1(VAR DirectDebitCollectionEntry@1002 : Record 1208;TableCaption@1000 : Text;FieldCaption@1001 : Text;KeyValue@1003 : Text);
    VAR
      ErrorText@1004 : Text;
    BEGIN
      IF KeyValue = '' THEN
        ErrorText := STRSUBSTNO(FieldBlankErr,FieldCaption,TableCaption)
      ELSE
        ErrorText := STRSUBSTNO(FieldKeyBlankErr,FieldCaption,TableCaption,KeyValue);
      DirectDebitCollectionEntry.InsertPaymentFileError(ErrorText);
    END;

    BEGIN
    END.
  }
}

