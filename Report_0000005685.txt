OBJECT Report 5685 Copy Fixed Asset
{
  OBJECT-PROPERTIES
  {
    Date=17.08.17;
    Time=10:31:33;
    Modified=Yes;
    Version List=NAVW110.00,NAVRU10.00,NOS-26;
  }
  PROPERTIES
  {
    Permissions=TableData 5612=ri;
    CaptionML=[ENU=Copy Fixed Asset;
               RUS=Копирование ОС];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DefaultDim.LOCKTABLE;
                  FADeprBook.LOCKTABLE;
                  FA.LOCKTABLE;
                  IF FANo = '' THEN
                    ERROR(Text000,FA.TABLECAPTION,FA.FIELDCAPTION("No."));
                  IF (FirstFANo = '') AND NOT UseFANoSeries THEN
                    ERROR(Text001);
                  FA.GET(FANo);
                  FADeprBook."FA No." := FANo;
                  FADeprBook.SETRANGE("FA No.",FANo);
                  DefaultDim."Table ID" := DATABASE::"Fixed Asset";
                  DefaultDim."No." := FANo;
                  DefaultDim.SETRANGE("Table ID",DATABASE::"Fixed Asset");
                  DefaultDim.SETRANGE("No.",FANo);
                  DefaultDim2 := DefaultDim;
                  FOR I := 1 TO NumberofCopies DO BEGIN
                    FA2 := FA;
                    FA2."No." := '';
                    FA2."Last Date Modified" := 0D;
                    FA2."Main Asset/Component" := FA2."Main Asset/Component"::" ";
                    FA2."Component of Main Asset" := '';
                    FA2."Initial Release Date" := 0D;
                    FA2."Status Document No." := '';
                    FA2.Status := 0;
                    FA2."FA Location Code" := '';
                    FA2."Responsible Employee" := '';
                    FA2."Location Code" := '';
                    IF UseFANoSeries THEN
                      FA2.INSERT(TRUE)
                    ELSE BEGIN
                      FA2."No." := FirstFANo;
                      IF NumberofCopies > 1 THEN
                        FirstFANo := INCSTR(FirstFANo);
                      IF FA2."No." = '' THEN
                        ERROR(Text002,FA.TABLECAPTION,FA.FIELDCAPTION("No."));
                      FA2.INSERT(TRUE);
                    END;
                    IF DefaultDim.FIND('-') THEN
                      REPEAT
                        DefaultDim2 := DefaultDim;
                        DefaultDim2."No." := FA2."No.";
                        //NC OFA41 > DP
                        IF DefaultDim2.INSERT(TRUE) THEN;
                        {
                        DefaultDim2.INSERT(TRUE);
                        }
                        //NC OFA41 < DP
                      UNTIL DefaultDim.NEXT = 0;
                    IF FADeprBook.FIND('-') THEN
                      REPEAT
                        FADeprBook2 := FADeprBook;
                        FADeprBook2."FA No." := FA2."No.";
                        FADeprBook2."Depreciation Starting Date" := 0D;
                        FADeprBook2."Depreciation Ending Date" := 0D;
                        FADeprBook2."Initial Acquisition" := FALSE;
                        IF NOT FADeprBook2.INSERT(TRUE) THEN
                          FADeprBook2.MODIFY(TRUE);
                      UNTIL FADeprBook.NEXT = 0;
                    IF FA2.FIND THEN BEGIN;
                      FA2."Last Date Modified" := 0D;
                      FA2.MODIFY;
                    END;
                  END;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF NumberofCopies < 1 THEN
                     NumberofCopies := 1;
                   FANo := FANo2;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Copy from FA No.;
                             RUS=Копировать из ОС с номером];
                  ToolTipML=[ENU=Specifies the number of the fixed asset that you want to copy from.;
                             RUS=Определяет номер основного средства, начиная с которого будет выполняться копирование.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=FANo;
                  TableRelation="Fixed Asset" }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=No. of Copies;
                             RUS=Число копий];
                  ToolTipML=[ENU=Specifies the number of new fixed asset that you want to create.;
                             RUS=Определяет номер нового основного средства, которое требуется создать.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=NumberofCopies;
                  MinValue=1 }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=First FA No.;
                             RUS=Первый номер ОС];
                  ToolTipML=[ENU=Specifies the number of the first fixed asset. If No. of Copies is greater than 1, the First FA No. field must include a number, for example FA045.;
                             RUS=Определяет номер первого основного средства. Если значение поля "Число копий" превышает 1, то в поле "Первый номер ОС" должно содержаться число, например FA045.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=FirstFANo;
                  OnValidate=BEGIN
                               IF FirstFANo <> '' THEN
                                 UseFANoSeries := FALSE;
                             END;
                              }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Use FA No. Series;
                             RUS=Использовать серию номеров ОС];
                  ToolTipML=[ENU=Specifies if you want the new fixed asset to have a number from the number series specified in Fixed Asset Nos. field in the Fixed Asset Setup window.;
                             RUS=Указывает, должно ли новое основное средство иметь номер из серии номеров, указанной в поле "Номера ОС" в окне "Настройка модуля ОС".];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=UseFANoSeries;
                  OnValidate=BEGIN
                               IF UseFANoSeries THEN
                                 FirstFANo := '';
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DefaultDim@1008 : Record 352;
      DefaultDim2@1009 : Record 352;
      FA@1004 : Record 5600;
      FA2@1006 : Record 5600;
      FADeprBook@1010 : Record 5612;
      FADeprBook2@1011 : Record 5612;
      FANo@1000 : Code[20];
      FANo2@1015 : Code[20];
      FirstFANo@1002 : Code[20];
      UseFANoSeries@1001 : Boolean;
      NumberofCopies@1003 : Integer;
      I@1005 : Integer;
      Text000@1012 : TextConst '@@@="%1: TABLECAPTION(Fixed Asset); %2: Field(No.)";ENU=You must specify a number in the Copy from %1 %2 field.;RUS=Необходимо указать номер в поле "Копировать из %1 %2".';
      Text001@1013 : TextConst 'ENU=You must specify a number in First FA No. field or use the FA No. Series.;RUS=Необходимо указать номер в поле "Первый номер ОС" или использовать серию номеров ОС.';
      Text002@1014 : TextConst '@@@="%1: TABLECAPTION(Fixed Asset); %2: Field(No.)";ENU=You must include a number in the First FA %1 %2 field.;RUS=Необходимо внести номер в поле "Первый номер ОС %1 %2"';

    PROCEDURE SetFANo@1(NewFANo@1000 : Code[20]);
    BEGIN
      FANo2 := NewFANo;
    END;

    PROCEDURE InitializeRequest@2(NewFANo@1000 : Code[20];NewNumberofCopies@1001 : Integer;NewFirstFANo@1002 : Code[20];NewUseFANoSeries@1003 : Boolean);
    BEGIN
      NumberofCopies := NewNumberofCopies;
      FirstFANo := NewFirstFANo;
      UseFANoSeries := NewUseFANoSeries;
      FANo := NewFANo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

