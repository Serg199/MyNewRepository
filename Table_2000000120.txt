OBJECT Table 2000000120 User
{
  OBJECT-PROPERTIES
  {
    Date=27.06.17;
    Time=14:37:54;
    Modified=Yes;
    Version List=NOS-26;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               //TDRANCS-901 >
               fn_ManageUserSetup("User Name",g_optKindOfChange::Insert);
               IF CONFIRM(g_Text001,FALSE,"User Name") THEN
                 fn_ManageGenJnlBatch("User Name",g_optKindOfChange::Insert);
               //TDRANCS-901 <
             END;

    OnDelete=BEGIN
               //TDRANCS-901 >
               fn_ManageUserSetup("User Name",g_optKindOfChange::Delete);
               fn_ManageGenJnlBatch("User Name",g_optKindOfChange::Delete);
               //TDRANCS-901 <
             END;

    OnRename=BEGIN
               //TDRANCS-901 >
               fn_ManageUserSetup("User Name",g_optKindOfChange::Rename);
               //TDRANCS-901 <
             END;

    CaptionML=[ENU=User;
               RUS=Пользователь];
  }
  FIELDS
  {
    { 1   ;   ;User Security ID    ;GUID          ;CaptionML=[ENU=User Security ID;
                                                              RUS=Код безопасности пользователя] }
    { 2   ;   ;User Name           ;Code50        ;CaptionML=[ENU=User Name;
                                                              RUS=Имя пользователя] }
    { 3   ;   ;Full Name           ;Text80        ;CaptionML=[ENU=Full Name;
                                                              RUS=Полное название] }
    { 4   ;   ;State               ;Option        ;CaptionML=[ENU=State;
                                                              RUS=Состояние];
                                                   OptionCaptionML=[ENU=Enabled,Disabled;
                                                                    RUS=Включено,Отключено];
                                                   OptionString=Enabled,Disabled }
    { 5   ;   ;Expiry Date         ;DateTime      ;CaptionML=[ENU=Expiry Date;
                                                              RUS=Дата истечения] }
    { 7   ;   ;Windows Security ID ;Text119       ;CaptionML=[ENU=Windows Security ID;
                                                              RUS=Код безопасности Windows] }
    { 8   ;   ;Change Password     ;Boolean       ;CaptionML=[ENU=Change Password;
                                                              RUS=Изменить пароль] }
    { 10  ;   ;License Type        ;Option        ;CaptionML=[ENU=License Type;
                                                              RUS=Тип лицензии];
                                                   OptionCaptionML=[ENU=Full User,Limited User,Device Only User,Windows Group,External User;
                                                                    RUS=Полный пользователь,Ограниченный пользователь,Только пользователь устройства,Группа Windows,Внешний пользователь];
                                                   OptionString=Full User,Limited User,Device Only User,Windows Group,External User }
    { 11  ;   ;Authentication Email;Text250       ;CaptionML=[ENU=Authentication Email;
                                                              RUS=Адрес проверки подлинности] }
    { 14  ;   ;Contact Email       ;Text250       ;CaptionML=[ENU=Contact Email;
                                                              RUS=Контактный адрес эл. почты] }
    { 15  ;   ;Exchange Identifier ;Text250       ;CaptionML=[ENU=Exchange Identifier;
                                                              RUS=Идентификатор Exchange] }
    { 16  ;   ;Application ID      ;GUID          ;CaptionML=[ENU=Application ID;
                                                              RUS=Код приложения] }
  }
  KEYS
  {
    {    ;User Security ID                        ;Clustered=Yes }
    {    ;User Name                                }
    {    ;Windows Security ID                      }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;User Name                                }
  }
  CODE
  {
    VAR
      g_cuLoginMgt@1101495001 : Codeunit 418;
      g_optKindOfChange@1101495000 : 'Insert,Modify,Delete,Rename';
      g_Text001@1101495002 : TextConst 'ENU=Create Gen. Journal Batch for user %1?;RUS=Создать Раздел Фин. Журнала для Пользователя %1?';

    PROCEDURE fn_ManageUserSetup@1000000000(p_txtWinLoginID@1000000000 : Text[65];p_optKindOfChange@1000000003 : 'Insert,Modify,Delete,Rename');
    VAR
      l_recUserSetup@1000000002 : Record 91;
      l_recCompany@1000000004 : Record 2000000006;
      l_codeUserID@1000000001 : Code[20];
    BEGIN
      //TDRANCS-901
      l_codeUserID := g_cuLoginMgt.ShortUserID(p_txtWinLoginID);
      IF l_codeUserID = '' THEN
        EXIT;

      //TDRAMIO-62>
      l_recCompany.FINDFIRST;
      REPEAT
      l_recUserSetup.CHANGECOMPANY(l_recCompany.Name);
      //TDRAMIO-62<
        CASE p_optKindOfChange OF
          p_optKindOfChange::Insert: BEGIN
            IF NOT l_recUserSetup.GET(l_codeUserID) THEN BEGIN
              l_recUserSetup.INIT;
              l_recUserSetup."User ID" := l_codeUserID;
              l_recUserSetup."Allow Posting From" := TODAY;
              l_recUserSetup.INSERT(FALSE); //TDRAMIO-62
            END;
          END;
          p_optKindOfChange::Modify: BEGIN
            EXIT;
          END;
          p_optKindOfChange::Delete: BEGIN
            IF l_recUserSetup.GET(l_codeUserID) THEN
              l_recUserSetup.DELETE(FALSE); //TDRAMIO-62
          END;
          p_optKindOfChange::Rename: BEGIN
            IF NOT l_recUserSetup.GET(l_codeUserID) THEN BEGIN
              l_recUserSetup.INIT;
              l_recUserSetup."User ID" := l_codeUserID;
              l_recUserSetup."Allow Posting From" := TODAY;
              l_recUserSetup.INSERT(FALSE); //TDRAMIO-62
            END;
          END;
        END;
      //TDRAMIO-62>
      UNTIL l_recCompany.NEXT = 0;
      //TDRAMIO-62<
    END;

    PROCEDURE fn_ManageGenJnlBatch@1000000001(p_txtWinLoginID@1000000001 : Text[65];p_optKindOfChange@1000000002 : 'Insert,Modify,Delete,Rename');
    VAR
      l_recGLSetup@1000000003 : Record 98;
      l_recGJBatch@1000000004 : Record 232;
      l_recGJTemplate@1000000005 : Record 80;
      l_recCompany@1000000006 : Record 2000000006;
      l_recGenJnlLine@1000000009 : Record 81;
      l_recGenJnlAlloc@1000000008 : Record 221;
      l_codeUserID@1000000000 : Code[20];
    BEGIN
      //TDRANCS-901
      l_codeUserID := g_cuLoginMgt.ShortUserID(p_txtWinLoginID);
      IF l_codeUserID = '' THEN
        EXIT;

      //TDRANCS-1331>
      l_codeUserID := COPYSTR(l_codeUserID,1,MAXSTRLEN(l_recGJBatch.Name));
      //TDRANCS-1331<

      l_recCompany.FINDFIRST;

      REPEAT
        l_recGJBatch.CHANGECOMPANY(l_recCompany.Name);
        l_recGJTemplate.CHANGECOMPANY(l_recCompany.Name);
        l_recGLSetup.CHANGECOMPANY(l_recCompany.Name);
        l_recGenJnlAlloc.CHANGECOMPANY(l_recCompany.Name);
        l_recGenJnlLine.CHANGECOMPANY(l_recCompany.Name);

        //NOS-26 > AAP
        //l_recJnlLineDim.CHANGECOMPANY(l_recCompany.Name);
        //NOS-26 < AAP

        l_recGLSetup.CHANGECOMPANY(l_recCompany.Name);

        IF l_recGLSetup.GET THEN BEGIN
          IF l_recGJTemplate.GET(l_recGLSetup."User's Gen. Jnl. Templ.") THEN BEGIN
            CASE p_optKindOfChange OF
              p_optKindOfChange::Insert: BEGIN
                IF NOT l_recGJBatch.GET(l_recGLSetup."User's Gen. Jnl. Templ.",l_codeUserID) THEN BEGIN
                  l_recGJBatch.INIT;
                  l_recGJBatch."Journal Template Name" := l_recGLSetup."User's Gen. Jnl. Templ.";
                  l_recGJBatch."Bal. Account Type" := l_recGJTemplate."Bal. Account Type";
                  l_recGJBatch."Bal. Account No." := l_recGJTemplate."Bal. Account No.";
                  l_recGJBatch."No. Series" := l_recGJTemplate."No. Series";
                  l_recGJBatch."Posting No. Series" := l_recGJTemplate."Posting No. Series";
                  l_recGJBatch."Reason Code" := l_recGJTemplate."Reason Code";
                  l_recGJBatch."Copy VAT Setup to Jnl. Lines" := l_recGJTemplate."Copy VAT Setup to Jnl. Lines";
                  l_recGJBatch."Allow VAT Difference" := l_recGJTemplate."Allow VAT Difference";
                  l_recGJBatch.Name := l_codeUserID;
                  IF STRPOS(p_txtWinLoginID,'\') IN [0,STRLEN(p_txtWinLoginID)] THEN
                    l_recGJBatch.Description := COPYSTR(p_txtWinLoginID,1,50)
                  ELSE
                    l_recGJBatch.Description := COPYSTR(p_txtWinLoginID,STRPOS(p_txtWinLoginID,'\') + 1,50);
                  l_recGJBatch.INSERT;
                END;
              END;
              p_optKindOfChange::Modify: BEGIN
                EXIT;
              END;
              p_optKindOfChange::Delete: BEGIN
                IF l_recGJBatch.GET(l_recGLSetup."User's Gen. Jnl. Templ.",l_codeUserID) THEN BEGIN
                  l_recGJBatch.DELETE;

                  l_recGenJnlAlloc.SETRANGE("Journal Template Name",l_recGJBatch."Journal Template Name");
                  l_recGenJnlAlloc.SETRANGE("Journal Batch Name",l_recGJBatch.Name);
                  l_recGenJnlAlloc.DELETEALL;

                  l_recGenJnlLine.SETRANGE("Journal Template Name",l_recGJBatch."Journal Template Name");
                  l_recGenJnlLine.SETRANGE("Journal Batch Name",l_recGJBatch.Name);
                  l_recGenJnlLine.DELETEALL;

                  //NOS-26 > AAP
                  //l_recJnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Jnl. Allocation");
                  //l_recJnlLineDim.SETRANGE("Journal Template Name",l_recGJBatch."Journal Template Name");
                  //l_recJnlLineDim.SETRANGE("Journal Batch Name",l_recGJBatch.Name);
                  //l_recJnlLineDim.DELETEALL;
                  //NOS-26 < AAP

                  {
                  // TDRANCS-820 >
                  l_recAccesstoGenJnl.SETRANGE("Journal Template Name","Journal Template Name");
                  l_recAccesstoGenJnl.SETRANGE("Journal Batch Name",Name);
                  l_recAccesstoGenJnl.DELETEALL;
                  // TDRANCS-820 <
                  }
                END;
              END;
              p_optKindOfChange::Rename: BEGIN
                EXIT;
              END;
            END;
          END;
        END;
      UNTIL l_recCompany.NEXT = 0;
    END;

    BEGIN
    {
      TDRANCS-901
        create User Setup and Gen. Journal Batch when windows login is created
      TDRANCS-1331 > BugFix
      TDRAMIO-62 Modified fn_ManageUserSetup
      NOS-26 AAP изменена функция fn_ManageGenJnlBatch. "ID" везде заменен на "User Name"
    }
    END.
  }
}

