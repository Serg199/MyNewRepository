OBJECT Page 9900 Import Data
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    Editable=Yes;
    CaptionML=[ENU=Import Data;
               RUS=Импорт данных];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table2000000006;
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             OriginalTenantId := '';
             ContainsApplication := FALSE;
             ContainsApplicationData := FALSE;
             ContainsGlobalData := FALSE;
           END;

    OnAfterGetRecord=BEGIN
                       Selected := SelectedCompany.GET(Name);
                     END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::OK THEN BEGIN
                         IF IncludeApplicationData OR IncludeGlobalData THEN BEGIN
                           IF NOT CONFIRM(OverwriteQst,FALSE) THEN
                             EXIT(FALSE);
                         END;
                         IF IMPORTDATA(
                              FALSE,
                              FileName,
                              IncludeApplicationData,
                              IncludeGlobalData,
                              SelectedCompany)
                         THEN BEGIN
                           MESSAGE(CompletedMsg);
                           EXIT(TRUE)
                         END;
                         EXIT(FALSE);
                       END;
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           RUS=Общее];
                GroupType=Group }

    { 12  ;2   ;Field     ;
                Name=FileName;
                Lookup=No;
                DrillDown=No;
                AssistEdit=Yes;
                CaptionML=[ENU=File Name;
                           RUS=Имя файла];
                ToolTipML=[ENU=Specifies the name and location of the .navdata file that you want to import data from.;
                           RUS=Определяет имя и расположение .navdata-файла, из которого требуется выполнить импорт данных.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=FileName;
                Editable=TRUE;
                OnValidate=VAR
                             Companies@1000 : TEMPORARY Record 2000000006;
                           BEGIN
                             IF NOT DATAFILEINFORMATION(
                                  FALSE,
                                  FileName,
                                  Description,
                                  ContainsApplication,
                                  ContainsApplicationData,
                                  ContainsGlobalData,
                                  OriginalTenantId,
                                  ExportDate,
                                  Companies)
                             THEN
                               EXIT;

                             DELETEALL;
                             ContainsCompanies := Companies.FINDSET;
                             IF ContainsCompanies THEN
                               REPEAT
                                 Rec := Companies;
                                 INSERT;
                               UNTIL Companies.NEXT = 0;

                             IncludeApplicationData := FALSE;
                             IncludeGlobalData := FALSE;
                             IncludeAllCompanies := ContainsCompanies;

                             MarkAll;
                           END;

                OnAssistEdit=VAR
                               Companies@1000 : TEMPORARY Record 2000000006;
                             BEGIN
                               IF NOT DATAFILEINFORMATION(
                                    TRUE,
                                    FileName,
                                    Description,
                                    ContainsApplication,
                                    ContainsApplicationData,
                                    ContainsGlobalData,
                                    OriginalTenantId,
                                    ExportDate,
                                    Companies)
                               THEN
                                 EXIT;

                               DELETEALL;
                               ContainsCompanies := Companies.FINDSET;
                               IF ContainsCompanies THEN
                                 REPEAT
                                   Rec := Companies;
                                   INSERT;
                                 UNTIL Companies.NEXT = 0;

                               IncludeApplicationData := FALSE;
                               IncludeGlobalData := FALSE;
                               IncludeAllCompanies := ContainsCompanies;

                               MarkAll;
                             END;
                              }

    { 6   ;2   ;Field     ;
                Name=TenantId;
                CaptionML=[ENU=Tenant ID;
                           RUS=Ид. арендатора];
                ToolTipML=[ENU=Specifies the ID of the tenant that is accessed when you run objects from the development environment. If your solution is not set up to deploy in a multitenant deployment architecture, leave the parameter empty.;
                           RUS=Определяет код арендатора, к которому вы обращаетесь при запуске объектов из среды разработки. Если решение не настроено на развертывание в конфигурации с несколькими арендаторами, оставьте этот параметр пустым.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TENANTID;
                Editable=FALSE }

    { 3   ;2   ;Field     ;
                Name=Description;
                CaptionML=[ENU=Description;
                           RUS=Описание];
                ToolTipML=[ENU=Specifies a description of the data to be imported.;
                           RUS=Определяет описание данных, которые должны быть импортированы.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                Editable=FALSE }

    { 7   ;2   ;Field     ;
                Name=ExportDate;
                CaptionML=[ENU=Date of Export;
                           RUS=Дата экспорта];
                ToolTipML=[ENU=Specifies when the data was exported.;
                           RUS=Указывает, когда были экспортированы данные.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ExportDate;
                Editable=FALSE }

    { 13  ;2   ;Group     ;
                CaptionML=[ENU=Import;
                           RUS=Импорт];
                GroupType=Group }

    { 14  ;3   ;Field     ;
                Name=IncludeAllCompanies;
                CaptionML=[ENU=All Companies;
                           RUS=Все организации];
                ToolTipML=[ENU=Specifies that data in all the companies will be imported into the database.;
                           RUS=Указывает, что данные во всех организациях будут импортированы в базу данных.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=IncludeAllCompanies;
                Editable=ContainsCompanies;
                OnValidate=BEGIN
                             MarkAll;
                           END;
                            }

    { 11  ;3   ;Field     ;
                Name=IncludeGlobalData;
                CaptionML=[ENU=Global Data;
                           RUS=Глобальные данные];
                ToolTipML=[ENU=Specifies that data that is common to all companies will be imported into the database. This includes the report list, user IDs, and printer selections, but no company-specific business data.;
                           RUS=Указывает, что данные, являющиеся общими для всех организаций, будут импортированы в базу данных. К ним относятся список отчетов, коды пользователей и выбранные принтеры, но не относятся бизнес данные о конкретных организациях.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=IncludeGlobalData;
                Editable=ContainsGlobalData }

    { 4   ;3   ;Field     ;
                Name=IncludeApplicationData;
                CaptionML=[ENU=Application Data;
                           RUS=Данные приложения];
                ToolTipML=[ENU=Specifies that the data that defines the application in the database is imported. This includes the permissions, permission sets, profiles, and style sheets.;
                           RUS=Указывает, что данные, которые определяют приложение в базе данных, импортируются. Они включают разрешения, наборы разрешений, профили и таблицы стилей.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=IncludeApplicationData;
                Editable=ContainsApplicationData }

    { 8   ;2   ;Group     ;
                GroupType=Repeater }

    { 10  ;3   ;Field     ;
                CaptionML=[ENU=Import;
                           RUS=Импорт];
                ToolTipML=[ENU=Specifies that data will be imported.;
                           RUS=Указывает, что данные будут импортированы.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Selected;
                OnValidate=BEGIN
                             IF Selected THEN BEGIN
                               SelectedCompany := Rec;
                               IF SelectedCompany.INSERT THEN;
                             END ELSE BEGIN
                               IncludeAllCompanies := FALSE;
                               IF SelectedCompany.GET(Name) THEN
                                 SelectedCompany.DELETE;
                             END;
                           END;
                            }

    { 9   ;3   ;Field     ;
                Width=30;
                CaptionML=[ENU=Company Name;
                           RUS=Название организации];
                ToolTipML=[ENU=Specifies the name of a company that has been created in the current database.;
                           RUS=Определяет название компании, которая была создана в текущей базе данных.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Name }

  }
  CODE
  {
    VAR
      SelectedCompany@1002 : TEMPORARY Record 2000000006;
      FileName@1007 : Text;
      Description@1008 : Text;
      OriginalTenantId@1009 : Text;
      ExportDate@1012 : DateTime;
      Selected@1001 : Boolean;
      ContainsApplication@1006 : Boolean;
      ContainsApplicationData@1004 : Boolean;
      ContainsGlobalData@1003 : Boolean;
      ContainsCompanies@1005 : Boolean;
      IncludeAllCompanies@1000 : Boolean;
      IncludeApplicationData@1010 : Boolean;
      IncludeGlobalData@1011 : Boolean;
      OverwriteQst@1013 : TextConst 'ENU=Application data, global data, or both types of data will be overwritten. Are you sure that you want to continue?;RUS=Будут перезаписаны данные приложения, глобальные данные или оба вида данных. Вы действительно хотите продолжить?';
      CompletedMsg@1014 : TextConst 'ENU=The data was imported successfully.;RUS=Данные успешно импортированы.';

    LOCAL PROCEDURE MarkAll@2();
    BEGIN
      SelectedCompany.DELETEALL;

      IF IncludeAllCompanies THEN
        IF FINDSET THEN
          REPEAT
            SelectedCompany := Rec;
            SelectedCompany.INSERT;
          UNTIL NEXT = 0;

      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

