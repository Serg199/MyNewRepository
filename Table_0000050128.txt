OBJECT Table 50128 Default Dimension Advanced
{
  OBJECT-PROPERTIES
  {
    Date=30.06.17;
    Time=15:33:28;
    Modified=Yes;
    Version List=NOS-26;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Table ID","Source No.","No.","Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Table ID","Source No.","No.","Dimension Value Code");
               FillLinkedDimensions("Table ID","Source No.","No.","Dimension Code","Dimension Value Code");
             END;

    OnModify=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Table ID","Source No.","No.","Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Table ID","Source No.","No.","Dimension Value Code");
               FillLinkedDimensions("Table ID","Source No.","No.","Dimension Code","Dimension Value Code");
             END;

    OnDelete=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Table ID","Source No.","No.",'');
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Table ID","Source No.","No.",'');
             END;

    LookupPageID=Page50204;
    DrillDownPageID=Page50204;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                TempAllObjWithCaption@1101495000 : TEMPORARY Record 2000000058;
                                                              BEGIN
                                                                //NC NOS-26 > ENK
                                                                {
                                                                CALCFIELDS("Table Name");
                                                                DimMgt.SetupObjectNoList(TempObject);
                                                                TempObject.Type := TempObject.Type::Table;
                                                                TempObject.ID := "Table ID";
                                                                IF NOT TempObject.FIND THEN
                                                                FIELDERROR("Table ID");
                                                                }
                                                                CALCFIELDS("Table Name");
                                                                DimMgt.SetupObjectNoList(TempAllObjWithCaption);
                                                                TempAllObjWithCaption."Object Type" := TempAllObjWithCaption."Object Type"::Table;
                                                                TempAllObjWithCaption."Object ID" := "Table ID";
                                                                IF NOT TempAllObjWithCaption.FIND THEN
                                                                  FIELDERROR("Table ID");
                                                                //NC NOS-26 < ENK
                                                              END;

                                                   OnLookup=VAR
                                                              TempAllObjWithCaption@1101495000 : TEMPORARY Record 2000000058;
                                                            BEGIN
                                                              //NC NOS-26 > ENK
                                                              {
                                                              CLEAR(TempObject);
                                                              DimMgt.SetupObjectNoList(TempObject);
                                                              IF FORM.RUNMODAL(358,TempObject) = ACTION::LookupOK THEN BEGIN
                                                                "Table ID" := TempObject.ID;
                                                                 VALIDATE("Table ID");
                                                              END;
                                                              }
                                                              CLEAR(TempAllObjWithCaption);
                                                              DimMgt.SetupObjectNoList(TempAllObjWithCaption);
                                                              IF PAGE.RUNMODAL(PAGE::Objects,TempAllObjWithCaption) = ACTION::LookupOK THEN BEGIN
                                                                "Table ID" := TempAllObjWithCaption."Object ID";
                                                                VALIDATE("Table ID");
                                                              END;
                                                              //NC NOS-26 < ENK
                                                            END;

                                                   CaptionML=[ENU=Table ID;
                                                              RUS=Таблица ID];
                                                   NotBlank=Yes }
    { 3   ;   ;Source No.          ;Code20        ;TableRelation=IF (Table ID=CONST(14901)) "Vendor Agreement"."Vendor No."
                                                                 ELSE IF (Table ID=CONST(14902)) "Customer Agreement"."Customer No.";
                                                   CaptionML=[ENU=Source No.;
                                                              RUS=Источник Но.] }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF (Table ID=CONST(14901)) "Vendor Agreement".No.
                                                                 ELSE IF (Table ID=CONST(14902)) "Customer Agreement".No.;
                                                   CaptionML=[ENU=No.;
                                                              RUS=Но.] }
    { 5   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDim("Dimension Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=[ENU=Dimension Code;
                                                              RUS=Код Измерения];
                                                   NotBlank=Yes }
    { 6   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDimValue("Dimension Code","Dimension Value Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=[ENU=Dimension Value Code;
                                                              RUS=Код Значения Измерения] }
    { 7   ;   ;Value Posting       ;Option        ;CaptionML=[ENU=Value Posting;
                                                              RUS=Учет Значения];
                                                   OptionCaptionML=[ENU=" ,Code Mandatory,Same Code,No Code";
                                                                    RUS=" ,Код Обязателен,Единый Код,Нет Кода"];
                                                   OptionString=[ ,Code Mandatory,Same Code,No Code] }
    { 8   ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              RUS=Название Таблицы];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Table ID,Source No.,No.,Dimension Code  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimMgt@1101495000 : Codeunit 408;
      GLSetup@1101495001 : Record 98;

    PROCEDURE GetCaption@3() : Text[250];
    VAR
      ObjTransl@1000 : Record 377;
      CurrTableID@1001 : Integer;
      NewTableID@1002 : Integer;
      NewNo@1003 : Code[20];
      SourceTableName@1004 : Text[100];
    BEGIN
      IF NOT EVALUATE(NewTableID,GETFILTER("Table ID")) THEN
        EXIT('');

      IF NewTableID = 0 THEN
        IF GETRANGEMIN("Table ID") = GETRANGEMAX("Table ID") THEN
          NewTableID := GETRANGEMIN("Table ID")
        ELSE
          NewTableID := 0;

      IF NewTableID <> CurrTableID THEN
        SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,NewTableID);
      CurrTableID := NewTableID;

      IF GETFILTER("No.") <> '' THEN
        IF GETRANGEMIN("No.") = GETRANGEMAX("No.") THEN
          NewNo := GETRANGEMIN("No.")
        ELSE
          NewNo := '';

      IF NewTableID <> 0 THEN
        EXIT(STRSUBSTNO('%1 %2',SourceTableName,NewNo));

      EXIT('');
    END;

    LOCAL PROCEDURE UpdateGlobalDimCode@25(GlobalDimCodeNo@1000 : Integer;"Table ID"@1001 : Integer;SourceNo@1101495000 : Code[20];No@1002 : Code[20];NewDimValue@1003 : Code[20]);
    VAR
      CustAgrmt@1210001 : Record 14902;
      VendAgrmt@1210002 : Record 14901;
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Customer Agreement": BEGIN
          IF CustAgrmt.GET(SourceNo,No) THEN BEGIN
            CASE GlobalDimCodeNo OF
              1:CustAgrmt."Global Dimension 1 Code" := NewDimValue;
              2:CustAgrmt."Global Dimension 2 Code" := NewDimValue;
            END;
            CustAgrmt.MODIFY(TRUE);
          END;
        END;
        DATABASE::"Vendor Agreement": BEGIN
          IF VendAgrmt.GET(SourceNo,No) THEN BEGIN
            CASE GlobalDimCodeNo OF
              1:VendAgrmt."Global Dimension 1 Code" := NewDimValue;
              2:VendAgrmt."Global Dimension 2 Code" := NewDimValue;
            END;
            VendAgrmt.MODIFY(TRUE);
          END;
        END;
      END;
    END;

    PROCEDURE FillLinkedDimensions@1000000000(TableID@1000000000 : Integer;SourceNo@1101495000 : Code[20];No@1000000001 : Code[20];DimCode@1000000002 : Code[20];DimValue@1000000003 : Code[20]) i : Integer;
    VAR
      DimensionLinkedValues@1000000004 : Record 50016;
      DefDimensionAdv@1210000 : Record 50128;
    BEGIN
      // Add linked dimensions
      DimensionLinkedValues.SETRANGE("Dimension Code",DimCode);
      DimensionLinkedValues.SETRANGE("Dimension Value",DimValue);
      DimensionLinkedValues.SETFILTER(Blocked,'%1',FALSE);
      DimensionLinkedValues.SETFILTER("Value Selection",'%1',TRUE);
      IF DimensionLinkedValues.FIND('-') THEN
        REPEAT
          DefDimensionAdv.SETRANGE("Table ID",TableID);
          DefDimensionAdv.SETRANGE("Source No.",SourceNo);
          DefDimensionAdv.SETRANGE("No.",No);
          DefDimensionAdv.SETRANGE("Dimension Code",DimensionLinkedValues."Linked Dimension Code");
          IF DimensionLinkedValues."Linked Dimension Value"<>'' THEN
            DefDimensionAdv.SETRANGE("Dimension Value Code",DimensionLinkedValues."Linked Dimension Value");
          IF NOT DefDimensionAdv.FIND('-') THEN BEGIN
            DefDimensionAdv.INIT;
            DefDimensionAdv."Table ID" := TableID;
            DefDimensionAdv."No." := No;
            DefDimensionAdv."Source No." := SourceNo;
            DefDimensionAdv."Dimension Code" := DimensionLinkedValues."Linked Dimension Code";
            IF DimensionLinkedValues."Linked Dimension Value"<>'' THEN
              DefDimensionAdv."Dimension Value Code" := DimensionLinkedValues."Linked Dimension Value";
            IF DefDimensionAdv.INSERT THEN
              IF DefDimensionAdv."Dimension Value Code"<>'' THEN BEGIN
                GLSetup.GET;
                IF DefDimensionAdv."Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                  UpdateGlobalDimCode(1,TableID,SourceNo,No,DefDimensionAdv."Dimension Value Code");
                IF DefDimensionAdv."Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                  UpdateGlobalDimCode(2,TableID,SourceNo,No,DefDimensionAdv."Dimension Value Code");
              END;
          END;

        UNTIL DimensionLinkedValues.NEXT=0;
    END;

    BEGIN
    {
      NC OAM31 PCH
      -----
      NC NOS-26 ENK
        Change record TempObject from table "Object" to table "AllObjWithCaption".
    }
    END.
  }
}

