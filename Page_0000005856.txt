OBJECT Page 5856 Get Post.Doc - P.RcptLn Sbfrm
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Lines;
               RUS=Строки];
    SaveValues=Yes;
    LinksAllowed=No;
    SourceTable=Table121;
    PageType=ListPart;
    OnFindRecord=BEGIN
                   IF NOT Visible THEN
                     EXIT(FALSE);

                   IF FIND(Which) THEN BEGIN
                     PurchRcptLine := Rec;
                     WHILE TRUE DO BEGIN
                       ShowRec := IsShowRec(Rec);
                       IF ShowRec THEN
                         EXIT(TRUE);
                       IF NEXT(1) = 0 THEN BEGIN
                         Rec := PurchRcptLine;
                         IF FIND(Which) THEN
                           WHILE TRUE DO BEGIN
                             ShowRec := IsShowRec(Rec);
                             IF ShowRec THEN
                               EXIT(TRUE);
                             IF NEXT(-1) = 0 THEN
                               EXIT(FALSE);
                           END;
                       END;
                     END;
                   END;
                   EXIT(FALSE);
                 END;

    OnNextRecord=VAR
                   RealSteps@1001 : Integer;
                   NextSteps@1000 : Integer;
                 BEGIN
                   IF Steps = 0 THEN
                     EXIT;

                   PurchRcptLine := Rec;
                   REPEAT
                     NextSteps := NEXT(Steps / ABS(Steps));
                     ShowRec := IsShowRec(Rec);
                     IF ShowRec THEN BEGIN
                       RealSteps := RealSteps + NextSteps;
                       PurchRcptLine := Rec;
                     END;
                   UNTIL (NextSteps = 0) OR (RealSteps = Steps);
                   Rec := PurchRcptLine;
                   FIND;
                   EXIT(RealSteps);
                 END;

    OnAfterGetRecord=BEGIN
                       DocumentNoHideValue := FALSE;
                       DocumentNoOnFormat;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1907935204;1 ;ActionGroup;
                      CaptionML=[ENU=&Line;
                                 RUS=&Строка];
                      Image=Line }
      { 1902759804;2 ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[ENU=Show Document;
                                 RUS=Показать документ];
                      Image=View;
                      OnAction=BEGIN
                                 ShowDocument;
                               END;
                                }
      { 1900206204;2 ;Action    ;
                      AccessByPermission=TableData 348=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=[ENU=Dimensions;
                                 RUS=Измерения];
                      ToolTipML=[ENU=View or edit dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                                 RUS=Просмотр или изменение измерений, например области, проекта или отдела, которые можно назначать документам покупки и продажи для распределения себестоимости и анализа истории транзакций.];
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                               END;
                                }
      { 1901652604;2 ;Action    ;
                      ShortCutKey=Shift+Ctrl+I;
                      CaptionML=[ENU=Item &Tracking Lines;
                                 RUS=Строки трассир&овки товаров];
                      Image=ItemTrackingLines;
                      OnAction=BEGIN
                                 ItemTrackingLines;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Lookup=No;
                ToolTipML=[ENU=Specifies the receipt number.;
                           RUS=Определяет номер приходной накладной.];
                SourceExpr="Document No.";
                HideValue=DocumentNoHideValue;
                StyleExpr='Strong' }

    { 78  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the date the items were expected.;
                           RUS=Определяет дату, когда ожидались товары.];
                SourceExpr="Expected Receipt Date" }

    { 50  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the vendor who invoiced the shipment.;
                           RUS=Определяет номер поставщика, который выставил счет по отгрузке.];
                SourceExpr="Pay-to Vendor No.";
                Visible=FALSE }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the vendor that you bought the items from.;
                           RUS=Определяет номер поставщика, у которого куплены товары.];
                SourceExpr="Buy-from Vendor No.";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the line type.;
                           RUS=Определяет тип строки.];
                SourceExpr=Type }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies an item number that identifies the item or a general ledger account number for the general ledger account used when posting.;
                           RUS=Задает номенклатурный номер, определяющий товар, или номер счета главной книги, определяющий счет главной книги, который использовался при учете.];
                SourceExpr="No." }

    { 28  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the cross-reference number related to the item.;
                           RUS=Определяет номер перекрестной ссылки, связанной с товаром.];
                SourceExpr="Cross-Reference No.";
                Visible=FALSE }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the variant code for the item.;
                           RUS=Определяет код варианта товара.];
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 42  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies that this item is a nonstock item.;
                           RUS=Определяет, что этот товар является нескладируемым.];
                SourceExpr=Nonstock;
                Visible=FALSE }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies either the name of or a description of the item or general ledger account.;
                           RUS=Определяет название или описание товара или счета главной книги.];
                SourceExpr=Description }

    { 44  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies a code that explains why the item is returned.;
                           RUS=Определяет код, объясняющий, почему товар возвращен.];
                SourceExpr="Return Reason Code";
                Visible=FALSE }

    { 48  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the location where the receipt line is registered.;
                           RUS=Определяет код склада, в котором зарегистрирована строка приходной накладной.];
                SourceExpr="Location Code";
                Visible=FALSE }

    { 54  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the bin code for the purchased items.;
                           RUS=Определяет код ячейки для приобретаемых товаров.];
                SourceExpr="Bin Code";
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the dimension value associated with the receipt.;
                           RUS=Определяет код значения измерения, связанный с приходной накладной.];
                SourceExpr="Shortcut Dimension 1 Code";
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the dimension value associated with the receipt.;
                           RUS=Определяет код значения измерения, связанный с приходной накладной.];
                SourceExpr="Shortcut Dimension 2 Code";
                Visible=FALSE }

    { 58  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the unit of measure code for the item.;
                           RUS=Определяет код единицы измерения товара.];
                SourceExpr="Unit of Measure Code" }

    { 60  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of units of the item specified on the line.;
                           RUS=Определяет количество единиц товара, указанное в строке.];
                SourceExpr=Quantity }

    { 26  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity of the returned item that has been posted as received but that has not yet been posted as invoiced.;
                           RUS=Определяет количество единиц возвращенного товара, которое было учтено как полученное, но которое еще не учтено как количество, по которому выставлен счет.];
                SourceExpr="Qty. Rcd. Not Invoiced" }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Remaining Quantity;
                           RUS=Остаток];
                ToolTipML=[ENU=Specifies the quantity from the posted document line that remains in inventory.;
                           RUS=Определяет количество из строки учтенного документа, которое остается в запасах.];
                DecimalPlaces=0:5;
                SourceExpr=RemainingQty }

    { 22  ;2   ;Field     ;
                CaptionML=[ENU=Applied Quantity;
                           RUS=Примененное количество];
                ToolTipML=[ENU=Specifies the quantity of the item in the line that has been used for outbound transactions.;
                           RUS=Определяет количество товара в строке, которое было использовано в исходящих транзакциях.];
                DecimalPlaces=0:5;
                SourceExpr=CalcAppliedQty }

    { 64  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the unit of measure for the item (bottle or piece, for example).;
                           RUS=Определяет единицу измерения товара (например, бутылка или штука).];
                SourceExpr="Unit of Measure";
                Visible=FALSE }

    { 66  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the cost, in LCY, of one unit of the item on the line.;
                           RUS=Определяет себестоимость (в рублях) единицы товара в строке.];
                SourceExpr="Unit Cost (LCY)";
                Visible=FALSE }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Reverse Unit Cost (LCY);
                           RUS=Затраты на возврат единицы (руб.)];
                ToolTipML=[ENU=Specifies the unit cost that will appear on the new document lines.;
                           RUS=Определяет себестоимость единицы, которая будет отображаться в новых строках документа.];
                SourceExpr=RevUnitCostLCY;
                AutoFormatType=2;
                Visible=FALSE }

    { 72  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the job number corresponding to the purchase document (quote, order, invoice, or credit memo).;
                           RUS=Определяет номер работы, относящейся к документу покупки (предложению, заказу, счету или кредит-ноте).];
                SourceExpr="Job No.";
                Visible=FALSE }

    { 80  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the blanket order that the purchase receipt originates from.;
                           RUS=Определяет номер общего заказа, на основе которого создается приходная накладная покупки.];
                SourceExpr="Blanket Order No.";
                Visible=FALSE }

    { 82  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the blanket order line that the purchase receipt line originates from.;
                           RUS=Определяет номер строки общего заказа, на основе которой создается строка приходной накладной покупки.];
                SourceExpr="Blanket Order Line No.";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      PurchRcptLine@1003 : Record 121;
      TempPurchRcptLine@1002 : TEMPORARY Record 121;
      RemainingQty@1004 : Decimal;
      RevUnitCostLCY@1000 : Decimal;
      RevQtyFilter@1005 : Boolean;
      FillExactCostReverse@1007 : Boolean;
      Visible@1010 : Boolean;
      ShowRec@1001 : Boolean;
      DocumentNoHideValue@19020538 : Boolean INDATASET;

    LOCAL PROCEDURE IsFirstDocLine@2() : Boolean;
    VAR
      PurchRcptLine2@1000 : Record 121;
      RemainingQty2@1001 : Decimal;
      RevUnitCostLCY2@1002 : Decimal;
    BEGIN
      TempPurchRcptLine.RESET;
      TempPurchRcptLine.COPYFILTERS(Rec);
      TempPurchRcptLine.SETRANGE("Document No.","Document No.");
      IF NOT TempPurchRcptLine.FINDFIRST THEN BEGIN
        RemainingQty2 := RemainingQty;
        RevUnitCostLCY2 := RevUnitCostLCY;
        PurchRcptLine2.COPYFILTERS(Rec);
        PurchRcptLine2.SETRANGE("Document No.","Document No.");
        IF NOT PurchRcptLine2.FINDSET THEN
          EXIT(FALSE);
        REPEAT
          ShowRec := IsShowRec(PurchRcptLine2);
          IF ShowRec THEN BEGIN
            TempPurchRcptLine := PurchRcptLine2;
            TempPurchRcptLine.INSERT;
          END;
        UNTIL (PurchRcptLine2.NEXT = 0) OR ShowRec;
        RemainingQty := RemainingQty2;
        RevUnitCostLCY := RevUnitCostLCY2;
      END;

      EXIT("Line No." = TempPurchRcptLine."Line No.");
    END;

    LOCAL PROCEDURE IsShowRec@3(PurchRcptLine2@1000 : Record 121) : Boolean;
    BEGIN
      WITH PurchRcptLine2 DO BEGIN
        RemainingQty := 0;
        IF RevQtyFilter AND (Type = Type::" ") THEN
          EXIT("Attached to Line No." = 0);
        IF Type <> Type::Item THEN
          EXIT(TRUE);
        IF ("Job No." <> '') OR ("Prod. Order No." <> '') THEN
          EXIT(NOT RevQtyFilter);
        CalcReceivedPurchNotReturned(RemainingQty,RevUnitCostLCY,FillExactCostReverse);
        IF NOT RevQtyFilter THEN
          EXIT(TRUE);
        EXIT(RemainingQty > 0);
      END;
    END;

    LOCAL PROCEDURE CalcAppliedQty@6() : Decimal;
    BEGIN
      IF (Type = Type::Item) AND (Quantity - RemainingQty > 0) THEN
        EXIT(Quantity - RemainingQty);
      EXIT(0);
    END;

    PROCEDURE Initialize@4(NewRevQtyFilter@1000 : Boolean;NewFillExactCostReverse@1001 : Boolean;NewVisible@1003 : Boolean);
    BEGIN
      RevQtyFilter := NewRevQtyFilter;
      FillExactCostReverse := NewFillExactCostReverse;
      Visible := NewVisible;

      IF Visible THEN BEGIN
        TempPurchRcptLine.RESET;
        TempPurchRcptLine.DELETEALL;
      END;
    END;

    PROCEDURE GetSelectedLine@1(VAR FromPurchRcptLine@1000 : Record 121);
    BEGIN
      FromPurchRcptLine.COPY(Rec);
      CurrPage.SETSELECTIONFILTER(FromPurchRcptLine);
    END;

    LOCAL PROCEDURE ShowDocument@5();
    VAR
      PurchRcptHeader@1000 : Record 120;
    BEGIN
      IF NOT PurchRcptHeader.GET("Document No.") THEN
        EXIT;
      PAGE.RUN(PAGE::"Posted Purchase Receipt",PurchRcptHeader);
    END;

    LOCAL PROCEDURE ItemTrackingLines@8();
    VAR
      FromPurchRcptLine@1000 : Record 121;
    BEGIN
      GetSelectedLine(FromPurchRcptLine);
      FromPurchRcptLine.ShowItemTrackingLines;
    END;

    LOCAL PROCEDURE DocumentNoOnFormat@19001080();
    BEGIN
      IF NOT IsFirstDocLine THEN
        DocumentNoHideValue := TRUE;
    END;

    BEGIN
    END.
  }
}

