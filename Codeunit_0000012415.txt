OBJECT Codeunit 12415 EmployeeVendor-Update
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVRU6.00.01.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Vend@1002 : Record 23;
      Text001@1470001 : TextConst 'ENU=%1 No %2 already exists.;RUS=%1 номер %2 уже существует.';

    PROCEDURE OnInsert@3(VAR Employee@1000 : Record 5200);
    BEGIN
      WITH Vend DO BEGIN
        IF GET(Employee."No.") THEN
          ERROR(Text001,FORMAT("Vendor Type"),Employee."No.");

        INIT;
        "No." := Employee."No.";
        "Vendor Type" := "Vendor Type"::"Resp. Employee";
        EmployeeToVendor(Employee,Vend);
        INSERT;
      END;
    END;

    PROCEDURE OnModify@1(VAR Employee@1000 : Record 5200);
    BEGIN
      Vend.SETRANGE("Employee No.",Employee."No.");
      IF Vend.FINDSET THEN
        REPEAT
          Vend.TESTFIELD("Vendor Type",Vend."Vendor Type"::"Resp. Employee");
          EmployeeToVendor(Employee,Vend);
          Vend.MODIFY(TRUE);
        UNTIL Vend.NEXT = 0;
    END;

    LOCAL PROCEDURE EmployeeToVendor@1470000(VAR Employee@1470000 : Record 5200;VAR Vendor@1470001 : Record 23);
    VAR
      PurchaseSetup@1470002 : Record 312;
    BEGIN
      WITH Vendor DO BEGIN
        VALIDATE(Name,Employee."Short Name");
        "Country/Region Code" := Employee."Country/Region Code";
        Address := Employee.Address;
        "Address 2" := Employee."Address 2";
        City := Employee.City;
        "Phone No." := Employee."Phone No.";
        "Fax No." := Employee."Fax No.";
        "Post Code" := Employee."Post Code";
        "E-Mail" := Employee."E-Mail";
        "Employee No." := Employee."No.";
        PurchaseSetup.GET;
        "Gen. Bus. Posting Group" := PurchaseSetup."Adv. Stmt. Gen.Bus. Posting Gr";
        "VAT Bus. Posting Group" := PurchaseSetup."Adv. Stmt. VAT Bus. Posting Gr";
        "Vendor Posting Group" := PurchaseSetup."Adv. Stmt. Vendor Posting Gr.";
      END;
    END;

    PROCEDURE OnRename@1210000(OldEmployeeNo@1210000 : Code[20];NewEmployeeNo@1210001 : Code[20]);
    BEGIN
      IF Vend.GET(OldEmployeeNo) THEN BEGIN
        Vend.TESTFIELD("Vendor Type",Vend."Vendor Type"::"Resp. Employee");
        Vend.RENAME(NewEmployeeNo);
      END;
    END;

    BEGIN
    END.
  }
}

