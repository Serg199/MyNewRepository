OBJECT Page 9901 Export Data
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Export Data;
               RUS=Экспортировать данные];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table2000000006;
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    OnOpenPage=VAR
                 Company@1000 : Record 2000000006;
               BEGIN
                 IncludeApplication := FALSE;
                 IncludeApplicationData := FALSE;
                 IncludeGlobalData := TRUE;
                 IncludeAllCompanies := TRUE;

                 IF Company.FINDSET THEN
                   REPEAT
                     Rec := Company;
                     INSERT;
                   UNTIL Company.NEXT = 0;

                 MarkAll;
               END;

    OnAfterGetRecord=BEGIN
                       Selected := SelectedCompany.GET(Name);
                     END;

    OnQueryClosePage=BEGIN
                       FileName := Description;
                       IF CloseAction = ACTION::OK THEN BEGIN
                         IF EXPORTDATA(
                              TRUE,
                              FileName,
                              Description,
                              IncludeApplication,
                              IncludeApplicationData,
                              IncludeGlobalData,
                              SelectedCompany)
                         THEN BEGIN
                           MESSAGE(CompletedMsg);
                           EXIT(TRUE)
                         END;
                         EXIT(FALSE)
                       END;

                       EXIT(TRUE);
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           RUS=Общее];
                GroupType=Group }

    { 3   ;2   ;Field     ;
                CaptionML=[ENU=Description;
                           RUS=Описание];
                ToolTipML=[ENU=Specifies a description of the data to be exported.;
                           RUS=Определяет описание данных, которые должны быть экспортированы.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description }

    { 11  ;2   ;Group     ;
                CaptionML=[ENU=Export;
                           RUS=Экспорт];
                GroupType=Group }

    { 12  ;3   ;Field     ;
                CaptionML=[ENU=All Companies;
                           RUS=Все организации];
                ToolTipML=[ENU=Specifies that data in all the companies will be imported into the database.;
                           RUS=Указывает, что данные во всех организациях будут импортированы в базу данных.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=IncludeAllCompanies;
                OnValidate=BEGIN
                             MarkAll;
                           END;
                            }

    { 6   ;3   ;Field     ;
                CaptionML=[ENU=Global Data;
                           RUS=Глобальные данные];
                ToolTipML=[ENU=Specifies that data that is common to all companies will be exported from the database. This includes the report list, user IDs, and printer selections, but no company-specific business data.;
                           RUS=Указывает, что данные, являющиеся общими для всех организаций, будут экспортированы из базы данных. К ним относятся список отчетов, коды пользователей и выбранные принтеры, но не относятся бизнес данные о конкретных организациях.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=IncludeGlobalData }

    { 4   ;3   ;Field     ;
                CaptionML=[ENU=Application Data;
                           RUS=Данные применения];
                ToolTipML=[ENU=Specifies that the data that defines the application in the database is exported. This includes the permissions, permission sets, profiles, and style sheets.;
                           RUS=Указывает, что данные, которые определяют приложение в базе данных, экспортируются. Они включают разрешения, наборы разрешений, профили и таблицы стилей.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=IncludeApplicationData }

    { 5   ;3   ;Field     ;
                CaptionML=[ENU=Application;
                           RUS=Применение];
                ToolTipML=[ENU=Specifies that all application objects are exported. Data is not included. This is similar to exporting all objects to an .fob file.;
                           RUS=Определяет, что все объекты приложения экспортируются. Данные не включаются. Это похоже на экспорт всех объектов в .fob-файл.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=IncludeApplication }

    { 8   ;2   ;Group     ;
                GroupType=Repeater }

    { 10  ;3   ;Field     ;
                CaptionML=[ENU=Export;
                           RUS=Экспорт];
                ToolTipML=[ENU=Specifies that data is exported.;
                           RUS=Указывает, что данные экспортируются.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Selected;
                OnValidate=BEGIN
                             IF Selected THEN BEGIN
                               SelectedCompany := Rec;
                               IF SelectedCompany.INSERT THEN;
                             END ELSE BEGIN
                               IncludeAllCompanies := FALSE;
                               IF SelectedCompany.GET(Name) THEN
                                 SelectedCompany.DELETE;
                             END;
                           END;
                            }

    { 9   ;3   ;Field     ;
                Width=30;
                CaptionML=[ENU=Company Name;
                           RUS=Название организации];
                ToolTipML=[ENU=Specifies the name of a company that has been created in the current database.;
                           RUS=Определяет название компании, которая была создана в текущей базе данных.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Name }

  }
  CODE
  {
    VAR
      SelectedCompany@1002 : TEMPORARY Record 2000000006;
      FileName@1007 : Text;
      Description@1000 : Text;
      IncludeApplication@1003 : Boolean;
      IncludeApplicationData@1004 : Boolean;
      IncludeGlobalData@1006 : Boolean;
      IncludeAllCompanies@1005 : Boolean;
      Selected@1001 : Boolean;
      CompletedMsg@1008 : TextConst 'ENU=The data was exported successfully.;RUS=Данные успешно экспортированы.';

    LOCAL PROCEDURE MarkAll@1();
    BEGIN
      SelectedCompany.DELETEALL;
      IF IncludeAllCompanies THEN BEGIN
        IF FINDSET THEN
          REPEAT
            SelectedCompany := Rec;
            SelectedCompany.INSERT;
          UNTIL NEXT = 0;
      END;

      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

