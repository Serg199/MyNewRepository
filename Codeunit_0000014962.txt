OBJECT Codeunit 14962 PayrlAViewEntryToPayrlEntries
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PayrollAnalysisView@1000 : Record 14965;
      GLSetup@1001 : Record 98;
      DimSetEntry@1210000 : Record 480;

    PROCEDURE GetPayrollEntries@1(VAR PayrollAnalysisViewEntry@1000 : Record 14966;VAR TempPayrollLedgerEntry@1001 : Record 17418);
    VAR
      PayrollLedgerEntry@1002 : Record 17418;
      PayrollAnalysisViewFilter@1004 : Record 14967;
      UpdatePayrollAnalysisView@1210000 : Codeunit 14961;
      StartDate@1007 : Date;
      EndDate@1008 : Date;
      GlobalDimValue@1009 : Code[20];
    BEGIN
      PayrollAnalysisView.GET(PayrollAnalysisViewEntry."Analysis View Code");

      GLSetup.GET;

      StartDate := PayrollAnalysisViewEntry."Posting Date";
      EndDate := StartDate;

      WITH PayrollAnalysisView DO
        IF StartDate < "Starting Date" THEN
          StartDate := 0D
        ELSE
          IF (PayrollAnalysisViewEntry."Posting Date" = NORMALDATE(PayrollAnalysisViewEntry."Posting Date")) AND
             NOT ("Date Compression" IN ["Date Compression"::None,"Date Compression"::Day])
          THEN
            CASE "Date Compression" OF
              "Date Compression"::Week:
                EndDate := PayrollAnalysisViewEntry."Posting Date" + 6;
              "Date Compression"::Month:
                EndDate := CALCDATE('<+1M-1D>',PayrollAnalysisViewEntry."Posting Date");
              "Date Compression"::Quarter:
                EndDate := CALCDATE('<+3M-1D>',PayrollAnalysisViewEntry."Posting Date");
              "Date Compression"::Year:
                EndDate := CALCDATE('<+1Y-1D>',PayrollAnalysisViewEntry."Posting Date");
            END;

      WITH PayrollLedgerEntry DO BEGIN
        SETCURRENTKEY("Employee No.");
        SETRANGE("Employee No.",PayrollAnalysisViewEntry."Employee No.");
        SETRANGE("Element Code",PayrollAnalysisViewEntry."Element Code");
        SETRANGE("Posting Date",StartDate,EndDate);
        SETRANGE("Entry No.",0,PayrollAnalysisView."Last Entry No.");

        IF GetGlobalDimValue(GLSetup."Global Dimension 1 Code",PayrollAnalysisViewEntry,GlobalDimValue) THEN
          SETRANGE("Global Dimension 1 Code",GlobalDimValue)
        ELSE
          IF PayrollAnalysisViewFilter.GET(
               PayrollAnalysisViewEntry."Analysis View Code",
               GLSetup."Global Dimension 1 Code")
          THEN
            SETFILTER("Global Dimension 1 Code",PayrollAnalysisViewFilter."Dimension Value Filter");

        IF GetGlobalDimValue(GLSetup."Global Dimension 2 Code",PayrollAnalysisViewEntry,GlobalDimValue) THEN
          SETRANGE("Global Dimension 2 Code",GlobalDimValue)
        ELSE
          IF PayrollAnalysisViewFilter.GET(
               PayrollAnalysisViewEntry."Analysis View Code",
               GLSetup."Global Dimension 2 Code")
          THEN
            SETFILTER("Global Dimension 2 Code",PayrollAnalysisViewFilter."Dimension Value Filter");

        IF FINDSET THEN
          REPEAT
            IF DimEntryOK("Dimension Set ID",PayrollAnalysisView."Dimension 1 Code",PayrollAnalysisViewEntry."Dimension 1 Value Code") AND
               DimEntryOK("Dimension Set ID",PayrollAnalysisView."Dimension 2 Code",PayrollAnalysisViewEntry."Dimension 2 Value Code") AND
               DimEntryOK("Dimension Set ID",PayrollAnalysisView."Dimension 3 Code",PayrollAnalysisViewEntry."Dimension 3 Value Code") AND
               DimEntryOK("Dimension Set ID",PayrollAnalysisView."Dimension 4 Code",PayrollAnalysisViewEntry."Dimension 4 Value Code") AND
               UpdatePayrollAnalysisView.DimSetIDInFilter("Dimension Set ID",PayrollAnalysisView)
            THEN BEGIN
              TempPayrollLedgerEntry := PayrollLedgerEntry;
              IF TempPayrollLedgerEntry.INSERT THEN;
            END;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE DimEntryOK@10(DimSetID@1000 : Integer;Dim@1001 : Code[20];DimValue@1002 : Code[20]) : Boolean;
    BEGIN
      IF Dim = '' THEN
        EXIT(TRUE);

      IF DimSetEntry.GET(DimSetID,Dim) THEN
        EXIT(DimSetEntry."Dimension Value Code" = DimValue);

      EXIT(DimValue = '');
    END;

    LOCAL PROCEDURE GetGlobalDimValue@2(GlobalDim@1000 : Code[20];VAR PayrollAnalysisViewEntry@1001 : Record 14966;VAR GlobalDimValue@1002 : Code[20]) : Boolean;
    VAR
      IsGlobalDim@1003 : Boolean;
    BEGIN
      CASE GlobalDim OF
        PayrollAnalysisView."Dimension 1 Code":
          BEGIN
            IsGlobalDim := TRUE;
            GlobalDimValue := PayrollAnalysisViewEntry."Dimension 1 Value Code";
          END;
        PayrollAnalysisView."Dimension 2 Code":
          BEGIN
            IsGlobalDim := TRUE;
            GlobalDimValue := PayrollAnalysisViewEntry."Dimension 2 Value Code";
          END;
        PayrollAnalysisView."Dimension 3 Code":
          BEGIN
            IsGlobalDim := TRUE;
            GlobalDimValue := PayrollAnalysisViewEntry."Dimension 3 Value Code";
          END;
        PayrollAnalysisView."Dimension 4 Code":
          BEGIN
            IsGlobalDim := TRUE;
            GlobalDimValue := PayrollAnalysisViewEntry."Dimension 4 Value Code";
          END;
      END;
      EXIT(IsGlobalDim);
    END;

    BEGIN
    END.
  }
}

