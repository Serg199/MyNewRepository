OBJECT Table 99000786 Routing Version
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Version Code" = '' THEN BEGIN
                 RoutingHeader.GET("Routing No.");
                 RoutingHeader.TESTFIELD("Version Nos.");
                 NoSeriesMgt.InitSeries(RoutingHeader."Version Nos.",xRec."No. Series",0D,VersionCode,"No. Series");
                 IF STRLEN(VersionCode) > MAXSTRLEN("Version Code") THEN
                   ERROR(Text000,
                     FIELDCAPTION("Version Code"),
                     NoSeriesLine.FIELDCAPTION("Starting No."),
                     RoutingHeader."Version Nos.",
                     NoSeries.TABLECAPTION,
                     MAXSTRLEN("Version Code"));

                 "Version Code" := VersionCode;
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               RtngLine@1000 : Record 99000764;
             BEGIN
               RtngLine.LOCKTABLE;
               RtngLine.SETRANGE("Routing No.","Routing No.");
               RtngLine.SETRANGE("Version Code","Version Code");
               RtngLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               IF Status = Status::Certified THEN
                 ERROR(Text001,TABLECAPTION,FIELDCAPTION(Status),FORMAT(Status));
             END;

    CaptionML=[ENU=Routing Version;
               RUS=Версия маршрута];
    LookupPageID=Page99000808;
    DrillDownPageID=Page99000808;
  }
  FIELDS
  {
    { 1   ;   ;Routing No.         ;Code20        ;TableRelation="Routing Header";
                                                   CaptionML=[ENU=Routing No.;
                                                              RUS=Номер маршрута];
                                                   NotBlank=Yes }
    { 2   ;   ;Version Code        ;Code20        ;CaptionML=[ENU=Version Code;
                                                              RUS=Код версии] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 10  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              RUS=Дата начала] }
    { 20  ;   ;Status              ;Option        ;OnValidate=VAR
                                                                RtngHeader@1000 : Record 99000763;
                                                              BEGIN
                                                                IF (Status <> xRec.Status) AND (Status = Status::Certified) THEN BEGIN
                                                                  RtngHeader.GET("Routing No.");
                                                                  CheckRouting.Calculate(RtngHeader,"Version Code");
                                                                END;
                                                                MODIFY(TRUE);
                                                                COMMIT;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              RUS=Статус];
                                                   OptionCaptionML=[ENU=New,Certified,Under Development,Closed;
                                                                    RUS=Новый,Сертифицирован,В разработке,Закрыт];
                                                   OptionString=New,Certified,Under Development,Closed }
    { 21  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Status = Status::Certified THEN
                                                                  FIELDERROR(Status);
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              RUS=Тип];
                                                   OptionCaptionML=[ENU=Serial,Parallel;
                                                                    RUS=Последовательный,Параллельный];
                                                   OptionString=Serial,Parallel }
    { 22  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              RUS=Дата последнего изменения];
                                                   Editable=No }
    { 50  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              RUS=Серия номеров];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Routing No.,Version Code                ;Clustered=Yes }
    {    ;Routing No.,Starting Date                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoSeries@1007 : Record 308;
      NoSeriesLine@1006 : Record 309;
      RoutingHeader@1000 : Record 99000763;
      RtngVersion@1001 : Record 99000786;
      CheckRouting@1002 : Codeunit 99000752;
      NoSeriesMgt@1003 : Codeunit 396;
      Text000@1008 : TextConst 'ENU=The new %1 cannot be generated by default\because the %2 for %3 %4 contains more than %5 characters.;RUS=Новый %1 не может быть сгенерирован по умолчанию\потому что %2 для %3 %4 содержит более %5 символов.';
      VersionCode@1005 : Code[20];
      Text001@1004 : TextConst 'ENU=You cannot rename the %1 when %2 is %3.;RUS="Вы не можете переименовать %1, когда %2 = %3."';

    PROCEDURE AssistEdit@2(OldRoutVersion@1000 : Record 99000786) : Boolean;
    BEGIN
      WITH RtngVersion DO BEGIN
        RtngVersion := Rec;
        RoutingHeader.GET("Routing No.");
        RoutingHeader.TESTFIELD("Version Nos.");
        IF NoSeriesMgt.SelectSeries(RoutingHeader."Version Nos.",OldRoutVersion."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries(VersionCode);
          IF STRLEN(VersionCode) > MAXSTRLEN("Version Code") THEN
            ERROR(Text000,
              FIELDCAPTION("Version Code"),
              NoSeriesLine.FIELDCAPTION("Starting No."),
              RoutingHeader."Version Nos.",
              NoSeries.TABLECAPTION,
              MAXSTRLEN("Version Code"));

          "Version Code" := VersionCode;
          Rec := RtngVersion;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE Caption@1() : Text[100];
    VAR
      RtngHeader@1000 : Record 99000763;
    BEGIN
      IF GETFILTERS = '' THEN
        EXIT('');

      IF "Routing No." = '' THEN
        EXIT('');

      RtngHeader.GET("Routing No.");
      EXIT(
        STRSUBSTNO(
          '%1 %2 %3',RtngHeader."No.",RtngHeader.Description,"Version Code"));
    END;

    BEGIN
    END.
  }
}

