OBJECT Page 17337 FA Tax Differences
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=FA Tax Differences;
               RUS=Налоговые разницы по ОС];
    SourceTable=Table5600;
    PageType=Worksheet;
    ShowFilter=Yes;
    OnOpenPage=BEGIN
                 FASetup.GET;
                 FASetup.TESTFIELD("Release Depr. Book");
                 TaxRegisterSetup.GET;
                 TaxRegisterSetup.TESTFIELD("Tax Depreciation Book");
                 DeprBookFilter := GetDeprBookFilter;
                 FindPeriod('');
               END;

    OnAfterGetRecord=BEGIN
                       CalcAmounts;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210008 ;1   ;Action    ;
                      CaptionML=[ENU=Previous Period;
                                 RUS=Предыдущий период];
                      ToolTipML=[ENU=Previous Period;
                                 RUS=Предыдущий период];
                      ApplicationArea=#FixedAssets;
                      Promoted=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FindPeriod('<=');
                               END;
                                }
      { 1210007 ;1   ;Action    ;
                      CaptionML=[ENU=Next Period;
                                 RUS=Следующий период];
                      ToolTipML=[ENU=Next Period;
                                 RUS=Следующий период];
                      ApplicationArea=#FixedAssets;
                      Promoted=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FindPeriod('>=');
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1903121901;1;Group  ;
                CaptionML=[ENU=Options;
                           RUS=Параметры] }

    { 1210014;2;Field     ;
                CaptionML=[ENU=View by;
                           RUS=Просмотр по];
                ToolTipML=[ENU=Day;
                           RUS=День];
                OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                                 RUS=День,Неделя,Месяц,Квартал,Год,Учетный период];
                ApplicationArea=#FixedAssets;
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             FindPeriod('');
                           END;
                            }

    { 1210000;1;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 1210001;2;Field     ;
                ApplicationArea=#FixedAssets;
                SourceExpr="No." }

    { 1210003;2;Field     ;
                ApplicationArea=#FixedAssets;
                SourceExpr=Description }

    { 1210018;2;Field     ;
                CaptionML=[ENU=Amount (Base);
                           RUS=Сумма (БУ)];
                ApplicationArea=#FixedAssets;
                SourceExpr=AmountBase;
                OnDrillDown=BEGIN
                              FALedgerEntry.RESET;
                              FALedgerEntry.FILTERGROUP(2);
                              FALedgerEntry.SETRANGE("FA No.","No.");
                              FALedgerEntry.SETRANGE("Depreciation Book Code",FASetup."Release Depr. Book");
                              COPYFILTER("Date Filter",FALedgerEntry."FA Posting Date");
                              FALedgerEntry.SETFILTER(
                                "FA Posting Type",
                                '%1|%2|%3',
                                FALedgerEntry."FA Posting Type"::"Acquisition Cost",
                                FALedgerEntry."FA Posting Type"::Appreciation,
                                FALedgerEntry."FA Posting Type"::"Salvage Value");
                              FALedgerEntry.FILTERGROUP(0);
                              PAGE.RUNMODAL(0,FALedgerEntry);
                            END;
                             }

    { 1210020;2;Field     ;
                CaptionML=[ENU=Amount (Tax);
                           RUS=Сумма (НУ)];
                ApplicationArea=#FixedAssets;
                SourceExpr=AmountTax;
                OnDrillDown=BEGIN
                              FALedgerEntry.RESET;
                              FALedgerEntry.FILTERGROUP(2);
                              FALedgerEntry.SETRANGE("FA No.","No.");
                              FALedgerEntry.SETRANGE("Depreciation Book Code",TaxRegisterSetup."Tax Depreciation Book");
                              COPYFILTER("Date Filter",FALedgerEntry."FA Posting Date");
                              FALedgerEntry.SETFILTER(
                                "FA Posting Type",
                                '%1|%2|%3',
                                FALedgerEntry."FA Posting Type"::"Acquisition Cost",
                                FALedgerEntry."FA Posting Type"::Appreciation,
                                FALedgerEntry."FA Posting Type"::"Salvage Value");
                              FALedgerEntry.FILTERGROUP(0);
                              PAGE.RUNMODAL(0,FALedgerEntry);
                            END;
                             }

    { 1210022;2;Field     ;
                CaptionML=[ENU=Difference Amount;
                           RUS=Сумма разниц];
                ApplicationArea=#FixedAssets;
                SourceExpr=DifferenceAmount }

    { 1210024;2;Field     ;
                CaptionML=[ENU=Difference Amount (Calc.);
                           RUS=Сумма разниц (расчет)];
                ApplicationArea=#FixedAssets;
                SourceExpr=DifferenceAmountCalc;
                OnDrillDown=BEGIN
                              FALedgerEntry.RESET;
                              FALedgerEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date",Reversed,"Tax Difference Code");
                              FALedgerEntry.FILTERGROUP(2);
                              FALedgerEntry.SETRANGE("FA No.","No.");
                              COPYFILTER("Date Filter",FALedgerEntry."FA Posting Date");
                              FALedgerEntry.SETFILTER("Depreciation Book Code",DeprBookFilter);
                              FALedgerEntry.SETRANGE(Reversed,FALSE);
                              FALedgerEntry.SETFILTER("Tax Difference Code",'<>%1',TaxRegisterSetup."Default FA TD Code");
                              FALedgerEntry.FILTERGROUP(0);
                              PAGE.RUNMODAL(0,FALedgerEntry);
                            END;
                             }

    { 1210028;2;Field     ;
                CaptionML=[ENU=Is Difference;
                           RUS=Является разницей];
                ApplicationArea=#FixedAssets;
                SourceExpr=IsDifference }

  }
  CODE
  {
    VAR
      FASetup@1210006 : Record 5603;
      FALedgerEntry@1210008 : Record 5601;
      DepreciationBook@1210011 : Record 5611;
      FADepreciationBook@1210009 : Record 5612;
      TaxRegisterSetup@1210007 : Record 17236;
      PeriodType@1210000 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      AmountBase@1210001 : Decimal;
      AmountTax@1210004 : Decimal;
      DifferenceAmount@1210002 : Decimal;
      DifferenceAmountCalc@1210003 : Decimal;
      IsDifference@1210005 : Boolean;
      DeprBookFilter@1210010 : Code[250];

    LOCAL PROCEDURE FindPeriod@2(SearchText@1000 : Code[10]);
    VAR
      Calendar@1001 : Record 2000000007;
      PeriodFormMgt@1002 : Codeunit 359;
    BEGIN
      IF GETFILTER("Date Filter") <> '' THEN BEGIN
        Calendar.SETFILTER("Period Start",GETFILTER("Date Filter"));
        IF NOT PeriodFormMgt.FindDate('+',Calendar,PeriodType) THEN
          PeriodFormMgt.FindDate('+',Calendar,PeriodType::Day);
        Calendar.SETRANGE("Period Start");
      END;
      PeriodFormMgt.FindDate(SearchText,Calendar,PeriodType);
      SETRANGE("Date Filter",Calendar."Period Start",Calendar."Period End");
      IF GETRANGEMIN("Date Filter") = GETRANGEMAX("Date Filter") THEN
        SETRANGE("Date Filter",GETRANGEMIN("Date Filter"));
    END;

    PROCEDURE CalcAmounts@1210003();
    BEGIN
      AmountBase := 0;
      AmountTax := 0;
      DifferenceAmount := 0;
      DifferenceAmountCalc := 0;
      IsDifference := FALSE;

      IF FADepreciationBook.GET("No.",FASetup."Release Depr. Book") THEN BEGIN
        COPYFILTER("Date Filter",FADepreciationBook."FA Posting Date Filter");
        FADepreciationBook.CALCFIELDS("Acquisition Cost",Appreciation,"Salvage Value");
        AmountBase :=
          FADepreciationBook."Acquisition Cost" +
          FADepreciationBook.Appreciation +
          FADepreciationBook."Salvage Value";
      END;

      IF FADepreciationBook.GET("No.",TaxRegisterSetup."Tax Depreciation Book") THEN BEGIN
        COPYFILTER("Date Filter",FADepreciationBook."FA Posting Date Filter");
        FADepreciationBook.CALCFIELDS("Acquisition Cost");
        AmountTax :=
          FADepreciationBook."Acquisition Cost" +
          FADepreciationBook.Appreciation +
          FADepreciationBook."Salvage Value";
      END;

      DifferenceAmount := AmountBase - AmountTax;

      FALedgerEntry.RESET;
      FALedgerEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date",Reversed,"Tax Difference Code");
      FALedgerEntry.SETRANGE("FA No.","No.");
      COPYFILTER("Date Filter",FALedgerEntry."FA Posting Date");
      FALedgerEntry.SETFILTER("Depreciation Book Code",DeprBookFilter);
      FALedgerEntry.SETRANGE(Reversed,FALSE);
      FALedgerEntry.SETFILTER("Tax Difference Code",'<>%1',TaxRegisterSetup."Default FA TD Code");
      FALedgerEntry.CALCSUMS(Amount);
      DifferenceAmountCalc := FALedgerEntry.Amount;

      IsDifference := DifferenceAmount <> DifferenceAmountCalc;
    END;

    PROCEDURE GetDeprBookFilter@1210011() Filter : Code[250];
    BEGIN
      DepreciationBook.SETRANGE("Control FA Acquis. Cost",TRUE);
      IF DepreciationBook.FIND('-') THEN
        REPEAT
          IF Filter = '' THEN
            Filter := DepreciationBook.Code
          ELSE
            Filter := Filter + '|' + DepreciationBook.Code;
        UNTIL DepreciationBook.NEXT = 0;
    END;

    LOCAL PROCEDURE DateFilterOnAfterValidate@19006009();
    BEGIN
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

