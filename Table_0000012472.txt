OBJECT Table 12472 Posted FA Doc. Line
{
  OBJECT-PROPERTIES
  {
    Date=09.09.14;
    Time=12:00:00;
    Version List=NAVRU8.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Posted FA Doc. Line;
               RUS=Строка учт. документа ОС];
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              RUS=Тип документа];
                                                   OptionCaptionML=[ENU=Writeoff,Release,Movement;
                                                                    RUS=Списание,Ввод в эксплуатацию,Перемещение];
                                                   OptionString=Writeoff,Release,Movement }
    { 2   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              RUS=Номер документа] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              RUS=Номер строки] }
    { 4   ;   ;FA No.              ;Code20        ;TableRelation="Fixed Asset";
                                                   CaptionML=[ENU=FA No.;
                                                              RUS=Номер ОС] }
    { 5   ;   ;New FA No.          ;Code20        ;CaptionML=[ENU=New FA No.;
                                                              RUS=Новый номер ОС] }
    { 6   ;   ;FA Posting Date     ;Date          ;CaptionML=[ENU=FA Posting Date;
                                                              RUS=Дата учета ОС] }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              RUS=Дата учета] }
    { 8   ;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   CaptionML=[ENU=Depreciation Book Code;
                                                              RUS=Код книги амортизации] }
    { 12  ;   ;External Document No.;Code35       ;CaptionML=[ENU=External Document No.;
                                                              RUS=Номер внешнего документа] }
    { 13  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 14  ;   ;Amount              ;Decimal       ;CaptionML=[ENU=Amount;
                                                              RUS=Сумма];
                                                   AutoFormatType=1 }
    { 15  ;   ;Book Value          ;Decimal       ;CaptionML=[ENU=Book Value;
                                                              RUS=Балансовая стоимость] }
    { 16  ;   ;Value %             ;Decimal       ;CaptionML=[ENU=Value %;
                                                              RUS=Значение %] }
    { 18  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              RUS=Кол-во];
                                                   DecimalPlaces=0:5 }
    { 24  ;   ;FA Posting Group    ;Code10        ;TableRelation="FA Posting Group";
                                                   CaptionML=[ENU=FA Posting Group;
                                                              RUS=Учетная группа ОС] }
    { 27  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              RUS=Код ярлыка измерения 1];
                                                   CaptionClass='1,2,1' }
    { 28  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              RUS=Код ярлыка измерения 2];
                                                   CaptionClass='1,2,2' }
    { 30  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              RUS=Статус];
                                                   OptionCaptionML=[ENU=" ,Montage,Operation,Maintenance,Repair";
                                                                    RUS=" ,Монтаж,Эксплуатация,Обслуживание,Ремонт"];
                                                   OptionString=[ ,Montage,Operation,Maintenance,Repair] }
    { 36  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              RUS=Код причины] }
    { 44  ;   ;FA Location Code    ;Code10        ;TableRelation="FA Location";
                                                   CaptionML=[ENU=FA Location Code;
                                                              RUS=Код местонахождения ОС] }
    { 45  ;   ;FA Employee No.     ;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=FA Employee No.;
                                                              RUS=Материал.-ответств. лицо] }
    { 46  ;   ;New Depreciation Book Code;Code10  ;CaptionML=[ENU=New Depreciation Book Code;
                                                              RUS=Новый код книги амортизации] }
    { 50  ;   ;Item Receipt No.    ;Code20        ;TableRelation="Item Document Header".No. WHERE (Document Type=CONST(Receipt));
                                                   CaptionML=[ENU=Item Receipt No.;
                                                              RUS=Номер акта оприходования товаров] }
    { 51  ;   ;Canceled            ;Boolean       ;CaptionML=[ENU=Canceled;
                                                              RUS=Отменено] }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              RUS=Код набора измерений];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Document Type,Document No.,Line No.     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimMgt@1210000 : Codeunit 408;
      Text14700@1210001 : TextConst 'ENU=The selected FA Movement Act lines will be canceled.;RUS=Выбранные строки акта перемещения ОС будут отменены.';
      Text14701@1210002 : TextConst 'ENU=FA Movement between Depreciation Books must be canceled using standard Cancel Entries function.;RUS=Перемещение ОС между книгами амортизации должно быть отменено с помощью стандартной функции отмены записей.';
      Text14702@1210003 : TextConst 'ENU=FA Movement must be last operation.;RUS=Перемещение ОС должно быть последней операцией.';
      Text14703@1210004 : TextConst 'ENU=FA Movement Act lines have been canceled.;RUS=Строки акта перемещения ОС отменены.';
      Text14704@1210005 : TextConst 'ENU=FA Movement Act line %1 is already canceled.;RUS=Строка акта перемещения ОС %1 уже отменена.';

    PROCEDURE ShowDimensions@25();
    BEGIN
      DimMgt.ShowDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2 %3',TABLECAPTION,"Document No.","Line No."));
    END;

    PROCEDURE ShowComments@1470003();
    VAR
      PostedFAComment@1210001 : Record 12479;
      PostedFAComments@1470000 : Page 12496;
    BEGIN
      TESTFIELD("Document No.");
      TESTFIELD("Line No.");
      PostedFAComment.SETRANGE("Document Type","Document Type");
      PostedFAComment.SETRANGE("Document No.","Document No.");
      PostedFAComment.SETRANGE("Document Line No.","Line No.");
      PostedFAComments.SETTABLEVIEW(PostedFAComment);
      PostedFAComments.RUNMODAL;
    END;

    PROCEDURE GetFAComments@1470000(VAR Comment@1470000 : ARRAY [5] OF Text[80];Type@1470001 : Integer);
    VAR
      PostedFAComment@1470003 : Record 12479;
      Index@1210000 : Integer;
    BEGIN
      CLEAR(Comment);
      Index := 0;
      PostedFAComment.RESET;
      PostedFAComment.SETCURRENTKEY("Document Type","Document No.","Document Line No.",Type);
      PostedFAComment.SETRANGE("Document Type","Document Type");
      PostedFAComment.SETRANGE("Document No.","Document No.");
      PostedFAComment.SETRANGE("Document Line No.","Line No.");
      PostedFAComment.SETRANGE(Type,Type);
      IF PostedFAComment.FINDSET THEN
        REPEAT
          Index += 1;
          Comment[Index] := PostedFAComment.Comment
        UNTIL (PostedFAComment.NEXT = 0) OR (Index = ARRAYLEN(Comment));
    END;

    PROCEDURE CancelFALocationMovement@1210002(VAR PstdFADocLine@1210000 : Record 12472);
    VAR
      FALedgEntry@1210002 : Record 5601;
      FA@1210001 : Record 5600;
      FADocPost@1210003 : Codeunit 12471;
      LedgEntriesCanceled@1210004 : Boolean;
    BEGIN
      IF NOT CONFIRM(Text14700,FALSE) THEN
        EXIT;
      IF PstdFADocLine.FINDSET THEN
        REPEAT
          IF PstdFADocLine.Canceled THEN
            ERROR(Text14704,PstdFADocLine."Line No.");
          IF PstdFADocLine."Depreciation Book Code" <> PstdFADocLine."New Depreciation Book Code" THEN
            ERROR(Text14701);
          IF FADocPost.FoundLateEntries(
               PstdFADocLine."FA No.",
               PstdFADocLine."Depreciation Book Code",
               PstdFADocLine."Posting Date")
          THEN
            ERROR(Text14702);
          FALedgEntry.SETCURRENTKEY("FA No.","Entry No.","Document No.");
          FALedgEntry.SETRANGE("Document No.",PstdFADocLine."Document No.");
          FALedgEntry.SETFILTER("FA No.",PstdFADocLine."FA No.");
          IF FALedgEntry.FINDFIRST THEN BEGIN
            FA.GET(PstdFADocLine."FA No.");
            FA.VALIDATE("FA Location Code",FALedgEntry."FA Location Code");
            FA.VALIDATE("Responsible Employee",FALedgEntry."Employee No.");
            FA.MODIFY(TRUE);
          END;
          FALedgEntry.MODIFYALL(Correction,TRUE);
          FALedgEntry.MODIFYALL("Canceled from FA No.",PstdFADocLine."FA No.");
          FALedgEntry.MODIFYALL("FA No.",'');
          PstdFADocLine.Canceled := TRUE;
          PstdFADocLine.MODIFY;
          LedgEntriesCanceled := TRUE;
        UNTIL PstdFADocLine.NEXT = 0;
      IF LedgEntriesCanceled THEN BEGIN
        MESSAGE(Text14703);
      END;
    END;

    BEGIN
    END.
  }
}

