OBJECT Page 17384 Organization Structure
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Organization Structure;
               RUS=Организационная структура];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table17371;
    SourceTableView=SORTING(ID);
    PageType=Worksheet;
    SourceTableTemporary=Yes;
    OnOpenPage=VAR
                 Position2@1000 : Record 17370;
               BEGIN
                 IF CurrentPositionNo = '' THEN BEGIN
                   Position2.RESET;
                   Position2.SETCURRENTKEY("Parent Position No.");
                   Position2.SETRANGE("Parent Position No.",'');
                   Position2.FINDFIRST;
                   CurrentPositionNo := Position2."No.";
                 END;

                 InitTempTable;
                 ExpandAll;
               END;

    OnAfterGetRecord=BEGIN
                       Position.GET("Position No.");
                       Position.CALCFIELDS("Organization Size","Filled Rate");
                       IF Position.Rate - Position."Filled Rate" = 0 THEN
                         PositionAvailability := 1
                       ELSE
                         PositionAvailability := 0;

                       EmployeeNames := '';
                       Employee.RESET;
                       Employee.SETCURRENTKEY("Position No.");
                       Employee.SETRANGE("Position No.","Position No.");
                       IF Employee.FINDSET THEN
                         REPEAT
                           IF EmployeeNames = '' THEN
                             EmployeeNames := Employee.GetFullName
                           ELSE
                             EmployeeNames := COPYSTR(EmployeeNames + ';' + Employee.GetFullName,1,MAXSTRLEN(EmployeeNames));
                         UNTIL Employee.NEXT = 0;

                       IF EmployeeNames = '' THEN
                         EmployeeNames := Text14700;

                       EmployeeNamesOnFormat;
                     END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 21      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Position;
                                 RUS=&Позиция] }
      { 22      ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[ENU=Card;
                                 RUS=Карточка];
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page 17380;
                      RunPageLink=No.=FIELD(Position No.);
                      Image=EditLines }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 43      ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 RUS=Ф&ункции];
                      Image=Action }
      { 46      ;2   ;Action    ;
                      CaptionML=[ENU=Exp&and/Collapse;
                                 RUS=Рас&крыть/свернуть];
                      ApplicationArea=#Basic,#Suite;
                      Image=ImportExport;
                      OnAction=BEGIN
                                 ToggleExpandCollapse;
                               END;
                                }
      { 47      ;2   ;Action    ;
                      Name=Expand All;
                      CaptionML=[ENU=E&xpand All;
                                 RUS=Р&аскрыть все];
                      ApplicationArea=#Basic,#Suite;
                      Image=ExpandDepositLine;
                      OnAction=BEGIN
                                 ExpandAll;
                               END;
                                }
      { 48      ;2   ;Action    ;
                      Name=Collapse All;
                      CaptionML=[ENU=C&ollapse All;
                                 RUS=С&вернуть все];
                      ApplicationArea=#Basic,#Suite;
                      Image=CollapseDepositLines;
                      OnAction=BEGIN
                                 CollapseAll;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 38  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           RUS=Общее] }

    { 39  ;2   ;Field     ;
                CaptionML=[ENU=Reporting to;
                           RUS=Подчиняется];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CurrentPositionNo;
                TableRelation=Position;
                OnValidate=BEGIN
                             CurrentPositionNoOnAfterValida;
                           END;
                            }

    { 1   ;1   ;Group     ;
                IndentationColumnName=Level;
                IndentationControls=Position No.;
                ShowAsTree=Yes;
                GroupType=Repeater }

    { 1210014;2;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Position No." }

    { 1210016;2;Field     ;
                Name=EmployeeNames;
                CaptionML=[ENU=Employee Name(s);
                           RUS=Имя(имена) сотрудника(ов)];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=EmployeeNames;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=EmployeeNamesEmphasize;
                OnLookup=BEGIN
                           Employee.RESET;
                           Employee.SETCURRENTKEY("Position No.");
                           Employee.SETRANGE("Position No.","Position No.");
                           PAGE.RUN(0,Employee);
                         END;
                          }

    { 1210006;2;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Manager No.";
                Editable=FALSE }

    { 1210002;2;Field     ;
                CaptionML=[ENU=Org. Size;
                           RUS=Размер орг.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Position."Organization Size";
                Editable=FALSE }

    { 1210000;2;Field     ;
                CaptionML=[ENU=Job Title Code;
                           RUS=Код должности];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Position."Job Title Code";
                Editable=FALSE }

    { 1210010;2;Field     ;
                CaptionML=[ENU=Job Title Name;
                           RUS=Название должности];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Position."Job Title Name";
                Editable=FALSE }

    { 1210004;2;Field     ;
                CaptionML=[ENU=Org. Unit Code;
                           RUS=Код подразделения];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Position."Org. Unit Code";
                Editable=FALSE }

    { 1210012;2;Field     ;
                CaptionML=[ENU=Org. Unit Name;
                           RUS=Название подразделения];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Position."Org. Unit Name";
                Editable=FALSE }

    { 1210026;2;Field     ;
                CaptionML=[ENU=Vacant;
                           RUS=Свободный];
                OptionCaptionML=[ENU=Integer;
                                 RUS=Целое];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PositionAvailability;
                Editable=False }

    { 1210022;2;Field     ;
                CaptionML=[ENU=Max. Rate;
                           RUS=Макс. ставка];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Position.Rate;
                Editable=FALSE }

    { 1210024;2;Field     ;
                CaptionML=[ENU=Filled Rate;
                           RUS=Занятые ставки];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Position."Filled Rate";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Employee@1210002 : Record 5200;
      Position@1210003 : Record 17370;
      ViewBuffer@1210005 : TEMPORARY Record 17371;
      CurrentPositionNo@1004 : Code[20];
      CurrentLevel@1210007 : Integer;
      PositionAvailability@1210004 : Integer;
      EmployeeNames@1210001 : Text[250];
      Text14700@1210006 : TextConst 'ENU=Open Position;RUS=Открытая позиция';
      EmployeeNamesEmphasize@19042413 : Boolean INDATASET;

    LOCAL PROCEDURE InitTempTable@4();
    VAR
      Position2@1210000 : Record 17370;
    BEGIN
      ViewBuffer.DELETEALL;
      ViewBuffer.RESET;

      Position2.GET(CurrentPositionNo);
      CurrentLevel := Position2.Level;
      ViewBuffer.ID := 1;
      ViewBuffer."Position No." := CurrentPositionNo;
      ViewBuffer."Manager No." := Position2."Parent Position No.";
      ViewBuffer.Hide := FALSE;
      ViewBuffer.Expanded := FALSE;
      ViewBuffer.Level := Position2.Level - CurrentLevel;
      ViewBuffer.INSERT;

      EnlistChildren(CurrentPositionNo);
      UpdateView(1);
    END;

    PROCEDURE EnlistChildren@1210000(ReportingTo@1210000 : Code[20]);
    VAR
      Position2@1210002 : Record 17370;
    BEGIN
      Position2.RESET;
      Position2.SETCURRENTKEY("Parent Position No.");
      Position2.SETRANGE("Parent Position No.",ReportingTo);
      IF Position2.FINDSET THEN
        REPEAT
          ViewBuffer.ID := ViewBuffer.ID + 1;
          ViewBuffer."Position No." := Position2."No.";
          ViewBuffer."Manager No." := Position2."Parent Position No.";
          ViewBuffer.Hide := TRUE;
          ViewBuffer.Expanded := FALSE;
          ViewBuffer.Level := Position2.Level - CurrentLevel;
          ViewBuffer.INSERT;
          Position2.CALCFIELDS("Organization Size");
          IF Position2."Organization Size" > 0 THEN
            EnlistChildren(Position2."No.")
        UNTIL Position2.NEXT = 0;
    END;

    PROCEDURE UpdateView@1210002(ID@1210000 : Integer);
    BEGIN
      DELETEALL;
      IF ViewBuffer.FINDFIRST THEN
        REPEAT
          IF NOT ViewBuffer.Hide THEN BEGIN
            Rec := ViewBuffer;
            INSERT;
          END;
        UNTIL ViewBuffer.NEXT = 0;
      GET(ID);
    END;

    LOCAL PROCEDURE ExpandAll@12();
    VAR
      Position@1000 : Record 17370;
    BEGIN
      ViewBuffer.MODIFYALL(Hide,FALSE);
      IF ViewBuffer.FINDFIRST THEN
        REPEAT
          IF NOT ViewBuffer.Expanded THEN BEGIN
            Position.GET("Position No.");
            Position.CALCFIELDS("Organization Size");
            IF Position."Organization Size" > 0 THEN BEGIN
              ViewBuffer.Expanded := TRUE;
              ViewBuffer.MODIFY;
            END;
          END;
        UNTIL ViewBuffer.NEXT = 0;

      UpdateView(1);
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE CollapseAll@1210001();
    BEGIN
      InitTempTable;
    END;

    PROCEDURE CollapseChildren@1210003(PositionNo@1210000 : Code[20]);
    VAR
      CurrViewBufferElement@1210001 : TEMPORARY Record 17371;
    BEGIN
      ViewBuffer.SETRANGE("Manager No.",PositionNo);
      IF ViewBuffer.FINDSET THEN
        REPEAT
          IF NOT ViewBuffer.Hide THEN BEGIN
            ViewBuffer.Hide := TRUE;
            IF ViewBuffer.Expanded THEN
              ViewBuffer.Expanded := FALSE;
            ViewBuffer.MODIFY;
            CurrViewBufferElement := ViewBuffer;
            CollapseChildren(ViewBuffer."Position No.");
            ViewBuffer := CurrViewBufferElement;
          END;
        UNTIL ViewBuffer.NEXT = 0;
    END;

    LOCAL PROCEDURE ToggleExpandCollapse@1();
    BEGIN
      IF Expanded THEN BEGIN // Collapse one level
        ViewBuffer.GET(ID);
        ViewBuffer.Expanded := FALSE;
        ViewBuffer.MODIFY;
        CollapseChildren(ViewBuffer."Position No.");
      END ELSE BEGIN // Expand one level
        ViewBuffer.RESET;
        ViewBuffer.SETCURRENTKEY("Manager No.");
        ViewBuffer.SETRANGE("Manager No.","Position No.");
        IF ViewBuffer.FINDSET THEN BEGIN
          REPEAT
            IF ViewBuffer.Hide THEN BEGIN
              ViewBuffer.Hide := FALSE;
              ViewBuffer.MODIFY;
            END;
          UNTIL ViewBuffer.NEXT = 0;
          ViewBuffer.GET(ID);
          ViewBuffer.Expanded := TRUE;
          ViewBuffer.MODIFY;
        END;
      END;
      ViewBuffer.SETRANGE("Manager No.");
      UpdateView(ID);
    END;

    LOCAL PROCEDURE CurrentPositionNoOnAfterValida@19075176();
    BEGIN
      InitTempTable;
      ExpandAll;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE EmployeeNamesOnFormat@19054873();
    BEGIN
      IF Position."Organization Size" > 0 THEN
        EmployeeNamesEmphasize := TRUE;
      IF EmployeeNames = Text14700 THEN;
    END;

    BEGIN
    END.
  }
}

