OBJECT Table 17203 Tax Register G/L Corr. Entry
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TaxRegGLCorrEntry.RESET;
               TaxRegGLCorrEntry.SETCURRENTKEY("Section Code","Entry No.");
               TaxRegGLCorrEntry.SETRANGE("Section Code","Section Code");
               IF TaxRegGLCorrEntry.FINDLAST THEN
                 "Entry No." := TaxRegGLCorrEntry."Entry No." + 1
               ELSE
                 "Entry No." := 1;
             END;

    OnDelete=BEGIN
               TaxRegDimCorrFilter.SETRANGE("Section Code","Section Code");
               TaxRegDimCorrFilter.SETRANGE("G/L Corr. Entry No.","Entry No.");
               TaxRegDimCorrFilter.DELETEALL;
             END;

    CaptionML=[ENU=Tax Register G/L Corr. Entry;
               RUS=Корр. операция ГК налог. регистра];
    LookupPageID=Page17214;
  }
  FIELDS
  {
    { 1   ;   ;Debit Account No.   ;Code20        ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Debit Account No.;
                                                              RUS=Номер дебетового счета] }
    { 2   ;   ;Credit Account No.  ;Code20        ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Credit Account No.;
                                                              RUS=Номер кредитового счета] }
    { 3   ;   ;Register Type       ;Option        ;CaptionML=[ENU=Register Type;
                                                              RUS=Тип регистра];
                                                   OptionCaptionML=[ENU=" ,Item,Payroll";
                                                                    RUS=" ,Товар,Зарплата"];
                                                   OptionString=[ ,Item,Payroll] }
    { 4   ;   ;Section Code        ;Code10        ;TableRelation="Tax Register Section";
                                                   CaptionML=[ENU=Section Code;
                                                              RUS=Код секции];
                                                   NotBlank=Yes }
    { 5   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              RUS=Номер операции];
                                                   Editable=No }
    { 6   ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              RUS=Дата начала] }
    { 7   ;   ;Ending Date         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              RUS=Дата окончания] }
    { 10  ;   ;Tax Register ID Totaling;Code61    ;CaptionML=[ENU=Tax Register ID Totaling;
                                                              RUS=Группировка кодов налоговых регистров] }
    { 11  ;   ;Where Used Register IDs;Code61     ;CaptionML=[ENU=Where Used Register IDs;
                                                              RUS=Коды регистра применимости] }
    { 21  ;   ;Debit Account Name  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("G/L Account".Name WHERE (No.=FIELD(Debit Account No.)));
                                                   CaptionML=[ENU=Debit Account Name;
                                                              RUS=Название дебетового счета];
                                                   Editable=No }
    { 22  ;   ;Credit Account Name ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("G/L Account".Name WHERE (No.=FIELD(Credit Account No.)));
                                                   CaptionML=[ENU=Credit Account Name;
                                                              RUS=Название кредитового счета];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Section Code,Debit Account No.,Credit Account No.,Register Type;
                                                   Clustered=Yes }
    {    ;Section Code,Entry No.                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TaxRegGLCorrEntry@1000 : Record 17203;
      TaxRegDimCorrFilter@1001 : Record 17219;

    PROCEDURE GetTaxRegName@1() Result : Text[150];
    VAR
      TaxReg@1000 : Record 17200;
    BEGIN
      IF "Where Used Register IDs" <> '' THEN BEGIN
        TaxReg.SETRANGE("Section Code","Section Code");
        TaxReg.SETFILTER("Register ID",CONVERTSTR(DELCHR("Where Used Register IDs",'<>','~'),'~','|'));
        IF TaxReg.FINDSET THEN BEGIN
          Result := TaxReg."No.";
          WHILE TaxReg.NEXT <> 0 DO
            Result := COPYSTR(STRSUBSTNO('%1, %2',Result,TaxReg."No."),1,MAXSTRLEN(Result));
          IF Result = TaxReg."No." THEN
            Result := COPYSTR(TaxReg.Description,1,MAXSTRLEN(Result));
        END;
      END;
    END;

    PROCEDURE LookupTaxRegName@2();
    VAR
      TaxReg@1000 : Record 17200;
    BEGIN
      TaxReg.FILTERGROUP(2);
      TaxReg.SETRANGE("Section Code","Section Code");
      TaxReg.SETFILTER("Register ID",CONVERTSTR(DELCHR("Where Used Register IDs",'<>','~'),'~','|'));
      TaxReg.FILTERGROUP(0);
      IF ACTION::OK = PAGE.RUNMODAL(0,TaxReg) THEN;
    END;

    PROCEDURE DrillDownTaxRegName@3();
    VAR
      TaxRegGLCorresDimFilters@1000 : Page 17242;
    BEGIN
      CLEAR(TaxRegGLCorresDimFilters);
      TaxRegGLCorresDimFilters.SetTaxRegGLCorr("Section Code","Entry No.");
      TaxRegGLCorresDimFilters.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

