OBJECT Report 93 Create Fiscal Year
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Fiscal Year;
               RUS=Создание фин. года];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  AccountingPeriod."Starting Date" := FiscalYearStartDate;
                  AccountingPeriod.TESTFIELD("Starting Date");

                  IF AccountingPeriod.FIND('-') THEN BEGIN
                    FirstPeriodStartDate := AccountingPeriod."Starting Date";
                    FirstPeriodLocked := AccountingPeriod."Date Locked";
                    IF (NOT HideDialog) AND (FiscalYearStartDate < FirstPeriodStartDate) AND FirstPeriodLocked THEN
                      IF NOT CONFIRM(CreateAndCloseQst) THEN
                        EXIT;
                    IF AccountingPeriod.FIND('+') THEN
                      LastPeriodStartDate := AccountingPeriod."Starting Date";
                  END ELSE
                    IF NOT HideDialog THEN
                      IF NOT CONFIRM(CreateQst) THEN
                        EXIT;

                  FiscalYearStartDate2 := FiscalYearStartDate;

                  FOR i := 1 TO NoOfPeriods + 1 DO BEGIN
                    IF (FiscalYearStartDate <= FirstPeriodStartDate) AND (i = NoOfPeriods + 1) THEN
                      EXIT;

                    IF FirstPeriodStartDate <> 0D THEN
                      IF (FiscalYearStartDate >= FirstPeriodStartDate) AND (FiscalYearStartDate < LastPeriodStartDate) THEN
                        ERROR(OnlyBeforeOrAfterErr);
                    AccountingPeriod.INIT;
                    AccountingPeriod."Starting Date" := FiscalYearStartDate;
                    AccountingPeriod.VALIDATE("Starting Date");
                    IF (i = 1) OR (i = NoOfPeriods + 1) THEN BEGIN
                      AccountingPeriod."New Fiscal Year" := TRUE;
                      InvtSetup.GET;
                      AccountingPeriod."Average Cost Calc. Type" := InvtSetup."Average Cost Calc. Type";
                      AccountingPeriod."Average Cost Period" := InvtSetup."Average Cost Period";
                    END;
                    IF (FirstPeriodStartDate = 0D) AND (i = 1) THEN
                      AccountingPeriod."Date Locked" := TRUE;
                    IF (AccountingPeriod."Starting Date" < FirstPeriodStartDate) AND FirstPeriodLocked THEN BEGIN
                      AccountingPeriod.Closed := TRUE;
                      AccountingPeriod."Date Locked" := TRUE;
                    END;
                    IF NOT AccountingPeriod.FIND('=') THEN
                      AccountingPeriod.INSERT;
                    FiscalYearStartDate := CALCDATE(PeriodLength,FiscalYearStartDate);
                  END;

                  AccountingPeriod.GET(FiscalYearStartDate2);
                  AccountingPeriod.UpdateAvgItems(0);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF NoOfPeriods = 0 THEN BEGIN
                     NoOfPeriods := 12;
                     EVALUATE(PeriodLength,'<1M>');
                   END;
                   IF AccountingPeriod.FIND('+') THEN
                     FiscalYearStartDate := AccountingPeriod."Starting Date";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[ENU=Starting Date;
                             RUS=Дата начала];
                  ToolTipML=[ENU=Specifies the date from which the report or batch job processes information.;
                             RUS=Определяет дату, начиная с которой информация будет обрабатываться отчетом или пакетным заданием.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=FiscalYearStartDate }

      { 2   ;2   ;Field     ;
                  Name=NoOfPeriods;
                  CaptionML=[ENU=No. of Periods;
                             RUS=Число периодов];
                  ToolTipML=[ENU=Specifies how many accounting periods to include.;
                             RUS=Определяет количество учетных периодов, которое следует включить.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=NoOfPeriods }

      { 3   ;2   ;Field     ;
                  Name=PeriodLength;
                  CaptionML=[ENU=Period Length;
                             RUS=Длина периода];
                  ToolTipML=[ENU=Specifies the length of each period, for example, enter "1M" for one month.;
                             RUS="Определяет длину каждого из периодов; например, ""1М"" означает один месяц."];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PeriodLength }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CreateAndCloseQst@1000 : TextConst 'ENU=The new fiscal year begins before an existing fiscal year, so the new year will be closed automatically.\\Do you want to create and close the fiscal year?;RUS=Новый финансовый год начинается перед существующим годом, поэтому новый год будет закрыт автоматически.\\Создать и закрыть финансовый год?';
      CreateQst@1002 : TextConst 'ENU=After you create the new fiscal year, you cannot change its starting date.\\Do you want to create the fiscal year?;RUS=После создания нового финансового года дату его начала изменить невозможно.\\Действительно создать финансовый год?';
      OnlyBeforeOrAfterErr@1004 : TextConst 'ENU=It is only possible to create new fiscal years before or after the existing ones.;RUS=Создать новый финансовый год можно только перед или после существующих годов.';
      AccountingPeriod@1005 : Record 50;
      InvtSetup@1013 : Record 313;
      NoOfPeriods@1006 : Integer;
      PeriodLength@1007 : DateFormula;
      FiscalYearStartDate@1008 : Date;
      FiscalYearStartDate2@1015 : Date;
      FirstPeriodStartDate@1009 : Date;
      LastPeriodStartDate@1010 : Date;
      FirstPeriodLocked@1011 : Boolean;
      i@1012 : Integer;
      HideDialog@1014 : Boolean;

    PROCEDURE InitializeRequest@1(NewNoOfPeriods@1000 : Integer;NewPeriodLength@1001 : DateFormula;StartingDate@1002 : Date);
    BEGIN
      NoOfPeriods := NewNoOfPeriods;
      PeriodLength := NewPeriodLength;
      IF AccountingPeriod.FINDLAST THEN
        FiscalYearStartDate := AccountingPeriod."Starting Date"
      ELSE
        FiscalYearStartDate := StartingDate;
    END;

    PROCEDURE HideConfirmationDialog@2(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

