OBJECT Codeunit 408 DimensionManagement
{
  OBJECT-PROPERTIES
  {
    Date=14.08.17;
    Time=13:41:38;
    Modified=Yes;
    Version List=NAVW110.00.00.15601,NAVRU10.00.00.15601,NOS-26;
  }
  PROPERTIES
  {
    Permissions=TableData 80=imd,
                TableData 232=imd,
                TableData 355=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Dimensions %1 and %2 can''t be used concurrently.;RUS=Измерения %1 и %2 нельзя использовать одновременно.';
      Text001@1001 : TextConst 'ENU=Dimension combinations %1 - %2 and %3 - %4 can''t be used concurrently.;RUS=Комбинации измерений %1 - %2 и %3 - %4 нельзя использовать одновременно.';
      Text002@1002 : TextConst 'ENU=This Shortcut Dimension is not defined in the %1.;RUS=Этот ярлык измерения не определен в %1.';
      Text003@1003 : TextConst 'ENU=%1 is not an available %2 for that dimension.;RUS=%1 не доступно %2 для этого измерения.';
      Text004@1004 : TextConst 'ENU=Select a %1 for the %2 %3.;RUS=Выберите %1 для %2 %3.';
      Text005@1005 : TextConst 'ENU=Select a %1 for the %2 %3 for %4 %5.;RUS=Выберите %1 для %2 %3 для %4 %5.';
      Text006@1006 : TextConst 'ENU=Select %1 %2 for the %3 %4.;RUS=Выберите %1 %2 для %3 %4.';
      Text007@1007 : TextConst 'ENU=Select %1 %2 for the %3 %4 for %5 %6.;RUS=Выберите %1 %2 для %3 %4 для %5 %6.';
      Text008@1008 : TextConst 'ENU=%1 %2 must be blank.;RUS=%1 %2 должно быть пусто.';
      Text009@1009 : TextConst 'ENU=%1 %2 must be blank for %3 %4.;RUS=%1 %2 должно быть пусто для %3 %4.';
      Text010@1010 : TextConst 'ENU=%1 %2 must not be mentioned.;RUS=%1 %2 не должно упоминаться.';
      Text011@1011 : TextConst 'ENU=%1 %2 must not be mentioned for %3 %4.;RUS=%1 %2 не должно упоминаться для %3 %4.';
      Text012@1012 : TextConst 'ENU=A %1 used in %2 has not been used in %3.;RUS=%1, использованное в %2, не использовано в %3.';
      Text013@1013 : TextConst 'ENU=%1 for %2 %3 is not the same in %4 and %5.;RUS=%1 для %2 %3 различны в %4 и %5.';
      Text014@1014 : TextConst 'ENU=%1 %2 is blocked.;RUS=%1 %2 заблокировано.';
      Text015@1015 : TextConst 'ENU=%1 %2 can''t be found.;RUS=Не удается найти %1 %2.';
      Text016@1016 : TextConst 'ENU=%1 %2 - %3 is blocked.;RUS=%1 %2 - %3 заблокировано.';
      Text017@1017 : TextConst 'ENU=%1 for %2 %3 - %4 must not be %5.;RUS=%1 для %2 %3 - %4 не должно быть %5.';
      Text018@1018 : TextConst 'ENU=%1 for %2 is missing.;RUS=%1 для %2 отсутствует.';
      Text019@1028 : TextConst 'ENU=You have changed a dimension.\\Do you want to update the lines?;RUS=Вы изменили измерение.\\Обновить строки?';
      TempDimBuf1@1019 : TEMPORARY Record 360;
      TempDimBuf2@1020 : TEMPORARY Record 360;
      ObjTransl@1027 : Record 377;
      DimValComb@1032 : Record 351;
      JobTaskDimTemp@1033 : TEMPORARY Record 1002;
      DefaultDim@1035 : Record 352;
      DimSetEntry@1034 : Record 480;
      TempDimSetEntry2@1029 : TEMPORARY Record 480;
      TempDimCombInitialized@1031 : Boolean;
      TempDimCombEmpty@1030 : Boolean;
      DimCombErr@1021 : Text[250];
      DimValuePostingErr@1022 : Text[250];
      DimErr@1023 : Text[250];
      DocDimConsistencyErr@1024 : Text[250];
      HasGotGLSetup@1025 : Boolean;
      GLSetupShortcutDimCode@1026 : ARRAY [20] OF Code[20];
      DimSetFilterCtr@1036 : Integer;
      LastDimSetIDInFilter@1037 : Integer;
      "NCV>"@1101495000 : Integer;
      gt_recTempDimBuf@1101495001 : TEMPORARY Record 360;
      TempTableErr@1101495002 : TextConst 'ENU=It''s necessary to use temporary table for the retrieving dimensions.;RUS=Для получения набора измерений необходимо использовать временную таблицу.';
      InsertGlobalDimension@1101495005 : Boolean;
      ConsSetup@1101495004 : Record 50000;
      g_recUserSetup@1101495003 : Record 91;
      "NCC>"@1101495012 : TextConst;
      Text50000@1101495011 : TextConst 'ENU=Define dimension value for %1;RUS=Не указано значение измерения %1';
      Text50001@1101495010 : TextConst 'ENU=Global Dimension Value %1 - %2 does not equal in %3 and %4.;RUS="Значение Глобального Измерения %1 - %2 не совпадает в %3 и в %4. "';
      Text50100@1101495014 : TextConst 'ENU=Not correct Dimension Value for Account %1;RUS=Недопустимое значение измерения для фин. счета %1';
      g_Text50002@1101495009 : TextConst 'ENU=For %1 %2 - %3 allowed posting from %4 to %5.;RUS=Для %1 %2 - %3 разрешен учет от %4 до %5.';
      g_Text50003@1101495008 : TextConst 'ENU=Dimension Value %1 %2 does not equal %3 %4. See  Accounts Setup %5.;RUS=Значение Измерения %1 %2 не равно %3 %4. См. Настройки Счета %5.';
      g_Text50004@1101495007 : TextConst 'ENU=Dimension Value %1 must be %2.;RUS=Значение Измерения %1 должно быть равно %2.';
      g_Text50005@1101495006 : TextConst 'ENU=Dimension %1 must be absent.;RUS=Измерение %1 должно отсутствовать на операции.';
      GlobalObject@1101495013 : Variant;

    PROCEDURE GetDimensionSetID@123(VAR DimSetEntry2@1000 : Record 480) : Integer;
    BEGIN
      EXIT(DimSetEntry.GetDimensionSetID(DimSetEntry2));
    END;

    PROCEDURE GetDimensionSet@124(VAR TempDimSetEntry@1000 : TEMPORARY Record 480;DimSetID@1001 : Integer);
    VAR
      DimSetEntry2@1002 : Record 480;
    BEGIN
      //NC NOS-26 > SPA
      // Так как по ссылке можно передать физическую таблицу,
      // важно удостовериться, что таблица действительно временная
      IF NOT TempDimSetEntry.ISTEMPORARY THEN
        ERROR(TempTableErr);
      //NC NOS-26 < SPA

      TempDimSetEntry.DELETEALL;
      WITH DimSetEntry2 DO BEGIN
        SETRANGE("Dimension Set ID",DimSetID);
        IF FINDSET THEN
          REPEAT
            TempDimSetEntry := DimSetEntry2;
            TempDimSetEntry.INSERT;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE ShowDimensionSet@125(DimSetID@1000 : Integer;NewCaption@1003 : Text[250]);
    VAR
      DimSetEntries@1002 : Page 479;
    BEGIN
      DimSetEntry.RESET;
      DimSetEntry.FILTERGROUP(2);
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      DimSetEntry.FILTERGROUP(0);
      DimSetEntries.SETTABLEVIEW(DimSetEntry);
      DimSetEntries.SetFormCaption(NewCaption);
      DimSetEntry.RESET;
      DimSetEntries.RUNMODAL;
    END;

    PROCEDURE EditDimensionSet@128(DimSetID@1000 : Integer;NewCaption@1003 : Text[250]) : Integer;
    VAR
      EditDimSetEntries@1002 : Page 480;
      NewDimSetID@1004 : Integer;
    BEGIN
      NewDimSetID := DimSetID;
      DimSetEntry.RESET;
      DimSetEntry.FILTERGROUP(2);
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      DimSetEntry.FILTERGROUP(0);
      EditDimSetEntries.SETTABLEVIEW(DimSetEntry);
      EditDimSetEntries.SetFormCaption(NewCaption);
      EditDimSetEntries.SetObjectFrom(GlobalObject); // NC NOS-26 SPA
      EditDimSetEntries.RUNMODAL;
      NewDimSetID := EditDimSetEntries.GetDimensionID;
      DimSetEntry.RESET;
      EXIT(NewDimSetID);
    END;

    PROCEDURE EditDimensionSet2@131(DimSetID@1000 : Integer;NewCaption@1003 : Text[250];VAR GlobalDimVal1@1006 : Code[20];VAR GlobalDimVal2@1005 : Code[20]) : Integer;
    VAR
      EditDimSetEntries@1002 : Page 480;
      NewDimSetID@1004 : Integer;
    BEGIN
      NewDimSetID := DimSetID;
      DimSetEntry.RESET;
      DimSetEntry.FILTERGROUP(2);
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      DimSetEntry.FILTERGROUP(0);
      EditDimSetEntries.SETTABLEVIEW(DimSetEntry);
      EditDimSetEntries.SetFormCaption(NewCaption);
      EditDimSetEntries.SetObjectFrom(GlobalObject); // NC NOS-26 SPA
      EditDimSetEntries.RUNMODAL;
      NewDimSetID := EditDimSetEntries.GetDimensionID;
      UpdateGlobalDimFromDimSetID(NewDimSetID,GlobalDimVal1,GlobalDimVal2);
      DimSetEntry.RESET;
      EXIT(NewDimSetID);
    END;

    PROCEDURE EditReclasDimensionSet2@133(VAR DimSetID@1001 : Integer;VAR NewDimSetID@1002 : Integer;NewCaption@1000 : Text[250];VAR GlobalDimVal1@1005 : Code[20];VAR GlobalDimVal2@1003 : Code[20];VAR NewGlobalDimVal1@1007 : Code[20];VAR NewGlobalDimVal2@1006 : Code[20]);
    VAR
      EditReclasDimensions@1004 : Page 484;
    BEGIN
      EditReclasDimensions.SetDimensionIDs(DimSetID,NewDimSetID);
      EditReclasDimensions.SetFormCaption(NewCaption);
      EditReclasDimensions.RUNMODAL;
      EditReclasDimensions.GetDimensionIDs(DimSetID,NewDimSetID);
      UpdateGlobalDimFromDimSetID(DimSetID,GlobalDimVal1,GlobalDimVal2);
      UpdateGlobalDimFromDimSetID(NewDimSetID,NewGlobalDimVal1,NewGlobalDimVal2);
    END;

    PROCEDURE UpdateGlobalDimFromDimSetID@130(DimSetID@1000 : Integer;VAR GlobalDimVal1@1001 : Code[20];VAR GlobalDimVal2@1002 : Code[20]);
    BEGIN
      GetGLSetup;
      GlobalDimVal1 := '';
      GlobalDimVal2 := '';
      IF GLSetupShortcutDimCode[1] <> '' THEN
        IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[1]) THEN
          GlobalDimVal1 := DimSetEntry."Dimension Value Code";
      IF GLSetupShortcutDimCode[2] <> '' THEN
        IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[2]) THEN
          GlobalDimVal2 := DimSetEntry."Dimension Value Code";
    END;

    PROCEDURE GetCombinedDimensionSetID@132(DimensionSetIDArr@1000 : ARRAY [10] OF Integer;VAR GlobalDimVal1@1004 : Code[20];VAR GlobalDimVal2@1005 : Code[20]) : Integer;
    VAR
      TempDimSetEntry@1003 : TEMPORARY Record 480;
      i@1001 : Integer;
    BEGIN
      GetGLSetup;
      GlobalDimVal1 := '';
      GlobalDimVal2 := '';
      DimSetEntry.RESET;
      FOR i := 1 TO 10 DO
        IF DimensionSetIDArr[i] <> 0 THEN BEGIN
          DimSetEntry.SETRANGE("Dimension Set ID",DimensionSetIDArr[i]);
          IF DimSetEntry.FINDSET THEN
            REPEAT
              IF TempDimSetEntry.GET(0,DimSetEntry."Dimension Code") THEN
                TempDimSetEntry.DELETE;
              TempDimSetEntry := DimSetEntry;
              TempDimSetEntry."Dimension Set ID" := 0;
              TempDimSetEntry.INSERT;
              IF GLSetupShortcutDimCode[1] = TempDimSetEntry."Dimension Code" THEN
                GlobalDimVal1 := TempDimSetEntry."Dimension Value Code";
              IF GLSetupShortcutDimCode[2] = TempDimSetEntry."Dimension Code" THEN
                GlobalDimVal2 := TempDimSetEntry."Dimension Value Code";
            UNTIL DimSetEntry.NEXT = 0;
        END;
      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    PROCEDURE GetDeltaDimSetID@137(DimSetID@1005 : Integer;NewParentDimSetID@1000 : Integer;OldParentDimSetID@1001 : Integer) : Integer;
    VAR
      TempDimSetEntry@1004 : TEMPORARY Record 480;
      TempDimSetEntryNew@1003 : TEMPORARY Record 480;
      TempDimSetEntryDeleted@1002 : TEMPORARY Record 480;
    BEGIN
      // Returns an updated DimSetID based on parent's old and new DimSetID
      IF NewParentDimSetID = OldParentDimSetID THEN
        EXIT(DimSetID);
      GetDimensionSet(TempDimSetEntry,DimSetID);
      GetDimensionSet(TempDimSetEntryNew,NewParentDimSetID);
      GetDimensionSet(TempDimSetEntryDeleted,OldParentDimSetID);
      IF TempDimSetEntryDeleted.FINDSET THEN
        REPEAT
          IF TempDimSetEntryNew.GET(NewParentDimSetID,TempDimSetEntryDeleted."Dimension Code") THEN BEGIN
            IF TempDimSetEntryNew."Dimension Value Code" = TempDimSetEntryDeleted."Dimension Value Code" THEN
              TempDimSetEntryNew.DELETE;
            TempDimSetEntryDeleted.DELETE;
          END;
        UNTIL TempDimSetEntryDeleted.NEXT = 0;

      IF TempDimSetEntryDeleted.FINDSET THEN
        REPEAT
          IF TempDimSetEntry.GET(DimSetID,TempDimSetEntryDeleted."Dimension Code") THEN
            TempDimSetEntry.DELETE;
        UNTIL TempDimSetEntryDeleted.NEXT = 0;

      IF TempDimSetEntryNew.FINDSET THEN
        REPEAT
          IF TempDimSetEntry.GET(DimSetID,TempDimSetEntryNew."Dimension Code") THEN BEGIN
            IF TempDimSetEntry."Dimension Value Code" <> TempDimSetEntryNew."Dimension Value Code" THEN BEGIN
              TempDimSetEntry."Dimension Value Code" := TempDimSetEntryNew."Dimension Value Code";
              TempDimSetEntry."Dimension Value ID" := TempDimSetEntryNew."Dimension Value ID";
              TempDimSetEntry.MODIFY;
            END;
          END ELSE BEGIN
            TempDimSetEntry := TempDimSetEntryNew;
            TempDimSetEntry."Dimension Set ID" := DimSetID;
            TempDimSetEntry.INSERT;
          END;
        UNTIL TempDimSetEntryNew.NEXT = 0;

      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    LOCAL PROCEDURE GetGLSetup@52();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      IF NOT HasGotGLSetup THEN BEGIN
        GLSetup.GET;
        GLSetupShortcutDimCode[1] := GLSetup."Shortcut Dimension 1 Code";
        GLSetupShortcutDimCode[2] := GLSetup."Shortcut Dimension 2 Code";
        GLSetupShortcutDimCode[3] := GLSetup."Shortcut Dimension 3 Code";
        GLSetupShortcutDimCode[4] := GLSetup."Shortcut Dimension 4 Code";
        GLSetupShortcutDimCode[5] := GLSetup."Shortcut Dimension 5 Code";
        GLSetupShortcutDimCode[6] := GLSetup."Shortcut Dimension 6 Code";
        GLSetupShortcutDimCode[7] := GLSetup."Shortcut Dimension 7 Code";
        GLSetupShortcutDimCode[8] := GLSetup."Shortcut Dimension 8 Code";

        //NC MDP01 > MP
        ConsSetup.GET;
        //NC NCS-2356 > DP
        GLSetupShortcutDimCode[9] := GLSetup."Shortcut Dimension 9 Code";
        GLSetupShortcutDimCode[10] := GLSetup."Shortcut Dimension 10 Code";
        GLSetupShortcutDimCode[20] := ConsSetup."Fin. Instrument Dimension Code";
        {
        GLSetupShortcutDimCode[9] := ConsSetup."Fin. Instrument Dimension Code";
        }
        //NC NCS-2356 < DP
        //NC MDP01 < MP


        //TDRANCS-1739>
        g_recUserSetup.GET(USERID);
        //TDRANCS-1739<
        HasGotGLSetup := TRUE;
      END;
    END;

    PROCEDURE CheckDimIDComb@138(DimSetID@1002 : Integer) : Boolean;
    BEGIN
      TempDimBuf1.RESET;
      TempDimBuf1.DELETEALL;
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          TempDimBuf1.INIT;
          TempDimBuf1."Table ID" := DATABASE::"Dimension Buffer";
          TempDimBuf1."Entry No." := 0;
          TempDimBuf1."Dimension Code" := DimSetEntry."Dimension Code";
          TempDimBuf1."Dimension Value Code" := DimSetEntry."Dimension Value Code";
          TempDimBuf1.INSERT;
        UNTIL DimSetEntry.NEXT = 0;

      DimSetEntry.RESET;
      EXIT(CheckDimComb);
    END;

    PROCEDURE CheckDimValuePosting@14(TableID@1000 : ARRAY [10] OF Integer;No@1001 : ARRAY [10] OF Code[20];DimSetID@1003 : Integer) : Boolean;
    VAR
      i@1004 : Integer;
      j@1005 : Integer;
      NoFilter@1006 : ARRAY [2] OF Text[250];
    BEGIN
      IF NOT CheckBlockedDimAndValues(DimSetID) THEN
        EXIT(FALSE);
      DefaultDim.SETFILTER("Value Posting",'<>%1',DefaultDim."Value Posting"::" ");
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      NoFilter[2] := '';
      FOR i := 1 TO ARRAYLEN(TableID) DO BEGIN
        IF (TableID[i] <> 0) AND (No[i] <> '') THEN BEGIN
          DefaultDim.SETRANGE("Table ID",TableID[i]);
          NoFilter[1] := No[i];
          FOR j := 1 TO 2 DO BEGIN
            DefaultDim.SETRANGE("No.",NoFilter[j]);
            IF DefaultDim.FINDSET THEN
              REPEAT
                DimSetEntry.SETRANGE("Dimension Code",DefaultDim."Dimension Code");
                CASE DefaultDim."Value Posting" OF
                  DefaultDim."Value Posting"::"Code Mandatory":
                    BEGIN
                      IF NOT DimSetEntry.FINDFIRST OR (DimSetEntry."Dimension Value Code" = '') THEN BEGIN
                        IF DefaultDim."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text004,
                              DefaultDim.FIELDCAPTION("Dimension Value Code"),
                              DefaultDim.FIELDCAPTION("Dimension Code"),DefaultDim."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text005,
                              DefaultDim.FIELDCAPTION("Dimension Value Code"),
                              DefaultDim.FIELDCAPTION("Dimension Code"),
                              DefaultDim."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                              DefaultDim."No.");
                        EXIT(FALSE);
                      END;
                    END;
                  DefaultDim."Value Posting"::"Same Code":
                    BEGIN
                      IF DefaultDim."Dimension Value Code" <> '' THEN BEGIN
                        IF NOT DimSetEntry.FINDFIRST OR
                           (DefaultDim."Dimension Value Code" <> DimSetEntry."Dimension Value Code")
                        THEN BEGIN
                          IF DefaultDim."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text006,
                                DefaultDim.FIELDCAPTION("Dimension Value Code"),DefaultDim."Dimension Value Code",
                                DefaultDim.FIELDCAPTION("Dimension Code"),DefaultDim."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text007,
                                DefaultDim.FIELDCAPTION("Dimension Value Code"),
                                DefaultDim."Dimension Value Code",
                                DefaultDim.FIELDCAPTION("Dimension Code"),
                                DefaultDim."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                                DefaultDim."No.");
                          EXIT(FALSE);
                        END;
                      END ELSE BEGIN
                        IF DimSetEntry.FINDFIRST THEN BEGIN
                          IF DefaultDim."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text008,
                                DimSetEntry.FIELDCAPTION("Dimension Code"),DimSetEntry."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text009,
                                DimSetEntry.FIELDCAPTION("Dimension Code"),
                                DimSetEntry."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                                DefaultDim."No.");
                          EXIT(FALSE);
                        END;
                      END;
                    END;
                  DefaultDim."Value Posting"::"No Code":
                    BEGIN
                      IF DimSetEntry.FINDFIRST THEN BEGIN
                        IF DefaultDim."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text010,
                              DimSetEntry.FIELDCAPTION("Dimension Code"),DimSetEntry."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text011,
                              DimSetEntry.FIELDCAPTION("Dimension Code"),
                              DimSetEntry."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                              DefaultDim."No.");
                        EXIT(FALSE);
                      END;
                    END;
                END;
              UNTIL DefaultDim.NEXT = 0;
          END;
        END;
      END;
      DimSetEntry.RESET;
      EXIT(TRUE);
    END;

    PROCEDURE CheckDimBuffer@64(VAR DimBuffer@1000 : Record 360) : Boolean;
    VAR
      i@1001 : Integer;
    BEGIN
      TempDimBuf1.RESET;
      TempDimBuf1.DELETEALL;
      IF DimBuffer.FINDSET THEN BEGIN
        i := 1;
        REPEAT
          TempDimBuf1.INIT;
          TempDimBuf1."Table ID" := DATABASE::"Dimension Buffer";
          TempDimBuf1."Entry No." := i;
          TempDimBuf1."Dimension Code" := DimBuffer."Dimension Code";
          TempDimBuf1."Dimension Value Code" := DimBuffer."Dimension Value Code";
          TempDimBuf1.INSERT;
          i := i + 1;
        UNTIL DimBuffer.NEXT = 0;
      END;
      EXIT(CheckDimComb);
    END;

    LOCAL PROCEDURE CheckDimComb@3() : Boolean;
    VAR
      DimComb@1000 : Record 350;
      CurrentDimCode@1002 : Code[20];
      CurrentDimValCode@1003 : Code[20];
      DimFilter@1004 : Text[1024];
      FilterTooLong@1005 : Boolean;
    BEGIN
      IF NOT TempDimCombInitialized THEN BEGIN
        TempDimCombInitialized := TRUE;
        IF DimComb.ISEMPTY THEN
          TempDimCombEmpty := TRUE;
      END;

      IF TempDimCombEmpty THEN
        EXIT(TRUE);

      IF NOT TempDimBuf1.FINDSET THEN
        EXIT(TRUE);

      REPEAT
        IF STRLEN(DimFilter) + 1 + STRLEN(TempDimBuf1."Dimension Code") > MAXSTRLEN(DimFilter) THEN
          FilterTooLong := TRUE
        ELSE
          IF DimFilter = '' THEN
            DimFilter := TempDimBuf1."Dimension Code"
          ELSE
            DimFilter := DimFilter + '|' + TempDimBuf1."Dimension Code";
      UNTIL FilterTooLong OR (TempDimBuf1.NEXT = 0);

      IF NOT FilterTooLong THEN BEGIN
        DimComb.SETFILTER("Dimension 1 Code",DimFilter);
        DimComb.SETFILTER("Dimension 2 Code",DimFilter);
        IF DimComb.FINDSET THEN
          REPEAT
            IF DimComb."Combination Restriction" = DimComb."Combination Restriction"::Blocked THEN BEGIN
              DimCombErr := STRSUBSTNO(Text000,DimComb."Dimension 1 Code",DimComb."Dimension 2 Code");
              EXIT(FALSE);
            END ELSE BEGIN
              TempDimBuf1.SETRANGE("Dimension Code",DimComb."Dimension 1 Code");
              TempDimBuf1.FINDFIRST;
              CurrentDimCode := TempDimBuf1."Dimension Code";
              CurrentDimValCode := TempDimBuf1."Dimension Value Code";
              TempDimBuf1.SETRANGE("Dimension Code",DimComb."Dimension 2 Code");
              TempDimBuf1.FINDFIRST;
              IF NOT
                 CheckDimValueComb(
                   TempDimBuf1."Dimension Code",TempDimBuf1."Dimension Value Code",
                   CurrentDimCode,CurrentDimValCode)
              THEN
                EXIT(FALSE);
              IF NOT
                 CheckDimValueComb(
                   CurrentDimCode,CurrentDimValCode,
                   TempDimBuf1."Dimension Code",TempDimBuf1."Dimension Value Code")
              THEN
                EXIT(FALSE);
            END;
          UNTIL DimComb.NEXT = 0;
        EXIT(TRUE);
      END;

      WHILE TempDimBuf1.FINDFIRST DO BEGIN
        CurrentDimCode := TempDimBuf1."Dimension Code";
        CurrentDimValCode := TempDimBuf1."Dimension Value Code";
        TempDimBuf1.DELETE;
        IF TempDimBuf1.FINDSET THEN
          REPEAT
            IF CurrentDimCode > TempDimBuf1."Dimension Code" THEN BEGIN
              IF DimComb.GET(TempDimBuf1."Dimension Code",CurrentDimCode) THEN BEGIN
                IF DimComb."Combination Restriction" = DimComb."Combination Restriction"::Blocked THEN BEGIN
                  DimCombErr :=
                    STRSUBSTNO(
                      Text000,
                      TempDimBuf1."Dimension Code",CurrentDimCode);
                  EXIT(FALSE);
                END;
                IF NOT
                   CheckDimValueComb(
                     TempDimBuf1."Dimension Code",TempDimBuf1."Dimension Value Code",
                     CurrentDimCode,CurrentDimValCode)
                THEN
                  EXIT(FALSE);
              END;
            END ELSE BEGIN
              IF DimComb.GET(CurrentDimCode,TempDimBuf1."Dimension Code") THEN BEGIN
                IF DimComb."Combination Restriction" = DimComb."Combination Restriction"::Blocked THEN BEGIN
                  DimCombErr :=
                    STRSUBSTNO(
                      Text000,
                      CurrentDimCode,TempDimBuf1."Dimension Code");
                  EXIT(FALSE);
                END;
                IF NOT
                   CheckDimValueComb(
                     CurrentDimCode,CurrentDimValCode,TempDimBuf1."Dimension Code",
                     TempDimBuf1."Dimension Value Code")
                THEN
                  EXIT(FALSE);
              END;
            END;
          UNTIL TempDimBuf1.NEXT = 0;
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CheckDimValueComb@9(Dim1@1000 : Code[20];Dim1Value@1001 : Code[20];Dim2@1002 : Code[20];Dim2Value@1003 : Code[20]) : Boolean;
    BEGIN
      IF DimValComb.GET(Dim1,Dim1Value,Dim2,Dim2Value) THEN BEGIN
        DimCombErr :=
          STRSUBSTNO(Text001,
            Dim1,Dim1Value,Dim2,Dim2Value);
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE GetDimCombErr@41() : Text[250];
    BEGIN
      EXIT(DimCombErr);
    END;

    PROCEDURE UpdateDefaultDim@59(TableID@1000 : Integer;No@1001 : Code[20];VAR GlobalDim1Code@1002 : Code[20];VAR GlobalDim2Code@1003 : Code[20]);
    VAR
      DefaultDim@1004 : Record 352;
    BEGIN
      GetGLSetup;
      IF DefaultDim.GET(TableID,No,GLSetupShortcutDimCode[1]) THEN
        GlobalDim1Code := DefaultDim."Dimension Value Code"
      ELSE
        GlobalDim1Code := '';
      IF DefaultDim.GET(TableID,No,GLSetupShortcutDimCode[2]) THEN
        GlobalDim2Code := DefaultDim."Dimension Value Code"
      ELSE
        GlobalDim2Code := '';
    END;

    PROCEDURE GetDefaultDimID@8(TableID@1000 : ARRAY [10] OF Integer;No@1001 : ARRAY [10] OF Code[20];SourceCode@1002 : Code[20];VAR GlobalDim1Code@1003 : Code[20];VAR GlobalDim2Code@1004 : Code[20];InheritFromDimSetID@1014 : Integer;InheritFromTableNo@1015 : Integer) : Integer;
    VAR
      DimVal@1012 : Record 349;
      DefaultDimPriority1@1005 : Record 354;
      DefaultDimPriority2@1006 : Record 354;
      DefaultDim@1007 : Record 352;
      TempDimSetEntry@1011 : TEMPORARY Record 480;
      TempDimSetEntry0@1016 : TEMPORARY Record 480;
      i@1010 : Integer;
      j@1009 : Integer;
      NoFilter@1008 : ARRAY [2] OF Code[20];
      NewDimSetID@1013 : Integer;
    BEGIN
      GetGLSetup;
      IF InheritFromDimSetID > 0 THEN
        GetDimensionSet(TempDimSetEntry0,InheritFromDimSetID);
      TempDimBuf2.RESET;
      TempDimBuf2.DELETEALL;
      IF TempDimSetEntry0.FINDSET THEN
        REPEAT
          TempDimBuf2.INIT;
          TempDimBuf2."Table ID" := InheritFromTableNo;
          TempDimBuf2."Entry No." := 0;
          TempDimBuf2."Dimension Code" := TempDimSetEntry0."Dimension Code";
          TempDimBuf2."Dimension Value Code" := TempDimSetEntry0."Dimension Value Code";
          TempDimBuf2.INSERT;
        UNTIL TempDimSetEntry0.NEXT = 0;

      NoFilter[2] := '';
      FOR i := 1 TO ARRAYLEN(TableID) DO BEGIN
        IF (TableID[i] <> 0) AND (No[i] <> '') THEN BEGIN
          DefaultDim.SETRANGE("Table ID",TableID[i]);
          NoFilter[1] := No[i];
          FOR j := 1 TO 2 DO BEGIN
            DefaultDim.SETRANGE("No.",NoFilter[j]);
            IF DefaultDim.FINDSET THEN
              REPEAT
                IF (DefaultDim."Dimension Value Code" <> '')
                  //NC MDP01 > MP
                  OR ((DefaultDim."Dimension Value Code" = '') AND (DefaultDim."Value Posting" <> DefaultDim."Value Posting"::" "))
                  //NC MDP01 < MP
                THEN BEGIN
                  TempDimBuf2.SETRANGE("Dimension Code",DefaultDim."Dimension Code");
                  IF NOT TempDimBuf2.FINDFIRST THEN BEGIN
                    TempDimBuf2.INIT;
                    TempDimBuf2."Table ID" := DefaultDim."Table ID";
                    TempDimBuf2."Entry No." := 0;
                    TempDimBuf2."Dimension Code" := DefaultDim."Dimension Code";
                    TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                    TempDimBuf2.INSERT;
                  END ELSE BEGIN
                    IF DefaultDimPriority1.GET(SourceCode,DefaultDim."Table ID") THEN BEGIN
                      IF DefaultDimPriority2.GET(SourceCode,TempDimBuf2."Table ID") THEN BEGIN
                        IF DefaultDimPriority1.Priority < DefaultDimPriority2.Priority THEN BEGIN
                          TempDimBuf2.DELETE;
                          TempDimBuf2."Table ID" := DefaultDim."Table ID";
                          TempDimBuf2."Entry No." := 0;
                          TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                          TempDimBuf2.INSERT;
                        END;
                      END ELSE BEGIN
                        TempDimBuf2.DELETE;
                        TempDimBuf2."Table ID" := DefaultDim."Table ID";
                        TempDimBuf2."Entry No." := 0;
                        TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                        TempDimBuf2.INSERT;
                      END;
                    END
                    //NC MDP01.1 > EB
                     ELSE
                      IF DefaultDim."Dimension Value Code" <> '' THEN BEGIN
                        TempDimBuf2.DELETE;
                        TempDimBuf2."Table ID" := DefaultDim."Table ID";
                        TempDimBuf2."Entry No." := 0;
                        TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                        TempDimBuf2.INSERT;
                      END;
                    //NC MDP01.1 < EB
                  END;
                  IF GLSetupShortcutDimCode[1] = TempDimBuf2."Dimension Code" THEN
                    GlobalDim1Code := TempDimBuf2."Dimension Value Code";
                  IF GLSetupShortcutDimCode[2] = TempDimBuf2."Dimension Code" THEN
                    GlobalDim2Code := TempDimBuf2."Dimension Value Code";
                END;
              UNTIL DefaultDim.NEXT = 0;
          END;
        END;
      END;
      TempDimBuf2.RESET;
      IF TempDimBuf2.FINDSET THEN BEGIN
        REPEAT
          //NC NOS-26 > SPA
          //DimVal.GET(TempDimBuf2."Dimension Code",TempDimBuf2."Dimension Value Code");
          IF DimVal.GET(TempDimBuf2."Dimension Code",TempDimBuf2."Dimension Value Code") THEN BEGIN
          //NC NOS-26 < SPA
            TempDimSetEntry."Dimension Code" := TempDimBuf2."Dimension Code";
            TempDimSetEntry."Dimension Value Code" := TempDimBuf2."Dimension Value Code";
            TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
            TempDimSetEntry.INSERT;
          //NC NOS-26 > SPA
          END;
          //NC NOS-26 < SPA
        UNTIL TempDimBuf2.NEXT = 0;
        NewDimSetID := GetDimensionSetID(TempDimSetEntry);
      END;
      EXIT(NewDimSetID);
    END;

    PROCEDURE TypeToTableID1@11(Type@1000 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner,FI') : Integer;
    BEGIN
      CASE Type OF
        Type::"G/L Account":
          EXIT(DATABASE::"G/L Account");
        Type::Customer:
          EXIT(DATABASE::Customer);
        Type::Vendor:
          EXIT(DATABASE::Vendor);
        Type::"Bank Account":
          EXIT(DATABASE::"Bank Account");
        Type::"Fixed Asset":
          EXIT(DATABASE::"Fixed Asset");
        Type::"IC Partner":
          EXIT(DATABASE::"IC Partner");
        //NC MTF02 > AKU
        Type::FI:
          EXIT(DATABASE::"Financial Instrument");
        //NC MTF02 < AKU
      END;
    END;

    PROCEDURE TypeToTableID2@13(Type@1000 : 'Resource,Item,G/L Account') : Integer;
    BEGIN
      CASE Type OF
        Type::Resource:
          EXIT(DATABASE::Resource);
        Type::Item:
          EXIT(DATABASE::Item);
        Type::"G/L Account":
          EXIT(DATABASE::"G/L Account");
      END;
    END;

    PROCEDURE TypeToTableID3@16(Type@1000 : ' ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)') : Integer;
    BEGIN
      CASE Type OF
        Type::" ":
          EXIT(0);
        Type::"G/L Account":
          EXIT(DATABASE::"G/L Account");
        Type::Item:
          EXIT(DATABASE::Item);
        Type::Resource:
          EXIT(DATABASE::Resource);
        Type::"Fixed Asset":
          EXIT(DATABASE::"Fixed Asset");
        Type::"Charge (Item)":
          EXIT(DATABASE::"Item Charge");
      END;
    END;

    PROCEDURE TypeToTableID4@1(Type@1000 : ' ,Item,Resource') : Integer;
    BEGIN
      CASE Type OF
        Type::" ":
          EXIT(0);
        Type::Item:
          EXIT(DATABASE::Item);
        Type::Resource:
          EXIT(DATABASE::Resource);
      END;
    END;

    PROCEDURE TypeToTableID5@119(Type@1000 : ' ,Item,Resource,Cost,G/L Account') : Integer;
    BEGIN
      CASE Type OF
        Type::" ":
          EXIT(0);
        Type::Item:
          EXIT(DATABASE::Item);
        Type::Resource:
          EXIT(DATABASE::Resource);
        Type::Cost:
          EXIT(DATABASE::"Service Cost");
        Type::"G/L Account":
          EXIT(DATABASE::"G/L Account");
      END;
    END;

    PROCEDURE DeleteDefaultDim@58(TableID@1000 : Integer;No@1001 : Code[20]);
    VAR
      DefaultDim@1002 : Record 352;
    BEGIN
      DefaultDim.SETRANGE("Table ID",TableID);
      DefaultDim.SETRANGE("No.",No);
      IF NOT DefaultDim.ISEMPTY THEN
        DefaultDim.DELETEALL;
    END;

    PROCEDURE LookupDimValueCode@21(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimVal@1002 : Record 349;
      GLSetup@1003 : Record 98;
    BEGIN
      GetGLSetup;
      IF GLSetupShortcutDimCode[FieldNumber] = '' THEN
        ERROR(Text002,GLSetup.TABLECAPTION);
      DimVal.SETRANGE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
      DimVal."Dimension Code" := GLSetupShortcutDimCode[FieldNumber];
      DimVal.Code := ShortcutDimCode;
      IF PAGE.RUNMODAL(0,DimVal) = ACTION::LookupOK THEN BEGIN
        CheckDim(DimVal."Dimension Code");
        CheckDimValue(DimVal."Dimension Code",DimVal.Code);
        ShortcutDimCode := DimVal.Code;
      END;
    END;

    PROCEDURE ValidateDimValueCode@22(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimVal@1002 : Record 349;
      GLSetup@1003 : Record 98;
    BEGIN
      GetGLSetup;
      IF (GLSetupShortcutDimCode[FieldNumber] = '') AND (ShortcutDimCode <> '') THEN
        ERROR(Text002,GLSetup.TABLECAPTION);
      DimVal.SETRANGE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
      IF ShortcutDimCode <> '' THEN BEGIN
        DimVal.SETRANGE(Code,ShortcutDimCode);
        IF NOT DimVal.FINDFIRST THEN BEGIN
          DimVal.SETFILTER(Code,STRSUBSTNO('%1*',ShortcutDimCode));
          IF DimVal.FINDFIRST THEN
            ShortcutDimCode := DimVal.Code
          ELSE
            ERROR(
              STRSUBSTNO(Text003,
                ShortcutDimCode,DimVal.FIELDCAPTION(Code)));
        END;
      END;
    END;

    PROCEDURE ValidateShortcutDimValues@127(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20];VAR DimSetID@1004 : Integer);
    VAR
      DimVal@1002 : Record 349;
      TempDimSetEntry@1005 : TEMPORARY Record 480;
    BEGIN
      ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimVal."Dimension Code" := GLSetupShortcutDimCode[FieldNumber];
      IF ShortcutDimCode <> '' THEN BEGIN
        DimVal.GET(DimVal."Dimension Code",ShortcutDimCode);
        IF NOT CheckDim(DimVal."Dimension Code") THEN
          ERROR(GetDimErr);
        IF NOT CheckDimValue(DimVal."Dimension Code",ShortcutDimCode) THEN
          ERROR(GetDimErr);
      END;
      GetDimensionSet(TempDimSetEntry,DimSetID);
      IF TempDimSetEntry.GET(TempDimSetEntry."Dimension Set ID",DimVal."Dimension Code") THEN
        IF TempDimSetEntry."Dimension Value Code" <> ShortcutDimCode THEN
          TempDimSetEntry.DELETE;
      IF ShortcutDimCode <> '' THEN BEGIN
        TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
        TempDimSetEntry."Dimension Value Code" := DimVal.Code;
        TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
        IF TempDimSetEntry.INSERT THEN;
      END;
      DimSetID := GetDimensionSetID(TempDimSetEntry);
    END;

    PROCEDURE SaveDefaultDim@53(TableID@1000 : Integer;No@1001 : Code[20];FieldNumber@1002 : Integer;ShortcutDimCode@1003 : Code[20]);
    VAR
      DefaultDim@1004 : Record 352;
    BEGIN
      GetGLSetup;
      IF ShortcutDimCode <> '' THEN BEGIN
        IF DefaultDim.GET(TableID,No,GLSetupShortcutDimCode[FieldNumber])
        THEN BEGIN
          DefaultDim.VALIDATE("Dimension Value Code",ShortcutDimCode);
          DefaultDim.MODIFY;
        END ELSE BEGIN
          DefaultDim.INIT;
          DefaultDim.VALIDATE("Table ID",TableID);
          DefaultDim.VALIDATE("No.",No);
          DefaultDim.VALIDATE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
          DefaultDim.VALIDATE("Dimension Value Code",ShortcutDimCode);
          DefaultDim.INSERT;
        END;
      END ELSE
        IF DefaultDim.GET(TableID,No,GLSetupShortcutDimCode[FieldNumber]) THEN
          DefaultDim.DELETE;
    END;

    PROCEDURE GetShortcutDimensions@129(DimSetID@1000 : Integer;VAR ShortcutDimCode@1004 : ARRAY [20] OF Code[20]);
    VAR
      i@1006 : Integer;
    BEGIN
      GetGLSetup;
      //NC NOS-26 > SPA
      //FOR i := 3 TO 8 DO BEGIN
      FOR i := 1 TO 10 DO BEGIN
      //NC NOS-26 < SPA
        ShortcutDimCode[i] := '';
        IF GLSetupShortcutDimCode[i] <> '' THEN
          IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[i]) THEN
            ShortcutDimCode[i] := DimSetEntry."Dimension Value Code";
      END;

      //NC NOS-26 > SPA
      // Financial Instrument
      ShortcutDimCode[20] := '';
      IF GLSetupShortcutDimCode[20] <> '' THEN
          IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[20]) THEN
            ShortcutDimCode[20] := DimSetEntry."Dimension Value Code";
      //NC NOS-26 < SPA
    END;

    PROCEDURE CheckDimBufferValuePosting@68(VAR DimBuffer@1000 : Record 360;TableID@1001 : ARRAY [10] OF Integer;No@1002 : ARRAY [10] OF Code[20]) : Boolean;
    VAR
      i@1005 : Integer;
    BEGIN
      TempDimBuf2.RESET;
      TempDimBuf2.DELETEALL;
      IF DimBuffer.FINDSET THEN BEGIN
        i := 1;
        REPEAT
          IF (NOT CheckDimValue(
                DimBuffer."Dimension Code",DimBuffer."Dimension Value Code")) OR
             (NOT CheckDim(DimBuffer."Dimension Code"))
          THEN BEGIN
            DimValuePostingErr := DimErr;
            EXIT(FALSE);
          END;
          TempDimBuf2.INIT;
          TempDimBuf2."Entry No." := i;
          TempDimBuf2."Dimension Code" := DimBuffer."Dimension Code";
          TempDimBuf2."Dimension Value Code" := DimBuffer."Dimension Value Code";
          TempDimBuf2.INSERT;
          i := i + 1;
        UNTIL DimBuffer.NEXT = 0;
      END;
      EXIT(CheckValuePosting(TableID,No));
    END;

    LOCAL PROCEDURE CheckValuePosting@36(TableID@1000 : ARRAY [10] OF Integer;No@1001 : ARRAY [10] OF Code[20]) : Boolean;
    VAR
      DefaultDim@1002 : Record 352;
      i@1004 : Integer;
      j@1005 : Integer;
      NoFilter@1006 : ARRAY [2] OF Text[250];
    BEGIN
      DefaultDim.SETFILTER("Value Posting",'<>%1',DefaultDim."Value Posting"::" ");
      NoFilter[2] := '';
      FOR i := 1 TO ARRAYLEN(TableID) DO BEGIN
        IF (TableID[i] <> 0) AND (No[i] <> '') THEN BEGIN
          DefaultDim.SETRANGE("Table ID",TableID[i]);
          NoFilter[1] := No[i];
          FOR j := 1 TO 2 DO BEGIN
            DefaultDim.SETRANGE("No.",NoFilter[j]);
            IF DefaultDim.FINDSET THEN BEGIN
              REPEAT
                TempDimBuf2.SETRANGE("Dimension Code",DefaultDim."Dimension Code");
                CASE DefaultDim."Value Posting" OF
                  DefaultDim."Value Posting"::"Code Mandatory":
                    BEGIN
                      IF (NOT TempDimBuf2.FINDFIRST) OR
                         (TempDimBuf2."Dimension Value Code" = '')
                      THEN BEGIN
                        IF DefaultDim."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text004,
                              DefaultDim.FIELDCAPTION("Dimension Value Code"),
                              DefaultDim.FIELDCAPTION("Dimension Code"),DefaultDim."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text005,
                              DefaultDim.FIELDCAPTION("Dimension Value Code"),
                              DefaultDim.FIELDCAPTION("Dimension Code"),
                              DefaultDim."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                              DefaultDim."No.");
                        EXIT(FALSE);
                      END;
                    END;
                  DefaultDim."Value Posting"::"Same Code":
                    BEGIN
                      IF DefaultDim."Dimension Value Code" <> '' THEN BEGIN
                        IF (NOT TempDimBuf2.FINDFIRST) OR
                           (DefaultDim."Dimension Value Code" <> TempDimBuf2."Dimension Value Code")
                        THEN BEGIN
                          IF DefaultDim."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text006,
                                DefaultDim.FIELDCAPTION("Dimension Value Code"),DefaultDim."Dimension Value Code",
                                DefaultDim.FIELDCAPTION("Dimension Code"),DefaultDim."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text007,
                                DefaultDim.FIELDCAPTION("Dimension Value Code"),
                                DefaultDim."Dimension Value Code",
                                DefaultDim.FIELDCAPTION("Dimension Code"),
                                DefaultDim."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                                DefaultDim."No.");
                          EXIT(FALSE);
                        END;
                      END ELSE BEGIN
                        IF TempDimBuf2.FINDFIRST THEN BEGIN
                          IF DefaultDim."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text008,
                                TempDimBuf2.FIELDCAPTION("Dimension Code"),TempDimBuf2."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text009,
                                TempDimBuf2.FIELDCAPTION("Dimension Code"),
                                TempDimBuf2."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                                DefaultDim."No.");
                          EXIT(FALSE);
                        END;
                      END;
                    END;
                  DefaultDim."Value Posting"::"No Code":
                    BEGIN
                      IF TempDimBuf2.FINDFIRST THEN BEGIN
                        IF DefaultDim."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text010,
                              TempDimBuf2.FIELDCAPTION("Dimension Code"),TempDimBuf2."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text011,
                              TempDimBuf2.FIELDCAPTION("Dimension Code"),
                              TempDimBuf2."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                              DefaultDim."No.");
                        EXIT(FALSE);
                      END;
                    END;
                END;
              UNTIL DefaultDim.NEXT = 0;
              TempDimBuf2.RESET;
            END;
          END;
        END;
      END;
      EXIT(TRUE);
    END;

    PROCEDURE GetDimValuePostingErr@98() : Text[250];
    BEGIN
      EXIT(DimValuePostingErr);
    END;

    PROCEDURE SetupObjectNoList@40(VAR TempAllObjWithCaption@1000 : TEMPORARY Record 2000000058);
    BEGIN
      InsertObject(TempAllObjWithCaption,DATABASE::"Salesperson/Purchaser");
      InsertObject(TempAllObjWithCaption,DATABASE::"G/L Account");
      InsertObject(TempAllObjWithCaption,DATABASE::Customer);
      InsertObject(TempAllObjWithCaption,DATABASE::Vendor);
      InsertObject(TempAllObjWithCaption,DATABASE::Item);
      InsertObject(TempAllObjWithCaption,DATABASE::"Resource Group");
      InsertObject(TempAllObjWithCaption,DATABASE::Resource);
      InsertObject(TempAllObjWithCaption,DATABASE::Job);
      InsertObject(TempAllObjWithCaption,DATABASE::"Bank Account");
      InsertObject(TempAllObjWithCaption,DATABASE::Campaign);
      InsertObject(TempAllObjWithCaption,DATABASE::Employee);
      InsertObject(TempAllObjWithCaption,DATABASE::"Fixed Asset");
      InsertObject(TempAllObjWithCaption,DATABASE::Insurance);
      InsertObject(TempAllObjWithCaption,DATABASE::"Responsibility Center");
      InsertObject(TempAllObjWithCaption,DATABASE::"Item Charge");
      InsertObject(TempAllObjWithCaption,DATABASE::"Work Center");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Contract Header");
      InsertObject(TempAllObjWithCaption,DATABASE::"Customer Template");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Contract Template");
      InsertObject(TempAllObjWithCaption,DATABASE::"IC Partner");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Order Type");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Item Group");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Item");
      InsertObject(TempAllObjWithCaption,DATABASE::"Cash Flow Manual Expense");
      InsertObject(TempAllObjWithCaption,DATABASE::"Cash Flow Manual Revenue");
      InsertObject(TempAllObjWithCaption,DATABASE::"FA Charge");
      InsertObject(TempAllObjWithCaption,DATABASE::"Customer Agreement");
      InsertObject(TempAllObjWithCaption,DATABASE::"Vendor Agreement");
      InsertObject(TempAllObjWithCaption,DATABASE::"Payroll Element");

      //NC MNET05 AG
      InsertObject(TempAllObjWithCaption,DATABASE::"Period. Activities Stand. Par.");
      //NC MNET05< AG

      //NC MMTM05 AG
      InsertObject(TempAllObjWithCaption,DATABASE::"Financial Instrument");
      //NC MMTM05< AG

      //NC MMTD02 DP
      InsertObject(TempAllObjWithCaption,DATABASE::"Legal Entity Type");
      //NC MMTD02< DP

      //NC MCO04.1 DP
      InsertObject(TempAllObjWithCaption,DATABASE::"Moving Data Setup");
      //NC MCO04.1< DP

      //NC NCS-1600 > USE
      InsertObject(TempAllObjWithCaption,DATABASE::"Vendor Posting Group");
      //NC NCS-1600 < USE

      //NC NCS-2296 > DP
      InsertObject(TempAllObjWithCaption,DATABASE::"VAT Product Posting Group");
      //NC NCS-2296 < DP

      OnAfterSetupObjectNoList(TempAllObjWithCaption);
    END;

    PROCEDURE GetDocDimConsistencyErr@56() : Text[250];
    BEGIN
      EXIT(DocDimConsistencyErr);
    END;

    PROCEDURE CheckDim@60(DimCode@1000 : Code[20]) : Boolean;
    VAR
      Dim@1001 : Record 348;
    BEGIN
      IF Dim.GET(DimCode) THEN BEGIN
        IF Dim.Blocked THEN BEGIN
          DimErr :=
            STRSUBSTNO(Text014,Dim.TABLECAPTION,DimCode);
          EXIT(FALSE);
        END;
      END ELSE BEGIN
        DimErr :=
          STRSUBSTNO(Text015,Dim.TABLECAPTION,DimCode);
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE CheckDimValue@61(DimCode@1000 : Code[20];DimValCode@1001 : Code[20]) : Boolean;
    VAR
      DimVal@1002 : Record 349;
    BEGIN
      IF (DimCode <> '') AND (DimValCode <> '') THEN BEGIN
        IF DimVal.GET(DimCode,DimValCode) THEN BEGIN
          IF DimVal.Blocked THEN BEGIN
            DimErr :=
              STRSUBSTNO(
                Text016,DimVal.TABLECAPTION,DimCode,DimValCode);
            EXIT(FALSE);
          END;
          IF NOT (DimVal."Dimension Value Type" IN

          //NC TR233 > DP
          {
                  [DimVal."Dimension Value Type"::Standard,
                   DimVal."Dimension Value Type"::"Begin-Total"])

          }
            [DimVal."Dimension Value Type"::Standard])
          //NC TR233 > DP
          THEN BEGIN
            DimErr :=
              STRSUBSTNO(Text017,DimVal.FIELDCAPTION("Dimension Value Type"),
                DimVal.TABLECAPTION,DimCode,DimValCode,FORMAT(DimVal."Dimension Value Type"));
            EXIT(FALSE);
          END;
        END ELSE BEGIN
          DimErr :=
            STRSUBSTNO(
              Text018,DimVal.TABLECAPTION,DimCode);
          EXIT(FALSE);
        END;
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CheckBlockedDimAndValues@6(DimSetID@1000 : Integer) : Boolean;
    VAR
      DimSetEntry@1001 : Record 480;
    BEGIN
      IF DimSetID = 0 THEN
        EXIT(TRUE);
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          IF NOT CheckDim(DimSetEntry."Dimension Code") OR
             NOT CheckDimValue(DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code")
          THEN BEGIN
            DimValuePostingErr := DimErr;
            EXIT(FALSE);
          END;
        UNTIL DimSetEntry.NEXT = 0;
      EXIT(TRUE);
    END;

    PROCEDURE GetDimErr@71() : Text[250];
    BEGIN
      EXIT(DimErr);
    END;

    PROCEDURE LookupDimValueCodeNoUpdate@20(FieldNumber@1000 : Integer);
    VAR
      DimVal@1001 : Record 349;
      GLSetup@1002 : Record 98;
    BEGIN
      GetGLSetup;
      IF GLSetupShortcutDimCode[FieldNumber] = '' THEN
        ERROR(Text002,GLSetup.TABLECAPTION);
      DimVal.SETRANGE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
      IF PAGE.RUNMODAL(0,DimVal) = ACTION::LookupOK THEN;
    END;

    PROCEDURE CopyJnlLineDimToICJnlDim@93(TableID@1000 : Integer;TransactionNo@1001 : Integer;PartnerCode@1002 : Code[20];TransactionSource@1008 : Option;LineNo@1003 : Integer;DimSetID@1004 : Integer);
    VAR
      InOutBoxJnlLineDim@1005 : Record 423;
      DimSetEntry@1009 : Record 480;
      ICDim@1006 : Code[20];
      ICDimValue@1007 : Code[20];
    BEGIN
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          ICDim := ConvertDimtoICDim(DimSetEntry."Dimension Code");
          ICDimValue := ConvertDimValuetoICDimVal(DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code");
          IF (ICDim <> '') AND (ICDimValue <> '') THEN BEGIN
            InOutBoxJnlLineDim.INIT;
            InOutBoxJnlLineDim."Table ID" := TableID;
            InOutBoxJnlLineDim."IC Partner Code" := PartnerCode;
            InOutBoxJnlLineDim."Transaction No." := TransactionNo;
            InOutBoxJnlLineDim."Transaction Source" := TransactionSource;
            InOutBoxJnlLineDim."Line No." := LineNo;
            InOutBoxJnlLineDim."Dimension Code" := ICDim;
            InOutBoxJnlLineDim."Dimension Value Code" := ICDimValue;
            InOutBoxJnlLineDim.INSERT;
          END;
        UNTIL DimSetEntry.NEXT = 0;
    END;

    PROCEDURE DefaultDimOnInsert@17(DefaultDimension@1000 : Record 352);
    VAR
      CallingTrigger@1001 : 'OnInsert,OnModify,OnDelete';
    BEGIN
      IF DefaultDimension."Table ID" = DATABASE::Job THEN
        UpdateJobTaskDim(DefaultDimension,FALSE);

      UpdateCostType(DefaultDimension,CallingTrigger::OnInsert);
    END;

    PROCEDURE DefaultDimOnModify@18(DefaultDimension@1000 : Record 352);
    VAR
      CallingTrigger@1001 : 'OnInsert,OnModify,OnDelete';
    BEGIN
      IF DefaultDimension."Table ID" = DATABASE::Job THEN
        UpdateJobTaskDim(DefaultDimension,FALSE);

      UpdateCostType(DefaultDimension,CallingTrigger::OnModify);
    END;

    PROCEDURE DefaultDimOnDelete@19(DefaultDimension@1000 : Record 352);
    VAR
      CallingTrigger@1001 : 'OnInsert,OnModify,OnDelete';
    BEGIN
      IF DefaultDimension."Table ID" = DATABASE::Job THEN
        UpdateJobTaskDim(DefaultDimension,TRUE);

      UpdateCostType(DefaultDimension,CallingTrigger::OnDelete);
    END;

    PROCEDURE CopyICJnlDimToICJnlDim@97(VAR FromInOutBoxLineDim@1001 : Record 423;VAR ToInOutBoxlineDim@1000 : Record 423);
    BEGIN
      IF FromInOutBoxLineDim.FINDSET THEN
        REPEAT
          ToInOutBoxlineDim := FromInOutBoxLineDim;
          ToInOutBoxlineDim.INSERT;
        UNTIL FromInOutBoxLineDim.NEXT = 0;
    END;

    PROCEDURE CopyDocDimtoICDocDim@107(TableID@1005 : Integer;TransactionNo@1004 : Integer;PartnerCode@1003 : Code[20];TransactionSource@1002 : Option;LineNo@1001 : Integer;DimSetEntryID@1000 : Integer);
    VAR
      InOutBoxDocDim@1008 : Record 442;
      DimSetEntry@1009 : Record 480;
      ICDim@1007 : Code[20];
      ICDimValue@1006 : Code[20];
    BEGIN
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetEntryID);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          ICDim := ConvertDimtoICDim(DimSetEntry."Dimension Code");
          ICDimValue := ConvertDimValuetoICDimVal(DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code");
          IF (ICDim <> '') AND (ICDimValue <> '') THEN BEGIN
            InOutBoxDocDim.INIT;
            InOutBoxDocDim."Table ID" := TableID;
            InOutBoxDocDim."IC Partner Code" := PartnerCode;
            InOutBoxDocDim."Transaction No." := TransactionNo;
            InOutBoxDocDim."Transaction Source" := TransactionSource;
            InOutBoxDocDim."Line No." := LineNo;
            InOutBoxDocDim."Dimension Code" := ICDim;
            InOutBoxDocDim."Dimension Value Code" := ICDimValue;
            InOutBoxDocDim.INSERT;
          END;
        UNTIL DimSetEntry.NEXT = 0;
    END;

    PROCEDURE CopyICDocDimtoICDocDim@108(FromSourceICDocDim@1003 : Record 442;VAR ToSourceICDocDim@1002 : Record 442;ToTableID@1001 : Integer;ToTransactionSource@1000 : Integer);
    BEGIN
      WITH FromSourceICDocDim DO BEGIN
        SetICDocDimFilters(FromSourceICDocDim,"Table ID","Transaction No.","IC Partner Code","Transaction Source","Line No.");
        IF FINDSET THEN
          REPEAT
            ToSourceICDocDim := FromSourceICDocDim;
            ToSourceICDocDim."Table ID" := ToTableID;
            ToSourceICDocDim."Transaction Source" := ToTransactionSource;
            ToSourceICDocDim.INSERT;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE MoveICDocDimtoICDocDim@112(FromSourceICDocDim@1003 : Record 442;VAR ToSourceICDocDim@1002 : Record 442;ToTableID@1001 : Integer;ToTransactionSource@1000 : Integer);
    BEGIN
      WITH FromSourceICDocDim DO BEGIN
        SetICDocDimFilters(FromSourceICDocDim,"Table ID","Transaction No.","IC Partner Code","Transaction Source","Line No.");
        IF FINDSET THEN
          REPEAT
            ToSourceICDocDim := FromSourceICDocDim;
            ToSourceICDocDim."Table ID" := ToTableID;
            ToSourceICDocDim."Transaction Source" := ToTransactionSource;
            ToSourceICDocDim.INSERT;
            DELETE;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE SetICDocDimFilters@110(VAR ICDocDim@1000 : Record 442;TableID@1001 : Integer;TransactionNo@1002 : Integer;PartnerCode@1003 : Code[20];TransactionSource@1004 : Integer;LineNo@1005 : Integer);
    BEGIN
      ICDocDim.RESET;
      ICDocDim.SETRANGE("Table ID",TableID);
      ICDocDim.SETRANGE("Transaction No.",TransactionNo);
      ICDocDim.SETRANGE("IC Partner Code",PartnerCode);
      ICDocDim.SETRANGE("Transaction Source",TransactionSource);
      ICDocDim.SETRANGE("Line No.",LineNo);
    END;

    PROCEDURE DeleteICDocDim@109(TableID@1000 : Integer;ICTransactionNo@1001 : Integer;ICPartnerCode@1002 : Code[20];TransactionSource@1003 : Option;LineNo@1005 : Integer);
    VAR
      ICDocDim@1004 : Record 442;
    BEGIN
      SetICDocDimFilters(ICDocDim,TableID,ICTransactionNo,ICPartnerCode,TransactionSource,LineNo);
      IF NOT ICDocDim.ISEMPTY THEN
        ICDocDim.DELETEALL;
    END;

    PROCEDURE DeleteICJnlDim@116(TableID@1000 : Integer;ICTransactionNo@1001 : Integer;ICPartnerCode@1002 : Code[20];TransactionSource@1003 : Option;LineNo@1005 : Integer);
    VAR
      ICJnlDim@1004 : Record 423;
    BEGIN
      ICJnlDim.SETRANGE("Table ID",TableID);
      ICJnlDim.SETRANGE("Transaction No.",ICTransactionNo);
      ICJnlDim.SETRANGE("IC Partner Code",ICPartnerCode);
      ICJnlDim.SETRANGE("Transaction Source",TransactionSource);
      ICJnlDim.SETRANGE("Line No.",LineNo);
      IF NOT ICJnlDim.ISEMPTY THEN
        ICJnlDim.DELETEALL;
    END;

    LOCAL PROCEDURE ConvertICDimtoDim@99(FromICDim@1000 : Code[20]) DimCode : Code[20];
    VAR
      ICDim@1002 : Record 411;
    BEGIN
      IF ICDim.GET(FromICDim) THEN
        DimCode := ICDim."Map-to Dimension Code";
    END;

    LOCAL PROCEDURE ConvertICDimValuetoDimValue@100(FromICDim@1000 : Code[20];FromICDimValue@1001 : Code[20]) DimValueCode : Code[20];
    VAR
      ICDimValue@1002 : Record 412;
    BEGIN
      IF ICDimValue.GET(FromICDim,FromICDimValue) THEN
        DimValueCode := ICDimValue."Map-to Dimension Value Code";
    END;

    PROCEDURE ConvertDimtoICDim@102(FromDim@1001 : Code[20]) ICDimCode : Code[20];
    VAR
      Dim@1000 : Record 348;
    BEGIN
      IF Dim.GET(FromDim) THEN
        ICDimCode := Dim."Map-to IC Dimension Code";
    END;

    PROCEDURE ConvertDimValuetoICDimVal@103(FromDim@1000 : Code[20];FromDimValue@1001 : Code[20]) ICDimValueCode : Code[20];
    VAR
      DimValue@1002 : Record 349;
    BEGIN
      IF DimValue.GET(FromDim,FromDimValue) THEN
        ICDimValueCode := DimValue."Map-to IC Dimension Value Code";
    END;

    PROCEDURE CheckICDimValue@113(ICDimCode@1001 : Code[20];ICDimValCode@1000 : Code[20]) : Boolean;
    VAR
      ICDimVal@1002 : Record 412;
    BEGIN
      IF (ICDimCode <> '') AND (ICDimValCode <> '') THEN BEGIN
        IF ICDimVal.GET(ICDimCode,ICDimValCode) THEN BEGIN
          IF ICDimVal.Blocked THEN BEGIN
            DimErr :=
              STRSUBSTNO(
                Text016,ICDimVal.TABLECAPTION,ICDimCode,ICDimValCode);
            EXIT(FALSE);
          END;
          IF NOT (ICDimVal."Dimension Value Type" IN
                  [ICDimVal."Dimension Value Type"::Standard,
                   ICDimVal."Dimension Value Type"::"Begin-Total"])
          THEN BEGIN
            DimErr :=
              STRSUBSTNO(Text017,ICDimVal.FIELDCAPTION("Dimension Value Type"),
                ICDimVal.TABLECAPTION,ICDimCode,ICDimValCode,FORMAT(ICDimVal."Dimension Value Type"));
            EXIT(FALSE);
          END;
        END ELSE BEGIN
          DimErr :=
            STRSUBSTNO(
              Text018,ICDimVal.TABLECAPTION,ICDimCode);
          EXIT(FALSE);
        END;
      END;
      EXIT(TRUE);
    END;

    PROCEDURE CheckICDim@114(ICDimCode@1000 : Code[20]) : Boolean;
    VAR
      ICDim@1001 : Record 411;
    BEGIN
      IF ICDim.GET(ICDimCode) THEN BEGIN
        IF ICDim.Blocked THEN BEGIN
          DimErr :=
            STRSUBSTNO(Text014,ICDim.TABLECAPTION,ICDimCode);
          EXIT(FALSE);
        END;
      END ELSE BEGIN
        DimErr :=
          STRSUBSTNO(Text015,ICDim.TABLECAPTION,ICDimCode);
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE SaveJobTaskDim@39(JobNo@1000 : Code[20];JobTaskNo@1001 : Code[20];FieldNumber@1003 : Integer;ShortcutDimCode@1002 : Code[20]);
    VAR
      JobTaskDim@1004 : Record 1002;
    BEGIN
      GetGLSetup;
      IF ShortcutDimCode <> '' THEN BEGIN
        IF JobTaskDim.GET(JobNo,JobTaskNo,GLSetupShortcutDimCode[FieldNumber])
        THEN BEGIN
          JobTaskDim.VALIDATE("Dimension Value Code",ShortcutDimCode);
          JobTaskDim.MODIFY;
        END ELSE BEGIN
          JobTaskDim.INIT;
          JobTaskDim.VALIDATE("Job No.",JobNo);
          JobTaskDim.VALIDATE("Job Task No.",JobTaskNo);
          JobTaskDim.VALIDATE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
          JobTaskDim.VALIDATE("Dimension Value Code",ShortcutDimCode);
          JobTaskDim.INSERT;
        END;
      END ELSE
        IF JobTaskDim.GET(JobNo,JobTaskNo,GLSetupShortcutDimCode[FieldNumber]) THEN
          JobTaskDim.DELETE;
    END;

    PROCEDURE SaveJobTaskTempDim@46(FieldNumber@1001 : Integer;ShortcutDimCode@1000 : Code[20]);
    BEGIN
      GetGLSetup;
      IF ShortcutDimCode <> '' THEN BEGIN
        IF JobTaskDimTemp.GET('','',GLSetupShortcutDimCode[FieldNumber])
        THEN BEGIN
          JobTaskDimTemp."Dimension Value Code" := ShortcutDimCode;
          JobTaskDimTemp.MODIFY;
        END ELSE BEGIN
          JobTaskDimTemp.INIT;
          JobTaskDimTemp."Dimension Code" := GLSetupShortcutDimCode[FieldNumber];
          JobTaskDimTemp."Dimension Value Code" := ShortcutDimCode;
          JobTaskDimTemp.INSERT;
        END;
      END ELSE
        IF JobTaskDimTemp.GET('','',GLSetupShortcutDimCode[FieldNumber]) THEN
          JobTaskDimTemp.DELETE;
    END;

    PROCEDURE InsertJobTaskDim@54(JobNo@1000 : Code[20];JobTaskNo@1001 : Code[20];VAR GlobalDim1Code@1005 : Code[20];VAR GlobalDim2Code@1004 : Code[20]);
    VAR
      DefaultDim@1002 : Record 352;
      JobTaskDim@1003 : Record 1002;
    BEGIN
      GetGLSetup;
      DefaultDim.SETRANGE("Table ID",DATABASE::Job);
      DefaultDim.SETRANGE("No.",JobNo);
      IF DefaultDim.FINDSET(FALSE,FALSE) THEN
        REPEAT
          IF DefaultDim."Dimension Value Code" <> '' THEN BEGIN
            JobTaskDim.INIT;
            JobTaskDim."Job No." := JobNo;
            JobTaskDim."Job Task No." := JobTaskNo;
            JobTaskDim."Dimension Code" := DefaultDim."Dimension Code";
            JobTaskDim."Dimension Value Code" := DefaultDim."Dimension Value Code";
            JobTaskDim.INSERT;
            IF JobTaskDim."Dimension Code" = GLSetupShortcutDimCode[1] THEN
              GlobalDim1Code := JobTaskDim."Dimension Value Code";
            IF JobTaskDim."Dimension Code" = GLSetupShortcutDimCode[2] THEN
              GlobalDim2Code := JobTaskDim."Dimension Value Code";
          END;
        UNTIL DefaultDim.NEXT = 0;

      JobTaskDimTemp.RESET;
      IF JobTaskDimTemp.FINDSET THEN
        REPEAT
          IF NOT JobTaskDim.GET(JobNo,JobTaskNo,JobTaskDimTemp."Dimension Code") THEN BEGIN
            JobTaskDim.INIT;
            JobTaskDim."Job No." := JobNo;
            JobTaskDim."Job Task No." := JobTaskNo;
            JobTaskDim."Dimension Code" := JobTaskDimTemp."Dimension Code";
            JobTaskDim."Dimension Value Code" := JobTaskDimTemp."Dimension Value Code";
            JobTaskDim.INSERT;
            IF JobTaskDim."Dimension Code" = GLSetupShortcutDimCode[1] THEN
              GlobalDim1Code := JobTaskDim."Dimension Value Code";
            IF JobTaskDim."Dimension Code" = GLSetupShortcutDimCode[2] THEN
              GlobalDim2Code := JobTaskDim."Dimension Value Code";
          END;
        UNTIL JobTaskDimTemp.NEXT = 0;
      JobTaskDimTemp.DELETEALL;
    END;

    LOCAL PROCEDURE UpdateJobTaskDim@15(DefaultDimension@1000 : Record 352;FromOnDelete@1003 : Boolean);
    VAR
      JobTaskDimension@1001 : Record 1002;
      JobTask@1002 : Record 1001;
    BEGIN
      IF DefaultDimension."Table ID" <> DATABASE::Job THEN
        EXIT;

      JobTask.SETRANGE("Job No.",DefaultDimension."No.");
      IF JobTask.ISEMPTY THEN
        EXIT;

      IF NOT CONFIRM(Text019,TRUE) THEN
        EXIT;

      JobTaskDimension.SETRANGE("Job No.",DefaultDimension."No.");
      JobTaskDimension.SETRANGE("Dimension Code",DefaultDimension."Dimension Code");
      JobTaskDimension.DELETEALL(TRUE);

      IF FromOnDelete OR
         (DefaultDimension."Value Posting" = DefaultDimension."Value Posting"::"No Code") OR
         (DefaultDimension."Dimension Value Code" = '')
      THEN
        EXIT;

      IF JobTask.FINDSET THEN
        REPEAT
          CLEAR(JobTaskDimension);
          JobTaskDimension."Job No." := JobTask."Job No.";
          JobTaskDimension."Job Task No." := JobTask."Job Task No.";
          JobTaskDimension."Dimension Code" := DefaultDimension."Dimension Code";
          JobTaskDimension."Dimension Value Code" := DefaultDimension."Dimension Value Code";
          JobTaskDimension.INSERT(TRUE);
        UNTIL JobTask.NEXT = 0;
    END;

    PROCEDURE DeleteJobTaskTempDim@76();
    BEGIN
      JobTaskDimTemp.RESET;
      JobTaskDimTemp.DELETEALL;
    END;

    PROCEDURE CopyJobTaskDimToJobTaskDim@77(JobNo@1000 : Code[20];JobTaskNo@1001 : Code[20];NewJobNo@1002 : Code[20];NewJobTaskNo@1003 : Code[20]);
    VAR
      JobTaskDimension@1004 : Record 1002;
      JobTaskDimension2@1005 : Record 1002;
    BEGIN
      JobTaskDimension.RESET;
      JobTaskDimension.SETRANGE("Job No.",JobNo);
      JobTaskDimension.SETRANGE("Job Task No.",JobTaskNo);
      IF JobTaskDimension.FINDSET THEN
        REPEAT
          IF NOT JobTaskDimension2.GET(NewJobNo,NewJobTaskNo,JobTaskDimension."Dimension Code") THEN BEGIN
            JobTaskDimension2.INIT;
            JobTaskDimension2."Job No." := NewJobNo;
            JobTaskDimension2."Job Task No." := NewJobTaskNo;
            JobTaskDimension2."Dimension Code" := JobTaskDimension."Dimension Code";
            JobTaskDimension2."Dimension Value Code" := JobTaskDimension."Dimension Value Code";
            JobTaskDimension2.INSERT(TRUE);
          END;
        UNTIL JobTaskDimension.NEXT = 0;
    END;

    PROCEDURE CheckDimIDConsistency@10(VAR DimSetEntry@1000 : Record 480;VAR PostedDimSetEntry@1001 : Record 480;DocTableID@1002 : Integer;PostedDocTableID@1003 : Integer) : Boolean;
    BEGIN
      IF DimSetEntry.FINDSET THEN;
      IF PostedDimSetEntry.FINDSET THEN;
      REPEAT
        CASE TRUE OF
          DimSetEntry."Dimension Code" > PostedDimSetEntry."Dimension Code":
            BEGIN
              DocDimConsistencyErr :=
                STRSUBSTNO(
                  Text012,
                  DimSetEntry.FIELDCAPTION("Dimension Code"),
                  ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DocTableID),
                  ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,PostedDocTableID));
              EXIT(FALSE);
            END;
          DimSetEntry."Dimension Code" < PostedDimSetEntry."Dimension Code":
            BEGIN
              DocDimConsistencyErr :=
                STRSUBSTNO(
                  Text012,
                  PostedDimSetEntry.FIELDCAPTION("Dimension Code"),
                  ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,PostedDocTableID),
                  ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DocTableID));
              EXIT(FALSE);
            END;
          DimSetEntry."Dimension Code" = PostedDimSetEntry."Dimension Code":
            BEGIN
              IF DimSetEntry."Dimension Value Code" <> PostedDimSetEntry."Dimension Value Code" THEN BEGIN
                DocDimConsistencyErr :=
                  STRSUBSTNO(
                    Text013,
                    DimSetEntry.FIELDCAPTION("Dimension Value Code"),
                    DimSetEntry.FIELDCAPTION("Dimension Code"),
                    DimSetEntry."Dimension Code",
                    ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DocTableID),
                    ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,PostedDocTableID));
                EXIT(FALSE);
              END;
            END;
        END;
      UNTIL (DimSetEntry.NEXT = 0) AND (PostedDimSetEntry.NEXT = 0);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CreateDimSetEntryFromDimValue@4(DimValue@1000 : Record 349;VAR TempDimSetEntry@1001 : TEMPORARY Record 480);
    BEGIN
      TempDimSetEntry."Dimension Code" := DimValue."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimValue.Code;
      TempDimSetEntry."Dimension Value ID" := DimValue."Dimension Value ID";
      TempDimSetEntry.INSERT;
    END;

    PROCEDURE CreateDimSetIDFromICDocDim@5(VAR ICDocDim@1000 : Record 442) : Integer;
    VAR
      DimValue@1004 : Record 349;
      TempDimSetEntry@1003 : TEMPORARY Record 480;
    BEGIN
      IF ICDocDim.FIND('-') THEN
        REPEAT
          DimValue.GET(
            ConvertICDimtoDim(ICDocDim."Dimension Code"),
            ConvertICDimValuetoDimValue(ICDocDim."Dimension Code",ICDocDim."Dimension Value Code"));
          CreateDimSetEntryFromDimValue(DimValue,TempDimSetEntry);
        UNTIL ICDocDim.NEXT = 0;
      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    PROCEDURE CreateDimSetIDFromICJnlLineDim@7(VAR ICInboxOutboxJnlLineDim@1000 : Record 423) : Integer;
    VAR
      DimValue@1001 : Record 349;
      TempDimSetEntry@1002 : TEMPORARY Record 480;
    BEGIN
      IF ICInboxOutboxJnlLineDim.FIND('-') THEN
        REPEAT
          DimValue.GET(
            ConvertICDimtoDim(ICInboxOutboxJnlLineDim."Dimension Code"),
            ConvertICDimValuetoDimValue(
              ICInboxOutboxJnlLineDim."Dimension Code",ICInboxOutboxJnlLineDim."Dimension Value Code"));
          CreateDimSetEntryFromDimValue(DimValue,TempDimSetEntry);
        UNTIL ICInboxOutboxJnlLineDim.NEXT = 0;
      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    PROCEDURE CopyDimBufToDimSetEntry@65(VAR FromDimBuf@1000 : Record 360;VAR DimSetEntry@1001 : Record 480);
    VAR
      DimValue@1005 : Record 349;
    BEGIN
      WITH FromDimBuf DO
        IF FINDSET THEN
          REPEAT
            DimValue.GET("Dimension Code","Dimension Value Code");
            DimSetEntry."Dimension Code" := "Dimension Code";
            DimSetEntry."Dimension Value Code" := "Dimension Value Code";
            DimSetEntry."Dimension Value ID" := DimValue."Dimension Value ID";
            DimSetEntry.INSERT;
          UNTIL NEXT = 0;
    END;

    PROCEDURE CreateDimSetIDFromDimBuf@12(VAR DimBuf@1000 : Record 360) : Integer;
    VAR
      DimValue@1001 : Record 349;
      TempDimSetEntry@1002 : TEMPORARY Record 480;
    BEGIN
      IF DimBuf.FINDSET THEN
        REPEAT
          DimValue.GET(DimBuf."Dimension Code",DimBuf."Dimension Value Code");
          CreateDimSetEntryFromDimValue(DimValue,TempDimSetEntry);
        UNTIL DimBuf.NEXT = 0;
      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    PROCEDURE GetDimSetIDsForFilter@23(DimCode@1000 : Code[20];DimValueFilter@1001 : Text[250]);
    VAR
      DimSetEntry2@1002 : Record 480;
    BEGIN
      DimSetEntry2.SETFILTER("Dimension Code",'%1',DimCode);
      DimSetEntry2.SETFILTER("Dimension Value Code",DimValueFilter);
      IF DimSetEntry2.FINDSET THEN
        REPEAT
          AddDimSetIDtoTempEntry(TempDimSetEntry2,DimSetEntry2."Dimension Set ID");
        UNTIL DimSetEntry2.NEXT = 0;
      IF FilterIncludesBlank(DimCode,DimValueFilter) THEN
        GetDimSetIDsForBlank(DimCode);
      DimSetFilterCtr += 1;
    END;

    LOCAL PROCEDURE GetDimSetIDsForBlank@27(DimCode@1000 : Code[20]);
    VAR
      TempDimSetEntry@1001 : TEMPORARY Record 480;
      DimSetEntry2@1002 : Record 480;
      PrevDimSetID@1004 : Integer;
      i@1003 : Integer;
    BEGIN
      AddDimSetIDtoTempEntry(TempDimSetEntry,0);
      FOR i := 1 TO 2 DO BEGIN
        IF i = 2 THEN
          DimSetEntry2.SETFILTER("Dimension Code",'%1',DimCode);
        IF DimSetEntry2.FINDSET THEN BEGIN
          PrevDimSetID := 0;
          REPEAT
            IF DimSetEntry2."Dimension Set ID" <> PrevDimSetID THEN BEGIN
              AddDimSetIDtoTempEntry(TempDimSetEntry,DimSetEntry2."Dimension Set ID");
              PrevDimSetID := DimSetEntry2."Dimension Set ID";
            END;
          UNTIL DimSetEntry2.NEXT = 0;
        END;
      END;
      TempDimSetEntry.SETFILTER("Dimension Value ID",'%1',1);
      IF TempDimSetEntry.FINDSET THEN
        REPEAT
          AddDimSetIDtoTempEntry(TempDimSetEntry2,TempDimSetEntry."Dimension Set ID");
        UNTIL TempDimSetEntry.NEXT = 0;
    END;

    PROCEDURE GetNextDimSetFilterChunk@24(Length@1000 : Integer) DimSetFilterChunk : Text[1024];
    VAR
      EndLoop@1002 : Boolean;
    BEGIN
      IF Length > MAXSTRLEN(DimSetFilterChunk) THEN
        Length := MAXSTRLEN(DimSetFilterChunk);
      TempDimSetEntry2.SETFILTER("Dimension Value ID",'%1',DimSetFilterCtr);
      TempDimSetEntry2.SETFILTER("Dimension Set ID",'>%1',LastDimSetIDInFilter);
      IF TempDimSetEntry2.FINDSET THEN BEGIN
        DimSetFilterChunk := FORMAT(TempDimSetEntry2."Dimension Set ID");
        LastDimSetIDInFilter := TempDimSetEntry2."Dimension Set ID";
        WHILE (STRLEN(DimSetFilterChunk) < Length) AND (NOT EndLoop) DO BEGIN
          IF TempDimSetEntry2.NEXT <> 0 THEN BEGIN
            IF STRLEN(DimSetFilterChunk + '|' + FORMAT(TempDimSetEntry2."Dimension Set ID")) <= Length THEN BEGIN
              DimSetFilterChunk += '|' + FORMAT(TempDimSetEntry2."Dimension Set ID");
              LastDimSetIDInFilter := TempDimSetEntry2."Dimension Set ID";
            END ELSE
              EndLoop := TRUE;
          END ELSE
            EndLoop := TRUE;
        END;
      END;
    END;

    LOCAL PROCEDURE FilterIncludesBlank@25(DimCode@1001 : Code[20];DimValueFilter@1000 : Text[250]) : Boolean;
    VAR
      TempDimSetEntry@1002 : TEMPORARY Record 480;
    BEGIN
      TempDimSetEntry."Dimension Code" := DimCode;
      TempDimSetEntry.INSERT;
      TempDimSetEntry.SETFILTER("Dimension Value Code",DimValueFilter);
      EXIT(NOT TempDimSetEntry.ISEMPTY);
    END;

    LOCAL PROCEDURE AddDimSetIDtoTempEntry@30(VAR TempDimSetEntry@1001 : TEMPORARY Record 480;DimSetID@1000 : Integer);
    BEGIN
      IF TempDimSetEntry.GET(DimSetID,'') THEN BEGIN
        TempDimSetEntry."Dimension Value ID" += 1;
        TempDimSetEntry.MODIFY;
      END ELSE BEGIN
        TempDimSetEntry."Dimension Set ID" := DimSetID;
        TempDimSetEntry."Dimension Value ID" := 1;
        TempDimSetEntry.INSERT
      END;
    END;

    PROCEDURE ClearDimSetFilter@26();
    BEGIN
      TempDimSetEntry2.RESET;
      TempDimSetEntry2.DELETEALL;
      DimSetFilterCtr := 0;
      LastDimSetIDInFilter := 0;
    END;

    PROCEDURE GetTempDimSetEntry@31(VAR TempDimSetEntry@1000 : TEMPORARY Record 480);
    BEGIN
      TempDimSetEntry.COPY(TempDimSetEntry2,TRUE);
    END;

    LOCAL PROCEDURE UpdateCostType@28(DefaultDimension@1000 : Record 352;CallingTrigger@1004 : 'OnInsert,OnModify,OnDelete');
    VAR
      GLAcc@1003 : Record 15;
      CostAccSetup@1002 : Record 1108;
      CostAccMgt@1001 : Codeunit 1100;
    BEGIN
      IF CostAccSetup.GET AND (DefaultDimension."Table ID" = DATABASE::"G/L Account") THEN
        IF GLAcc.GET(DefaultDimension."No.") THEN
          CostAccMgt.UpdateCostTypeFromDefaultDimension(DefaultDimension,GLAcc,CallingTrigger);
    END;

    PROCEDURE CreateDimSetFromJobTaskDim@32(JobNo@1006 : Code[20];JobTaskNo@1001 : Code[20];VAR GlobalDimVal1@1005 : Code[20];VAR GlobalDimVal2@1004 : Code[20]) NewDimSetID : Integer;
    VAR
      JobTaskDimension@1000 : Record 1002;
      DimValue@1002 : Record 349;
      TempDimSetEntry@1003 : TEMPORARY Record 480;
    BEGIN
      WITH JobTaskDimension DO BEGIN
        SETRANGE("Job No.",JobNo);
        SETRANGE("Job Task No.",JobTaskNo);
        IF FINDSET THEN BEGIN
          REPEAT
            DimValue.GET("Dimension Code","Dimension Value Code");
            TempDimSetEntry."Dimension Code" := "Dimension Code";
            TempDimSetEntry."Dimension Value Code" := "Dimension Value Code";
            TempDimSetEntry."Dimension Value ID" := DimValue."Dimension Value ID";
            TempDimSetEntry.INSERT(TRUE);
          UNTIL NEXT = 0;
          NewDimSetID := GetDimensionSetID(TempDimSetEntry);
          UpdateGlobalDimFromDimSetID(NewDimSetID,GlobalDimVal1,GlobalDimVal2);
        END;
      END;
    END;

    PROCEDURE UpdateGenJnlLineDim@34(VAR GenJnlLine@1000 : Record 81;DimSetID@1001 : Integer);
    BEGIN
      GenJnlLine."Dimension Set ID" := DimSetID;
      //NC NOS-26 > SPA
      // UpdateGlobalDimFromDimSetID(
      //  GenJnlLine."Dimension Set ID",
      //  GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");
      UpdateGlobalJnlDimFromDimSetID(GenJnlLine."Dimension Set ID",
        GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code",GenJnlLine."Financial Instrument No.");
      //NC NOS-26 < SPA
    END;

    PROCEDURE UpdateGenJnlLineDimFromCustLedgEntry@33(VAR GenJnlLine@1000 : Record 81;DtldCustLedgEntry@1001 : Record 379);
    VAR
      CustLedgEntry@1002 : Record 21;
    BEGIN
      IF DtldCustLedgEntry."Cust. Ledger Entry No." <> 0 THEN BEGIN
        CustLedgEntry.GET(DtldCustLedgEntry."Cust. Ledger Entry No.");
        UpdateGenJnlLineDim(GenJnlLine,CustLedgEntry."Dimension Set ID");
      END;
    END;

    PROCEDURE UpdateGenJnlLineDimFromVendLedgEntry@29(VAR GenJnlLine@1001 : Record 81;DtldVendLedgEntry@1000 : Record 380);
    VAR
      VendLedgEntry@1002 : Record 25;
    BEGIN
      IF DtldVendLedgEntry."Vendor Ledger Entry No." <> 0 THEN BEGIN
        VendLedgEntry.GET(DtldVendLedgEntry."Vendor Ledger Entry No.");
        UpdateGenJnlLineDim(GenJnlLine,VendLedgEntry."Dimension Set ID");
      END;
    END;

    PROCEDURE GetDimSetEntryDefaultDim@45(VAR DimSetEntry2@1001 : Record 480);
    VAR
      DimValue@1000 : Record 349;
    BEGIN
      IF NOT DimSetEntry2.ISEMPTY THEN
        DimSetEntry2.DELETEALL;
      IF TempDimBuf2.FINDSET THEN
        REPEAT
          DimValue.GET(TempDimBuf2."Dimension Code",TempDimBuf2."Dimension Value Code");
          DimSetEntry2."Dimension Code" := TempDimBuf2."Dimension Code";
          DimSetEntry2."Dimension Value Code" := TempDimBuf2."Dimension Value Code";
          DimSetEntry2."Dimension Value ID" := DimValue."Dimension Value ID";
          DimSetEntry2.INSERT;
        UNTIL TempDimBuf2.NEXT = 0;
      TempDimBuf2.RESET;
      TempDimBuf2.DELETEALL;
    END;

    PROCEDURE InsertObject@35(VAR TempAllObjWithCaption@1000 : TEMPORARY Record 2000000058;TableID@1001 : Integer);
    VAR
      AllObjWithCaption@1002 : Record 2000000058;
    BEGIN
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Table);
      AllObjWithCaption.SETRANGE("Object ID",TableID);
      IF AllObjWithCaption.FINDFIRST THEN BEGIN
        TempAllObjWithCaption := AllObjWithCaption;
        TempAllObjWithCaption.INSERT;
      END;
    END;

    PROCEDURE GetConsolidatedDimFilterByDimFilter@37(VAR Dimension@1000 : Record 348;DimFilter@1001 : Text) ConsolidatedDimFilter : Text;
    BEGIN
      Dimension.SETFILTER("Consolidation Code",DimFilter);
      ConsolidatedDimFilter += DimFilter;
      IF Dimension.FINDSET THEN
        REPEAT
          ConsolidatedDimFilter += '|' + Dimension.Code;
        UNTIL Dimension.NEXT = 0;
    END;

    [Integration]
    PROCEDURE OnAfterSetupObjectNoList@2(VAR TempAllObjWithCaption@1000 : TEMPORARY Record 2000000058);
    BEGIN
    END;

    LOCAL PROCEDURE "//NCF>"@1101495000();
    BEGIN
    END;

    PROCEDURE GetDimensionSetIDFromCompany@1101495003(VAR DimSetEntry2@1000 : Record 480;CompanyToUse@1101495000 : Text[30]) : Integer;
    VAR
      DimSetEntryToGetID@1101495001 : Record 480;
    BEGIN
      //NC NOS-26 > SPA
      DimSetEntryToGetID.SetNewCompanyToUse(CompanyToUse);
      EXIT(DimSetEntryToGetID.GetDimensionSetID(DimSetEntry2));
      //NC NOS-26 < SPA
    END;

    PROCEDURE GetDefaultDimID_Jnl@1101495012(TableID@1000 : ARRAY [10] OF Integer;No@1001 : ARRAY [10] OF Code[20];SourceCode@1002 : Code[20];VAR GlobalDim1Code@1003 : Code[20];VAR GlobalDim2Code@1004 : Code[20];InheritFromDimSetID@1014 : Integer;InheritFromTableNo@1015 : Integer;JnlTemplateName@1101495000 : Code[10];JnlBatchName@1101495001 : Code[10]) : Integer;
    VAR
      DimVal@1012 : Record 349;
      DefaultDimPriority1@1005 : Record 354;
      DefaultDimPriority2@1006 : Record 354;
      DefaultDim@1007 : Record 352;
      TempDimSetEntry@1011 : TEMPORARY Record 480;
      TempDimSetEntry0@1016 : TEMPORARY Record 480;
      i@1010 : Integer;
      j@1009 : Integer;
      NoFilter@1008 : ARRAY [2] OF Code[20];
      NewDimSetID@1013 : Integer;
    BEGIN
      //NC NOS-26 > SPA
      // Измерения по умолчанию для журналов
      GetGLSetup;
      IF InheritFromDimSetID > 0 THEN
        GetDimensionSet(TempDimSetEntry0,InheritFromDimSetID);
      TempDimBuf2.RESET;
      TempDimBuf2.DELETEALL;
      IF TempDimSetEntry0.FINDSET THEN
        REPEAT
          TempDimBuf2.INIT;
          TempDimBuf2."Table ID" := InheritFromTableNo;
          TempDimBuf2."Entry No." := 0;
          TempDimBuf2."Dimension Code" := TempDimSetEntry0."Dimension Code";
          TempDimBuf2."Dimension Value Code" := TempDimSetEntry0."Dimension Value Code";
          TempDimBuf2.INSERT;
        UNTIL TempDimSetEntry0.NEXT = 0;

      NoFilter[2] := '';
      FOR i := 1 TO ARRAYLEN(TableID) DO BEGIN
        IF (TableID[i] <> 0) AND (No[i] <> '') THEN BEGIN
          DefaultDim.SETRANGE("Table ID",TableID[i]);
          NoFilter[1] := No[i];
          FOR j := 1 TO 2 DO BEGIN
            DefaultDim.SETRANGE("No.",NoFilter[j]);
            IF DefaultDim.FINDSET THEN
              REPEAT
                IF (DefaultDim."Dimension Value Code" <> '')
                  //NC MDP01 > MP
                  OR ((DefaultDim."Dimension Value Code" = '') AND (DefaultDim."Value Posting" <> DefaultDim."Value Posting"::" "))
                  //NC MDP01 < MP
                THEN BEGIN
                  TempDimBuf2.SETRANGE("Dimension Code",DefaultDim."Dimension Code");
                  IF NOT TempDimBuf2.FINDFIRST THEN BEGIN
                    TempDimBuf2.INIT;
                    TempDimBuf2."Table ID" := DefaultDim."Table ID";
                    TempDimBuf2."Entry No." := 0;
                    TempDimBuf2."Dimension Code" := DefaultDim."Dimension Code";
                    TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                    TempDimBuf2.INSERT;
                  END ELSE BEGIN
                    IF DefaultDimPriority1.GET(SourceCode,DefaultDim."Table ID") THEN BEGIN
                      IF DefaultDimPriority2.GET(SourceCode,TempDimBuf2."Table ID") THEN BEGIN
                        IF DefaultDimPriority1.Priority < DefaultDimPriority2.Priority THEN BEGIN
                          TempDimBuf2.DELETE;
                          TempDimBuf2."Table ID" := DefaultDim."Table ID";
                          TempDimBuf2."Entry No." := 0;
                          TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                          TempDimBuf2.INSERT;
                        END;
                      END ELSE BEGIN
                        TempDimBuf2.DELETE;
                        TempDimBuf2."Table ID" := DefaultDim."Table ID";
                        TempDimBuf2."Entry No." := 0;
                        TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                        TempDimBuf2.INSERT;
                      END;
                    END
                    //NC MDP01.1 > EB
                     ELSE
                      IF DefaultDim."Dimension Value Code" <> '' THEN BEGIN
                        TempDimBuf2.DELETE;
                        TempDimBuf2."Table ID" := DefaultDim."Table ID";
                        TempDimBuf2."Entry No." := 0;
                        TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                        TempDimBuf2.INSERT;
                      END;
                    //NC MDP01.1 < EB
                  END;
                  IF GLSetupShortcutDimCode[1] = TempDimBuf2."Dimension Code" THEN
                    GlobalDim1Code := TempDimBuf2."Dimension Value Code";
                  IF GLSetupShortcutDimCode[2] = TempDimBuf2."Dimension Code" THEN
                    GlobalDim2Code := TempDimBuf2."Dimension Value Code";
                END;
              UNTIL DefaultDim.NEXT = 0;
          END;
        END;
      END;
      TempDimBuf2.RESET;
      IF TempDimBuf2.FINDSET THEN BEGIN
        REPEAT
          IF DimVal.GET(TempDimBuf2."Dimension Code",TempDimBuf2."Dimension Value Code") THEN BEGIN
            TempDimSetEntry."Dimension Code" := TempDimBuf2."Dimension Code";
            IF TempDimSetEntry.FIND THEN
              TempDimSetEntry.DELETE;
            TempDimSetEntry."Dimension Value Code" := TempDimBuf2."Dimension Value Code";
            TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
            TempDimSetEntry.INSERT;

            //NC NCS-1457 > DP
            // NC NOS-26 SPA -- перенесено с изменениями
            //fn_FindGlobalDimValue(TableID,JnlTemplateName,JnlBatchName,JnlLineNo,AllocationLineNo,JnlLineDim,GlobalDim1Code,GlobalDim2Code);
            fn_GetLinkDim(TempDimSetEntry."Dimension Code",
                          TempDimSetEntry."Dimension Value Code");
            fn_UpdateJnlLineLinkDim(TempDimSetEntry,JnlTemplateName,JnlBatchName);
            //NC NCS-1457 < DP
          END;
        UNTIL TempDimBuf2.NEXT = 0;
        NewDimSetID := GetDimensionSetID(TempDimSetEntry);
        UpdateGlobalDimFromDimSetID(NewDimSetID,GlobalDim1Code,GlobalDim2Code);
      END;
      EXIT(NewDimSetID);
      //NC NOS-26 < SPA
    END;

    PROCEDURE ValidateShortcutDimValues_Jnl@1101495008(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20];VAR DimSetID@1004 : Integer;GenJournalLine@1101495000 : Record 81);
    VAR
      DimVal@1002 : Record 349;
      TempDimSetEntry@1005 : TEMPORARY Record 480;
    BEGIN
      //NC NOS-26 > SPA
      // использовать вместо функций fn_SaveTempDim и fn_SaveJnlLineDim
      // вызывается только из 81 таблицы

      ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimVal."Dimension Code" := GLSetupShortcutDimCode[FieldNumber];
      IF ShortcutDimCode <> '' THEN BEGIN
        DimVal.GET(DimVal."Dimension Code",ShortcutDimCode);
        IF NOT CheckDim(DimVal."Dimension Code") THEN
          ERROR(GetDimErr);
        IF NOT CheckDimValue(DimVal."Dimension Code",ShortcutDimCode) THEN
          ERROR(GetDimErr);
        IF NOT fn_CheckDimValue(TempDimSetEntry."Dimension Code",TempDimSetEntry."Dimension Value Code",GenJournalLine."Posting Date") THEN
          ERROR(GetDimErr);
      END;

      GetDimensionSet(TempDimSetEntry,DimSetID);
      IF TempDimSetEntry.GET(TempDimSetEntry."Dimension Set ID",DimVal."Dimension Code") THEN
        IF TempDimSetEntry."Dimension Value Code" <> ShortcutDimCode THEN
          TempDimSetEntry.DELETE;
      IF ShortcutDimCode <> '' THEN BEGIN
        TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
        IF TempDimSetEntry.FIND THEN
          TempDimSetEntry.DELETE;
        TempDimSetEntry."Dimension Value Code" := DimVal.Code;
        TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
        CheckAccountDim(GenJournalLine,TempDimSetEntry);
        TempDimSetEntry.INSERT;
      END;
      DimSetID := GetDimensionSetID(TempDimSetEntry);
      //NC NOS-26 < SPA
    END;

    PROCEDURE CheckAccountDim@1101495013(GenJournalLine@1101495000 : Record 81;DimSetEntry@1101495004 : Record 480);
    VAR
      GLSetup@1101495001 : Record 98;
      GenJnlLineLoc@1101495002 : Record 81;
      AccDim@1101495003 : Record 50030;
    BEGIN
      //NC MDTC07 > MP
      //NC NOS-26 SPA : перенесено из таблицы 356 NAV5.0
      GLSetup.GET;
      IF NOT GLSetup."Check Account Dimension" THEN
        EXIT;

      WITH GenJournalLine DO
        IF NOT GenJnlLineLoc.GET("Journal Template Name","Journal Batch Name","Line No.") THEN
          EXIT;

      IF (GenJnlLineLoc."Account Type" = GenJnlLineLoc."Account Type"::"G/L Account") AND
         (GenJnlLineLoc."Account No." <> '')
      THEN BEGIN
        AccDim.SETRANGE("Account No.", GenJnlLineLoc."Account No.");
        AccDim.SETRANGE("Dimension Code", DimSetEntry."Dimension Code");
        IF AccDim.FINDFIRST THEN BEGIN
          IF NOT AccDim.GET(GenJnlLineLoc."Account No.", DimSetEntry."Dimension Code", DimSetEntry."Dimension Value Code") THEN
            ERROR(Text50100, GenJnlLineLoc."Account No.");
        END;
      END;

      IF (GenJnlLineLoc."Bal. Account Type" = GenJnlLineLoc."Bal. Account Type"::"G/L Account") AND
         (GenJnlLineLoc."Bal. Account No." <> '')
      THEN BEGIN
        AccDim.SETRANGE("Account No.", GenJnlLineLoc."Bal. Account No.");
        AccDim.SETRANGE("Dimension Code", DimSetEntry."Dimension Code");
        IF AccDim.FINDFIRST THEN BEGIN
          IF NOT AccDim.GET(GenJnlLineLoc."Bal. Account No.", DimSetEntry."Dimension Code", DimSetEntry."Dimension Value Code") THEN
            ERROR(Text50100, GenJnlLineLoc."Bal. Account No.");
        END;
      END;
    END;

    PROCEDURE UpdateGlobalJnlDimFromDimSetID@1101495017(DimSetID@1000 : Integer;VAR GlobalDimVal1@1001 : Code[20];VAR GlobalDimVal2@1002 : Code[20];VAR GlobalDimVal20@1101495000 : Code[20]);
    BEGIN
      //NC NOS-26 SPA
      // обновление глобальных измерений, включая ФИ
      GetGLSetup;
      GlobalDimVal1 := '';
      GlobalDimVal2 := '';
      GlobalDimVal20 := '';
      IF GLSetupShortcutDimCode[1] <> '' THEN
        IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[1]) THEN
          GlobalDimVal1 := DimSetEntry."Dimension Value Code";
      IF GLSetupShortcutDimCode[2] <> '' THEN
        IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[2]) THEN
          GlobalDimVal2 := DimSetEntry."Dimension Value Code";
      IF GLSetupShortcutDimCode[20] <> '' THEN
        IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[20]) THEN
          GlobalDimVal20 := DimSetEntry."Dimension Value Code";
    END;

    PROCEDURE EditDimensionSet3@1101495020(DimSetID@1000 : Integer;NewCaption@1003 : Text[250];VAR GlobalDimVal1@1006 : Code[20];VAR GlobalDimVal2@1005 : Code[20];VAR GlobalDimVal20@1101495000 : Code[20];TableID@1101495002 : ARRAY [10] OF Integer;No@1101495001 : ARRAY [10] OF Code[20]) : Integer;
    VAR
      EditDimSetEntries@1002 : Page 480;
      NewDimSetID@1004 : Integer;
      TempDimSetEntry@1101495003 : TEMPORARY Record 480;
    BEGIN
      // NC NOS-26 SPA
      NewDimSetID := DimSetID;
      GetDimensionSet(TempDimSetEntry,DimSetID);
      GetEmptyDimFromDefaultDim(TempDimSetEntry,TableID,No);

      EditDimSetEntries.SetFormCaption(NewCaption);
      EditDimSetEntries.SetObjectFrom(GlobalObject);
      EditDimSetEntries.SetRec(TempDimSetEntry);
      EditDimSetEntries.RUNMODAL;
      NewDimSetID := EditDimSetEntries.GetDimensionID;
      UpdateGlobalJnlDimFromDimSetID(NewDimSetID,GlobalDimVal1,GlobalDimVal2,GlobalDimVal20);
      DimSetEntry.RESET;
      EXIT(NewDimSetID);
    END;

    PROCEDURE EditDimensionSet4@1101495014(DimSetID@1000 : Integer;NewCaption@1003 : Text[250];TableID@1101495002 : ARRAY [10] OF Integer;No@1101495001 : ARRAY [10] OF Code[20]) : Integer;
    VAR
      EditDimSetEntries@1002 : Page 480;
      NewDimSetID@1004 : Integer;
      TempDimSetEntry@1101495000 : TEMPORARY Record 480;
    BEGIN
      // NC NOS-26 SPA
      NewDimSetID := DimSetID;
      GetDimensionSet(TempDimSetEntry,DimSetID);
      GetEmptyDimFromDefaultDim(TempDimSetEntry,TableID,No);

      EditDimSetEntries.SetFormCaption(NewCaption);
      EditDimSetEntries.SetObjectFrom(GlobalObject);
      EditDimSetEntries.SetRec(TempDimSetEntry);
      EditDimSetEntries.RUNMODAL;
      NewDimSetID := EditDimSetEntries.GetDimensionID;
      DimSetEntry.RESET;
      EXIT(NewDimSetID);
    END;

    PROCEDURE GetEmptyDimFromDefaultDim@1101495010(VAR TempDimSetEntry@1101495000 : Record 480;TableID@1101495002 : ARRAY [10] OF Integer;No@1101495001 : ARRAY [10] OF Code[20]);
    VAR
      i@1101495003 : Integer;
    BEGIN
      // NC NOS-26 SPA
      // добавить пустые аналитики
      DefaultDim.SETFILTER("Value Posting",'<>%1',DefaultDim."Value Posting"::" ");
      DefaultDim.SETFILTER("Dimension Value Code",'=%1','');
      FOR i := 1 TO ARRAYLEN(TableID) DO
        IF No[i] <> '' THEN BEGIN
          DefaultDim.SETRANGE("Table ID",TableID[i]);
          DefaultDim.SETRANGE("No.",No[i]);
          IF DefaultDim.FINDSET THEN REPEAT
            IF NOT TempDimSetEntry.GET(TempDimSetEntry."Dimension Set ID",DefaultDim."Dimension Code") THEN BEGIN
              TempDimSetEntry."Dimension Code" := DefaultDim."Dimension Code";
              TempDimSetEntry."Dimension Value Code" := '';
              TempDimSetEntry.INSERT;
            END;
          UNTIL DefaultDim.NEXT = 0;
        END;
      DefaultDim.RESET;
    END;

    PROCEDURE "NCF>"@1000000001();
    BEGIN
    END;

    PROCEDURE FillLinkedDimensions@1000000005(TableID@1000000000 : Integer;JnlTemplate@1210001 : Code[20];JnlBatch@1210002 : Code[10];LineNo@1000000001 : Integer;DimCode@1000000002 : Code[20];DimValue@1000000003 : Code[20];VAR TempDimSetEntry@1101495002 : TEMPORARY Record 480) i : Integer;
    VAR
      LinkedDimensionValue@1000000004 : Record 50016;
      GLSetup@1101495000 : Record 98;
      GenJnl@1000000005 : Record 81;
      DimensionValue@1101495001 : Record 349;
    BEGIN
      //NC PIF010 > MP

      // BMN 29.11.2011 >
      // Для проводок из внешних систем необходимо сохранять оригинальные значения измерений
      IF TableID = 81 THEN BEGIN
        GenJnl.RESET;
        GenJnl.SETRANGE("Journal Template Name", JnlTemplate);
        GenJnl.SETRANGE("Journal Batch Name", JnlBatch);
        GenJnl.SETRANGE("Line No.", LineNo);
        IF GenJnl.FINDFIRST THEN
          IF GenJnl."Buffer Entry No." <> 0 THEN
            EXIT;
      END;
      // BMN 29.11.2011 <

      LinkedDimensionValue.SETRANGE("Dimension Code",DimCode);
      LinkedDimensionValue.SETRANGE("Dimension Value",DimValue);
      LinkedDimensionValue.SETFILTER(Blocked,'%1',FALSE);
      LinkedDimensionValue.SETFILTER("Value Selection",'%1',FALSE);
      IF LinkedDimensionValue.FINDSET THEN
        REPEAT
          //NC NOS-26 > SPA
          TempDimSetEntry.SETRANGE("Dimension Code", LinkedDimensionValue."Linked Dimension Code");
          IF LinkedDimensionValue."Linked Dimension Value" <> '' THEN
            TempDimSetEntry.SETRANGE("Dimension Value Code", LinkedDimensionValue."Linked Dimension Value");
          IF TempDimSetEntry.ISEMPTY THEN BEGIN
            TempDimSetEntry."Dimension Code" := LinkedDimensionValue."Linked Dimension Code";
            IF TempDimSetEntry.FIND THEN
              TempDimSetEntry.DELETE;
            IF LinkedDimensionValue."Linked Dimension Value" <> '' THEN
              TempDimSetEntry."Dimension Value Code" := LinkedDimensionValue."Linked Dimension Value";
            IF DimensionValue.GET(LinkedDimensionValue."Linked Dimension Code", LinkedDimensionValue."Linked Dimension Value") THEN
              TempDimSetEntry."Dimension Value ID" := DimensionValue."Dimension Value ID";
            TempDimSetEntry.INSERT;
          END;
          TempDimSetEntry.SETRANGE("Dimension Code");
          TempDimSetEntry.SETRANGE("Dimension Value Code");
          //NC NOS-26 < SPA
        UNTIL LinkedDimensionValue.NEXT=0;
      //NC PIF010 < MP
    END;

    PROCEDURE FillDocLinkedDimensions@1000000004(TableID@1000000000 : Integer;DocType@1210001 : Integer;DocNo@1210002 : Code[20];LineNo@1000000001 : Integer;DimCode@1000000002 : Code[20];DimValue@1000000003 : Code[20];VAR TempDimSetEntry@1101495000 : TEMPORARY Record 480) i : Integer;
    VAR
      LinkedDimensionValue@1000000004 : Record 50016;
      DimensionValue@1101495001 : Record 349;
    BEGIN
      //NC PIF010 > MP
      LinkedDimensionValue.SETRANGE("Dimension Code",DimCode);
      LinkedDimensionValue.SETRANGE("Dimension Value",DimValue);
      LinkedDimensionValue.SETFILTER(Blocked,'%1',FALSE);
      LinkedDimensionValue.SETFILTER("Value Selection",'%1',FALSE);
      IF LinkedDimensionValue.FINDSET(FALSE,FALSE) THEN
        REPEAT
          //NC NOS-26 > SPA
          TempDimSetEntry.SETRANGE("Dimension Code", LinkedDimensionValue."Linked Dimension Code");
          IF LinkedDimensionValue."Linked Dimension Value" <> '' THEN
            TempDimSetEntry.SETRANGE("Dimension Value Code", LinkedDimensionValue."Linked Dimension Value");
          IF TempDimSetEntry.ISEMPTY THEN BEGIN
            IF TempDimSetEntry.FIND THEN
              TempDimSetEntry.DELETE;
            TempDimSetEntry."Dimension Code" := LinkedDimensionValue."Linked Dimension Code";
            IF LinkedDimensionValue."Linked Dimension Value" <> '' THEN
              TempDimSetEntry."Dimension Value Code" := LinkedDimensionValue."Linked Dimension Value";
            IF DimensionValue.GET(LinkedDimensionValue."Linked Dimension Code", LinkedDimensionValue."Linked Dimension Value") THEN
              TempDimSetEntry."Dimension Value ID" := DimensionValue."Dimension Value ID";
            TempDimSetEntry.INSERT;
          END;
          TempDimSetEntry.SETRANGE("Dimension Code");
          TempDimSetEntry.SETRANGE("Dimension Value Code");
          //NC NOS-26 , SPA
        UNTIL LinkedDimensionValue.NEXT=0;
      //NC PIF010 < MP
    END;

    PROCEDURE InsertRFPDim@1101495009(InsertGlobalDim@1101495000 : Boolean);
    BEGIN
      //NCS HF4 > AB
      InsertGlobalDimension := InsertGlobalDim;
    END;

    PROCEDURE SetObjectFrom@1101495011(Object@1101495000 : Variant);
    BEGIN
      //NC NOS-26 SPA
      GlobalObject := Object;
    END;

    PROCEDURE "NCSF<"@1000000002();
    BEGIN
    END;

    PROCEDURE fn_GetLinkDim@1101495001(p_codeDim@1101495001 : Code[20];p_codeDimValue@1101495000 : Code[20]);
    VAR
      DefaultDimPriority1@1101495010 : Record 354;
      DefaultDimPriority2@1101495009 : Record 354;
      DefaultDim@1101495008 : Record 352;
      l_recLinkDimValue@1101495007 : Record 50016;
      i@1101495006 : Integer;
      j@1101495005 : Integer;
      NoFilter@1101495004 : ARRAY [2] OF Code[20];
    BEGIN
      // TDRANCS-897 >
      GetGLSetup;
      gt_recTempDimBuf.RESET;
      gt_recTempDimBuf.DELETEALL;

      l_recLinkDimValue.SETRANGE("Dimension Code",p_codeDim);
      l_recLinkDimValue.SETRANGE("Dimension Value",p_codeDimValue);
      l_recLinkDimValue.SETFILTER("Linked Dimension Code",'<>%1','');
      l_recLinkDimValue.SETFILTER("Linked Dimension Value",'<>%1','');
      l_recLinkDimValue.SETRANGE(Blocked,FALSE);
      l_recLinkDimValue.SETRANGE("Value Selection",FALSE);

      IF l_recLinkDimValue.FINDSET THEN BEGIN
        REPEAT
          gt_recTempDimBuf.INIT;
          gt_recTempDimBuf."Table ID" := DATABASE::"Gen. Journal Line";
          gt_recTempDimBuf."Entry No." := 0;
          gt_recTempDimBuf."Dimension Code" := l_recLinkDimValue."Linked Dimension Code";
          gt_recTempDimBuf."Dimension Value Code" := l_recLinkDimValue."Linked Dimension Value";
          gt_recTempDimBuf.INSERT;
          //NC NOS-26 > SPA
          {
          IF (GLSetupShortcutDimCode[1] = gt_recTempDimBuf."Dimension Code") AND
             (GlobalDim1Code = '')
          THEN
            GlobalDim1Code := gt_recTempDimBuf."Dimension Value Code";
          IF (GLSetupShortcutDimCode[2] = gt_recTempDimBuf."Dimension Code") AND
             (GlobalDim2Code = '')
          THEN
            GlobalDim2Code := gt_recTempDimBuf."Dimension Value Code";
          }
          //NC NOS-26 < SPA
        UNTIL l_recLinkDimValue.NEXT = 0;
      END;

      gt_recTempDimBuf.RESET;
    END;

    PROCEDURE fn_UpdateJnlLineLinkDim@1101495006(VAR TempDimSetEntry@1101495000 : TEMPORARY Record 480;JnlTemplateName@1101495004 : Code[20];JnlBatchName@1101495005 : Code[20]) r_boolUpdated : Boolean;
    VAR
      GenJournalBatch@1101495006 : Record 232;
      ConsolidationSetup@1101495007 : Record 50000;
      BuffTemp@1101495001 : TEMPORARY Record 480;
    BEGIN
      //NC NOS-26 > SPA
      // в функцию передаётся ссылка на временную таблицу с измерениями
      IF NOT TempDimSetEntry.ISTEMPORARY THEN
        EXIT(FALSE);

      BuffTemp := TempDimSetEntry;
      GetGLSetup;
      ConsolidationSetup.GET;
      IF gt_recTempDimBuf.FINDSET THEN BEGIN
        REPEAT
          TempDimSetEntry.SETRANGE("Dimension Code", gt_recTempDimBuf."Dimension Code");
          IF TempDimSetEntry.ISEMPTY THEN BEGIN
            TempDimSetEntry.VALIDATE("Dimension Code", gt_recTempDimBuf."Dimension Code");
            TempDimSetEntry.VALIDATE("Dimension Value Code", gt_recTempDimBuf."Dimension Value Code");
            TempDimSetEntry.INSERT;
            r_boolUpdated := TRUE;
          END ELSE BEGIN
            TempDimSetEntry.FINDFIRST;
            IF TempDimSetEntry."Dimension Value Code" = '' THEN BEGIN
              TempDimSetEntry.DELETE;

              TempDimSetEntry.INIT;
              TempDimSetEntry."Dimension Code" := gt_recTempDimBuf."Dimension Code";
              TempDimSetEntry.VALIDATE("Dimension Value Code", gt_recTempDimBuf."Dimension Value Code");
              TempDimSetEntry.INSERT;
              r_boolUpdated := TRUE;
            END ELSE BEGIN
              IF GenJournalBatch.GET(JnlTemplateName, JnlBatchName) AND
                 (GenJournalBatch."Batch Type" = GenJournalBatch."Batch Type"::Manual) AND
                 (TempDimSetEntry."Dimension Code" = ConsolidationSetup."IC Dimension Code")
              THEN BEGIN
                TempDimSetEntry.DELETE;

                TempDimSetEntry.INIT;
                TempDimSetEntry."Dimension Code" := gt_recTempDimBuf."Dimension Code";
                TempDimSetEntry.VALIDATE("Dimension Value Code", gt_recTempDimBuf."Dimension Value Code");
                TempDimSetEntry.INSERT;
                r_boolUpdated := TRUE;
              END;
            END;
          END;
        UNTIL gt_recTempDimBuf.NEXT = 0;

        gt_recTempDimBuf.RESET;
        gt_recTempDimBuf.DELETEALL;
      END;
      TempDimSetEntry.SETRANGE("Dimension Code");
      TempDimSetEntry := BuffTemp;
      EXIT(r_boolUpdated);

      {
      GetGLSetup;
      IF gt_recTempDimBuf.FINDSET THEN BEGIN
        REPEAT
          IF NOT l_recJnlLineDim.GET(TableID,
                                     JnlTemplateName,
                                     JnlBatchName,
                                     JnlLineNo,
                                     AllocationLineNo,
                                     gt_recTempDimBuf."Dimension Code")
                                    THEN BEGIN
            JnlLineDim.INIT;
            JnlLineDim.VALIDATE("Table ID",TableID);
            JnlLineDim.VALIDATE("Journal Template Name",JnlTemplateName);
            JnlLineDim.VALIDATE("Journal Batch Name",JnlBatchName);
            JnlLineDim.VALIDATE("Journal Line No.",JnlLineNo);
            JnlLineDim.VALIDATE("Allocation Line No.",AllocationLineNo);
            JnlLineDim."Dimension Code" := gt_recTempDimBuf."Dimension Code";
            JnlLineDim."Dimension Value Code" := gt_recTempDimBuf."Dimension Value Code";
            JnlLineDim."New Dimension Value Code" := gt_recTempDimBuf."New Dimension Value Code";
            JnlLineDim.INSERT;
            r_boolUpdated := TRUE;
            RecRef.GETTABLE(JnlLineDim);
            ChangeLogMgt.LogInsertion(RecRef);
            IF (JnlLineDim."Dimension Code" = GLSetupShortcutDimCode[1]) THEN
              GlobalDim1Code := JnlLineDim."Dimension Value Code";
            IF (JnlLineDim."Dimension Code" = GLSetupShortcutDimCode[2]) THEN
              GlobalDim2Code := JnlLineDim."Dimension Value Code";
          END
          //NC R1074 > PCH
          ELSE IF l_recJnlLineDim."Dimension Value Code" = '' THEN BEGIN
            l_recJnlLineDim."Dimension Value Code" := gt_recTempDimBuf."Dimension Value Code";
            l_recJnlLineDim."New Dimension Value Code" := gt_recTempDimBuf."New Dimension Value Code";
            l_recJnlLineDim.MODIFY;
            r_boolUpdated := TRUE;
            RecRef.GETTABLE(l_recJnlLineDim);
            ChangeLogMgt.LogInsertion(RecRef);
            IF (l_recJnlLineDim."Dimension Code" = GLSetupShortcutDimCode[1]) THEN
              GlobalDim1Code := l_recJnlLineDim."Dimension Value Code";
            IF (l_recJnlLineDim."Dimension Code" = GLSetupShortcutDimCode[2]) THEN
              GlobalDim2Code := l_recJnlLineDim."Dimension Value Code";
          END
          //NC R1074 < PCH

          //TDRANCS-1616.1>
          ELSE BEGIN

            IF l_recJnlBatch.GET(JnlTemplateName,JnlBatchName) AND
               (l_recJnlBatch."Batch Type"=l_recJnlBatch."Batch Type"::Manual) AND
               (gt_recTempDimBuf."Dimension Code" = ConsSetup."IC Dimension Code") THEN BEGIN
              l_recJnlLineDim."Dimension Value Code" := gt_recTempDimBuf."Dimension Value Code";
              l_recJnlLineDim."New Dimension Value Code" := gt_recTempDimBuf."New Dimension Value Code";
              l_recJnlLineDim.MODIFY;
              r_boolUpdated := TRUE;
              RecRef.GETTABLE(l_recJnlLineDim);
              ChangeLogMgt.LogInsertion(RecRef);
            END;

          END;
          //TDRANCS-1616.1<

        UNTIL gt_recTempDimBuf.NEXT = 0;
        gt_recTempDimBuf.RESET;
        gt_recTempDimBuf.DELETEALL;
      END;
      }
      //NC NOS-26 , SPA
    END;

    PROCEDURE fn_CheckGlobalDim@1000000006(DimSetID@1000000002 : Integer;GenJournalLine@1101495000 : Record 81) : Boolean;
    VAR
      l_recGenJnlLine@1000000003 : Record 81;
      DimensionSetEntry@1101495002 : Record 480;
    BEGIN
      //TDRANCS-897 >

      // NC NOS-26 SPA Функция переработана из-за проблем с производительностью при учете
      //      Вместо многократных GET фин. журнала запись передается в функцию извне

      GetGLSetup;
      IF DimensionSetEntry.GET(DimSetID, GLSetupShortcutDimCode[1]) THEN
        IF DimensionSetEntry."Dimension Value Code" <> GenJournalLine."Shortcut Dimension 1 Code" THEN
          EXIT(FALSE);
      IF DimensionSetEntry.GET(DimSetID, GLSetupShortcutDimCode[2]) THEN
        IF DimensionSetEntry."Dimension Value Code" <> GenJournalLine."Shortcut Dimension 2 Code" THEN
          EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE fn_CheckDimValue@1000000011(DimCode@1000 : Code[20];DimValCode@1001 : Code[20];p_datePosting@1000000000 : Date) : Boolean;
    VAR
      DimVal@1002 : Record 349;
    BEGIN
      //TDRANCS-1071 >
      IF (DimCode <> '') AND (DimValCode <> '') THEN BEGIN
        IF DimVal.GET(DimCode,DimValCode) THEN BEGIN
          IF DimVal.Blocked THEN BEGIN
            IF p_datePosting <> 0D THEN BEGIN
              IF (DimVal."Allow Posting From"  = 0D) AND (DimVal."Allow Posting To" = 0D) THEN BEGIN
                DimErr :=
                  STRSUBSTNO(
                    Text016,DimVal.TABLECAPTION,DimCode,DimValCode);
                EXIT(FALSE);
              END;
              IF DimVal."Allow Posting To" = 0D THEN
                DimVal."Allow Posting To" := 31129999D;
              IF (p_datePosting < DimVal."Allow Posting From")
                  OR (p_datePosting > DimVal."Allow Posting To") THEN BEGIN
                DimErr :=
                  STRSUBSTNO(
                    g_Text50002,DimVal.TABLECAPTION,DimCode,DimValCode,DimVal."Allow Posting From",DimVal."Allow Posting To");
                EXIT(FALSE);
              END;
            END ELSE BEGIN
              DimErr :=
                STRSUBSTNO(
                  Text016,DimVal.TABLECAPTION,DimCode,DimValCode);
              EXIT(FALSE);
            END;
          END;
          IF NOT (DimVal."Dimension Value Type" IN
          //NC TR233 > DP
          {
            [DimVal."Dimension Value Type"::Standard,
             DimVal."Dimension Value Type"::"Begin-Total"])
          }
            [DimVal."Dimension Value Type"::Standard])
          //NC TR233 > DP
          THEN BEGIN
            DimErr :=
              STRSUBSTNO(Text017,DimVal.FIELDCAPTION("Dimension Value Type"),
              DimVal.TABLECAPTION,DimCode,DimValCode,FORMAT(DimVal."Dimension Value Type"));
            EXIT(FALSE);
          END;
        END ELSE BEGIN
          DimErr :=
            STRSUBSTNO(
              //NC S1753 > PCH
              //Text018,DimVal.TABLECAPTION,DimCode);
              Text018,DimVal.TABLECAPTION,DimCode +', '+ DimValCode);
              //NC S1753 < PCH
          EXIT(FALSE);
        END;
      END;
      EXIT(TRUE);
    END;

    PROCEDURE fn_CheckJnlLineDimValuePosting@1101495002(TableID@1101495002 : ARRAY [10] OF Integer;No@1101495001 : ARRAY [10] OF Code[20];DimSetID@1101495000 : Integer;p_datePosting@1101495003 : Date) : Boolean;
    VAR
      i@1101495017 : Integer;
    BEGIN
      // TDRANCS-1071> for func Reverse in cu17

      // NC NOS-26 SPA Функция переработана из-за проблем с производительностью при учете
      //    Добавлены доп. функции, которые ранее вызывалиь здесь : fn_CheckRealCounterparty, fn_CheckDimWithJnlBatchParameters

      TempDimBuf2.RESET;
      TempDimBuf2.DELETEALL;

      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", DimSetID);
      IF DimSetEntry.FINDSET THEN BEGIN
        i := 1;
        REPEAT
          IF NOT fn_CheckDimValue(DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code",p_datePosting) THEN BEGIN
            DimValuePostingErr := DimErr;
            EXIT(FALSE);
          END;

          IF NOT CheckDim(DimSetEntry."Dimension Code") THEN BEGIN
            DimValuePostingErr := DimErr;
            EXIT(FALSE);
          END;

          TempDimBuf2.INIT;
          TempDimBuf2."Entry No." := i;
          TempDimBuf2."Table ID" := TableID[1];
          TempDimBuf2."Dimension Code" := DimSetEntry."Dimension Code";
          TempDimBuf2."Dimension Value Code" := DimSetEntry."Dimension Value Code";
          TempDimBuf2.INSERT;
          i := i + 1;
        UNTIL DimSetEntry.NEXT = 0;
      END;

      EXIT(CheckValuePosting(TableID,No));
    END;

    PROCEDURE fn_CheckAgrDimValuePosting@1101495004(p_intTableID@1101495000 : ARRAY [10] OF Integer;p_codeSourceNo@1101495001 : Code[20];p_codeNo@1101495002 : ARRAY [10] OF Code[20];DimSetID@1101495007 : Integer) : Boolean;
    VAR
      l_recDefaultDimAdv@1101495006 : Record 50128;
      i@1101495005 : Integer;
      j@1101495004 : Integer;
      l_txtNoFilter@1101495003 : ARRAY [2] OF Text[250];
    BEGIN
      //TDRANCS-1806>
      l_recDefaultDimAdv.SETRANGE(l_recDefaultDimAdv."Source No.",p_codeSourceNo);
      l_recDefaultDimAdv.SETFILTER("Value Posting",'<>%1',l_recDefaultDimAdv."Value Posting"::" ");

      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);

      l_txtNoFilter[2] := '';
      FOR i := 1 TO ARRAYLEN(p_intTableID) DO BEGIN
        IF (p_intTableID[i] <> 0) AND (p_codeNo[i] <> '') THEN BEGIN
          l_recDefaultDimAdv.SETRANGE("Table ID",p_intTableID[i]);
          l_txtNoFilter[1] := p_codeNo[i];
          FOR j := 1 TO 2 DO BEGIN
            l_recDefaultDimAdv.SETRANGE("No.",l_txtNoFilter[j]);
            IF l_recDefaultDimAdv.FINDSET THEN BEGIN
              REPEAT
                DimSetEntry.SETRANGE("Dimension Code",l_recDefaultDimAdv."Dimension Code");
                CASE l_recDefaultDimAdv."Value Posting" OF
                  l_recDefaultDimAdv."Value Posting"::"Code Mandatory":
                    BEGIN
                      IF (NOT DimSetEntry.FINDFIRST) OR
                         (DimSetEntry."Dimension Value Code" = '')
                      THEN BEGIN
                        IF l_recDefaultDimAdv."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text004,
                              l_recDefaultDimAdv.FIELDCAPTION("Dimension Value Code"),
                              l_recDefaultDimAdv.FIELDCAPTION("Dimension Code"),l_recDefaultDimAdv."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text005,
                              l_recDefaultDimAdv.FIELDCAPTION("Dimension Value Code"),
                              l_recDefaultDimAdv.FIELDCAPTION("Dimension Code"),
                              l_recDefaultDimAdv."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,l_recDefaultDimAdv."Table ID"),
                              l_recDefaultDimAdv."No.");
                        EXIT(FALSE);
                      END;
                    END;
                  l_recDefaultDimAdv."Value Posting"::"Same Code":
                    BEGIN
                      IF (l_recDefaultDimAdv."Dimension Value Code" <> '') THEN BEGIN
                        IF (NOT DimSetEntry.FINDFIRST) OR
                           (l_recDefaultDimAdv."Dimension Value Code" <> DimSetEntry."Dimension Value Code")
                        THEN BEGIN
                          IF l_recDefaultDimAdv."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text006,
                                l_recDefaultDimAdv.FIELDCAPTION("Dimension Value Code"),l_recDefaultDimAdv."Dimension Value Code",
                                l_recDefaultDimAdv.FIELDCAPTION("Dimension Code"),l_recDefaultDimAdv."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text007,
                                l_recDefaultDimAdv.FIELDCAPTION("Dimension Value Code"),
                                l_recDefaultDimAdv."Dimension Value Code",
                                l_recDefaultDimAdv.FIELDCAPTION("Dimension Code"),
                                l_recDefaultDimAdv."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,l_recDefaultDimAdv."Table ID"),
                                l_recDefaultDimAdv."No.");
                          EXIT(FALSE);
                        END;
                      END ELSE BEGIN
                        IF DimSetEntry.FINDFIRST THEN BEGIN
                          IF l_recDefaultDimAdv."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text008,
                                DimSetEntry.FIELDCAPTION("Dimension Code"),DimSetEntry."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text009,
                                DimSetEntry.FIELDCAPTION("Dimension Code"),
                                DimSetEntry."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,l_recDefaultDimAdv."Table ID"),
                                l_recDefaultDimAdv."No.");
                          EXIT(FALSE);
                        END;
                      END;
                    END;
                  l_recDefaultDimAdv."Value Posting"::"No Code":
                    BEGIN
                      IF DimSetEntry.FINDFIRST THEN BEGIN
                        IF l_recDefaultDimAdv."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text010,
                              DimSetEntry.FIELDCAPTION("Dimension Code"),DimSetEntry."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text011,
                              DimSetEntry.FIELDCAPTION("Dimension Code"),
                              DimSetEntry."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,l_recDefaultDimAdv."Table ID"),
                              l_recDefaultDimAdv."No.");
                        EXIT(FALSE);
                      END;
                    END;
                END;
              UNTIL l_recDefaultDimAdv.NEXT = 0;
            END;
          END;
        END;
      END;
      EXIT(TRUE);

      //TDRANCS-1806<
    END;

    PROCEDURE fn_CheckMapDimValue@1101495005(p_codeMappingCode@1101495004 : Code[20];p_codeDimCode@1101495005 : Code[20];p_codeDimVal@1101495006 : Code[20];p_codeDimCode2@1101495007 : Code[20];p_codeDimVal2@1101495008 : Code[20];VAR p_txtErrMessage@1101495014 : Text[255]) : Boolean;
    VAR
      l_recConvDimensions@1101495000 : Record 50003;
      l_blnIsError@1101495001 : Boolean;
      l_blnIsErrorMap@1101495002 : Boolean;
      l_txtErrMessage@1101495003 : Text[255];
      Text50701@1101495011 : TextConst 'ENU="For the value ";RUS="Для значения "';
      Text50702@1101495010 : TextConst 'ENU=" of ";RUS=" измерения "';
      Text50703@1101495009 : TextConst 'ENU=" dimension ";RUS=" не задано преобразование."';
      Text50704@1101495012 : TextConst 'ENU=" dimension has been used the value ";RUS=" использовано значение "';
      Text50705@1101495013 : TextConst 'ENU=" dimension without corresponding in mapping.";RUS=,  не соответствующее преобразованию измерений.';
    BEGIN
      //NC NCS-2277 > DP
      l_txtErrMessage :='';
      l_blnIsError := FALSE;
      l_recConvDimensions.SETCURRENTKEY("Company Name","Source Dimension Code","Source Dimension Value Code");
      l_recConvDimensions.SETRANGE("Mapping Code",p_codeMappingCode);
      l_recConvDimensions.SETRANGE("Company Name",COMPANYNAME);
      l_recConvDimensions.SETRANGE("Source Dimension Code",p_codeDimCode);
      l_recConvDimensions.SETRANGE("Source Dimension Value Code",p_codeDimVal);
      l_recConvDimensions.SETRANGE("Receiver Dimension Code",p_codeDimCode2);
      IF NOT l_recConvDimensions.FINDFIRST THEN BEGIN
        l_recConvDimensions.SETRANGE("Company Name",'');
        IF NOT l_recConvDimensions.FINDFIRST THEN BEGIN
            l_txtErrMessage := Text50701 + FORMAT(p_codeDimVal) + Text50702 + FORMAT(p_codeDimCode) + Text50703;
            l_blnIsError := TRUE;
          END ELSE BEGIN
            IF (l_recConvDimensions."Receiver Dimension Value Code" = '') THEN BEGIN
              l_txtErrMessage := Text50701 + FORMAT(p_codeDimVal) + Text50702 + FORMAT(p_codeDimCode) + Text50703;
              l_blnIsError := TRUE;
            END ELSE BEGIN
              IF p_codeDimVal2<>l_recConvDimensions."Receiver Dimension Value Code" THEN BEGIN
                l_txtErrMessage := Text50701 + FORMAT(p_codeDimVal) + Text50702 + FORMAT(p_codeDimCode) + Text50704 +
                                      FORMAT(p_codeDimVal2) + Text50702 + FORMAT(p_codeDimCode2) + Text50705;
                l_blnIsError := TRUE;
              END;
            END;
          END;
        END;

      p_txtErrMessage := l_txtErrMessage;
      EXIT(l_blnIsError);
      //NC NCS-2277 < DP
    END;

    PROCEDURE fn_CheckRealCounterparty@1101495015(DimSetID@1101495001 : Integer;AccNo@1101495000 : Code[20]) : Boolean;
    VAR
      GLAccount@1101495006 : Record 15;
      MovDSetup@1101495005 : Record 50064;
      DimSetEntry@1101495004 : Record 480;
      Ctr@1101495003 : Code[20];
      RealCtr@1101495002 : Code[20];
    BEGIN
      //NC NOS-26 > SPA
      IF GLAccount.GET(AccNo) THEN
        IF GLAccount."Real Counterparty Value" = GLAccount."Real Counterparty Value"::"Same Value" THEN BEGIN
          MovDSetup.GET;
          DimSetEntry.SETRANGE("Dimension Set ID", DimSetID);
          DimSetEntry.SETRANGE("Dimension Code",MovDSetup."Customer Dimension Code");
          IF DimSetEntry.FINDFIRST THEN
            Ctr := DimSetEntry."Dimension Value Code";

          DimSetEntry.SETRANGE("Dimension Code",MovDSetup."Real Counterparty Dim. Code");
          IF DimSetEntry.FINDFIRST THEN
            RealCtr := DimSetEntry."Dimension Value Code";

          IF (RealCtr <> '')  AND (RealCtr <> Ctr) THEN BEGIN
            DimValuePostingErr :=
              STRSUBSTNO(g_Text50003,
                MovDSetup."Customer Dimension Code",Ctr,
                MovDSetup."Real Counterparty Dim. Code",RealCtr,GLAccount."No.");
            EXIT(FALSE);
          END;
        END;

      EXIT(TRUE);
      //NC NOS-26 < SPA
    END;

    PROCEDURE fn_CheckDimForManualJnlBatch@1101495022(DimSetID@1101495000 : Integer) : Boolean;
    VAR
      DimSetEntry@1101495002 : Record 480;
      LinkDimValue@1101495005 : Record 50016;
      MovDSetup@1101495006 : Record 50064;
      Ctr@1101495003 : Code[20];
      IC@1101495004 : Code[20];
      Book@1101495009 : Code[20];
      CC@1101495010 : Code[20];
      MappingCode@1101495007 : Code[20];
      ErrMessage@1101495008 : Text[255];
    BEGIN
      //NC NOS-26 > SPA
      GetGLSetup;
      DimSetEntry.SETRANGE("Dimension Set ID", DimSetID);
      IF (NOT g_recUserSetup."Data Admin") AND (NOT DimSetEntry.ISEMPTY) THEN BEGIN
        DimSetEntry.SETRANGE("Dimension Code",ConsSetup."Counterparty Dim Code");
        IF DimSetEntry.FINDFIRST THEN
          Ctr := DimSetEntry."Dimension Value Code"
        ELSE
          Ctr := '';

        DimSetEntry.SETRANGE("Dimension Code",ConsSetup."IC Dimension Code");
        IF DimSetEntry.FINDFIRST THEN
          IC := DimSetEntry."Dimension Value Code"
        ELSE
          IC := '';

        IF ((Ctr <> '') OR (IC <> '')) THEN BEGIN
          CASE TRUE OF
            (Ctr <> '') AND (IC = ''):
              IF LinkDimValue.GET(ConsSetup."Counterparty Dim Code",Ctr,ConsSetup."IC Dimension Code") THEN BEGIN
                DimValuePostingErr :=
                  STRSUBSTNO(g_Text50004,
                    ConsSetup."IC Dimension Code",
                    LinkDimValue."Linked Dimension Value");
                EXIT(FALSE);
              END;

            (Ctr <> '') AND (IC <> ''): BEGIN
              IF LinkDimValue.GET(ConsSetup."Counterparty Dim Code",Ctr,ConsSetup."IC Dimension Code") THEN BEGIN
                IF IC <> LinkDimValue."Linked Dimension Value" THEN BEGIN
                  DimValuePostingErr :=
                    STRSUBSTNO(g_Text50004,
                      ConsSetup."IC Dimension Code",
                      LinkDimValue."Linked Dimension Value");
                  EXIT(FALSE);
                END;
              END ELSE BEGIN
                DimValuePostingErr :=
                  STRSUBSTNO(g_Text50005,
                    ConsSetup."IC Dimension Code");
                EXIT(FALSE);
              END;
            END;

            (Ctr = '') AND (IC <> ''): BEGIN
              DimValuePostingErr :=
                STRSUBSTNO(g_Text50005,
                  ConsSetup."IC Dimension Code");
              EXIT(FALSE);
            END;
          END;
        END;
      END;

      IF ConsSetup."CostCenter Mapping Code" <> '' THEN BEGIN
        MovDSetup.GET;
        ErrMessage := '';
        MappingCode := ConsSetup."CostCenter Mapping Code";
        DimSetEntry.SETRANGE("Dimension Code");
        IF (NOT g_recUserSetup."Data Admin") AND (NOT DimSetEntry.ISEMPTY) THEN BEGIN
          DimSetEntry.SETRANGE("Dimension Code",MovDSetup."BOOK Dimension Code");
          IF DimSetEntry.FINDFIRST THEN
            Book := DimSetEntry."Dimension Value Code"
          ELSE
            Book := '';

          DimSetEntry.SETRANGE("Dimension Code",MovDSetup."PROFIT CENTER Dimension Code");
          IF DimSetEntry.FINDFIRST THEN
            CC := DimSetEntry."Dimension Value Code"
          ELSE
            CC := '';

          IF ((Book <> '') OR (CC <> '')) THEN BEGIN
            CASE TRUE OF
              (Book <> '') AND (CC = ''):
                IF fn_CheckMapDimValue(MappingCode,MovDSetup."BOOK Dimension Code",Book,
                  MovDSetup."PROFIT CENTER Dimension Code",CC,ErrMessage)
                THEN BEGIN
                    DimValuePostingErr := STRSUBSTNO(ErrMessage);
                    EXIT(FALSE);
                END;

              (Book <> '') AND (CC <> ''):
                BEGIN
                  IF fn_CheckMapDimValue(MappingCode,MovDSetup."BOOK Dimension Code",Book,
                    MovDSetup."PROFIT CENTER Dimension Code",CC,ErrMessage)
                  THEN BEGIN
                      DimValuePostingErr := STRSUBSTNO(ErrMessage);
                      EXIT(FALSE);
                  END;
                END;

              (Book = '') AND (CC <> ''):
                BEGIN
                END;
            END;
          END;
        END;
      END;

      EXIT(TRUE);
      //NC NOS-26 < SPA
    END;

    PROCEDURE fn_ColorBlockDim@1101495007(p_DimSetId@1101495001 : Integer;p_datePosting@1101495000 : Date) : Text;
    VAR
      l_DimensionSetEntry@1101495002 : Record 480;
      l_DimensionValue@1101495003 : Record 349;
    BEGIN
      //NC NOS-26 > ENK
      {
      //TDRANCS-1101 >
      IF (DimCode <> '') AND (DimValCode <> '') THEN BEGIN
        IF l_recDimVal.GET(DimCode,DimValCode) THEN BEGIN
          IF l_recDimVal.Blocked THEN BEGIN

            IF p_datePosting <> 0D THEN BEGIN
              IF (l_recDimVal."Allow Posting From"  = 0D) AND (l_recDimVal."Allow Posting To" = 0D) THEN
                EXIT(255);

              IF l_recDimVal."Allow Posting To" = 0D THEN
                l_recDimVal."Allow Posting To" := 31129999D;

              IF (p_datePosting < l_recDimVal."Allow Posting From")
                  OR (p_datePosting > l_recDimVal."Allow Posting To") THEN
                EXIT(255)
              ELSE
                EXIT(16711680);
            END ELSE
              EXIT(255);

          END;
        END;
      END;
      EXIT(0);
      }
      //TDRANCS-1101 <
      IF (p_DimSetId  <> 0) THEN BEGIN
        l_DimensionSetEntry.SETRANGE("Dimension Set ID", p_DimSetId);
        IF l_DimensionSetEntry.FINDSET THEN BEGIN
          REPEAT
            IF l_DimensionValue.GET(l_DimensionSetEntry."Dimension Code", l_DimensionSetEntry."Dimension Value Code") THEN BEGIN
              IF l_DimensionValue.Blocked THEN BEGIN
                IF p_datePosting <> 0D THEN BEGIN
                  IF (l_DimensionValue."Allow Posting From"  = 0D) AND (l_DimensionValue."Allow Posting To" = 0D) THEN
                    EXIT('Attention');
                  IF l_DimensionValue."Allow Posting To" = 0D THEN
                    l_DimensionValue."Allow Posting To" := 31129999D;
                  IF (p_datePosting < l_DimensionValue."Allow Posting From")
                      OR (p_datePosting > l_DimensionValue."Allow Posting To") THEN
                    EXIT('Attention')
                  ELSE
                    EXIT('StandardAccent');
                END ELSE
                  EXIT('Attention');
              END;
            END;
          UNTIL l_DimensionSetEntry.NEXT = 0;
        END;
      END;
      EXIT('Standard');
      //NC NOS-26 < ENK
    END;

    BEGIN
    {
      NC NOS-26 ENK
        Copy function from table 356 - fn_ColorBlockDim.
    }
    END.
  }
}

