OBJECT Report 50223 Modify Dimension Value
{
  OBJECT-PROPERTIES
  {
    Date=07.04.17;
    Time=17:57:41;
    Modified=Yes;
    Version List=NOS-26;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT DelDim THEN BEGIN
                    DimValue.MODIFY(TRUE);

                    // SPA : Изменение логируется автоматически
                    //g_RecRef.GETTABLE(DimValue);
                    //g_cuChangLogMgt.fn_LogModification(g_RecRef, g_xRecRef, COMPANYNAME);
                  END ELSE
                    IF CONFIRM(Text008,FALSE,DimValue.Code,DimValue.Name) THEN BEGIN
                      IF (DimValue."Dimension Value Type" <> DimValue."Dimension Value Type"::Standard) AND
                         (DimValue.Totaling <> '')
                      THEN BEGIN
                        InsertDimValue.SETRANGE("Dimension Code",DimValue."Dimension Code");
                        InsertDimValue.SETFILTER(Code,'%1',DimValue.Totaling);

                        IF NOT InsertDimValue.FINDFIRST THEN
                          DimValue.DELETE(TRUE)
                        ELSE
                          ERROR(Text009,DimValue.Code,DimValue.Name,InsertDimValue.COUNT)
                      END ELSE
                        DimValue.DELETE(TRUE);
                    END;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               BUVisible := TRUE;
               TotVisible := TRUE;
               HPCVisible := TRUE;
             END;

      OnOpenPage=BEGIN
                   GLSetup.GET;

                   IF GLSetup."Global Dimension 1 Code" = DimValue."Dimension Code" THEN BEGIN
                     BUVisible := FALSE;
                     TotVisible := FALSE;
                   END ELSE BEGIN
                     HPCVisible := FALSE;
                   END;

                   OldHPC := DimValue."Hierarchy Parent Code";
                 END;

    }
    CONTROLS
    {
      { 1101495000;;Container;
                  ContainerType=ContentArea }

      { 1101495001;1;Group  ;
                  CaptionML=RUS=Редактирование измерения;
                  GroupType=Group }

      { 1101495002;2;Field  ;
                  CaptionML=RUS=Код;
                  SourceExpr=DimValue.Code;
                  Editable=FALSE }

      { 1101495004;2;Field  ;
                  CaptionML=RUS=Удалить;
                  SourceExpr=DelDim }

      { 1101495003;2;Field  ;
                  CaptionML=RUS=Название;
                  SourceExpr=DimValue.Name }

      { 1101495005;2;Field  ;
                  CaptionML=RUS=Тип Измерения;
                  SourceExpr=TypeDim;
                  OnValidate=BEGIN
                               DimValue.VALIDATE("Dimension Value Type",TypeDim);
                             END;
                              }

      { 1101495006;2;Field  ;
                  CaptionML=RUS=Группировка;
                  SourceExpr=TotalDim;
                  Visible=TotVisible;
                  Enabled=TotVisible;
                  OnValidate=BEGIN
                               DimValue.VALIDATE(DimValue.Totaling,TotalDim);
                             END;
                              }

      { 1101495007;2;Field  ;
                  CaptionML=RUS=BUSINESS.UNIT;
                  SourceExpr=DimValue."Parent Code";
                  Visible=BUVisible;
                  Enabled=BUVisible;
                  OnValidate=BEGIN
                               DimValue.VALIDATE("Parent Code");
                             END;

                  OnLookup=VAR
                             DimensionValuesOverview@1101495000 : Page 50294;
                             DimValueView@1101495001 : Record 349;
                           BEGIN
                             IF BUVisible THEN BEGIN
                               DimensionValuesOverview.LOOKUPMODE := TRUE;
                               DimensionValuesOverview.SetFilter('','BUSINESS.UNIT');
                               IF DimensionValuesOverview.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                 DimensionValuesOverview.GETRECORD(DimValueView);
                                 IF DimValueView."Dimension Value Type" <> DimValueView."Dimension Value Type"::Standard THEN
                                   ERROR(Text005);

                                 DimBU := DimValueView.Code;
                                 DimValue.VALIDATE("Parent Code",DimBU);
                               END;
                             END;
                           END;
                            }

      { 1101495008;2;Field  ;
                  CaptionML=RUS=Иерархия Родитель;
                  SourceExpr=DimValue."Hierarchy Parent Code";
                  Visible=HPCVisible;
                  Enabled=HPCVisible;
                  OnValidate=BEGIN
                               DimValue.VALIDATE("Hierarchy Parent Code");
                             END;

                  OnLookup=VAR
                             DimensionValuesOverview@1101495001 : Page 50294;
                             DimValueView@1101495000 : Record 349;
                           BEGIN
                             IF GLSetup."Global Dimension 1 Code" <> DimValue."Dimension Code" THEN BEGIN
                               IF HPCVisible THEN BEGIN
                                 DimensionValuesOverview.LOOKUPMODE := TRUE;
                                 DimensionValuesOverview.SetFilter('', 'BUSINESS.UNIT');

                                 IF DimensionValuesOverview.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                   DimensionValuesOverview.GETRECORD(DimValueView);
                                   DimBU := DimValueView.Code;
                                   DimValue.VALIDATE("Hierarchy Parent Code", DimBU);
                                 END;
                               END;
                             END;
                           END;
                            }

      { 1101495009;2;Field  ;
                  CaptionML=RUS=Блокировка;
                  SourceExpr=DimValue.Blocked;
                  OnValidate=BEGIN
                               DimValue.VALIDATE(Blocked);

                               IF DimValue.Blocked = TRUE THEN
                                 MESSAGE(g_Text010, DimValue.FIELDCAPTION("Allow Posting From"), DimValue.FIELDCAPTION("Allow Posting To"))
                               ELSE BEGIN
                                 DimValue."Allow Posting From" := 0D;
                                 DimValue."Allow Posting To" := 0D;
                               END;
                             END;
                              }

      { 1101495010;2;Field  ;
                  CaptionML=RUS=Разрешить Учет От;
                  SourceExpr=DimValue."Allow Posting From" }

      { 1101495011;2;Field  ;
                  CaptionML=RUS=Разрешить Учет До;
                  SourceExpr=DimValue."Allow Posting To" }

      { 1101495012;2;Field  ;
                  CaptionML=RUS=Родитель Код Измерения;
                  SourceExpr=DimValue."Parent Dimension Code";
                  OnValidate=BEGIN
                               IF DimValue."Parent Dimension Code" = '' THEN
                                 DimValue."Parent Code" := '';
                             END;
                              }

      { 1101495013;2;Field  ;
                  CaptionML=RUS=Код Элемента Родителя;
                  SourceExpr=DimValue."Parent Code";
                  OnLookup=VAR
                             l_recDimValue@1101495000 : Record 349;
                           BEGIN
                             l_recDimValue.SETRANGE("Dimension Code", DimValue."Parent Dimension Code");
                             IF PAGE.RUNMODAL(PAGE::"Dimension Value List", l_recDimValue) = ACTION::LookupOK THEN
                               DimValue."Parent Code" := l_recDimValue.Code;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DimValue@1101495025 : Record 349;
      g_RecRef@1101495023 : RecordRef;
      g_xRecRef@1101495022 : RecordRef;
      BusinessUnit@1101495017 : Code[2];
      DimBU@1101495015 : Code[20];
      InsertDimValue@1101495012 : Record 349;
      DimName@1101495011 : Text[50];
      TypeDim@1101495009 : 'Стандартное,Заголовок,Сумма,От-Сумма,До-Сумма';
      TotalDim@1101495008 : Text[250];
      DelDim@1101495004 : Boolean;
      GLSetup@1101495003 : Record 98;
      OldHPC@1101495002 : Code[20];
      g_cuChangLogMgt@1101495000 : Codeunit 423;
      BUVisible@1101495026 : Boolean INDATASET;
      TotVisible@1101495027 : Boolean INDATASET;
      HPCVisible@1101495028 : Boolean INDATASET;
      Text001@1101495038 : TextConst 'RUS=Статья затрат не должна быть пустой!';
      Text002@1101495037 : TextConst 'RUS=Значение измерения BUSINESS.UNIT должно быть заполнено!';
      Text003@1101495036 : TextConst 'RUS="Для Значения Измерения %1 установлено Значение BUSINESS.UNIT = %2. Продолжить создание новой записи?"';
      Text004@1101495035 : TextConst 'RUS=Действие прервано пользователем.';
      Text005@1101495034 : TextConst 'RUS="Измерение должно иметь Значение Тип = Стандартое!"';
      Text006@1101495033 : TextConst 'RUS=Выберите вначале значение измерения BUSINESS.UNIT.';
      Text007@1101495032 : TextConst 'RUS="Измерение не должно иметь Значение Тип = Стандартое!"';
      Text008@1101495031 : TextConst 'RUS=Вы уверены, что хотите удалить измерение %1 %2?';
      Text009@1101495030 : TextConst 'RUS=Уровень %1 %2 включает в себя %3 элемента!';
      g_Text010@1101495029 : TextConst 'ENU=Check or setup value in the fields %1, %2;RUS=Проверьте или установите значения в полях %1, %2';

    PROCEDURE SetParam@1000000001(DimCode@1000000000 : Code[20];DimCodeValue@1000000001 : Code[20]);
    BEGIN
      IF DimValue.GET(DimCode,DimCodeValue) THEN;
      //g_xRecRef.GETTABLE(DimValue);
      TypeDim := DimValue."Dimension Value Type";
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

