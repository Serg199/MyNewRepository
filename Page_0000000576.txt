OBJECT Page 576 VAT Specification Subform
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               RUS=Строки];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table290;
    PageType=ListPart;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             InvoiceDiscountAmountEditable := TRUE;
             VATAmountEditable := TRUE;
           END;

    OnAfterGetRecord=BEGIN
                       IF MainFormActiveTab = MainFormActiveTab::Other THEN
                         VATAmountEditable := AllowVATDifference AND NOT "Includes Prepayment"
                       ELSE
                         VATAmountEditable := AllowVATDifference;
                       InvoiceDiscountAmountEditable := AllowInvDisc AND NOT "Includes Prepayment";
                     END;

    OnModifyRecord=BEGIN
                     ModifyRec;
                     EXIT(FALSE);
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the contents of this field from the VAT Identifier field in the VAT Posting Setup table.;
                           RUS=Определяет содержимое этого поля из поля "Идентификатор НДС" таблицы "Настройка учета НДС".];
                SourceExpr="VAT Identifier";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the VAT percentage that was used on the sales or purchase lines with this VAT Identifier.;
                           RUS=Определяет процент НДС, который использовался в строках продажи или покупки с этим идентификатором НДС.];
                SourceExpr="VAT %" }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=This field is used internally.;
                           RUS=Это поле предназначено для внутреннего использования.];
                SourceExpr="VAT Calculation Type";
                Visible=FALSE }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the total amount for sales or purchase lines with a specific VAT identifier.;
                           RUS=Определяет общую сумму по строкам продажи или покупки с конкретным идентификатором НДС.];
                SourceExpr="Line Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the invoice discount base amount.;
                           RUS=Определяет базовую сумму скидки по счету.];
                SourceExpr="Inv. Disc. Base Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the invoice discount amount for a specific VAT identifier.;
                           RUS=Определяет сумму скидки по счету для конкретного идентификатора НДС.];
                SourceExpr="Invoice Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE;
                Editable=InvoiceDiscountAmountEditable;
                OnValidate=BEGIN
                             CalcVATFields(CurrencyCode,PricesIncludingVAT,VATBaseDiscPct);
                             ModifyRec;
                           END;
                            }

    { 16  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the total net amount (amount excluding VAT) for sales or purchase lines with a specific VAT Identifier.;
                           RUS=Определяет общую чистую сумму (без НДС) строк продажи или покупки с конкретным идентификатором НДС.];
                SourceExpr="VAT Base";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode }

    { 20  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the total of the VAT amounts on sales or purchase lines with a specific VAT Identifier.;
                           RUS=Определяет итог по суммам НДС из строк продажи или покупки с конкретным идентификатором НДС.];
                SourceExpr="VAT Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Editable=VATAmountEditable;
                OnValidate=BEGIN
                             IF AllowVATDifference AND NOT AllowVATDifferenceOnThisTab THEN
                               IF ParentControl = PAGE::"Service Order Statistics" THEN
                                 ERROR(Text000,FIELDCAPTION("VAT Amount"),Text002)
                               ELSE
                                 ERROR(Text000,FIELDCAPTION("VAT Amount"),Text003);

                             IF PricesIncludingVAT THEN
                               "VAT Base" := "Amount Including VAT" - "VAT Amount"
                             ELSE
                               "Amount Including VAT" := "VAT Amount" + "VAT Base";

                             FormCheckVATDifference;
                             ModifyRec;
                           END;
                            }

    { 24  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the calculated VAT amount and is only used for reference when the user changes the VAT Amount manually.;
                           RUS=Определяет расчетную сумму НДС и используется только в справочных целях при корректировке значения "Сумма НДС" вручную.];
                SourceExpr="Calculated VAT Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE }

    { 26  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the VAT Difference for one VAT Identifier.;
                           RUS=Определяет разницу НДС для одного идентификатора НДС.];
                SourceExpr="VAT Difference";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE }

    { 22  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the total of the amounts in the amount fields on the sales or purchase lines with a specific VAT Identifier, including VAT.;
                           RUS=Определяет итог по всем полям суммы строк продажи или покупки с конкретным идентификатором НДС. В эту сумму включен НДС.];
                SourceExpr="Amount Including VAT";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                OnValidate=BEGIN
                             FormCheckVATDifference;
                           END;
                            }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 can only be modified on the %2 tab.;RUS=%1 можно изменить только на вкладке %2.';
      Text001@1001 : TextConst 'ENU=The total %1 for a document must not exceed the value %2 in the %3 field.;RUS=Итоговое значение %1 документа не должно превышать значение %2 в поле %3.';
      Currency@1003 : Record 4;
      ServHeader@1011 : Record 5900;
      CurrencyCode@1004 : Code[10];
      AllowVATDifference@1005 : Boolean;
      AllowVATDifferenceOnThisTab@1006 : Boolean;
      PricesIncludingVAT@1007 : Boolean;
      AllowInvDisc@1008 : Boolean;
      VATBaseDiscPct@1009 : Decimal;
      ParentControl@1010 : Integer;
      Text002@1012 : TextConst 'ENU=Details;RUS=Подробности';
      Text003@1013 : TextConst 'ENU=Invoicing;RUS=Счет';
      CurrentTabNo@1002 : Integer;
      MainFormActiveTab@1014 : 'Other,Prepayment';
      VATAmountEditable@19075252 : Boolean INDATASET;
      InvoiceDiscountAmountEditable@19042140 : Boolean INDATASET;

    PROCEDURE SetTempVATAmountLine@1(VAR NewVATAmountLine@1000 : Record 290);
    BEGIN
      DELETEALL;
      IF NewVATAmountLine.FIND('-') THEN
        REPEAT
          COPY(NewVATAmountLine);
          INSERT;
        UNTIL NewVATAmountLine.NEXT = 0;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE GetTempVATAmountLine@5(VAR NewVATAmountLine@1000 : Record 290);
    BEGIN
      NewVATAmountLine.DELETEALL;
      IF FIND('-') THEN
        REPEAT
          NewVATAmountLine.COPY(Rec);
          NewVATAmountLine.INSERT;
        UNTIL NEXT = 0;
    END;

    PROCEDURE InitGlobals@2(NewCurrencyCode@1000 : Code[10];NewAllowVATDifference@1001 : Boolean;NewAllowVATDifferenceOnThisTab@1002 : Boolean;NewPricesIncludingVAT@1003 : Boolean;NewAllowInvDisc@1004 : Boolean;NewVATBaseDiscPct@1005 : Decimal);
    BEGIN
      CurrencyCode := NewCurrencyCode;
      AllowVATDifference := NewAllowVATDifference;
      AllowVATDifferenceOnThisTab := NewAllowVATDifferenceOnThisTab;
      PricesIncludingVAT := NewPricesIncludingVAT;
      AllowInvDisc := NewAllowInvDisc;
      VATBaseDiscPct := NewVATBaseDiscPct;
      VATAmountEditable := AllowVATDifference;
      InvoiceDiscountAmountEditable := AllowInvDisc;
      IF CurrencyCode = '' THEN
        Currency.InitRoundingPrecision
      ELSE
        Currency.GET(CurrencyCode);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE FormCheckVATDifference@4();
    VAR
      VATAmountLine2@1000 : Record 290;
      TotalVATDifference@1001 : Decimal;
    BEGIN
      CheckVATDifference(CurrencyCode,AllowVATDifference);
      VATAmountLine2 := Rec;
      TotalVATDifference := ABS("VAT Difference") - ABS(xRec."VAT Difference");
      IF FIND('-') THEN
        REPEAT
          TotalVATDifference := TotalVATDifference + ABS("VAT Difference");
        UNTIL NEXT = 0;
      Rec := VATAmountLine2;
      IF TotalVATDifference > Currency."Max. VAT Difference Allowed" THEN
        ERROR(
          Text001,FIELDCAPTION("VAT Difference"),
          Currency."Max. VAT Difference Allowed",Currency.FIELDCAPTION("Max. VAT Difference Allowed"));
    END;

    LOCAL PROCEDURE ModifyRec@3();
    VAR
      ServLine@1000 : Record 5902;
    BEGIN
      Modified := TRUE;
      MODIFY;

      IF ((ParentControl = PAGE::"Service Order Statistics") AND
          (CurrentTabNo <> 1)) OR
         (ParentControl = PAGE::"Service Statistics")
      THEN
        IF GetAnyLineModified THEN BEGIN
          ServLine.UpdateVATOnLines(0,ServHeader,ServLine,Rec);
          ServLine.UpdateVATOnLines(1,ServHeader,ServLine,Rec);
        END;
    END;

    PROCEDURE SetParentControl@6(ID@1000 : Integer);
    BEGIN
      ParentControl := ID;
    END;

    PROCEDURE SetServHeader@7(ServiceHeader@1000 : Record 5900);
    BEGIN
      ServHeader := ServiceHeader;
    END;

    PROCEDURE SetCurrentTabNo@8(TabNo@1000 : Integer);
    BEGIN
      CurrentTabNo := TabNo;
    END;

    BEGIN
    END.
  }
}

