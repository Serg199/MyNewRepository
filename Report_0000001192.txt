OBJECT Report 1192 Suggest Res. Price Chg.(Price)
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Suggest Res. Price Chg.(Price);
               RUS=Предлож. изменения цен ресурсов (цена)];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);
                  IF ToCurrency.Code <> '' THEN
                    ToCurrency.FIND;
                END;

  }
  DATASET
  {
    { 9832;    ;DataItem;                    ;
               DataItemTable=Table201;
               DataItemTableView=SORTING(Type,Code,Work Type Code,Currency Code);
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text001 +
                                 Text002 +
                                 Text003);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,Type);
                                  Window.UPDATE(2,Code);
                                  ResPriceChg.Type := Type;
                                  ResPriceChg.Code := Code;

                                  IF ToCurrency.Code = '' THEN BEGIN
                                    ResPriceChg."Currency Code" := "Currency Code";
                                    SETRANGE("Currency Code",'');
                                  END ELSE
                                    ResPriceChg."Currency Code" := ToCurrency.Code;

                                  IF ToWorkType.Code = '' THEN
                                    ResPriceChg."Work Type Code" := "Work Type Code"
                                  ELSE
                                    ResPriceChg."Work Type Code" := ToWorkType.Code;

                                  ResPriceChg."New Unit Price" := "Unit Price";

                                  IF ResPriceChg."Currency Code" <> "Currency Code" THEN BEGIN
                                    IF "Currency Code" <> '' THEN
                                      ResPriceChg."New Unit Price" :=
                                        CurrExchRate.ExchangeAmtFCYToLCY(
                                          WORKDATE,"Currency Code",ResPriceChg."New Unit Price",
                                          CurrExchRate.ExchangeRate(
                                            WORKDATE,"Currency Code"));
                                    IF ResPriceChg."Currency Code" <> '' THEN
                                      ResPriceChg."New Unit Price" :=
                                        CurrExchRate.ExchangeAmtLCYToFCY(
                                          WORKDATE,ResPriceChg."Currency Code",
                                          ResPriceChg."New Unit Price",
                                          CurrExchRate.ExchangeRate(
                                            WORKDATE,ResPriceChg."Currency Code"));
                                  END;

                                  IF ResPriceChg."Currency Code" = '' THEN
                                    Currency2.InitRoundingPrecision
                                  ELSE BEGIN
                                    Currency2.GET(ResPriceChg."Currency Code");
                                    Currency2.TESTFIELD("Unit-Amount Rounding Precision");
                                  END;

                                  ResPriceChg."New Unit Price" :=
                                    ROUND(ResPriceChg."New Unit Price",Currency2."Unit-Amount Rounding Precision");

                                  IF ResPriceChg."New Unit Price" > PriceLowerLimit THEN
                                    ResPriceChg."New Unit Price" := ResPriceChg."New Unit Price" * UnitPricefactor;

                                  IF RoundingMethod.Code <> '' THEN BEGIN
                                    RoundingMethod."Minimum Amount" := ResPriceChg."New Unit Price";
                                    IF RoundingMethod.FIND('=<') THEN BEGIN
                                      ResPriceChg."New Unit Price" := ResPriceChg."New Unit Price" + RoundingMethod."Amount Added Before";
                                      IF RoundingMethod.Precision > 0 THEN
                                        ResPriceChg."New Unit Price" :=
                                          ROUND(ResPriceChg."New Unit Price",
                                            RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                                      ResPriceChg."New Unit Price" := ResPriceChg."New Unit Price" + RoundingMethod."Amount Added After";
                                    END;
                                  END;

                                  ResPrice.SETRANGE("Work Type Code",ResPriceChg."Work Type Code");
                                  ResPrice.SETRANGE(Code,ResPriceChg.Code);
                                  ResPrice.SETRANGE(Type,ResPriceChg.Type);
                                  ResPrice.SETRANGE("Currency Code",ResPriceChg."Currency Code");
                                  IF ResPrice.FINDLAST THEN BEGIN
                                    ResPriceChg."Current Unit Price" := ResPrice."Unit Price";
                                    PriceAlreadyExists := TRUE;
                                  END ELSE BEGIN
                                    ResPriceChg."Current Unit Price" := 0;
                                    PriceAlreadyExists := FALSE;
                                  END;
                                  IF PriceAlreadyExists OR CreateNewPrices THEN BEGIN
                                    ResPriceChg2 := ResPriceChg;
                                    IF ResPriceChg2.FIND('=') THEN
                                      ResPriceChg.MODIFY
                                    ELSE
                                      ResPriceChg.INSERT;
                                  END;
                                END;

               ReqFilterFields=Type,Code,Work Type Code,Currency Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF UnitPricefactor = 0 THEN BEGIN
                     UnitPricefactor := 1;
                     CreateNewPrices := TRUE;
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 15  ;2   ;Group     ;
                  CaptionML=[ENU=Copy to Res. Price Change...;
                             RUS=Копировать в изменение цен ресурсов...] }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Currency Code;
                             RUS=Код валюты];
                  ToolTipML=[ENU=Specifies the code for the currency that amounts are shown in.;
                             RUS=Определяет код валюты, в которой отображаются суммы.];
                  ApplicationArea=#Jobs;
                  SourceExpr=ToCurrency.Code;
                  TableRelation=Currency }

      { 6   ;3   ;Field     ;
                  CaptionML=[ENU=Work Type;
                             RUS=Вид работы];
                  ToolTipML=[ENU=Specifies the work types to which the new prices apply. If you want the new price to be calculated for all work types, enter ALL.;
                             RUS=Определяет типы работ, к которым относятся новые цены. Если новая цена должна быть рассчитана для всех типов работ, введите ВСЕ.];
                  ApplicationArea=#Jobs;
                  SourceExpr=ToWorkType.Code;
                  TableRelation="Work Type" }

      { 16  ;2   ;Field     ;
                  CaptionML=[ENU=Only Prices Above;
                             RUS=Только цены свыше];
                  ToolTipML=[ENU=Specifies an amount to determine the lowest unit price that is changed. Only prices that are higher than this are changed. If a price is lower than or equal to this amount, a line for it is created in the Resource Price Changes window, but with the same unit price as in the Resource Prices window.;
                             RUS=Определяет значение минимальной цены единицы, которая будет изменена. Будут изменены только цены свыше этой суммы. Если цена меньше или равна этой сумме, для нее будет создана строка в окне "Изменение цен ресурсов", но с той же ценой единицы, что и в окне "Цены ресурсов".];
                  ApplicationArea=#Jobs;
                  DecimalPlaces=2:5;
                  SourceExpr=PriceLowerLimit }

      { 13  ;2   ;Field     ;
                  CaptionML=[ENU=Adjustment Factor;
                             RUS=Коэффициент коррекции];
                  ToolTipML=[ENU=Specifies an adjustment factor to multiply the amounts that you want suggested. By entering an adjustment factor, you can increase or decrease the amounts that are suggested.;
                             RUS=Определяет коэффициент коррекции, на который умножаются предлагаемые суммы. Этот коэффициент позволяет увеличивать или уменьшать суммы, которые предлагаются.];
                  ApplicationArea=#Jobs;
                  DecimalPlaces=0:5;
                  SourceExpr=UnitPricefactor;
                  MinValue=0 }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=Rounding Method;
                             RUS=Метод округления];
                  ToolTipML=[ENU=Specifies a code for the rounding method that you want to apply to costs or prices that you adjust.;
                             RUS=Определяет код метода округления, который применяется к корректируемым значениям себестоимости и цены.];
                  ApplicationArea=#Jobs;
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Create New Prices;
                             RUS=Создать новые цены];
                  ToolTipML=[ENU=Specifies if you want the batch job to create new price suggestions, such as a new combination of currency, job number, or work type. If you only want to adjust existing alternative prices, do not select.;
                             RUS=Указывает, что при выполнении пакетного задания должны формироваться предложения по новым ценам, например новая комбинация валюты, номера работы или типа работы. Если требуется скорректировать только существующие альтернативные цены, не устанавливайте этот флажок.];
                  ApplicationArea=#Jobs;
                  SourceExpr=CreateNewPrices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Processing resources...\\;RUS=Обработка ресурсов...\\';
      Text002@1002 : TextConst 'ENU=Type         #1##########\;RUS=Тип         #1##########\';
      Text003@1003 : TextConst 'ENU=Code         #2##########\;RUS=Код         #2##########\';
      RoundingMethod@1008 : Record 42;
      ToCurrency@1007 : Record 4;
      CurrExchRate@1006 : Record 330;
      ToWorkType@1016 : Record 200;
      ResPriceChg@1015 : Record 335;
      ResPriceChg2@1014 : Record 335;
      Currency2@1013 : Record 4;
      ResPrice@1009 : Record 201;
      Window@1005 : Dialog;
      CreateNewPrices@1010 : Boolean;
      UnitPricefactor@1011 : Decimal;
      PriceLowerLimit@1012 : Decimal;
      PriceAlreadyExists@1019 : Boolean;

    PROCEDURE InitializeCopyToResPrice@3(CurrencyCode@1000 : Code[10];WorkTypeCode@1002 : Code[10]);
    BEGIN
      ToCurrency.Code := CurrencyCode;
      ToWorkType.Code := WorkTypeCode;
    END;

    PROCEDURE InitializeRequest@2(PriceLowerLimitFrom@1003 : Decimal;UnitPriceFactorFrom@1004 : Decimal;RoundingMethodCode@1005 : Code[10];CreateNewPricesFrom@1006 : Boolean);
    BEGIN
      PriceLowerLimit := PriceLowerLimitFrom;
      UnitPricefactor := UnitPriceFactorFrom;
      RoundingMethod.Code := RoundingMethodCode;
      CreateNewPrices := CreateNewPricesFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

