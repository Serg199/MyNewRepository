OBJECT Report 12494 FA Posted Writeoff Act FA-4
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=FA Posted Writeoff Act FA-4;
               RUS=Учт. акт списания ОС-4];
    ProcessingOnly=Yes;
    OnInitReport=VAR
                   Employee@1470000 : Record 5200;
                 BEGIN
                   CompanyInfo.GET;
                   FASetup.GET;
                   IF Employee.GET(CompanyInfo."Director No.") THEN
                     DirectorPosition := Employee.GetJobTitleName;
                 END;

    OnPreReport=BEGIN
                  FA4Helper.InitReportTemplate(REPORT::"FA Posted Writeoff Act FA-4");
                END;

    OnPostReport=BEGIN
                   IF FileName = '' THEN
                     FA4Helper.ExportData
                   ELSE
                     FA4Helper.ExportDataFile(FileName);
                 END;

  }
  DATASET
  {
    { 3950;    ;DataItem;                    ;
               DataItemTable=Table12471;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Writeoff));
               OnAfterGetRecord=BEGIN
                                  FASetup.GET;

                                  CheckSignature(Chairman,Chairman."Employee Type"::Chairman);
                                  CheckSignature(Member1,Member1."Employee Type"::Member1);
                                  CheckSignature(Member2,Member2."Employee Type"::Member2);
                                END;

               OnPostDataItem=BEGIN
                                FA4Helper.SetDocSheet;
                              END;

               ReqFilterFields=No. }

    { 6338;1   ;DataItem;                    ;
               DataItemTable=Table12472;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnPreDataItem=BEGIN
                               IsHeaderPrinted := FALSE;
                             END;

               OnAfterGetRecord=BEGIN
                                  FA.GET("FA No.");
                                  FADepreciationBook.GET("FA No.","Depreciation Book Code");
                                  FADepreciationBook.CALCFIELDS("Acquisition Cost",Depreciation);

                                  IF FASetup."FA Location Mandatory" THEN
                                    TESTFIELD("FA Location Code");
                                  IF FASetup."Employee No. Mandatory" THEN
                                    TESTFIELD("FA Employee No.");

                                  FactYears := LocMgt.GetPeriodDate(FA."Initial Release Date","Posted FA Doc. Header"."Posting Date",2);

                                  GetFAComments(Reason,PostedFAComment.Type::Reason);
                                  GetFAComments(Conclusion,PostedFAComment.Type::Conclusion);
                                  GetFAComments(Appendix,PostedFAComment.Type::Appendix);
                                  GetFAComments(Result,PostedFAComment.Type::Result);

                                  FA4Helper.SetDocSheet;
                                  IF NOT IsHeaderPrinted THEN BEGIN
                                    FA4Helper.FillHeader(
                                      StdRepMgt.GetEmpDepartment("FA Employee No."),StdRepMgt.GetEmpName("FA Employee No."),
                                      Reason[1],FORMAT("Posted FA Doc. Header"."FA Posting Date"),"Posted FA Doc. Header"."Reason Document No.",
                                      FORMAT("Posted FA Doc. Header"."Reason Document Date"),"FA Employee No.",
                                      DirectorPosition,"Posted FA Doc. Header"."No.",FORMAT("Posted FA Doc. Header"."Posting Date"),
                                      "Posted FA Doc. Header"."Posting Description");
                                    FA4Helper.FillStatePageHeader;
                                    IsHeaderPrinted := TRUE;
                                  END;

                                  FA4Helper.FillStateLine(
                                    Description,FA."Inventory Number",FA."Factory No.",FA."Manufacturing Year",FORMAT(FA."Initial Release Date"),
                                    FactYears,StdRepMgt.FormatReportValue(ABS(FADepreciationBook."Acquisition Cost"),2),
                                    StdRepMgt.FormatReportValue(ABS(FADepreciationBook.Depreciation),2),
                                    StdRepMgt.FormatReportValue(FADepreciationBook."Acquisition Cost" + FADepreciationBook.Depreciation,2));
                                END;

               ReqFilterFields=Line No.;
               DataItemLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.) }

    { 6729;2   ;DataItem;Part 2              ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               FA4Helper.SetAssetSheet;
                             END;

               OnPostDataItem=BEGIN
                                FA4Helper.FillAssetPageFooter;
                              END;
                               }

    { 9342;3   ;DataItem;                    ;
               DataItemTable=Table5640;
               DataItemTableView=SORTING(Main Asset No.,FA No.);
               OnPreDataItem=BEGIN
                               FA4Helper.FillAssetHeader;
                             END;

               OnAfterGetRecord=BEGIN
                                  FA4Helper.FillAssetLine(
                                    Description,StdRepMgt.FormatReportValue(Quantity,2),'','','','','');
                                END;

               DataItemLinkReference=Posted FA Doc. Line;
               DataItemLink=Main Asset No.=FIELD(FA No.) }

    { 8951;3   ;DataItem;                    ;
               DataItemTable=Table12474;
               DataItemTableView=SORTING(Item Type,No.,Precious Metals Code);
               OnAfterGetRecord=BEGIN
                                  FA4Helper.FillAssetLine(
                                    '','',Name,"Precious Metals Code",StdRepMgt.GetUoMDesc("Unit of Measure Code"),
                                    StdRepMgt.FormatReportValue(Quantity,2),StdRepMgt.FormatReportValue(Mass,2));
                                END;

               DataItemLinkReference=Posted FA Doc. Line;
               DataItemLink=No.=FIELD(FA No.) }

    { 2746;2   ;DataItem;Part 3              ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               FA4Helper.FillConclusionHeader(
                                 Conclusion[1],Conclusion[2],Appendix[1],Appendix[2],
                                 Chairman."Employee Job Title",Chairman."Employee Name",
                                 Member1."Employee Job Title",Member1."Employee Name",
                                 Member2."Employee Job Title",Member2."Employee Name");
                             END;

               OnPostDataItem=BEGIN
                                FA4Helper.FillConclusionPageFooter;
                              END;
                               }

    { 2461;3   ;DataItem;                    ;
               DataItemTable=Table12452;
               DataItemTableView=SORTING(Document No.,Line No.);
               OnPreDataItem=BEGIN
                               SETRANGE("Document No.","Posted FA Doc. Line"."Item Receipt No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  FA4Helper.FillConclusionLine(
                                    "Document No.",Description,"Item No.","Unit of Measure Code",
                                    StdRepMgt.FormatReportValue(Quantity,2),StdRepMgt.FormatReportValue("Unit Amount",2),Amount);
                                END;

               DataItemLinkReference=Posted FA Doc. Line }

    { 3435;2   ;DataItem;Footer              ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               FA4Helper.FillReportFooter(Result[1]);
                             END;
                              }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CompanyInfo@1210005 : Record 79;
      FASetup@1210006 : Record 5603;
      FADepreciationBook@1470003 : Record 5612;
      FA@1210001 : Record 5600;
      PostedFAComment@1470000 : Record 12479;
      Chairman@1210008 : Record 12421;
      Member1@1210003 : Record 12421;
      Member2@1210000 : Record 12421;
      LocMgt@1210004 : Codeunit 12400;
      DocSignMgt@1470001 : Codeunit 12420;
      StdRepMgt@1210010 : Codeunit 12401;
      FA4Helper@1210007 : Codeunit 14953;
      IsHeaderPrinted@1210009 : Boolean;
      Reason@1470004 : ARRAY [5] OF Text[80];
      Conclusion@1210052 : ARRAY [5] OF Text[80];
      Appendix@1210053 : ARRAY [5] OF Text[80];
      Result@1470002 : ARRAY [5] OF Text[80];
      FactYears@1210018 : Text[30];
      DirectorPosition@1470005 : Text[80];
      FileName@1210002 : Text;

    PROCEDURE SetFileNameSilent@1210005(NewFileName@1210000 : Text);
    BEGIN
      FileName := NewFileName;
    END;

    PROCEDURE CheckSignature@1470002(VAR PostedDocSign@1470000 : Record 12421;EmpType@1470001 : Integer);
    BEGIN
      DocSignMgt.GetPostedDocSign(
        PostedDocSign,DATABASE::"Posted FA Doc. Header",
        "Posted FA Doc. Header"."Document Type","Posted FA Doc. Header"."No.",EmpType,FALSE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

