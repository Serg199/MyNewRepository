OBJECT Table 6083 Serv. Price Adjustment Detail
{
  OBJECT-PROPERTIES
  {
    Date=15.09.15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ValidateRecord;
             END;

    OnModify=BEGIN
               ValidateRecord;
             END;

    OnRename=BEGIN
               ValidateRecord;
             END;

    CaptionML=[ENU=Serv. Price Adjustment Detail;
               RUS=Подробности по группе коррекции цен на сервис];
  }
  FIELDS
  {
    { 1   ;   ;Serv. Price Adjmt. Gr. Code;Code10 ;TableRelation="Service Price Adjustment Group";
                                                   CaptionML=[ENU=Serv. Price Adjmt. Gr. Code;
                                                              RUS=Код группы корр. цены на серв. обслуж.];
                                                   NotBlank=Yes }
    { 2   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              RUS=Тип];
                                                   OptionCaptionML=[ENU=Item,Resource,Resource Group,Service Cost,G/L Account;
                                                                    RUS=Товар,Ресурс,Группа ресурсов,Затраты на серв. обслуживание,Счет ГК];
                                                   OptionString=Item,Resource,Resource Group,Service Cost,G/L Account }
    { 3   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST(Resource Group)) "Resource Group"
                                                                 ELSE IF (Type=CONST(Service Cost)) "Service Cost"
                                                                 ELSE IF (Type=CONST(G/L Account)) "G/L Account";
                                                   OnValidate=VAR
                                                                Item@1000 : Record 27;
                                                                Resource@1001 : Record 156;
                                                                ResourceGrp@1002 : Record 152;
                                                                ServiceCost@1003 : Record 5905;
                                                              BEGIN
                                                                IF "No." <> '' THEN
                                                                  CASE Type OF
                                                                    Type::Item:
                                                                      BEGIN
                                                                        Item.GET("No.");
                                                                        Description := Item.Description;
                                                                      END;
                                                                    Type::Resource:
                                                                      BEGIN
                                                                        Resource.GET("No.");
                                                                        Description := Resource.Name;
                                                                      END;
                                                                    Type::"Resource Group":
                                                                      BEGIN
                                                                        ResourceGrp.GET("No.");
                                                                        Description := ResourceGrp.Name;
                                                                      END;
                                                                    Type::"Service Cost":
                                                                      BEGIN
                                                                        ServiceCost.GET("No.");
                                                                        Description := ServiceCost.Description;
                                                                      END;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              RUS=Номер] }
    { 4   ;   ;Work Type           ;Code10        ;TableRelation="Work Type";
                                                   OnValidate=VAR
                                                                ServPriceAdjmtDetail@1000 : Record 6083;
                                                                ServPriceAdjmtDetail2@1001 : Record 6083;
                                                              BEGIN
                                                                IF NOT (Type IN [Type::Resource,Type::"Resource Group"]) THEN BEGIN
                                                                  ServPriceAdjmtDetail.Type := Type::Resource;
                                                                  ServPriceAdjmtDetail2.Type := Type::"Resource Group";
                                                                  ERROR(Text003,
                                                                    FIELDCAPTION("Work Type"),
                                                                    FIELDCAPTION(Type),
                                                                    ServPriceAdjmtDetail.Type,
                                                                    ServPriceAdjmtDetail2.Type);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Work Type;
                                                              RUS=Вид работы] }
    { 5   ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              RUS=Общая товарная группа] }
    { 6   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
  }
  KEYS
  {
    {    ;Serv. Price Adjmt. Gr. Code,Type,No.,Work Type,Gen. Prod. Posting Group;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=%1 already exists with adjustments for specific %2 numbers. Delete these lines if you need an adjustment for all %2 records.;RUS=%1 уже есть с коррекцией для определенных номеров %2. Удалите эти строки, если требуется коррекция всех записей %2.';
      Text002@1000 : TextConst 'ENU=%1 already exists for this %2 with %3 set to blank. Delete this line if you need adjustments for specific %4 numbers.;RUS=%1 уже есть для этого %2 с пустым %3. Удалите эту строку, если требуется коррекция определенных номеров %4.';
      Text003@1002 : TextConst 'ENU=%1 can only be entered when %2 is %3 or %4.;RUS=%1 можно ввести, только если %2 равно %3 или %4.';

    LOCAL PROCEDURE ValidateRecord@1();
    VAR
      ServPriceAdjmtDetail@1000 : Record 6083;
    BEGIN
      IF "No." = '' THEN BEGIN
        ServPriceAdjmtDetail.RESET;
        ServPriceAdjmtDetail.SETRANGE("Serv. Price Adjmt. Gr. Code","Serv. Price Adjmt. Gr. Code");
        ServPriceAdjmtDetail.SETRANGE(Type,Type);
        ServPriceAdjmtDetail.SETRANGE("Work Type","Work Type");
        ServPriceAdjmtDetail.SETRANGE("Gen. Prod. Posting Group","Gen. Prod. Posting Group");
        IF ServPriceAdjmtDetail.FINDFIRST THEN BEGIN
          IF
             (ServPriceAdjmtDetail."Serv. Price Adjmt. Gr. Code" <> "Serv. Price Adjmt. Gr. Code") OR
             (ServPriceAdjmtDetail.Type <> Type) OR
             (ServPriceAdjmtDetail."No." <> "No.") OR
             (ServPriceAdjmtDetail."Work Type" <> "Work Type") OR
             (ServPriceAdjmtDetail."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group")
          THEN
            ERROR(Text001,ServPriceAdjmtDetail.TABLECAPTION,FORMAT(Type))
        END;
      END ELSE
        IF ServPriceAdjmtDetail.GET(
             "Serv. Price Adjmt. Gr. Code",
             Type,
             '',
             "Work Type",
             "Gen. Prod. Posting Group")
        THEN
          ERROR(Text002,
            ServPriceAdjmtDetail.TABLECAPTION,
            FIELDCAPTION(Type),
            FIELDCAPTION("No."),
            FORMAT(Type));
    END;

    BEGIN
    END.
  }
}

