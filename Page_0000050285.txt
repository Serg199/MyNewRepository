OBJECT Page 50285 Gen. Jnl. Lines for Approve
{
  OBJECT-PROPERTIES
  {
    Date=30.05.17;
    Time=10:13:22;
    Modified=Yes;
    Version List=NOS-26;
  }
  PROPERTIES
  {
    SourceTable=Table50215;
    PageType=Worksheet;
    OnOpenPage=BEGIN
                 FillJournalBuffer;
               END;

    ActionList=ACTIONS
    {
      { 1101495002;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1101495024;1 ;Action    ;
                      CaptionML=[ENU=Refresh data;
                                 RUS=Обновить данные];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=RefreshLines;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 FillJournalBuffer;
                               END;
                                }
      { 1101495025;1 ;ActionGroup;
                      CaptionML=[ENU=Functions;
                                 RUS=Функции] }
      { 1101495026;2 ;Action    ;
                      CaptionML=[ENU=A&pprove;
                                 RUS=Утвердить];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 // NC NCS-1880 ORM
                                 ApproveSelected;
                               END;
                                }
      { 1101495027;2 ;Action    ;
                      CaptionML=[ENU=&Reopen;
                                 RUS=Открыть];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReOpen;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 // NC NCS-1880 ORM
                                 ReopenSelected;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1101495000;0;Container;
                ContainerType=ContentArea }

    { 1101495003;1;Field  ;
                Lookup=Yes;
                CaptionML=[ENU=Approve status filter;
                           RUS=Фильтр статуса утверждения];
                OptionCaptionML=[ENU=To Approve,Approved,All;
                                 RUS=Для Утверждения,Утвержденные,Все];
                SourceExpr=ApproveStatusFilter }

    { 1101495001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1101495004;2;Field  ;
                SourceExpr="Journal Template Name" }

    { 1101495018;2;Field  ;
                SourceExpr="Journal Batch Name" }

    { 1101495005;2;Field  ;
                SourceExpr="Line No." }

    { 1101495006;2;Field  ;
                SourceExpr="Account Type" }

    { 1101495007;2;Field  ;
                SourceExpr="Account No." }

    { 1101495008;2;Field  ;
                SourceExpr="Posting Date" }

    { 1101495009;2;Field  ;
                SourceExpr="Document Type" }

    { 1101495010;2;Field  ;
                SourceExpr="Document No." }

    { 1101495011;2;Field  ;
                SourceExpr=Description }

    { 1101495019;2;Field  ;
                SourceExpr="Bal. Account Type" }

    { 1101495012;2;Field  ;
                SourceExpr="Bal. Account No." }

    { 1101495013;2;Field  ;
                SourceExpr="Currency Code" }

    { 1101495014;2;Field  ;
                SourceExpr=Amount }

    { 1101495015;2;Field  ;
                SourceExpr="Amount (LCY)" }

    { 1101495016;2;Field  ;
                SourceExpr="Shortcut Dimension 1 Code" }

    { 1101495017;2;Field  ;
                SourceExpr="Shortcut Dimension 2 Code" }

    { 1101495020;2;Field  ;
                SourceExpr="Modified By" }

    { 1101495021;2;Field  ;
                SourceExpr="Approved By" }

    { 1101495022;2;Field  ;
                SourceExpr="User ID" }

    { 1101495023;1;Part   ;
                SubPageLink=Journal Template Name=FIELD(Journal Template Name),
                            Journal Batch Name=FIELD(Journal Batch Name),
                            Document No.=FIELD(Document No.),
                            Posting Date=FIELD(Posting Date);
                PagePartID=Page50286;
                PartType=Page }

  }
  CODE
  {
    VAR
      GenJournalLineForApprove@1101495003 : Record 50215;
      TempGenJournalLine@1101495002 : TEMPORARY Record 81;
      GenJnlManagement@1101495001 : Codeunit 230;
      ApproveStatusFilter@1101495000 : 'To Approve,Approved,All';
      test@1101495004 : Code[10];
      ProgressTxt@1101495005 : TextConst 'ENU=Selecting lines for approval \@1@@@@@@@@@@@@@@;RUS=Выборка строк для утверждения \@1@@@@@@@@@@@@@@';

    PROCEDURE FillJournalBuffer@1170000003();
    VAR
      GenJournalBatch@1170000001 : Record 232;
      GenJournalLine@1170000000 : Record 81;
      Window@1170000002 : Dialog;
      TotalQty@1170000003 : Integer;
      Counter@1170000004 : Integer;
    BEGIN
      GenJournalLineForApprove.RESET;
      GenJournalLineForApprove.SETRANGE(GenJournalLineForApprove."User ID",USERID);
      GenJournalLineForApprove.DELETEALL;

      GenJournalBatch.SETRANGE("Batch Type",GenJournalBatch."Batch Type"::Manual);
      TotalQty := GenJournalBatch.COUNT;
      Window.OPEN(ProgressTxt);
      IF GenJournalBatch.FINDSET THEN
        REPEAT
          Counter += 1;
          Window.UPDATE(1,ROUND(Counter / TotalQty * 10000,1));
          FillJournalBufferFromBatch(GenJournalBatch);
        UNTIL GenJournalBatch.NEXT = 0;

      FilterBuffer;
    END;

    PROCEDURE FillJournalBufferFromBatch@1170000004(GenJournalBatch@1170000001 : Record 232);
    VAR
      GenJournalLine@1170000000 : Record 81;
      ExchAccGenJournalLine@1170000002 : Record 81;
      AccountOK@1170000003 : Boolean;
      BalAccountOK@1170000004 : Boolean;
    BEGIN
      GenJournalLine.SETRANGE("Journal Template Name",GenJournalBatch."Journal Template Name");
      GenJournalLine.SETRANGE("Journal Batch Name",GenJournalBatch.Name);
      IF GenJournalLine.FINDSET THEN
        REPEAT
          AccountOK := HasJnlApprovalSetup(GenJournalLine.GetGLAccountNo(FALSE),ABS(GenJournalLine."Amount (LCY)"));
          IF GenJournalLine."Bal. Account No." <> '' THEN BEGIN
            GenJournalLine.GetExchAccJnlLine(ExchAccGenJournalLine);
            BalAccountOK :=
              HasJnlApprovalSetup(ExchAccGenJournalLine.GetGLAccountNo(FALSE),ABS(ExchAccGenJournalLine."Amount (LCY)"));
          END ELSE
            BalAccountOK := TRUE;

          IF AccountOK AND BalAccountOK THEN
            AddJnlLineToBuffer(GenJournalLine)
        UNTIL GenJournalLine.NEXT = 0;
    END;

    PROCEDURE HasJnlApprovalSetup@1170000008(AccountNo@1170000000 : Code[20];LineAmount@1170000002 : Decimal) : Boolean;
    VAR
      GenJnlApprovalSetup@1170000001 : Record 50214;
    BEGIN
      IF AccountNo = '' THEN
        EXIT(FALSE);

      GenJnlApprovalSetup.SETCURRENTKEY("User ID","Limit Amount");
      GenJnlApprovalSetup.SETRANGE("User ID",USERID);
      GenJnlApprovalSetup.SETFILTER("Limit Amount",'>%1',LineAmount);
      IF GenJnlApprovalSetup.FINDSET THEN
        REPEAT
          IF TempGenJournalLine.IsGLAccountInFilter(AccountNo,GenJnlApprovalSetup."G/L Account Filter") THEN
            EXIT(TRUE);
        UNTIL GenJnlApprovalSetup.NEXT = 0;

      EXIT(FALSE);
    END;

    PROCEDURE AddJnlLineToBuffer@1170000011(GenJournalLine@1170000000 : Record 81);
    BEGIN
      GenJournalLineForApprove.TRANSFERFIELDS(GenJournalLine);
      GenJournalLineForApprove."User ID" := USERID;
      IF NOT GenJournalLineForApprove.INSERT THEN;
    END;

    PROCEDURE ApproveSelected@1170000000();
    VAR
      GenJournalLine@1170000000 : Record 81;
    BEGIN
      MarkJnlLinesAsSelected(GenJournalLine);
      GenJnlManagement.ApproveGenJnlLines(GenJournalLine);
      UpdateFromProcessedJnlLines(GenJournalLine);
      UpdateFormContent;
    END;

    PROCEDURE ReopenSelected@1170000001();
    VAR
      GenJournalLine@1170000000 : Record 81;
      ReopenedQty@1170000001 : Integer;
    BEGIN
      MarkJnlLinesAsSelected(GenJournalLine);
      GenJnlManagement.ReopenGenJnlLines(GenJournalLine);
      UpdateFromProcessedJnlLines(GenJournalLine);
      UpdateFormContent;
    END;

    PROCEDURE MarkJnlLinesAsSelected@1170000013(VAR GenJournalLine@1170000000 : Record 81);
    BEGIN
      GenJournalLine.CLEARMARKS;

      GenJournalLineForApprove.COPY(Rec);
      CurrPage.SETSELECTIONFILTER(GenJournalLineForApprove);
      IF GenJournalLineForApprove.FINDSET THEN
        REPEAT
          IF GenJournalLine.GET(
              GenJournalLineForApprove."Journal Template Name",
              GenJournalLineForApprove."Journal Batch Name",
              GenJournalLineForApprove."Line No.")
          THEN
            GenJournalLine.MARK(TRUE);
        UNTIL GenJournalLineForApprove.NEXT = 0;

      GenJournalLine.MARKEDONLY(TRUE);
    END;

    LOCAL PROCEDURE UpdateFormContent@1170000009();
    BEGIN
      FilterBuffer;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE UpdateFromProcessedJnlLines@1170000005(VAR GenJournalLine@1170000000 : Record 81);
    VAR
      GenJnlLineForApprove@1170000001 : Record 50215;
    BEGIN
      IF GenJournalLine.FINDSET THEN
        REPEAT
          IF GenJnlLineForApprove.GET(
            GenJournalLine."Journal Template Name",
            GenJournalLine."Journal Batch Name",
            GenJournalLine."Line No.",
            USERID)
          THEN
            GenJnlLineForApprove."Approved By" := GenJournalLine."Approved By";
            GenJnlLineForApprove.MODIFY;
        UNTIL GenJournalLine.NEXT = 0;
    END;

    PROCEDURE FilterBuffer@1170000002();
    BEGIN
      CASE ApproveStatusFilter OF
        ApproveStatusFilter::"To Approve":
          SETRANGE("Approved By",'');
        ApproveStatusFilter::Approved:
          SETRANGE("Approved By",USERID);
        ApproveStatusFilter::All:
          SETRANGE("Approved By");
      END;
    END;

    BEGIN
    END.
  }
}

