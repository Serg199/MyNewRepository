OBJECT XMLport 1230 Export Generic CSV
{
  OBJECT-PROPERTIES
  {
    Date=15.09.15;
    Time=12:00:00;
    Version List=NAVW19.00,NAVRU9.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Export Generic CSV;
               RUS=Экспорт универсального CSV];
    Direction=Export;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    Window.OPEN(ProgressMsg);
                  END;

    OnPreXMLport=BEGIN
                   InitializeGlobals;
                 END;

    OnPostXMLport=BEGIN
                    IF ErrorText <> '' THEN
                      ERROR(ErrorText);

                    Window.CLOSE;

                    IF DataExch.GET(DataExchEntryNo) THEN
                      IF DataExchDef.GET(DataExch."Data Exch. Def Code") THEN
                        currXMLport.FILENAME := DataExchDef.Name + '.csv';
                  END;

    Format=Variable Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{C19D763B-C1F1-473D-8D3E-3640AD129D95}];  ;root                ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{125F3B85-5B53-4F67-9434-589A04D6DCA1}];1 ;DataExchField       ;Element ;Table   ;
                                                  SourceTable=Table1221;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             IF ColumnsAsRows THEN BEGIN
                                                                               QuitLoop := FALSE;
                                                                               IF SkipHeaderFooterExport("Data Exch. Field") THEN
                                                                                 currXMLport.SKIP;
                                                                             END;
                                                                           END;
                                                                            }

    { [{A64C65BE-4E70-436F-9680-A7C77EC03315}];2 ;ColumnX             ;Element ;Text    ;
                                                  DataType=Text;
                                                  MinOccurs=Zero;
                                                  Unbound=Yes;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 IF QuitLoop THEN
                                                                                   currXMLport.BREAKUNBOUND;

                                                                                 IF ColumnsAsRows THEN BEGIN
                                                                                   QuitLoop := TRUE;
                                                                                   CASE "Data Exch. Field"."Column Type" OF
                                                                                     "Data Exch. Field"."Column Type"::Header,
                                                                                     "Data Exch. Field"."Column Type"::Footer:
                                                                                       IF ("Data Exch. Field"."Column Name" <> "Data Exch. Field".Value) AND
                                                                                          ("Data Exch. Field"."Column Name" <> '')
                                                                                       THEN
                                                                                         ColumnX := "Data Exch. Field"."Column Name" + '=' + "Data Exch. Field".Value
                                                                                       ELSE
                                                                                         ColumnX := "Data Exch. Field".Value;
                                                                                     ELSE
                                                                                       ColumnX := "Data Exch. Field"."Column Name" + '=' + "Data Exch. Field".Value;
                                                                                   END;
                                                                                 END ELSE BEGIN
                                                                                   IF "Data Exch. Field"."Line No." <> LastLineNo THEN BEGIN
                                                                                     IF "Data Exch. Field"."Line No." <> LastLineNo + 1 THEN
                                                                                       ErrorText += LinesNotSequentialErr
                                                                                     ELSE BEGIN
                                                                                       LastLineNo := "Data Exch. Field"."Line No.";
                                                                                       PrevColumnNo := 0;
                                                                                       "Data Exch. Field".NEXT(-1);
                                                                                       Window.UPDATE(1,LastLineNo);
                                                                                     END;
                                                                                     currXMLport.BREAKUNBOUND;
                                                                                   END;

                                                                                   CheckColumnSequence;
                                                                                   ColumnX := "Data Exch. Field".Value;

                                                                                   IF "Data Exch. Field".NEXT = 0 THEN
                                                                                     QuitLoop := TRUE;
                                                                                 END;
                                                                               END;
                                                                                }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      DataExchDef@1064 : Record 1222;
      DataExch@1063 : Record 1220;
      Window@1000 : Dialog;
      ErrorText@1005 : Text;
      DataExchEntryNo@1062 : Integer;
      LastLineNo@1065 : Integer;
      PrevColumnNo@1003 : Integer;
      QuitLoop@1001 : Boolean;
      ColumnsNotSequentialErr@1002 : TextConst 'ENU=The data to be exported is not structured correctly. The columns in the dataset must be sequential.;RUS=Экспортируемые данные неправильно структурированы. Столбцы в наборе данных должны быть последовательными.';
      LinesNotSequentialErr@1004 : TextConst 'ENU=The data to be exported is not structured correctly. The lines in the dataset must be sequential.;RUS=Экспортируемые данные неправильно структурированы. Строки в наборе данных должны быть последовательными.';
      ProgressMsg@1060 : TextConst 'ENU=Exporting line no. #1######;RUS=Экспорт строки № #1######';
      ColumnsAsRows@1210000 : Boolean;

    LOCAL PROCEDURE InitializeGlobals@11();
    BEGIN
      DataExchEntryNo := "Data Exch. Field".GETRANGEMIN("Data Exch. No.");
      LastLineNo := 1;
      PrevColumnNo := 0;
      QuitLoop := FALSE;

      IF DataExch.GET(DataExchEntryNo) THEN
        IF DataExchDef.GET(DataExch."Data Exch. Def Code") THEN BEGIN
          ColumnsAsRows := DataExchDef."Columns as Rows";
          IF ColumnsAsRows THEN
            currXMLport.FIELDDELIMITER := '';
        END;
    END;

    PROCEDURE CheckColumnSequence@3();
    BEGIN
      IF "Data Exch. Field"."Column No." <> PrevColumnNo + 1 THEN BEGIN
        ErrorText += ColumnsNotSequentialErr;
        currXMLport.BREAKUNBOUND;
      END;

      PrevColumnNo := "Data Exch. Field"."Column No.";
    END;

    LOCAL PROCEDURE SkipHeaderFooterExport@1210011(DataExchFieldHeaderFooter@1210000 : Record 1221) : Boolean;
    VAR
      DataExchField@1210001 : Record 1221;
    BEGIN
      IF DataExchFieldHeaderFooter."Column Type" = DataExchFieldHeaderFooter."Column Type"::" " THEN
        EXIT(FALSE);

      WITH DataExchField DO BEGIN
        SETRANGE("Data Exch. No.",DataExchFieldHeaderFooter."Data Exch. No.");
        SETRANGE("Column No.",DataExchFieldHeaderFooter."Column No.");
        CASE DataExchFieldHeaderFooter."Column Type" OF
          DataExchFieldHeaderFooter."Column Type"::Header:
            BEGIN
              IF (DataExchFieldHeaderFooter."Column Name" = DataExchDef."Document Start Tag") AND
                 (DataExchDef."Document Start Tag" <> '')
              THEN
                EXIT(FALSE);
              SETFILTER("Line No.",'<%1',DataExchFieldHeaderFooter."Line No.");
            END;
          DataExchFieldHeaderFooter."Column Type"::Footer:
            BEGIN
              IF (DataExchFieldHeaderFooter."Column Name" = DataExchDef."Document End Tag") AND
                 (DataExchDef."Document End Tag" <> '')
              THEN
                EXIT(FALSE);
              SETFILTER("Line No.",'>%1',DataExchFieldHeaderFooter."Line No.");
            END;
        END;
        EXIT(NOT ISEMPTY);
      END;
    END;

    BEGIN
    END.
  }
}

