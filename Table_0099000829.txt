OBJECT Table 99000829 Planning Component
{
  OBJECT-PROPERTIES
  {
    Date=27.06.17;
    Time=13:15:17;
    Modified=Yes;
    Version List=NAVW110.00,NOS-26;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ReservePlanningComponent.VerifyQuantity(Rec,xRec);

               ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
               ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");

               GetReqLine;
               "Planning Line Origin" := ReqLine."Planning Line Origin";
               IF "Planning Line Origin" <> "Planning Line Origin"::"Order Planning" THEN
                 TESTFIELD("Worksheet Template Name");

               "Due Date" := ReqLine."Starting Date";
             END;

    OnModify=BEGIN
               ReservePlanningComponent.VerifyChange(Rec,xRec);
             END;

    OnDelete=BEGIN
               ReservePlanningComponent.DeleteLine(Rec);

               CALCFIELDS("Reserved Qty. (Base)");
               TESTFIELD("Reserved Qty. (Base)",0);
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=[ENU=Planning Component;
               RUS=Компонент произв. плана];
    LookupPageID=Page99000861;
    DrillDownPageID=Page99000861;
  }
  FIELDS
  {
    { 1   ;   ;Worksheet Template Name;Code10     ;TableRelation="Req. Wksh. Template";
                                                   CaptionML=[ENU=Worksheet Template Name;
                                                              RUS=Название шаблона журнала] }
    { 2   ;   ;Worksheet Batch Name;Code10        ;TableRelation="Requisition Wksh. Name".Name WHERE (Worksheet Template Name=FIELD(Worksheet Template Name));
                                                   CaptionML=[ENU=Worksheet Batch Name;
                                                              RUS=Код раздела журнала] }
    { 3   ;   ;Worksheet Line No.  ;Integer       ;TableRelation="Requisition Line"."Line No." WHERE (Worksheet Template Name=FIELD(Worksheet Template Name),
                                                                                                      Journal Batch Name=FIELD(Worksheet Batch Name));
                                                   CaptionML=[ENU=Worksheet Line No.;
                                                              RUS=Номер строки произв. плана] }
    { 5   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              RUS=Номер строки];
                                                   BlankZero=Yes }
    { 11  ;   ;Item No.            ;Code20        ;TableRelation=Item WHERE (Type=CONST(Inventory));
                                                   OnValidate=BEGIN
                                                                ReservePlanningComponent.VerifyChange(Rec,xRec);
                                                                CALCFIELDS("Reserved Qty. (Base)");
                                                                TESTFIELD("Reserved Qty. (Base)",0);

                                                                IF "Item No." = '' THEN BEGIN
                                                                  "Dimension Set ID" := 0;
                                                                  "Shortcut Dimension 1 Code" := '';
                                                                  "Shortcut Dimension 2 Code" := '';
                                                                  EXIT;
                                                                END;

                                                                GetItem;
                                                                Description := Item.Description;
                                                                VALIDATE("Unit of Measure Code",Item."Base Unit of Measure");
                                                                GetUpdateFromSKU;
                                                                CreateDim(DATABASE::Item,"Item No.");
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              RUS=Код товара] }
    { 12  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              RUS=Описание] }
    { 13  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Item No.");

                                                                GetItem;
                                                                GetGLSetup;

                                                                "Unit Cost" := Item."Unit Cost";

                                                                IF "Unit of Measure Code" <> '' THEN BEGIN
                                                                  "Qty. per Unit of Measure" := UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code");
                                                                  "Unit Cost" :=
                                                                    ROUND(
                                                                      Item."Unit Cost" * "Qty. per Unit of Measure",
                                                                      GLSetup."Unit-Amount Rounding Precision");
                                                                END ELSE
                                                                  "Qty. per Unit of Measure" := 1;

                                                                "Indirect Cost %" := ROUND(Item."Indirect Cost %" * "Qty. per Unit of Measure",0.00001);

                                                                "Overhead Rate" :=
                                                                  ROUND(Item."Overhead Rate" * "Qty. per Unit of Measure",
                                                                    GLSetup."Unit-Amount Rounding Precision");

                                                                "Direct Unit Cost" :=
                                                                  ROUND(
                                                                    ("Unit Cost" - "Overhead Rate") / (1 + "Indirect Cost %" / 100),
                                                                    GLSetup."Unit-Amount Rounding Precision");

                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              RUS=Код единицы измерения] }
    { 14  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              RUS=Кол-во];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 15  ;   ;Position            ;Code10        ;CaptionML=[ENU=Position;
                                                              RUS=Позиция] }
    { 16  ;   ;Position 2          ;Code10        ;CaptionML=[ENU=Position 2;
                                                              RUS=Позиция 2] }
    { 17  ;   ;Position 3          ;Code10        ;CaptionML=[ENU=Position 3;
                                                              RUS=Позиция 3] }
    { 18  ;   ;Lead-Time Offset    ;DateFormula   ;CaptionML=[ENU=Lead-Time Offset;
                                                              RUS=Подготовительный период] }
    { 19  ;   ;Routing Link Code   ;Code10        ;TableRelation="Routing Link";
                                                   OnValidate=VAR
                                                                PlanningRtngLine@1001 : Record 99000830;
                                                              BEGIN
                                                                VALIDATE("Expected Quantity",Quantity * PlanningNeeds);

                                                                "Due Date" := ReqLine."Starting Date";
                                                                "Due Time" := ReqLine."Starting Time";
                                                                IF "Routing Link Code" <> '' THEN BEGIN
                                                                  PlanningRtngLine.SETRANGE("Worksheet Template Name","Worksheet Template Name");
                                                                  PlanningRtngLine.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
                                                                  PlanningRtngLine.SETRANGE("Worksheet Line No.","Worksheet Line No.");
                                                                  PlanningRtngLine.SETRANGE("Routing Link Code","Routing Link Code");
                                                                  IF PlanningRtngLine.FINDFIRST THEN BEGIN
                                                                    "Due Date" := PlanningRtngLine."Starting Date";
                                                                    "Due Time" := ReqLine."Starting Time";
                                                                  END;
                                                                END;
                                                                IF FORMAT("Lead-Time Offset") <> '' THEN BEGIN
                                                                  IF "Due Date" = 0D THEN
                                                                    "Due Date" := ReqLine."Ending Date";
                                                                  "Due Date" :=
                                                                    "Due Date" -
                                                                    (CALCDATE("Lead-Time Offset",WORKDATE) - WORKDATE);
                                                                  "Due Time" := 0T;
                                                                END;
                                                                VALIDATE("Due Date");
                                                              END;

                                                   CaptionML=[ENU=Routing Link Code;
                                                              RUS=Код связи маршрута] }
    { 20  ;   ;Scrap %             ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Expected Quantity",Quantity * PlanningNeeds);
                                                              END;

                                                   CaptionML=[ENU=Scrap %;
                                                              RUS=Брак (%)];
                                                   DecimalPlaces=0:5;
                                                   MaxValue=100;
                                                   BlankNumbers=BlankNeg }
    { 21  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=BEGIN
                                                                ReservePlanningComponent.VerifyChange(Rec,xRec);
                                                                CALCFIELDS("Reserved Qty. (Base)");
                                                                TESTFIELD("Reserved Qty. (Base)",0);
                                                                GetUpdateFromSKU;
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              RUS=Код варианта] }
    { 25  ;   ;Expected Quantity   ;Decimal       ;OnValidate=BEGIN
                                                                IF Item.GET("Item No.") AND ("Ref. Order Type" <> "Ref. Order Type"::Assembly) THEN
                                                                  IF Item."Rounding Precision" > 0 THEN
                                                                    "Expected Quantity" := ROUND("Expected Quantity",Item."Rounding Precision",'>');
                                                                "Expected Quantity (Base)" := ROUND("Expected Quantity" * "Qty. per Unit of Measure",0.00001);
                                                                "Net Quantity (Base)" := "Expected Quantity (Base)" - "Original Expected Qty. (Base)";

                                                                ReservePlanningComponent.VerifyQuantity(Rec,xRec);

                                                                "Cost Amount" := ROUND("Expected Quantity" * "Unit Cost");
                                                                "Overhead Amount" :=
                                                                  ROUND(
                                                                    "Expected Quantity" *
                                                                    (("Direct Unit Cost" * "Indirect Cost %" / 100) + "Overhead Rate"));
                                                                "Direct Cost Amount" := ROUND("Expected Quantity" * "Direct Unit Cost");
                                                              END;

                                                   CaptionML=[ENU=Expected Quantity;
                                                              RUS=Ожидаемое количество];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 28  ;   ;Flushing Method     ;Option        ;CaptionML=[ENU=Flushing Method;
                                                              RUS=Метод списания];
                                                   OptionCaptionML=[ENU=Manual,Forward,Backward,Pick + Forward,Pick + Backward;
                                                                    RUS=Ручной,Прямой,Обратный,Подбор + прямой,Подбор + обратный];
                                                   OptionString=Manual,Forward,Backward,Pick + Forward,Pick + Backward }
    { 30  ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE (Use As In-Transit=CONST(No));
                                                   OnValidate=BEGIN
                                                                ReservePlanningComponent.VerifyChange(Rec,xRec);
                                                                GetUpdateFromSKU;
                                                                GetDefaultBin;
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              RUS=Код склада] }
    { 31  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              RUS=Код ярлыка измерения 1];
                                                   CaptionClass='1,2,1' }
    { 32  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              RUS=Код ярлыка измерения 2];
                                                   CaptionClass='1,2,2' }
    { 33  ;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                ReservePlanningComponent.VerifyChange(Rec,xRec);
                                                              END;

                                                   CaptionML=[ENU=Bin Code;
                                                              RUS=Код ячейки] }
    { 35  ;   ;Supplied-by Line No.;Integer       ;TableRelation="Requisition Line"."Line No." WHERE (Worksheet Template Name=FIELD(Worksheet Template Name),
                                                                                                      Journal Batch Name=FIELD(Worksheet Batch Name),
                                                                                                      Line No.=FIELD(Supplied-by Line No.));
                                                   CaptionML=[ENU=Supplied-by Line No.;
                                                              RUS=Номер строки поставки из] }
    { 36  ;   ;Planning Level Code ;Integer       ;CaptionML=[ENU=Planning Level Code;
                                                              RUS=Код уровня план.];
                                                   Editable=No }
    { 37  ;   ;Ref. Order Status   ;Option        ;CaptionML=[ENU=Ref. Order Status;
                                                              RUS=Статус ссыл. заказа];
                                                   OptionCaptionML=[ENU=Simulated,Planned,Firm Planned,Released;
                                                                    RUS=Смоделировано,Запланировано,Утверждено,Выпущено];
                                                   OptionString=Simulated,Planned,Firm Planned,Released }
    { 38  ;   ;Ref. Order No.      ;Code20        ;CaptionML=[ENU=Ref. Order No.;
                                                              RUS=Номер ссыл. заказа] }
    { 39  ;   ;Ref. Order Line No. ;Integer       ;CaptionML=[ENU=Ref. Order Line No.;
                                                              RUS=Номер строки ссыл. заказа] }
    { 40  ;   ;Length              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Length;
                                                              RUS=Длина];
                                                   DecimalPlaces=0:5 }
    { 41  ;   ;Width               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Width;
                                                              RUS=Ширина];
                                                   DecimalPlaces=0:5 }
    { 42  ;   ;Weight              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Weight;
                                                              RUS=Вес];
                                                   DecimalPlaces=0:5 }
    { 43  ;   ;Depth               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Depth;
                                                              RUS=Глубина];
                                                   DecimalPlaces=0:5 }
    { 44  ;   ;Calculation Formula ;Option        ;OnValidate=BEGIN
                                                                CASE "Calculation Formula" OF
                                                                  "Calculation Formula"::" ":
                                                                    Quantity := "Quantity per";
                                                                  "Calculation Formula"::Length:
                                                                    Quantity := ROUND(Length * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width":
                                                                    Quantity := ROUND(Length * Width * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width * Depth":
                                                                    Quantity := ROUND(Length * Width * Depth * "Quantity per",0.00001);
                                                                  "Calculation Formula"::Weight:
                                                                    Quantity := ROUND(Weight * "Quantity per",0.00001);
                                                                END;
                                                                "Quantity (Base)" := Quantity * "Qty. per Unit of Measure";
                                                                VALIDATE("Expected Quantity",Quantity * PlanningNeeds);
                                                              END;

                                                   CaptionML=[ENU=Calculation Formula;
                                                              RUS=Формула расчета];
                                                   OptionCaptionML=[ENU=" ,Length,Length * Width,Length * Width * Depth,Weight";
                                                                    RUS=" ,Длина,Длина * Ширина,Длина * Ширина * Высота,Вес"];
                                                   OptionString=[ ,Length,Length * Width,Length * Width * Depth,Weight] }
    { 45  ;   ;Quantity per        ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Quantity per;
                                                              RUS=Кол-во в];
                                                   DecimalPlaces=0:5 }
    { 46  ;   ;Ref. Order Type     ;Option        ;CaptionML=[ENU=Ref. Order Type;
                                                              RUS=Тип ссылочного заказа];
                                                   OptionCaptionML=[ENU=" ,Purchase,Prod. Order,Transfer,Assembly";
                                                                    RUS=" ,Покупка,Произв. заказ,Перемещение,Сборка"];
                                                   OptionString=[ ,Purchase,Prod. Order,Transfer,Assembly];
                                                   Editable=No }
    { 50  ;   ;Unit Cost           ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Item No.");

                                                                GetItem;
                                                                GetGLSetup;

                                                                IF Item."Costing Method" = Item."Costing Method"::Standard THEN BEGIN
                                                                  IF CurrFieldNo = FIELDNO("Unit Cost") THEN
                                                                    ERROR(
                                                                      Text001,
                                                                      FIELDCAPTION("Unit Cost"),Item.FIELDCAPTION("Costing Method"),Item."Costing Method");

                                                                  "Unit Cost" :=
                                                                    ROUND(Item."Unit Cost" * "Qty. per Unit of Measure");
                                                                  "Indirect Cost %" :=
                                                                    ROUND(Item."Indirect Cost %" * "Qty. per Unit of Measure",0.00001);
                                                                  "Overhead Rate" :=
                                                                    ROUND(Item."Overhead Rate" * "Qty. per Unit of Measure",
                                                                      GLSetup."Unit-Amount Rounding Precision");
                                                                  "Direct Unit Cost" :=
                                                                    ROUND(("Unit Cost" - "Overhead Rate") / (1 + "Indirect Cost %" / 100),
                                                                      GLSetup."Unit-Amount Rounding Precision");
                                                                END;

                                                                VALIDATE("Expected Quantity");
                                                              END;

                                                   CaptionML=[ENU=Unit Cost;
                                                              RUS=Себестоимость единицы];
                                                   AutoFormatType=1 }
    { 51  ;   ;Cost Amount         ;Decimal       ;CaptionML=[ENU=Cost Amount;
                                                              RUS=Сумма себестоимости];
                                                   AutoFormatType=2 }
    { 52  ;   ;Due Date            ;Date          ;OnValidate=VAR
                                                                CheckDateConflict@1000 : Codeunit 99000815;
                                                              BEGIN
                                                                CheckDateConflict.PlanningComponentCheck(Rec,CurrFieldNo <> 0);
                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=[ENU=Due Date;
                                                              RUS=Дата завершения] }
    { 53  ;   ;Due Time            ;Time          ;OnValidate=BEGIN
                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=[ENU=Due Time;
                                                              RUS=Время] }
    { 55  ;   ;Direct Unit Cost    ;Decimal       ;CaptionML=[ENU=Direct Unit Cost;
                                                              RUS=Прямая себестоимость единицы];
                                                   DecimalPlaces=2:5 }
    { 56  ;   ;Indirect Cost %     ;Decimal       ;OnValidate=BEGIN
                                                                "Direct Unit Cost" :=
                                                                  ROUND("Unit Cost" / (1 + "Indirect Cost %" / 100) - "Overhead Rate");
                                                              END;

                                                   CaptionML=[ENU=Indirect Cost %;
                                                              RUS=Косвенные затраты (%)];
                                                   DecimalPlaces=0:5 }
    { 57  ;   ;Overhead Rate       ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=[ENU=Overhead Rate;
                                                              RUS=Норма накладных расходов];
                                                   DecimalPlaces=0:5 }
    { 58  ;   ;Direct Cost Amount  ;Decimal       ;CaptionML=[ENU=Direct Cost Amount;
                                                              RUS=Сумма прямых затрат];
                                                   DecimalPlaces=2:2 }
    { 59  ;   ;Overhead Amount     ;Decimal       ;CaptionML=[ENU=Overhead Amount;
                                                              RUS=Сумма накл. расх.];
                                                   DecimalPlaces=2:2 }
    { 60  ;   ;Qty. per Unit of Measure;Decimal   ;CaptionML=[ENU=Qty. per Unit of Measure;
                                                              RUS=Кол-во в единице измерения];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 62  ;   ;Quantity (Base)     ;Decimal       ;CaptionML=[ENU=Quantity (Base);
                                                              RUS=Количество (баз.)];
                                                   DecimalPlaces=0:5 }
    { 63  ;   ;Reserved Qty. (Base);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry"."Quantity (Base)" WHERE (Source ID=FIELD(Worksheet Template Name),
                                                                                                                 Source Ref. No.=FIELD(Line No.),
                                                                                                                 Source Type=CONST(99000829),
                                                                                                                 Source Subtype=CONST(0),
                                                                                                                 Source Batch Name=FIELD(Worksheet Batch Name),
                                                                                                                 Source Prod. Order Line=FIELD(Worksheet Line No.),
                                                                                                                 Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Qty. (Base);
                                                              RUS=Зарезервированное кол-во (баз.)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 71  ;   ;Reserved Quantity   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry".Quantity WHERE (Source ID=FIELD(Worksheet Template Name),
                                                                                                        Source Ref. No.=FIELD(Line No.),
                                                                                                        Source Type=CONST(99000829),
                                                                                                        Source Subtype=CONST(0),
                                                                                                        Source Batch Name=FIELD(Worksheet Batch Name),
                                                                                                        Source Prod. Order Line=FIELD(Worksheet Line No.),
                                                                                                        Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Quantity;
                                                              RUS=Зарезервированное кол-во];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 73  ;   ;Expected Quantity (Base);Decimal   ;CaptionML=[ENU=Expected Quantity (Base);
                                                              RUS=Ожидаемое кол-во (баз.)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 74  ;   ;Original Expected Qty. (Base);Decimal;
                                                   CaptionML=[ENU=Original Expected Qty. (Base);
                                                              RUS=Оригин. ожид. кол-во (баз.)];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 75  ;   ;Net Quantity (Base) ;Decimal       ;CaptionML=[ENU=Net Quantity (Base);
                                                              RUS=Чистое количество (баз.)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 76  ;   ;Due Date-Time       ;DateTime      ;OnValidate=BEGIN
                                                                "Due Date" := DT2DATE("Due Date-Time");
                                                                "Due Time" := DT2TIME("Due Date-Time");
                                                                VALIDATE("Due Date");
                                                              END;

                                                   CaptionML=[ENU=Due Date-Time;
                                                              RUS=Дата и время завершения] }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              RUS=Код набора измерений];
                                                   Editable=No }
    { 99000875;;Critical           ;Boolean       ;CaptionML=[ENU=Critical;
                                                              RUS=Критический] }
    { 99000915;;Planning Line Origin;Option       ;CaptionML=[ENU=Planning Line Origin;
                                                              RUS=Происхождение строки планирования];
                                                   OptionCaptionML=[ENU=" ,Action Message,Planning,Order Planning";
                                                                    RUS=" ,Указание,Планирование,Планирование заказов"];
                                                   OptionString=[ ,Action Message,Planning,Order Planning];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Worksheet Template Name,Worksheet Batch Name,Worksheet Line No.,Line No.;
                                                   SumIndexFields=Cost Amount;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Item No.,Variant Code,Location Code,Due Date,Planning Line Origin;
                                                   SumIndexFields=Expected Quantity (Base),Cost Amount;
                                                   MaintainSIFTIndex=No }
    { No ;Item No.,Variant Code,Location Code,Shortcut Dimension 1 Code,Shortcut Dimension 2 Code,Planning Line Origin,Due Date;
                                                   SumIndexFields=Expected Quantity (Base),Cost Amount;
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    {    ;Worksheet Template Name,Worksheet Batch Name,Worksheet Line No.,Item No.;
                                                   MaintainSQLIndex=No }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot rename a %1.;RUS=Нельзя переименовать %1.';
      Text001@1001 : TextConst 'ENU=You cannot change %1 when %2 is %3.;RUS=Изменение %1, когда %2 равно %3, невозможно.';
      Item@1003 : Record 27;
      ReservEntry@1004 : Record 337;
      GLSetup@1018 : Record 98;
      ReqLine@1011 : Record 246;
      Location@1006 : Record 14;
      ReservEngineMgt@1007 : Codeunit 99000831;
      ReservePlanningComponent@1008 : Codeunit 99000840;
      UOMMgt@1009 : Codeunit 5402;
      DimMgt@1019 : Codeunit 408;
      Reservation@1002 : Page 498;
      GLSetupRead@1005 : Boolean;

    PROCEDURE Caption@5() : Text[100];
    VAR
      ReqWkshName@1000 : Record 245;
      ReqLine@1001 : Record 246;
    BEGIN
      IF GETFILTERS = '' THEN
        EXIT('');

      IF NOT ReqWkshName.GET("Worksheet Template Name","Worksheet Batch Name") THEN
        EXIT('');

      IF NOT ReqLine.GET("Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.") THEN
        CLEAR(ReqLine);

      EXIT(
        STRSUBSTNO('%1 %2 %3 %4 %5',
          "Worksheet Batch Name",ReqWkshName.Description,ReqLine.Type,ReqLine."No.",ReqLine.Description));
    END;

    LOCAL PROCEDURE PlanningNeeds@1() : Decimal;
    VAR
      PlanningRtngLine@1001 : Record 99000830;
    BEGIN
      GetReqLine;

      "Due Date" := ReqLine."Starting Date";

      PlanningRtngLine.RESET;
      PlanningRtngLine.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.","Worksheet Line No.");
      IF "Routing Link Code" <> '' THEN
        PlanningRtngLine.SETRANGE("Routing Link Code","Routing Link Code");
      IF PlanningRtngLine.FINDFIRST THEN
        EXIT(
          ReqLine.Quantity *
          (1 + ReqLine."Scrap %" / 100) *
          (1 + PlanningRtngLine."Scrap Factor % (Accumulated)") *
          (1 + "Scrap %" / 100) +
          PlanningRtngLine."Fixed Scrap Qty. (Accum.)");

      EXIT(ReqLine.Quantity * (1 + ReqLine."Scrap %" / 100) * (1 + "Scrap %" / 100));
    END;

    PROCEDURE ShowReservation@8();
    BEGIN
      TESTFIELD("Item No.");
      CLEAR(Reservation);
      Reservation.SetPlanningComponent(Rec);
      Reservation.RUNMODAL;
    END;

    PROCEDURE ShowReservationEntries@21(Modal@1000 : Boolean);
    BEGIN
      TESTFIELD("Item No.");
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,TRUE);
      ReservePlanningComponent.FilterReservFor(ReservEntry,Rec);
      IF Modal THEN
        PAGE.RUNMODAL(PAGE::"Reservation Entries",ReservEntry)
      ELSE
        PAGE.RUN(PAGE::"Reservation Entries",ReservEntry);
    END;

    PROCEDURE TransferFromComponent@3(VAR ProdOrderComp@1000 : Record 5407);
    BEGIN
      "Ref. Order Type" := "Ref. Order Type"::"Prod. Order";
      "Ref. Order Status" := ProdOrderComp.Status;
      "Ref. Order No." := ProdOrderComp."Prod. Order No.";
      "Ref. Order Line No." := ProdOrderComp."Prod. Order Line No.";
      "Line No." := ProdOrderComp."Line No.";
      "Item No." := ProdOrderComp."Item No.";
      Description := ProdOrderComp.Description;
      "Unit of Measure Code" := ProdOrderComp."Unit of Measure Code";
      "Quantity per" := ProdOrderComp."Quantity per";
      Quantity := ProdOrderComp.Quantity;
      Position := ProdOrderComp.Position;
      "Position 2" := ProdOrderComp."Position 2";
      "Position 3" := ProdOrderComp."Position 3";
      "Lead-Time Offset" := ProdOrderComp."Lead-Time Offset";
      "Routing Link Code" := ProdOrderComp."Routing Link Code";
      "Scrap %" := ProdOrderComp."Scrap %";
      "Variant Code" := ProdOrderComp."Variant Code";
      "Expected Quantity" := ProdOrderComp."Expected Quantity";
      "Location Code" := ProdOrderComp."Location Code";
      "Dimension Set ID" := ProdOrderComp."Dimension Set ID";
      "Shortcut Dimension 1 Code" := ProdOrderComp."Shortcut Dimension 1 Code";
      "Shortcut Dimension 2 Code" := ProdOrderComp."Shortcut Dimension 2 Code";
      "Bin Code" := ProdOrderComp."Bin Code";
      Length := ProdOrderComp.Length;
      Width := ProdOrderComp.Width;
      Weight := ProdOrderComp.Weight;
      Depth := ProdOrderComp.Depth;
      "Calculation Formula" := ProdOrderComp."Calculation Formula";
      "Planning Level Code" := ProdOrderComp."Planning Level Code";
      "Unit Cost" := ProdOrderComp."Unit Cost";
      "Cost Amount" := ProdOrderComp."Cost Amount";
      "Due Date" := ProdOrderComp."Due Date";
      "Direct Unit Cost" := ProdOrderComp."Direct Unit Cost";
      "Indirect Cost %" := ProdOrderComp."Indirect Cost %";
      "Overhead Rate" := ProdOrderComp."Overhead Rate";
      "Direct Cost Amount" := ProdOrderComp."Direct Cost Amount";
      "Overhead Amount" := ProdOrderComp."Overhead Amount";
      "Qty. per Unit of Measure" := ProdOrderComp."Qty. per Unit of Measure";
      "Quantity (Base)" := ProdOrderComp."Quantity (Base)";
      "Expected Quantity (Base)" := ProdOrderComp."Expected Qty. (Base)";
      "Original Expected Qty. (Base)" := ProdOrderComp."Expected Qty. (Base)";
      UpdateDatetime;
    END;

    PROCEDURE TransferFromAsmLine@11(VAR AsmLine@1000 : Record 901);
    BEGIN
      "Ref. Order Type" := "Ref. Order Type"::Assembly;
      "Ref. Order Status" := AsmLine."Document Type";
      "Ref. Order No." := AsmLine."Document No.";
      "Ref. Order Line No." := AsmLine."Line No.";
      "Line No." := AsmLine."Line No.";
      "Item No." := AsmLine."No.";
      Description := COPYSTR(AsmLine.Description,1,MAXSTRLEN(Description));
      "Unit of Measure Code" := AsmLine."Unit of Measure Code";
      "Quantity per" := AsmLine."Quantity per";
      Quantity := AsmLine."Quantity per";
      "Lead-Time Offset" := AsmLine."Lead-Time Offset";
      Position := AsmLine.Position;
      "Position 2" := AsmLine."Position 2";
      "Position 3" := AsmLine."Position 3";
      "Variant Code" := AsmLine."Variant Code";
      "Expected Quantity" := AsmLine.Quantity;
      "Location Code" := AsmLine."Location Code";
      "Dimension Set ID" := AsmLine."Dimension Set ID";
      "Shortcut Dimension 1 Code" := AsmLine."Shortcut Dimension 1 Code";
      "Shortcut Dimension 2 Code" := AsmLine."Shortcut Dimension 2 Code";
      "Bin Code" := AsmLine."Bin Code";
      "Unit Cost" := AsmLine."Unit Cost";
      "Cost Amount" := AsmLine."Cost Amount";
      "Due Date" := AsmLine."Due Date";
      "Qty. per Unit of Measure" := AsmLine."Qty. per Unit of Measure";
      "Quantity (Base)" := AsmLine."Quantity per";
      "Expected Quantity (Base)" := AsmLine."Quantity (Base)";
      "Original Expected Qty. (Base)" := AsmLine."Quantity (Base)";
      UpdateDatetime;
    END;

    LOCAL PROCEDURE GetUpdateFromSKU@4();
    VAR
      SKU@1000 : Record 5700;
      GetPlanningParameters@1001 : Codeunit 99000855;
    BEGIN
      GetPlanningParameters.AtSKU(SKU,"Item No.","Variant Code","Location Code");
      VALIDATE("Flushing Method",SKU."Flushing Method");
    END;

    PROCEDURE BlockDynamicTracking@17(SetBlock@1000 : Boolean);
    BEGIN
      ReservePlanningComponent.Block(SetBlock);
    END;

    LOCAL PROCEDURE UpdateDatetime@20();
    BEGIN
      "Due Date-Time" := CREATEDATETIME("Due Date","Due Time");
    END;

    PROCEDURE OpenItemTrackingLines@6500();
    BEGIN
      IF "Item No." <> '' THEN
        ReservePlanningComponent.CallItemTracking(Rec);
    END;

    LOCAL PROCEDURE CreateDim@12(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      TableID@1003 : ARRAY [10] OF Integer;
      No@1004 : ARRAY [10] OF Code[20];
      DimensionSetIDArr@1005 : ARRAY [10] OF Integer;
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      GetReqLine;

      //NC NOS-26 > SPA
      //DimensionSetIDArr[1] := DimMgt.GetDefaultDimID(TableID,No,'',"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0);
      DimensionSetIDArr[1] := DimMgt.GetDefaultDimID_Jnl(TableID,No,'',"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0,"Worksheet Template Name","Worksheet Batch Name");
      //NC NOS-26 < SPA
      DimensionSetIDArr[2] := ReqLine."Dimension Set ID";
      "Dimension Set ID" :=
        DimMgt.GetCombinedDimensionSetID(DimensionSetIDArr,"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@9(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    LOCAL PROCEDURE GetGLSetup@14();
    BEGIN
      IF NOT GLSetupRead THEN
        GLSetup.GET;
      GLSetupRead := TRUE;
    END;

    LOCAL PROCEDURE GetItem@2();
    BEGIN
      IF "Item No." <> Item."No." THEN
        Item.GET("Item No.");
    END;

    LOCAL PROCEDURE GetReqLine@6();
    BEGIN
      ReqLine.GET("Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.");
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        CLEAR(Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    PROCEDURE GetDefaultBin@50();
    BEGIN
      IF (Quantity * xRec.Quantity > 0) AND
         ("Item No." = xRec."Item No.") AND
         ("Location Code" = xRec."Location Code") AND
         ("Variant Code" = xRec."Variant Code")
      THEN
        EXIT;

      "Bin Code" := '';
      IF ("Location Code" <> '') AND ("Item No." <> '') THEN
        VALIDATE("Bin Code",GetDefaultConsumptionBin);
    END;

    LOCAL PROCEDURE FilterLinesWithItemToPlan@70(VAR Item@1000 : Record 27);
    BEGIN
      RESET;
      SETCURRENTKEY("Item No.");
      SETRANGE("Item No.",Item."No.");
      SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      SETFILTER("Due Date",Item.GETFILTER("Date Filter"));
      Item.COPYFILTER("Global Dimension 1 Filter","Shortcut Dimension 1 Code");
      Item.COPYFILTER("Global Dimension 2 Filter","Shortcut Dimension 2 Code");
      SETFILTER("Quantity (Base)",'<>0');
    END;

    PROCEDURE FindLinesWithItemToPlan@68(VAR Item@1000 : Record 27) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item);
      EXIT(FIND('-'));
    END;

    PROCEDURE FindCurrForecastName@40(VAR ForecastName@1001 : Code[10]) : Boolean;
    VAR
      UntrackedPlngElement@1000 : Record 99000855;
    BEGIN
      UntrackedPlngElement.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      UntrackedPlngElement.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
      UntrackedPlngElement.SETRANGE("Item No.","Item No.");
      UntrackedPlngElement.SETRANGE("Source Type",DATABASE::"Production Forecast Entry");
      IF UntrackedPlngElement.FINDFIRST THEN BEGIN
        ForecastName := COPYSTR(UntrackedPlngElement."Source ID",1,10);
        EXIT(TRUE);
      END;
    END;

    PROCEDURE ShowDimensions@7();
    BEGIN
      DimMgt.SetObjectFrom(Rec); // NC NOS-26 SPA
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet2(
          "Dimension Set ID",
          STRSUBSTNO(
            '%1 %2 %3',"Worksheet Template Name","Worksheet Batch Name",
            "Worksheet Line No."),
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    LOCAL PROCEDURE GetDefaultConsumptionBin@15() BinCode : Code[20];
    VAR
      RequisitionLine@1001 : Record 246;
      WMSManagement@1002 : Codeunit 7302;
    BEGIN
      RequisitionLine.GET("Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.");
      IF "Location Code" = RequisitionLine."Location Code" THEN
        BinCode :=
          WMSManagement.GetLastOperationFromBinCode(
            RequisitionLine."Routing No.",
            RequisitionLine."Routing Version Code",
            "Location Code",
            TRUE,
            "Flushing Method");

      IF BinCode <> '' THEN
        EXIT;

      GetLocation("Location Code");
      BinCode := Location.GetBinCode(TRUE,"Flushing Method");

      IF BinCode <> '' THEN
        EXIT;

      IF Location."Bin Mandatory" AND NOT Location."Directed Put-away and Pick" THEN
        WMSManagement.GetDefaultBin("Item No.","Variant Code","Location Code",BinCode);
    END;

    BEGIN
    {
      NC NOS-26 SPA
    }
    END.
  }
}

