OBJECT Codeunit 7309 Whse. Jnl.-Register+Print
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    TableNo=7311;
    OnRun=BEGIN
            WhseJnlLine.COPY(Rec);
            Code;
            COPY(WhseJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Do you want to register the journal lines?;RUS=Зарегистрировать строки журнала?';
      Text002@1002 : TextConst 'ENU=There is nothing to register.;RUS=Нечего регистрировать.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully registered.;RUS=Строки журнала успешно зарегистрированы.';
      Text004@1005 : TextConst 'ENU=You are now in the %1 journal.;RUS=Вы сейчас в журнале %1.';
      WhseJnlTemplate@1006 : Record 7309;
      WhseJnlLine@1007 : Record 7311;
      WarehouseReg@1000 : Record 7313;
      WhseJnlRegisterBatch@1008 : Codeunit 7304;
      TempJnlBatchName@1009 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH WhseJnlLine DO BEGIN
        WhseJnlTemplate.GET("Journal Template Name");
        WhseJnlTemplate.TESTFIELD("Registering Report ID");

        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        WhseJnlRegisterBatch.RUN(WhseJnlLine);

        IF WarehouseReg.GET(WhseJnlRegisterBatch.GetWhseRegNo) THEN BEGIN
          WarehouseReg.SETRECFILTER;
          REPORT.RUN(WhseJnlTemplate."Registering Report ID",FALSE,FALSE,WarehouseReg);
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text003 +
              Text004,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          SETRANGE("Location Code","Location Code");
          FILTERGROUP(0);
          "Line No." := 10000;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

