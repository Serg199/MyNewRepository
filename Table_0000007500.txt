OBJECT Table 7500 Item Attribute
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name;
    OnDelete=BEGIN
               IF HasBeenUsed THEN
                 IF NOT CONFIRM(DeleteUsedAttributeQst) THEN
                   ERROR('');
               DeleteValuesAndTranslations;
             END;

    OnRename=VAR
               ItemAttributeValue@1000 : Record 7501;
             BEGIN
               ItemAttributeValue.SETRANGE("Attribute ID",xRec.ID);
               IF ItemAttributeValue.FINDSET THEN
                 REPEAT
                   ItemAttributeValue.RENAME(ID,ItemAttributeValue.ID);
                 UNTIL ItemAttributeValue.NEXT = 0;
             END;

    CaptionML=[ENU=Item Attribute;
               RUS=Атрибут товара];
    LookupPageID=Page7500;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              RUS=Код];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text250       ;OnValidate=BEGIN
                                                                IF xRec.Name = Name THEN
                                                                  EXIT;

                                                                TESTFIELD(Name);
                                                                IF HasBeenUsed THEN
                                                                  IF NOT CONFIRM(RenameUsedAttributeQst) THEN
                                                                    ERROR('');
                                                                CheckNameUniqueness(Rec,Name);
                                                                DeleteValuesAndTranslationsConditionally(xRec,Name);
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              RUS=Имя];
                                                   NotBlank=Yes }
    { 6   ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              RUS=Заблокировано] }
    { 7   ;   ;Type                ;Option        ;InitValue=Text;
                                                   OnValidate=VAR
                                                                ItemAttributeValue@1000 : Record 7501;
                                                              BEGIN
                                                                IF xRec.Type <> Type THEN BEGIN
                                                                  ItemAttributeValue.SETRANGE("Attribute ID",ID);
                                                                  IF NOT ItemAttributeValue.ISEMPTY THEN
                                                                    ERROR(ChangingAttributeTypeErr,Name);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              RUS=Тип];
                                                   OptionCaptionML=[ENU=Option,Text,Integer,Decimal;
                                                                    RUS=Параметр,Текст,Целое,Десятичное];
                                                   OptionString=Option,Text,Integer,Decimal }
    { 8   ;   ;Unit of Measure     ;Text30        ;OnValidate=BEGIN
                                                                IF (xRec."Unit of Measure" <> '') AND (xRec."Unit of Measure" <> "Unit of Measure") THEN
                                                                  IF HasBeenUsed THEN
                                                                    IF NOT CONFIRM(ChangeUsedAttributeUoMQst) THEN
                                                                      ERROR('');
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure;
                                                              RUS=Единица измерения] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Name                                     }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name                                     }
    { 2   ;Brick               ;ID,Name                                  }
  }
  CODE
  {
    VAR
      ItemAttributeTranslation@1022 : Record 7502;
      NameAlreadyExistsErr@1000 : TextConst '@@@=%1 - arbitrary name;ENU=The item attribute with name ''%1'' already exists.;RUS=Атрибут товара с именем "%1" уже существует.';
      ReuseValueTranslationsQst@1001 : TextConst '@@@=%1 - arbitrary name,%2 - arbitrary name;ENU=There are values and translations for item attribute ''%1''.\\Do you want to reuse them after changing the item attribute name to ''%2''?;RUS=Существуют значения и переводы атрибута товара "%1".\\Вы хотите снова использовать их после изменения имени атрибута товара на "%2"?';
      ChangingAttributeTypeErr@1002 : TextConst '@@@=%1 - arbirtrary text;ENU=You cannot change the type of item attribute ''%1'', because it is either in use or it has predefined values.;RUS=Вы не можете изменить тип атрибута товара "%1", поскольку он используется или имеет заранее определенные значения.';
      DeleteUsedAttributeQst@1004 : TextConst 'ENU=This item attribute has been assigned to at least one item.\\Are you sure you want to delete it?;RUS=Этот атрибут товара был назначен по крайней мере одному товару.\\Вы действительно хотите удалить его?';
      RenameUsedAttributeQst@1005 : TextConst 'ENU=This item attribute has been assigned to at least one item.\\Are you sure you want to rename it?;RUS=Этот атрибут товара был назначен по крайней мере одному товару.\\Вы действительно хотите переименовать его?';
      ChangeUsedAttributeUoMQst@1003 : TextConst 'ENU=This item attribute has been assigned to at least one item.\\Are you sure you want to change its unit of measure?;RUS=Этот атрибут товара был назначен по крайней мере одному товару.\\Вы действительно хотите изменить его единицу измерения?';
      ChangeToOptionQst@1006 : TextConst 'ENU=Predefined values can be defined only for item attributes of type Option.\\Do you want to change the type of this item attribute to Option?;RUS=Заранее определенные значения могут задаваться только для атрибутов товаров типа "Параметр".\\Вы хотите изменить тип этого атрибута товара на "Параметр"?';

    PROCEDURE GetTranslatedName@3(LanguageID@1000 : Integer) : Text[250];
    VAR
      Language@1001 : Record 8;
    BEGIN
      Language.SETRANGE("Windows Language ID",LanguageID);
      IF Language.FINDFIRST THEN BEGIN
        GetAttributeTranslation(Language.Code);
        EXIT(ItemAttributeTranslation.Name);
      END;
      EXIT('');
    END;

    PROCEDURE GetNameInCurrentLanguage@4() : Text[250];
    BEGIN
      EXIT(GetTranslatedName(GLOBALLANGUAGE));
    END;

    LOCAL PROCEDURE GetAttributeTranslation@19(LanguageCode@1000 : Code[10]);
    BEGIN
      IF (ItemAttributeTranslation."Attribute ID" <> ID) OR (ItemAttributeTranslation."Language Code" <> LanguageCode) THEN
        IF NOT ItemAttributeTranslation.GET(ID,LanguageCode) THEN BEGIN
          ItemAttributeTranslation.INIT;
          ItemAttributeTranslation."Attribute ID" := ID;
          ItemAttributeTranslation."Language Code" := LanguageCode;
          ItemAttributeTranslation.Name := Name;
        END;
    END;

    PROCEDURE GetValues@1() Values : Text;
    VAR
      ItemAttributeValue@1000 : Record 7501;
    BEGIN
      IF Type <> Type::Option THEN
        EXIT('');
      ItemAttributeValue.SETRANGE("Attribute ID",ID);
      IF ItemAttributeValue.FINDSET THEN
        REPEAT
          IF Values <> '' THEN
            Values += ',';
          Values += ItemAttributeValue.Value;
        UNTIL ItemAttributeValue.NEXT = 0;
    END;

    PROCEDURE HasBeenUsed@8() : Boolean;
    VAR
      ItemAttributeValueMapping@1000 : Record 7505;
    BEGIN
      ItemAttributeValueMapping.SETRANGE("Item Attribute ID",ID);
      EXIT(NOT ItemAttributeValueMapping.ISEMPTY);
    END;

    PROCEDURE RemoveUnusedArbitraryValues@9();
    VAR
      ItemAttributeValue@1000 : Record 7501;
    BEGIN
      IF Type = Type::Option THEN
        EXIT;

      ItemAttributeValue.SETRANGE("Attribute ID",ID);
      IF ItemAttributeValue.FINDSET THEN
        REPEAT
          IF NOT ItemAttributeValue.HasBeenUsed THEN
            ItemAttributeValue.DELETE;
        UNTIL ItemAttributeValue.NEXT = 0;
    END;

    PROCEDURE OpenItemAttributeValues@2();
    VAR
      ItemAttributeValue@1000 : Record 7501;
    BEGIN
      ItemAttributeValue.SETRANGE("Attribute ID",ID);
      IF (Type <> Type::Option) AND ItemAttributeValue.ISEMPTY THEN
        IF CONFIRM(ChangeToOptionQst) THEN BEGIN
          VALIDATE(Type,Type::Option);
          MODIFY;
        END;

      IF Type = Type::Option THEN
        PAGE.RUN(PAGE::"Item Attribute Values",ItemAttributeValue);
    END;

    LOCAL PROCEDURE CheckNameUniqueness@5(ItemAttribute@1000 : Record 7500;NameToCheck@1001 : Text[250]);
    BEGIN
      ItemAttribute.SETRANGE(Name,NameToCheck);
      ItemAttribute.SETFILTER(ID,'<>%1',ItemAttribute.ID);
      IF NOT ItemAttribute.ISEMPTY THEN
        ERROR(NameAlreadyExistsErr,NameToCheck);
    END;

    LOCAL PROCEDURE DeleteValuesAndTranslationsConditionally@7(ItemAttribute@1000 : Record 7500;NameToCheck@1001 : Text[250]);
    VAR
      ItemAttributeTranslation@1002 : Record 7502;
      ItemAttributeValue@1003 : Record 7501;
      ValuesOrTranslationsExist@1004 : Boolean;
    BEGIN
      IF (ItemAttribute.Name <> '') AND (ItemAttribute.Name <> NameToCheck) THEN BEGIN
        ItemAttributeValue.SETRANGE("Attribute ID",ID);
        ItemAttributeTranslation.SETRANGE("Attribute ID",ID);
        ValuesOrTranslationsExist := NOT (ItemAttributeValue.ISEMPTY AND ItemAttributeTranslation.ISEMPTY);
        IF ValuesOrTranslationsExist THEN
          IF NOT CONFIRM(STRSUBSTNO(ReuseValueTranslationsQst,ItemAttribute.Name,NameToCheck)) THEN
            DeleteValuesAndTranslations;
      END;
    END;

    LOCAL PROCEDURE DeleteValuesAndTranslations@6();
    VAR
      ItemAttributeValue@1000 : Record 7501;
      ItemAttributeValueMapping@1001 : Record 7505;
    BEGIN
      ItemAttributeValueMapping.SETRANGE("Item Attribute ID",ID);
      ItemAttributeValueMapping.DELETEALL;

      ItemAttributeValue.SETRANGE("Attribute ID",ID);
      ItemAttributeValue.DELETEALL;

      ItemAttributeTranslation.SETRANGE("Attribute ID",ID);
      ItemAttributeTranslation.DELETEALL;
    END;

    BEGIN
    END.
  }
}

