OBJECT Codeunit 1101 CA Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    TableNo=1101;
    OnRun=BEGIN
            SourceCodeSetup.GET;
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      SourceCodeSetup@1000 : Record 242;
      Text000@1001 : TextConst 'ENU=Cost type or balance cost type must be defined.\Line %1, document %2, amount %3.;RUS=Должен быть определен тип затрат или тип балансовых затрат.\Строка %1, документ %2, сумма %3.';
      Text001@1002 : TextConst 'ENU=You cannot define both cost center and cost object.\Line %1, document %2, amount %3.;RUS=Невозможно определить и центр затрат, и объект затрат.\Строка %1, документ %2, сумма %3.';
      Text002@1003 : TextConst 'ENU=Balance cost center or balance cost object must be defined.\Line %1, document %2, amount %3.;RUS=Должен быть определен центр балансовых затрат или объект балансовых затрат.\Строка %1, документ %2, сумма %3.';
      Text003@1004 : TextConst 'ENU=You cannot define both balance cost center and balance cost object.\Line %1, document %2, amount %3.;RUS=Невозможно определить и центр балансовых затрат, и объект балансовых затрат.\Строка %1, документ %2, сумма %3.';
      Text004@1005 : TextConst 'ENU=Cost center or cost object must be defined. \Line %1, document %2, amount %3.;RUS=Должны быть определены центр затрат или объект затрат. \Строка %1, документ %2, сумма %3.';
      Text005@1006 : TextConst '@@@=starts with "Posting Date";ENU=is not within the permitted range of posting dates;RUS=находится за пределами разрешенного диапазона дат учета.';

    PROCEDURE RunCheck@3(VAR CostJnlLine@1000 : Record 1101);
    VAR
      CostType@1001 : Record 1103;
      GenJnlCheckLine@1002 : Codeunit 11;
    BEGIN
      WITH CostJnlLine DO BEGIN
        TESTFIELD("Posting Date");
        TESTFIELD("Document No.");

        SourceCodeSetup.GET;
        TESTFIELD(Amount);

        IF ("Cost Type No." = '') AND ("Bal. Cost Type No." = '') THEN
          ERROR(Text000,"Line No.","Document No.",Amount);

        IF "Cost Type No." <> '' THEN BEGIN
          CostType.GET("Cost Type No.");
          CostType.TESTFIELD(Blocked,FALSE);
          CostType.TESTFIELD(Type,CostType.Type::"Cost Type");

          IF "Source Code" <> SourceCodeSetup."G/L Entry to CA" THEN
            IF ("Cost Center Code" = '') AND ("Cost Object Code" = '') THEN
              ERROR(Text004,"Line No.","Document No.",Amount);
          IF ("Cost Center Code" <> '') AND ("Cost Object Code" <> '') THEN
            ERROR(Text001,"Line No.","Document No.",Amount);
        END;

        IF "Bal. Cost Type No." <> '' THEN BEGIN
          CostType.GET("Bal. Cost Type No.");
          CostType.TESTFIELD(Blocked,FALSE);
          CostType.TESTFIELD(Type,CostType.Type::"Cost Type");

          IF ("Bal. Cost Center Code" = '') AND ("Bal. Cost Object Code" = '') THEN
            ERROR(Text002,"Line No.","Document No.",Amount);
          IF ("Bal. Cost Center Code" <> '') AND ("Bal. Cost Object Code" <> '') THEN
            ERROR(Text003,"Line No.","Document No.",Amount);
        END;

        IF GenJnlCheckLine.DateNotAllowed("Posting Date") THEN
          FIELDERROR("Posting Date",Text005);
      END;
    END;

    BEGIN
    END.
  }
}

