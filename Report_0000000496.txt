OBJECT Report 496 Batch Post Purchase Orders
{
  OBJECT-PROPERTIES
  {
    Date=28.01.17;
    Time=12:00:00;
    Version List=NAVW110.00.00.15140;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Batch Post Purchase Orders;
               RUS=Пакет. учет заказов на покупку];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Order));
               ReqFilterHeadingML=[ENU=Purchase Order;
                                   RUS=Заказ на покупку];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  IF ApprovalsMgmt.IsPurchaseApprovalsWorkflowEnabled("Purchase Header") OR (Status = Status::"Pending Approval") THEN
                                    CurrReport.SKIP;

                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));

                                  IF NOT IsApprovedForPostingBatch THEN
                                    CurrReport.SKIP;

                                  BatchConfirmUpdateDeferralDate(BatchConfirm,ReplacePostingDate,PostingDateReq);

                                  Receive := ReceiveReq;
                                  Invoice := InvReq;
                                  CLEAR(PurchPost);
                                  PurchPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  IF PurchPost.RUN("Purchase Header") THEN BEGIN
                                    CounterOK := CounterOK + 1;
                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No.,Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   InitValue;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1   ;2   ;Field     ;
                  Name=Receive;
                  CaptionML=[ENU=Receive;
                             RUS=Получить];
                  SourceExpr=ReceiveReq }

      { 2   ;2   ;Field     ;
                  Name=Invoice;
                  CaptionML=[ENU=Invoice;
                             RUS=Счет];
                  SourceExpr=InvReq }

      { 3   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             RUS=Дата учета];
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  Name=ReplacePostingDate;
                  CaptionML=[ENU=Replace Posting Date;
                             RUS=Заменить дату учета];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=ReplaceDocumentDate;
                  CaptionML=[ENU=Replace Document Date;
                             RUS=Заменить дату документа];
                  SourceExpr=ReplaceDocumentDate }

      { 7   ;2   ;Field     ;
                  Name=CalcInvDiscount;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             RUS=Расчет скидки по счету];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the posting date.;RUS=Введите дату разноски.';
      Text001@1001 : TextConst 'ENU=Posting orders  #1########## @2@@@@@@@@@@@@@;RUS=Учет заказов  #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 orders out of a total of %2 have now been posted.;RUS=%1 заказов из общего числа %2 успешно учтены.';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      PurchCalcDisc@1005 : Codeunit 70;
      PurchPost@1006 : Codeunit 90;
      Window@1007 : Dialog;
      ReceiveReq@1008 : Boolean;
      InvReq@1009 : Boolean;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocumentDate@1015 : Boolean;
      CalcInvDisc@1016 : Boolean;
      Text003@1017 : TextConst 'ENU=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.;RUS=Курс валюты, связанный с новой датой учета в заголовке покупки, не будет применен к строкам покупки.';
      BatchConfirm@1018 : ' ,Skip,Update';

    LOCAL PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FINDFIRST THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    PROCEDURE InitializeRequest@2(NewReceiveReq@1006 : Boolean;NewInvReq@1003 : Boolean;NewPostingDateReq@1001 : Date;NewReplacePostingDate@1002 : Boolean;NewReplaceDocumentDate@1004 : Boolean;NewCalcInvDisc@1005 : Boolean);
    BEGIN
      InitValue;
      ReceiveReq := NewReceiveReq;
      InvReq := NewInvReq;
      PostingDateReq := NewPostingDateReq;
      ReplacePostingDate := NewReplacePostingDate;
      ReplaceDocumentDate := NewReplaceDocumentDate;
      IF NewCalcInvDisc THEN
        PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
      CalcInvDisc := NewCalcInvDisc;
    END;

    LOCAL PROCEDURE InitValue@3();
    BEGIN
      PurchSetup.GET;
      CalcInvDisc := PurchSetup."Calc. Inv. Discount";
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

