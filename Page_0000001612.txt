OBJECT Page 1612 Office Admin. Credentials
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1612=rimd;
    CaptionML=[ENU=Office Admin. Credentials;
               RUS=Учетные данные администратора Office];
    SaveValues=Yes;
    MultipleNewLines=No;
    InsertAllowed=Yes;
    DeleteAllowed=No;
    ModifyAllowed=Yes;
    SourceTable=Table1612;
    PageType=NavigatePage;
    OnOpenPage=BEGIN
                 ShowQuestion;
                 EmailHostedInO365 := TRUE;
               END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 9       ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 RUS=Назад];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 8       ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 RUS=Далее];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 RUS=Готово];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 IF (Email = '') OR (Password = '') OR (EmailHostedInO365 AND (Endpoint = '')) THEN
                                   ERROR(MissingCredentialErr);

                                 IF NOT INSERT(TRUE) THEN
                                   MODIFY(TRUE);

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                Name=Question;
                CaptionML=[ENU="";
                           RUS=""];
                Visible=QuestionVisible;
                GroupType=Group }

    { 12  ;2   ;Field     ;
                Name=UseO365;
                DrillDown=Yes;
                CaptionML=[ENU=Are you using an Office 365 mailbox?;
                           RUS=Вы используете почтовый ящик Office 365?];
                ToolTipML=[ENU=Specifies whether you use Office 365 for your email.;
                           RUS=Определяет, используется ли Office 365 для электронной почты.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=EmailHostedInO365 }

    { 11  ;1   ;Group     ;
                Name=O365Credential;
                CaptionML=[ENU="";
                           RUS=""];
                Visible=O365CredentialVisible;
                GroupType=Group }

    { 14  ;2   ;Field     ;
                Name=O365Email;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Office 365 admin email address;
                           RUS=Адрес эл. почты администратора Office 365];
                ToolTipML=[ENU=Specifies the email address of a user account that has permission to manage Office add-ins. For example, with Office 365, this account has the administrator role.;
                           RUS=Определяет адрес электронной почты учетной записи пользователя, у которой есть права на управление надстройками Office. Например, в случае Office 365 у этой учетной записи есть роль администратора.];
                ApplicationArea=#Basic,#Suite;
                NotBlank=Yes;
                SourceExpr=Email }

    { 13  ;2   ;Field     ;
                Name=O365Password;
                CaptionML=[ENU=Office 365 admin password;
                           RUS=Пароль администратора Office 365];
                ToolTipML=[ENU=Specifies the password of a user account that has permission to manage Office add-ins.;
                           RUS=Определяет пароль учетной записи пользователя, у которой есть права на управление надстройками Office.];
                ApplicationArea=#Basic,#Suite;
                NotBlank=Yes;
                SourceExpr=Password }

    { 6   ;1   ;Group     ;
                Name=OnPremCredential;
                CaptionML=[ENU="";
                           RUS=""];
                Visible=OnPremCredentialVisible;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                Name=OnPremUsername;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Exchange admin username;
                           RUS=Имя пользователя администратора Exchange];
                ToolTipML=[ENU=Specifies the domain username of a user account that has permission to deploy Exchange. For example, with Exchange, this account could have the administrator role.;
                           RUS=Определяет имя пользователя домена учетной записи пользователя, у которой есть права на развертывание Exchange. Например, в случае Exchange у этой учетной записи может быть роль администратора.];
                ApplicationArea=#Basic,#Suite;
                NotBlank=Yes;
                SourceExpr=Email }

    { 4   ;2   ;Field     ;
                Name=OnPremPassword;
                CaptionML=[ENU=Exchange admin password;
                           RUS=Пароль администратора Exchange];
                ToolTipML=[ENU=Specifies the password of a user account that has permission to manage Exchange add-ins.;
                           RUS=Определяет пароль учетной записи пользователя, у которой есть права на управление надстройками Exchange.];
                ApplicationArea=#Basic,#Suite;
                NotBlank=Yes;
                SourceExpr=Password }

    { 5   ;2   ;Field     ;
                Name=Endpoint;
                CaptionML=[ENU=Exchange PowerShell Endpoint;
                           RUS=Конечная точка Exchange PowerShell];
                ToolTipML=[ENU=Specifies the Exchange remote PowerShell endpoint for your Exchange server.;
                           RUS=Определяет конечную точку Exchange PowerShell для Exchange Server.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Endpoint }

  }
  CODE
  {
    VAR
      Step@1005 : 'Question,O365Credential,OnPremCredential';
      EmailHostedInO365@1009 : Boolean;
      QuestionVisible@1006 : Boolean;
      O365CredentialVisible@1007 : Boolean;
      OnPremCredentialVisible@1008 : Boolean;
      BackEnabled@1002 : Boolean;
      NextEnabled@1003 : Boolean;
      FinishEnabled@1004 : Boolean;
      MissingCredentialErr@1000 : TextConst 'ENU=You must specify both an email address and a password.;RUS=Необходимо указать как адрес электронной почты, так и пароль.';

    LOCAL PROCEDURE NextStep@1(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      CASE Step OF
        Step::Question:
          ShowQuestion;
        Step::O365Credential:
          ShowO365Credential(Backwards);
        Step::OnPremCredential:
          ShowOnPremCredential;
      END;

      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE ShowQuestion@2();
    BEGIN
      ResetControls;

      BackEnabled := FALSE;
      QuestionVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowO365Credential@3(Backwards@1000 : Boolean);
    BEGIN
      ResetControls;

      // Skip to the next window if we're not using O365.
      IF NOT EmailHostedInO365 THEN BEGIN
        NextStep(Backwards);
        EXIT;
      END;

      FinishEnabled := TRUE;
      NextEnabled := FALSE;
      O365CredentialVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowOnPremCredential@4();
    BEGIN
      ResetControls;

      FinishEnabled := TRUE;
      NextEnabled := FALSE;
      OnPremCredentialVisible := TRUE;
    END;

    LOCAL PROCEDURE ResetControls@9();
    BEGIN
      NextEnabled := TRUE;
      BackEnabled := TRUE;
      FinishEnabled := FALSE;

      QuestionVisible := FALSE;
      O365CredentialVisible := FALSE;
      OnPremCredentialVisible := FALSE;
    END;

    BEGIN
    END.
  }
}

