OBJECT Report 17302 Calculate FE Depreciation
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Calculate FE Depreciation;
               RUS=Расчет амортизации РБП];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF DeprUntilDate = 0D THEN
                    ERROR(Text000,FAJnlLine.FIELDCAPTION("FA Posting Date"));
                  IF PostingDate = 0D THEN
                    PostingDate := DeprUntilDate;
                  IF UseForceNoOfDays AND (DaysInPeriod = 0) THEN
                    ERROR(Text001);

                  Window.OPEN(
                    Text003 +
                    Text004 +
                    Text005);
                END;

  }
  DATASET
  {
    { 3794;    ;DataItem;                    ;
               DataItemTable=Table5600;
               DataItemTableView=SORTING(No.)
                                 WHERE(FA Type=CONST(Future Expense));
               OnAfterGetRecord=BEGIN
                                  IF Inactive OR Blocked OR "Undepreciable FA" THEN
                                    CurrReport.SKIP;
                                END;

               OnPostDataItem=BEGIN
                                WITH FAJnlLine DO BEGIN
                                  FAJnlLineTmp.RESET;
                                  IF FAJnlLineTmp.FIND('-') THEN BEGIN
                                    LOCKTABLE;
                                    REPEAT
                                      IF (FAJnlLineTmp."Journal Template Name" <> "Journal Template Name") OR
                                         (FAJnlLineTmp."Journal Batch Name" <> "Journal Batch Name")
                                      THEN BEGIN
                                        DeprBook.GET(FAJnlLineTmp."Depreciation Book Code");
                                        FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
                                        NoSeries := FAJnlSetup.GetFANoSeries(FAJnlLine);
                                        IF DocumentNo = '' THEN
                                          DocumentNo2 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,DeprUntilDate,TRUE)
                                        ELSE
                                          DocumentNo2 := DocumentNo;
                                      END;
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      IF "Posting Date" = "FA Posting Date" THEN
                                        "Posting Date" := 0D;
                                      "FA Posting Type" := FAJnlLineTmp."FA Posting Type";
                                      VALIDATE("FA No.",FAJnlLineTmp."FA No.");
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      Description := PostingDescription;
                                      VALIDATE("Depreciation Book Code",FAJnlLineTmp."Depreciation Book Code");
                                      VALIDATE(Amount,FAJnlLineTmp.Amount);
                                      "No. of Depreciation Days" := FAJnlLineTmp."No. of Depreciation Days";
                                      "FA Error Entry No." := FAJnlLineTmp."FA Error Entry No.";
                                      FAJnlNextLineNo := FAJnlNextLineNo + 10000;
                                      "Line No." := FAJnlNextLineNo;
                                      "Location Code" := FAJnlLineTmp."Location Code";
                                      "Employee No." := FAJnlLineTmp."Employee No.";
                                      "Depr. Period Starting Date" := FAJnlLineTmp."Depr. Period Starting Date";
                                      INSERT(TRUE);
                                    UNTIL FAJnlLineTmp.NEXT = 0;
                                  END;
                                END;

                                WITH GenJnlLine DO BEGIN
                                  GenJnlLineTmp.RESET;
                                  IF GenJnlLineTmp.FIND('-') THEN BEGIN
                                    LOCKTABLE;
                                    REPEAT
                                      IF (GenJnlLineTmp."Journal Template Name" <> "Journal Template Name") OR
                                         (GenJnlLineTmp."Journal Batch Name" <> "Journal Batch Name")
                                      THEN BEGIN
                                        DeprBook.GET(GenJnlLineTmp."Depreciation Book Code");
                                        FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
                                        NoSeries := FAJnlSetup.GetGenNoSeries(GenJnlLine);
                                        IF DocumentNo = '' THEN
                                          DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,DeprUntilDate,TRUE)
                                        ELSE
                                          DocumentNo2 := DocumentNo;
                                      END;
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      IF "Posting Date" = "FA Posting Date" THEN
                                        "FA Posting Date" := 0D;
                                      "FA Posting Type" := GenJnlLineTmp."FA Posting Type";
                                      "Account Type" := "Account Type"::"Fixed Asset";
                                      VALIDATE("Account No.",GenJnlLineTmp."Account No.");
                                      Description := PostingDescription;
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      VALIDATE("Depreciation Book Code",GenJnlLineTmp."Depreciation Book Code");
                                      VALIDATE(Amount,GenJnlLineTmp.Amount);
                                      "No. of Depreciation Days" := GenJnlLineTmp."No. of Depreciation Days";
                                      "FA Error Entry No." := GenJnlLineTmp."FA Error Entry No.";
                                      GenJnlNextLineNo := GenJnlNextLineNo + 10000;
                                      "Line No." := GenJnlNextLineNo;
                                      "Employee No." := GenJnlLineTmp."Employee No.";
                                      "FA Location Code" := GenJnlLineTmp."FA Location Code";
                                      "Depr. Period Starting Date" := GenJnlLineTmp."Depr. Period Starting Date";
                                      INSERT(TRUE);
                                      IF BalAccount THEN BEGIN
                                        FAInsertGLAcc.GetBalAcc(GenJnlLine);
                                        IF FINDLAST THEN ;
                                        GenJnlNextLineNo := "Line No.";
                                      END;
                                    UNTIL GenJnlLineTmp.NEXT = 0;
                                  END;
                                END;
                              END;

               ReqFilterFields=No. }

    { 2285;1   ;DataItem;                    ;
               DataItemTable=Table5612;
               DataItemTableView=SORTING(Depreciation Book Code,FA No.);
               OnPreDataItem=BEGIN
                               IF DepreciationBookFilter <> '' THEN
                                 SETFILTER("Depreciation Book Code",DepreciationBookFilter);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF "Depreciation Book Code" <> DeprBook.Code THEN BEGIN
                                    DeprBook.GET("Depreciation Book Code");
                                    IF (DeprUntilDate <> PostingDate) AND DeprBook."Use Same FA+G/L Posting Dates" THEN
                                      ERROR(
                                        Text002,
                                        FAJnlLine.FIELDCAPTION("FA Posting Date"),
                                        FAJnlLine.FIELDCAPTION("Posting Date"),
                                        DeprBook.FIELDCAPTION("Use Same FA+G/L Posting Dates"),
                                        FALSE,
                                        DeprBook.TABLECAPTION,
                                        DeprBook.FIELDCAPTION(Code),
                                        DeprBook.Code);
                                    IF NOT (DeprBook."No. of Days in Fiscal Year" IN [0,360]) THEN
                                      IF NOT CONFIRM(Text12412,FALSE,DeprBook.Code,DeprBook."No. of Days in Fiscal Year") THEN
                                        CurrReport.BREAK;
                                  END;

                                  IF NOT (DeprBook."Posting Book Type" IN
                                          [DeprBook."Posting Book Type"::Accounting,DeprBook."Posting Book Type"::"Tax Accounting"])
                                  THEN
                                    EXIT;

                                  CLEAR(CalculateDepr);
                                  CalculateDepr.Calculate(
                                    DeprAmount,Custom1Amount,NumberOfDays,Custom1NumberOfDays,
                                    "FA No.","Depreciation Book Code",DeprUntilDate,EntryAmounts,0D,DaysInPeriod);
                                  IF (DeprAmount <> 0) OR (Custom1Amount <> 0) THEN
                                    Window.UPDATE(1,"FA No.")
                                  ELSE
                                    Window.UPDATE(2,"FA No.");

                                  IF Custom1Amount <> 0 THEN
                                    IF NOT DeprBook."G/L Integration - Custom 1" OR "Fixed Asset"."Budgeted Asset" THEN BEGIN
                                      FAJnlSetup.FAJnlName(DeprBook,FAJnlLineTmp,JnlNextLineNoLoc);
                                      CheckFEJnlTemplate(FAJnlLineTmp."Journal Template Name");
                                      FAJnlLineTmp."FA No." := "FA No.";
                                      FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::"Custom 1";
                                      FAJnlLineTmp.Amount := Custom1Amount;
                                      FAJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                                      FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                      FAJnlLineTmp."Location Code" := "Fixed Asset"."FA Location Code";
                                      FAJnlLineTmp."Employee No." := "Fixed Asset"."Responsible Employee";
                                      FAJnlLineTmp."Depr. Period Starting Date" := Period;
                                      FAJnlLineTmp."Depreciation Book Code" := "Depreciation Book Code";
                                      FAJnlLineTmp.INSERT;
                                    END ELSE BEGIN
                                      FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,JnlNextLineNoLoc);
                                      GenJnlLineTmp."Account No." := "FA No.";
                                      GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::"Custom 1";
                                      GenJnlLineTmp.Amount := Custom1Amount;
                                      GenJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                                      GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                      GenJnlLineTmp."FA Location Code" := "Fixed Asset"."FA Location Code";
                                      GenJnlLineTmp."Employee No." := "Fixed Asset"."Responsible Employee";
                                      GenJnlLineTmp."Depr. Period Starting Date" := Period;
                                      GenJnlLineTmp."Depreciation Book Code" := "Depreciation Book Code";
                                      GenJnlLineTmp.INSERT;
                                    END;

                                  IF DeprAmount <> 0 THEN
                                    IF NOT DeprBook."G/L Integration - Depreciation" OR "Fixed Asset"."Budgeted Asset" THEN BEGIN
                                      FAJnlSetup.FAJnlName(DeprBook,FAJnlLineTmp,JnlNextLineNoLoc);
                                      CheckFEJnlTemplate(FAJnlLineTmp."Journal Template Name");
                                      FAJnlLineTmp."FA No." := "FA No.";
                                      FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::Depreciation;
                                      FAJnlLineTmp.Amount := DeprAmount;
                                      FAJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                      FAJnlLineTmp."Depr. Period Starting Date" := Period;
                                      FAJnlLineTmp."Location Code" := "Fixed Asset"."FA Location Code";
                                      FAJnlLineTmp."Employee No." := "Fixed Asset"."Responsible Employee";
                                      FAJnlLineTmp."Depreciation Book Code" := "Depreciation Book Code";
                                      FAJnlLineTmp.INSERT;
                                    END ELSE BEGIN
                                      FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,JnlNextLineNoLoc);
                                      GenJnlLineTmp."Account No." := "FA No.";
                                      GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::Depreciation;
                                      GenJnlLineTmp.Amount := DeprAmount;
                                      GenJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                      GenJnlLineTmp."FA Location Code" := "Fixed Asset"."FA Location Code";
                                      GenJnlLineTmp."Employee No." := "Fixed Asset"."Responsible Employee";
                                      GenJnlLineTmp."Depr. Period Starting Date" := Period;
                                      GenJnlLineTmp."Depreciation Book Code" := "Depreciation Book Code";
                                      GenJnlLineTmp.INSERT;
                                    END;
                                END;

               DataItemLink=FA No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   PeriodReportManagement.InitPeriod(CalendarPeriod,0);
                   PeriodReportManagement.SetCaptionPeriodYear(AccountPeriod,CalendarPeriod,ProgressiveTotal);
                   DatePeriod.COPY(CalendarPeriod);
                   PeriodReportManagement.PeriodSetup(DatePeriod,ProgressiveTotal);

                   Period := DatePeriod."Period Start";
                   SetProperties;

                   UseForceNoOfDays := TRUE;
                   DaysInPeriod := 30;
                   BalAccount := TRUE;
                   Details := FALSE;

                   IF NOT UseForceNoOfDays THEN
                     DaysInPeriod := 0;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1210008;2;Field     ;
                  Name=DepreciationBookFilter;
                  CaptionML=[ENU=Depreciation Book Filter;
                             RUS=Фильтр по книге амортизации];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DepreciationBookFilter;
                  TableRelation="Depreciation Book" }

      { 2   ;2   ;Field     ;
                  Name=AccountingPeriod;
                  CaptionML=[ENU=Accounting Period;
                             RUS=Учетный период];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=AccountPeriod;
                  OnValidate=BEGIN
                               DatePeriod.COPY(CalendarPeriod);
                               PeriodReportManagement.PeriodSetup(DatePeriod,ProgressiveTotal);

                               Period := DatePeriod."Period Start";
                               SetProperties;
                               AccountPeriodOnAfterValidate;
                             END;

                  OnLookup=BEGIN
                             PeriodReportManagement.SelectPeriod(Text,CalendarPeriod,ProgressiveTotal);
                             DatePeriod.COPY(CalendarPeriod);
                             PeriodReportManagement.PeriodSetup(DatePeriod,ProgressiveTotal);
                             RequestOptionsPage.UPDATE;
                             EXIT(TRUE);
                           END;
                            }

      { 1210005;2;Field     ;
                  Name=<Control1210005>;
                  CaptionML=[ENU=From;
                             RUS=От];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DatePeriod."Period Start";
                  Editable=FALSE }

      { 1210003;2;Field     ;
                  Name=<Control1210003>;
                  CaptionML=[ENU=To;
                             RUS=До];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DatePeriod."Period End";
                  Editable=FALSE }

      { 1210000;2;Field     ;
                  Name=ChangeDetails;
                  CaptionML=[ENU=Change Details;
                             RUS=Изменить сведения];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Details;
                  OnValidate=BEGIN
                               DaysFieldEnable := Details;
                             END;
                              }

      { 9   ;2   ;Field     ;
                  Name=DeprDate;
                  CaptionML=[ENU=FA Posting Date;
                             RUS=Дата учета ОС];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DeprUntilDate;
                  Enabled=Details;
                  OnValidate=BEGIN
                               IF DeprUntilDate <> 0D THEN
                                 IF (DeprUntilDate < Period) OR (DeprUntilDate > CALCDATE('<CM>',Period)) THEN
                                   ERROR(Text12407);
                             END;
                              }

      { 11  ;2   ;Field     ;
                  Name=PostDate;
                  CaptionML=[ENU=Posting Date;
                             RUS=Дата учета];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDate;
                  Enabled=Details;
                  OnValidate=BEGIN
                               IF PostingDate <> 0D THEN
                                 IF (PostingDate < Period) OR (PostingDate > CALCDATE('<CM>',Period)) THEN
                                   ERROR(Text12409);
                             END;
                              }

      { 1   ;2   ;Field     ;
                  Name=DocNo;
                  CaptionML=[ENU=Document No.;
                             RUS=Номер документа];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentNo;
                  Enabled=Details }

      { 13  ;2   ;Field     ;
                  Name=PostingDescr;
                  CaptionML=[ENU=Posting Description;
                             RUS=Описание учета];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDescription;
                  Enabled=Details }

      { 8   ;2   ;Field     ;
                  Name=UseNumbDays;
                  CaptionML=[ENU=Use Force No. of Days;
                             RUS=Исп. "Число дней обязательно"];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=UseForceNoOfDays;
                  Enabled=Details;
                  OnValidate=BEGIN
                               IF NOT UseForceNoOfDays THEN
                                 DaysInPeriod := 0;
                               DaysFieldEnable := UseForceNoOfDays;
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=DaysField;
                  CaptionML=[ENU=Force No. of Days;
                             RUS=Число дней обязательно];
                  ApplicationArea=#Basic,#Suite;
                  BlankZero=Yes;
                  SourceExpr=DaysInPeriod;
                  MinValue=0;
                  Enabled=DaysFieldEnable }

      { 3   ;2   ;Field     ;
                  Name=BalAccount;
                  CaptionML=[ENU=Insert Bal. Account;
                             RUS=Вставить баланс. счет];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=BalAccount;
                  Enabled=Details }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.;RUS=Необходимо указать %1.';
      Text001@1001 : TextConst 'ENU=Force No. of Days must be activated.;RUS=Параметр "Число дней обязательно" должен быть активирован.';
      Text002@1002 : TextConst 'ENU="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7.";RUS="%1 и %2 должны быть равны. %3 должно быть %4 в %5 %6 = %7."';
      Text003@1003 : TextConst 'ENU=Depreciating future expense      #1##########\;RUS=Амортизируемые расходы будущих периодов   #1##########\';
      Text004@1004 : TextConst 'ENU=Not depreciating future expense  #2##########\;RUS=Неамортизируемые расходы будущих периодов #2##########\';
      Text005@1005 : TextConst 'ENU=Inserting journal lines          #3##########;RUS=Вставка строк журнала                     #3##########';
      GenJnlLine@1007 : Record 81;
      GenJnlLineTmp@1008 : TEMPORARY Record 81;
      FAJnlLine@1010 : Record 5621;
      FAJnlLineTmp@1011 : TEMPORARY Record 5621;
      DeprBook@1012 : Record 5611;
      FAJnlSetup@1013 : Record 5605;
      CalendarPeriod@1210003 : Record 2000000007;
      DatePeriod@1210001 : Record 2000000007;
      CalculateDepr@1014 : Codeunit 5610;
      FAInsertGLAcc@1015 : Codeunit 5601;
      PeriodReportManagement@1210002 : Codeunit 12406;
      Window@1016 : Dialog;
      DeprUntilDate@1017 : Date;
      UseForceNoOfDays@1018 : Boolean;
      PostingDate@1019 : Date;
      DocumentNo@1020 : Code[20];
      DocumentNo2@1021 : Code[20];
      NoSeries@1022 : Code[10];
      PostingDescription@1023 : Text[50];
      BalAccount@1024 : Boolean;
      DaysInPeriod@1025 : Integer;
      FAJnlNextLineNo@1028 : Integer;
      GenJnlNextLineNo@1029 : Integer;
      LineNo@1030 : Integer;
      Period@1031 : Date;
      Text12407@1032 : TextConst 'ENU=FE Posting Date must be into Accounting Period.;RUS=Дата учета РБП должна входить в учетный период';
      DeprAmount@1033 : Decimal;
      Custom1Amount@1034 : Decimal;
      NumberOfDays@1035 : Integer;
      Custom1NumberOfDays@1036 : Integer;
      EntryAmounts@1037 : ARRAY [4] OF Decimal;
      JnlNextLineNoLoc@1038 : Integer;
      AccountPeriod@1045 : Text[30];
      ProgressiveTotal@1046 : Boolean;
      Text12409@1048 : TextConst 'ENU=Posting Date must be into Accounting Period.;RUS=Дата учета должна входить в учетный период';
      Details@1049 : Boolean INDATASET;
      Text12411@1050 : TextConst 'ENU=FED-;RUS=AРБП-';
      Text12410@1051 : TextConst 'ENU=" FE Depreciation";RUS=" Амортизация РБП"';
      Text12412@1052 : TextConst 'ENU="No. of Days in Fiscal Year for Depr. Book %1 = %2 will calculate incorrect depreciation amounts. Continue?";RUS="Число дней в фин. году для книги амортизации %1 = %2 приведет к некорректному расчету сумм амортизации. Продолжить?"';
      DepreciationBookFilter@1210000 : Code[250];
      DaysFieldEnable@19061781 : Boolean INDATASET;

    PROCEDURE SetProperties@2();
    BEGIN
      PostingDate := DatePeriod."Period End";
      DeprUntilDate := DatePeriod."Period End";
      PostingDescription := FORMAT(DatePeriod."Period Start",0,'<Month Text> ') +
        FORMAT(DATE2DMY(DatePeriod."Period Start",3)) + Text12410;
      IF DATE2DMY(DatePeriod."Period Start",2) > 9 THEN
        DocumentNo := Text12411 + FORMAT(DatePeriod."Period Start",0,'<Year>-<Month>')
      ELSE
        DocumentNo := Text12411 + FORMAT(DatePeriod."Period Start",2,'<Year>') + '-0' + FORMAT(DatePeriod."Period Start",0,'<Month>');
    END;

    PROCEDURE CheckFEJnlTemplate@4(JnlTemplateName@1000 : Code[10]);
    VAR
      FAJnlTemplate@1001 : Record 5619;
    BEGIN
      FAJnlTemplate.GET(JnlTemplateName);
      FAJnlTemplate.TESTFIELD(Type,FAJnlTemplate.Type::"Future Expenses");
    END;

    LOCAL PROCEDURE AccountPeriodOnAfterValidate@19032701();
    BEGIN
      UseForceNoOfDays := TRUE;
      DaysInPeriod := 30;
      DaysFieldEnable := FALSE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

