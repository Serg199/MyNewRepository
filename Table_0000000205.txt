OBJECT Table 205 Resource Unit of Measure
{
  OBJECT-PROPERTIES
  {
    Date=15.09.15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               TestResSetup;
               VerifyDelete;
             END;

    OnRename=BEGIN
               TestResSetup;
               VerifyRename;
             END;

    CaptionML=[ENU=Resource Unit of Measure;
               RUS=Единица измерения ресурса];
    LookupPageID=Page210;
  }
  FIELDS
  {
    { 1   ;   ;Resource No.        ;Code20        ;TableRelation=Resource;
                                                   CaptionML=[ENU=Resource No.;
                                                              RUS=Номер ресурса];
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Code;
                                                              RUS=Код];
                                                   NotBlank=Yes }
    { 3   ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   OnValidate=VAR
                                                                Resource@1000 : Record 156;
                                                              BEGIN
                                                                IF "Qty. per Unit of Measure" <= 0 THEN
                                                                  FIELDERROR("Qty. per Unit of Measure",Text000);
                                                                Resource.GET("Resource No.");
                                                                IF Resource."Base Unit of Measure" = Code THEN
                                                                  TESTFIELD("Qty. per Unit of Measure",1);
                                                              END;

                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              RUS=Кол-во в единице измерения];
                                                   DecimalPlaces=0:5 }
    { 4   ;   ;Related to Base Unit of Meas.;Boolean;
                                                   InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF NOT "Related to Base Unit of Meas." THEN
                                                                  "Qty. per Unit of Measure" := 1;
                                                              END;

                                                   CaptionML=[ENU=Related to Base Unit of Meas.;
                                                              RUS=Связано с баз. единицей измерения] }
  }
  KEYS
  {
    {    ;Resource No.,Code                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst '@@@=starts with "Qty. per Unit of Measure";ENU=must be greater than 0;RUS=должно быть больше 0';
      Text001@1001 : TextConst '@@@="%1 = Resource Unit of Measure, %2 = Resource Unit of Measure Code, %3 = Resource No., %4 = Base Unit of Measure";ENU=You cannot change the value %2 of the %1 field for resource %3 because it is the resource''''s %4, and there are one or more open ledger entries for the resource.;RUS=Невозможно изменить значение %2 поля %1 для ресурса %3, так как оно является %4 ресурса, при этом имеется одна или несколько открытых операций ГК для ресурса.';
      Text002@1002 : TextConst '@@@="%1 = Resource Unit of Measure, %2 = Resource Unit of Measure Code, %3 = Resource No., %4 = Base Unit of Measure";ENU=You cannot delete the value %2 of the %1 field for resource %3 because it is the resource''''s %4.;RUS=Невозможно удалить значение %2 поля %1 ресурса %3, так как оно является %4 для ресурса.';
      Res@1003 : Record 156;
      CannotModifyBaseUnitOfMeasureErr@1004 : TextConst '@@@=%1 Table name (Item Unit of measure), %2 Value of Measure (KG, PCS...), %3 Item ID, %4 Base unit of Measure;ENU=You cannot modify %1 %2 for resource %3 because it is the resource''s %4.;RUS=Нельзя изменить %1 %2 ресурса %3, так как это %4 этого ресурса.';

    LOCAL PROCEDURE VerifyDelete@2();
    VAR
      Resource@1000 : Record 156;
    BEGIN
      IF Resource.GET("Resource No.") THEN
        IF Resource."Base Unit of Measure" = Code THEN
          ERROR(Text002,TABLECAPTION,Code,"Resource No.",Resource.FIELDCAPTION("Base Unit of Measure"));
    END;

    LOCAL PROCEDURE VerifyRename@3();
    VAR
      Resource@1000 : Record 156;
      ResLedgerEntry@1001 : Record 203;
    BEGIN
      IF Resource.GET("Resource No.") THEN
        IF Resource."Base Unit of Measure" = xRec.Code THEN BEGIN
          ResLedgerEntry.SETCURRENTKEY("Resource No.");
          ResLedgerEntry.SETRANGE("Resource No.","Resource No.");
          IF NOT ResLedgerEntry.ISEMPTY THEN
            ERROR(Text001,TABLECAPTION,xRec.Code,"Resource No.",Resource.FIELDCAPTION("Base Unit of Measure"));
        END;
    END;

    LOCAL PROCEDURE TestResSetup@4();
    BEGIN
      IF Res.GET("Resource No.") THEN
        IF Res."Base Unit of Measure" = xRec.Code THEN
          ERROR(CannotModifyBaseUnitOfMeasureErr,TABLECAPTION,xRec.Code,"Resource No.",Res.FIELDCAPTION("Base Unit of Measure"));
    END;

    BEGIN
    END.
  }
}

