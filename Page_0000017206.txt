OBJECT Page 17206 Tax Register Entr. Templ. Subf
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVRU10.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Template Lines;
               RUS=Строки шаблона];
    SourceTable=Table17202;
    DelayedInsert=Yes;
    PageType=ListPart;
    AutoSplitKey=Yes;
    OnAfterGetRecord=BEGIN
                       DescriptionIndent := 0;
                       CALCFIELDS("Dimensions Filters","G/L Corr. Dimensions Filters");
                       IF "Dimensions Filters" THEN
                         DimFilters := Text1001
                       ELSE
                         DimFilters := '';

                       IF "G/L Corr. Dimensions Filters" THEN
                         GLCorrDimFilters := Text1001
                       ELSE
                         GLCorrDimFilters := '';
                       DescriptionOnFormat;
                     END;

    OnNewRecord=BEGIN
                  "Expression Type" := "Expression Type"::SumField;
                  "Link Tax Register No." := Code;
                  DimFilters := '';
                  GLCorrDimFilters := '';
                END;

    OnInsertRecord=BEGIN
                     "Expression Type" := "Expression Type"::SumField;
                     "Link Tax Register No." := Code;
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                IndentationColumnName=DescriptionIndent;
                IndentationControls=Description;
                GroupType=Repeater }

    { 24  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the report line code associated with the tax register template.;
                           RUS=Определяет код строки отчета, связанный с шаблоном налогового регистра.];
                SourceExpr="Report Line Code";
                Visible=FALSE }

    { 18  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the link tax register number associated with the tax register template.;
                           RUS=Определяет номер ссылки на налоговый регистр, связанный с шаблоном налогового регистра.];
                SourceExpr="Link Tax Register No.";
                Visible=FALSE;
                Editable=FALSE }

    { 20  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the sum field number associated with the tax register template.;
                           RUS=Определяет номер поля суммы, связанный с шаблоном налогового регистра.];
                SourceExpr="Sum Field No.";
                Visible=FALSE;
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the line code associated with the tax register template.;
                           RUS=Определяет код строки, связанный с шаблоном налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Line Code" }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the description associated with the tax register template.;
                           RUS=Определяет описание, связанное с шаблоном налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                Style=Strong;
                StyleExpr=DescriptionEmphasize }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the expression associated with the tax register template.;
                           RUS=Определяет выражение, связанное с шаблоном налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Expression }

    { 22  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the term line code associated with the tax register template.;
                           RUS=Определяет код строки термина, связанный с шаблоном налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Term Line Code" }

    { 14  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the indentation associated with the tax register template.;
                           RUS=Определяет отступ, связанный с шаблоном налогового регистра.];
                SourceExpr=Indentation;
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies if the line associated with the tax register template is bolded.;
                           RUS=Определяет, выделена ли строка, связанная с шаблоном налогового регистра, жирным шрифтом.];
                SourceExpr=Bold;
                Visible=FALSE }

    { 200 ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the period associated with the tax register template.;
                           RUS=Определяет период, связанный с шаблоном налогового регистра.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Period }

    { 100 ;2   ;Field     ;
                DrillDown=No;
                CaptionML=[ENU=Dimensions Filters;
                           RUS=Фильтры измерений];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DimFilters;
                OnAssistEdit=BEGIN
                               ShowDimensionsFilters;
                             END;
                              }

    { 1210000;2;Field     ;
                DrillDown=No;
                CaptionML=[ENU=G/L Corr. Dimensions Filters;
                           RUS=Фильтры изм. по корресп.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=GLCorrDimFilters;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               ShowGLCorrDimensionsFilters;
                             END;
                              }

  }
  CODE
  {
    VAR
      Text1001@1000 : TextConst 'ENU=Present;RUS=Есть';
      TaxRegisterName@1210003 : Record 17200;
      DimFilters@1210001 : Text[30];
      GLCorrDimFilters@1210000 : Text[30];
      Text1002@1210002 : TextConst 'ENU=%1 should be used for this type of tax register.;RUS=Для этого типа налогового регистра необходимо использовать %1.';
      DescriptionEmphasize@19010547 : Boolean INDATASET;
      DescriptionIndent@19057867 : Integer INDATASET;

    PROCEDURE ShowDimensionsFilters@2();
    VAR
      TaxRegDimFilter@1000 : Record 17218;
      TaxRegSection@1001 : Record 17207;
      DimCodeFilter@1002 : Text[1024];
    BEGIN
      CurrPage.SAVERECORD;
      COMMIT;
      TaxRegisterName.GET("Section Code",Code);
      IF TaxRegisterName."Table ID" = DATABASE::"Tax Register G/L Entry" THEN
        ERROR(Text1002,FIELDCAPTION("G/L Corr. Dimensions Filters"));

      IF ("Line No." <> 0) AND ("Expression Type" = "Expression Type"::SumField) THEN BEGIN
        TaxRegSection.GET("Section Code");
        IF (TaxRegSection."Dimension 1 Code" <> '') OR
           (TaxRegSection."Dimension 2 Code" <> '') OR
           (TaxRegSection."Dimension 3 Code" <> '') OR
           (TaxRegSection."Dimension 4 Code" <> '')
        THEN BEGIN
          TaxRegDimFilter.FILTERGROUP(2);
          TaxRegDimFilter.SETRANGE("Section Code","Section Code");
          TaxRegDimFilter.SETRANGE("Tax Register No.",Code);
          TaxRegDimFilter.SETRANGE(Define,TaxRegDimFilter.Define::Template);
          IF TaxRegSection."Dimension 1 Code" <> '' THEN
            DimCodeFilter := TaxRegSection."Dimension 1 Code";
          IF TaxRegSection."Dimension 2 Code" <> '' THEN
            DimCodeFilter := STRSUBSTNO('%1|%2',DimCodeFilter,TaxRegSection."Dimension 2 Code");
          IF TaxRegSection."Dimension 3 Code" <> '' THEN
            DimCodeFilter := STRSUBSTNO('%1|%2',DimCodeFilter,TaxRegSection."Dimension 3 Code");
          IF TaxRegSection."Dimension 4 Code" <> '' THEN
            DimCodeFilter := STRSUBSTNO('%1|%2',DimCodeFilter,TaxRegSection."Dimension 4 Code");
          DimCodeFilter := DELCHR(DimCodeFilter,'<','|');
          TaxRegDimFilter.SETFILTER("Dimension Code",DimCodeFilter);
          TaxRegDimFilter.FILTERGROUP(0);
          TaxRegDimFilter.SETRANGE("Line No.","Line No.");
          PAGE.RUNMODAL(0,TaxRegDimFilter);
        END;
      END;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE ShowGLCorrDimensionsFilters@1210001();
    VAR
      TaxRegGLCorrDimFilter@1210001 : Record 17244;
    BEGIN
      CurrPage.SAVERECORD;
      COMMIT;
      TaxRegisterName.GET("Section Code",Code);
      IF (TaxRegisterName."Table ID" = DATABASE::"Tax Register G/L Entry") AND ("Line No." <> 0) THEN BEGIN
        TaxRegGLCorrDimFilter.FILTERGROUP(2);
        TaxRegGLCorrDimFilter.SETRANGE("Section Code","Section Code");
        TaxRegGLCorrDimFilter.SETRANGE("Tax Register No.",Code);
        TaxRegGLCorrDimFilter.SETRANGE(Define,TaxRegGLCorrDimFilter.Define::Template);
        TaxRegGLCorrDimFilter.SETRANGE("Line No.","Line No.");
        TaxRegGLCorrDimFilter.FILTERGROUP(0);
        PAGE.RUNMODAL(0,TaxRegGLCorrDimFilter);
      END ELSE
        ERROR(Text1002,FIELDCAPTION("Dimensions Filters"));
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE DescriptionOnFormat@19023855();
    BEGIN
      DescriptionIndent := Indentation;
      DescriptionEmphasize := Bold;
    END;

    BEGIN
    END.
  }
}

