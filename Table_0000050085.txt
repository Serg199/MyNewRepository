OBJECT Table 50085 Field Transfer
{
  OBJECT-PROPERTIES
  {
    Date=03.07.17;
    Time=18:05:10;
    Modified=Yes;
    Version List=NOS-26;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Field Transfer;
               RUS=Поля Переноса];
  }
  FIELDS
  {
    { 1   ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                "Primary Key" := FALSE;
                                                                "Add Record" := FALSE;
                                                              END;

                                                   OnLookup=BEGIN
                                                              CLEAR(Object);
                                                              Object.SETRANGE("Object Type",Object."Object Type"::Table);
                                                              IF Object.GET(Object."Object Type"::Table,"Table No.") THEN ;
                                                              IF PAGE.RUNMODAL(PAGE::Objects,Object) = ACTION::LookupOK THEN BEGIN
                                                                VALIDATE("Table No.", Object."Object ID");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Table No.;
                                                              RUS=Таблица Но.];
                                                   BlankZero=Yes }
    { 5   ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Class=CONST(Normal));
                                                   OnValidate=BEGIN
                                                                "Primary Key" := FALSE;
                                                                "Add Record" := FALSE;

                                                                RecRef.OPEN("Table No.");
                                                                KeyR := RecRef.KEYINDEX(1);
                                                                FOR i := 1 TO KeyR.FIELDCOUNT DO BEGIN
                                                                  FieldR := KeyR.FIELDINDEX(i);
                                                                  IF FieldR.NUMBER = "Field No." THEN BEGIN
                                                                    "Primary Key" := TRUE;
                                                                  END;
                                                                END;
                                                                RecRef.CLOSE;
                                                              END;

                                                   OnLookup=BEGIN
                                                              CLEAR(Field);
                                                              Field.SETRANGE(TableNo,"Table No.");
                                                              Field.SETFILTER(Class,'%1',Field.Class::Normal);
                                                              IF Field.GET("Table No.","Field No.") THEN ;
                                                              IF PAGE.RUNMODAL(PAGE::"Field List",Field) = ACTION::LookupOK THEN BEGIN
                                                                VALIDATE("Field No.", Field."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              RUS=Поле Но.];
                                                   BlankZero=Yes }
    { 10  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              RUS=Блокировано] }
    { 15  ;   ;Primary Key         ;Boolean       ;CaptionML=[ENU=Primary Key;
                                                              RUS=Первичный ключ];
                                                   Editable=No }
    { 20  ;   ;Add Record          ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Primary Key",TRUE);

                                                                RecRef.OPEN("Table No.");
                                                                KeyR := RecRef.KEYINDEX(1);
                                                                FOR i := 1 TO KeyR.FIELDCOUNT DO BEGIN
                                                                  FieldR := KeyR.FIELDINDEX(i);
                                                                  IF "Field No." <> FieldR.NUMBER THEN BEGIN
                                                                    IF "Add Record" AND
                                                                       NOT FieldTransfer.GET("Table No.",FieldR.NUMBER) THEN
                                                                      ERROR(Text001);
                                                                    IF FieldTransfer.GET("Table No.",FieldR.NUMBER) THEN BEGIN
                                                                      FieldTransfer."Primary Key" := TRUE;
                                                                      FieldTransfer."Add Record" := "Add Record";
                                                                      FieldTransfer.MODIFY;
                                                                    END;
                                                                  END;
                                                                END;
                                                                RecRef.CLOSE;
                                                              END;

                                                   CaptionML=[ENU=Add Record;
                                                              RUS=Добавлять Запись] }
    { 25  ;   ;Table Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(TableData),
                                                                                         ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              RUS=Таблица Название];
                                                   Editable=No }
    { 30  ;   ;Field Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              RUS=Поле Название];
                                                   Editable=No }
    { 35  ;   ;Check Relation Value;Option        ;CaptionML=[ENU=Check Relationl Value;
                                                              RUS=Контроль Связи Значения];
                                                   OptionCaptionML=[ENU=Error,Ignore;
                                                                    RUS=Ошибка,Игнорировать];
                                                   OptionString=Error,Ignore }
  }
  KEYS
  {
    {    ;Table No.,Field No.                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Object@1000000000 : Record 2000000058;
      Field@1000000001 : Record 2000000041;
      RecRef@1000000002 : RecordRef;
      KeyR@1000000003 : KeyRef;
      i@1000000004 : Integer;
      FieldR@1000000005 : FieldRef;
      FieldTransfer@1000000006 : Record 50085;
      Text001@1000000007 : TextConst 'ENU=Must be configured in all the fields included in the primary key.;RUS=Должны быть настроены все поля входящие в первичный ключ.';

    BEGIN
    {
      NC MDR06 PCH создана таблица
    }
    END.
  }
}

