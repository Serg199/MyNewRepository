OBJECT Table 17374 Staff List Order Line
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GetHeader;
               StaffListOrderHeader.TESTFIELD(Status,StaffListOrderHeader.Status::Open);
             END;

    OnModify=BEGIN
               GetHeader;
               StaffListOrderHeader.TESTFIELD(Status,StaffListOrderHeader.Status::Open);
             END;

    OnDelete=BEGIN
               GetHeader;
               StaffListOrderHeader.TESTFIELD(Status,StaffListOrderHeader.Status::Open);
             END;

    CaptionML=[ENU=Staff List Order Line;
               RUS=Строка приказа по штатному расписанию];
  }
  FIELDS
  {
    { 1   ;   ;Document No.        ;Code20        ;TableRelation="Staff List Order Header";
                                                   CaptionML=[ENU=Document No.;
                                                              RUS=Номер документа] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              RUS=Номер строки] }
    { 3   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              RUS=Тип];
                                                   OptionCaptionML=[ENU=" ,Position,Org. Unit";
                                                                    RUS=" ,Позиция,Подразделение"];
                                                   OptionString=[ ,Position,Org. Unit] }
    { 4   ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Org. Unit),
                                                                     Action=CONST(Approve)) "Organizational Unit" WHERE (Type=CONST(Unit),
                                                                                                                         Status=CONST(Open))
                                                                                                                         ELSE IF (Type=CONST(Org. Unit),
                                                                                                                                  Action=FILTER(Reopen|Close|Rename)) "Organizational Unit" WHERE (Type=CONST(Unit),
                                                                                                                                                                                                   Status=CONST(Approved))
                                                                                                                                                                                                   ELSE IF (Type=CONST(Position),
                                                                                                                                                                                                            Action=CONST(Approve)) Position WHERE (Status=CONST(Planned))
                                                                                                                                                                                                            ELSE IF (Type=CONST(Position),
                                                                                                                                                                                                                     Action=FILTER(Reopen|Close)) Position WHERE (Status=CONST(Approved));
                                                   OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::"Org. Unit":
                                                                    BEGIN
                                                                      OrganizationalUnit.GET(Code);
                                                                      OrganizationalUnit.TESTFIELD(Blocked,FALSE);
                                                                      CASE Action OF
                                                                        Action::Approve:
                                                                          OrganizationalUnit.TESTFIELD(Status,OrganizationalUnit.Status::Open);
                                                                        Action::Reopen:
                                                                          OrganizationalUnit.TESTFIELD(Status,OrganizationalUnit.Status::Approved);
                                                                        Action::Close:
                                                                          OrganizationalUnit.TESTFIELD(Status,OrganizationalUnit.Status::Approved);
                                                                        Action::Rename:
                                                                          OrganizationalUnit.TESTFIELD(Status,OrganizationalUnit.Status::Approved);
                                                                      END;
                                                                      Name := OrganizationalUnit.Name;
                                                                    END;
                                                                  Type::Position:
                                                                    BEGIN
                                                                      Position.GET(Code);
                                                                      CASE Action OF
                                                                        Action::Approve:
                                                                          Position.TESTFIELD(Status,Position.Status::Planned);
                                                                        Action::Reopen:
                                                                          Position.TESTFIELD(Status,Position.Status::Approved);
                                                                        Action::Close:
                                                                          Position.TESTFIELD(Status,Position.Status::Approved);
                                                                        Action::Rename:
                                                                          ERROR(Text001,Action,Type);
                                                                      END;
                                                                      Name := COPYSTR(Position."Job Title Name" + '/' + Position."Org. Unit Name",1,MAXSTRLEN(Name));
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              RUS=Код] }
    { 5   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              RUS=Название];
                                                   Editable=No }
    { 6   ;   ;Action              ;Option        ;CaptionML=[ENU=Action;
                                                              RUS=Действие];
                                                   OptionCaptionML=[ENU=Approve,Reopen,Close,Rename;
                                                                    RUS=Утвердить,Открыть,Закрыть,Переименовать];
                                                   OptionString=Approve,Reopen,Close,Rename }
    { 7   ;   ;New Name            ;Text50        ;OnValidate=BEGIN
                                                                IF "New Name" <> '' THEN
                                                                  TESTFIELD(Action,Action::Reopen);
                                                              END;

                                                   CaptionML=[ENU=New Name;
                                                              RUS=Новое название] }
  }
  KEYS
  {
    {    ;Document No.,Line No.                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OrganizationalUnit@1210000 : Record 12422;
      Position@1210002 : Record 17370;
      StaffListOrderHeader@1210001 : Record 17373;
      Text001@1210003 : TextConst 'ENU=Action %1 should not be used for %2.;RUS=Действие %1 не может использоваться для %2.';

    PROCEDURE GetHeader@1210000();
    BEGIN
      IF StaffListOrderHeader."No." = '' THEN
        StaffListOrderHeader.GET("Document No.");
    END;

    PROCEDURE ShowComments@61();
    VAR
      HROrderCommentLine@1000 : Record 17368;
      HROrderCommentLines@1001 : Page 17394;
    BEGIN
      TESTFIELD("Document No.");
      TESTFIELD("Line No.");
      HROrderCommentLine.SETRANGE("Table Name",HROrderCommentLine."Table Name"::"SL Order");
      HROrderCommentLine.SETRANGE("No.","Document No.");
      HROrderCommentLine.SETRANGE("Line No.","Line No.");
      HROrderCommentLines.SETTABLEVIEW(HROrderCommentLine);
      HROrderCommentLines.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

