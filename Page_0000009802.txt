OBJECT Page 9802 Permission Sets
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Permission Sets;
               RUS=Наборы разрешений];
    SourceTable=Table2000000167;
    PageType=List;
    OnAfterGetRecord=BEGIN
                       PermissionEditable := ISNULLGUID("App ID");
                       RoleIDEditable := FALSE;
                     END;

    OnNewRecord=BEGIN
                  // Allow edits only on a new record - underlying tables do not allow renames.
                  RoleIDEditable := TRUE;
                END;

    OnInsertRecord=VAR
                     PermissionSet@1000 : Record 2000000004;
                   BEGIN
                     PermissionSet.INIT;
                     PermissionSet."Role ID" := "Role ID";
                     PermissionSet.Name := Name;
                     PermissionSet.INSERT;
                     PermissionEditable := ISNULLGUID("App ID");
                     RoleIDEditable := FALSE;
                     EXIT(FALSE); // Causes UI to stop processing the action - we handled it manually
                   END;

    OnModifyRecord=VAR
                     PermissionSet@1000 : Record 2000000004;
                   BEGIN
                     IF NOT ISNULLGUID("App ID") THEN
                       ERROR(AlterAppPermSetErr);

                     PermissionSet.GET(xRec."Role ID");
                     IF xRec."Role ID" <> "Role ID" THEN BEGIN
                       PermissionSet.RENAME(xRec."Role ID","Role ID");
                       PermissionSet.GET("Role ID");
                     END;
                     PermissionSet.Name := Name;
                     PermissionSet.MODIFY;
                     PermissionEditable := ISNULLGUID("App ID");
                     EXIT(FALSE); // Causes UI to stop processing the action - we handled it manually
                   END;

    OnDeleteRecord=VAR
                     PermissionSet@1000 : Record 2000000004;
                   BEGIN
                     IF NOT ISNULLGUID("App ID") THEN
                       ERROR(AlterAppPermSetErr);

                     PermissionSet.GET("Role ID");
                     PermissionSet.DELETE;
                     CurrPage.UPDATE;
                     PermissionEditable := ISNULLGUID("App ID");
                     EXIT(FALSE); // Causes UI to stop processing the action - we handled it manually
                   END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      Name=PermissionActions;
                      CaptionML=[ENU=Permissions;
                                 RUS=Права доступа];
                      ActionContainerType=RelatedInformation }
      { 7       ;1   ;ActionGroup;
                      Name=ShowPermissions;
                      CaptionML=[ENU=Permissions;
                                 RUS=Права доступа];
                      Image=Permission }
      { 6       ;2   ;Action    ;
                      Name=Permissions;
                      ShortCutKey=Shift+Ctrl+p;
                      CaptionML=[ENU=Permissions;
                                 RUS=Права доступа];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Permission;
                      PromotedCategory=Process;
                      Scope=Repeater;
                      OnAction=VAR
                                 Permission@1001 : Record 2000000005;
                                 Permissions@1000 : Page 9803;
                               BEGIN
                                 Permission.SETRANGE("Role ID","Role ID");
                                 Permissions.SETRECORD(Permission);
                                 Permissions.SETTABLEVIEW(Permission);
                                 Permissions.EDITABLE := PermissionEditable;
                                 Permissions.RUN;
                               END;
                                }
      { 11      ;2   ;Action    ;
                      CaptionML=[ENU=Permission Set by User;
                                 RUS=Набор разрешений по пользователю];
                      RunObject=Page 9816;
                      Promoted=Yes;
                      Image=Permission;
                      PromotedCategory=Process }
      { 12      ;2   ;Action    ;
                      CaptionML=[ENU=Permission Set by User Group;
                                 RUS=Набор разрешений по группе пользователей];
                      RunObject=Page 9837;
                      Promoted=Yes;
                      Image=Permission;
                      PromotedCategory=Process }
      { 15      ;1   ;ActionGroup;
                      CaptionML=[ENU=User Groups;
                                 RUS=Группы пользователей] }
      { 14      ;2   ;Action    ;
                      CaptionML=[ENU=User by User Group;
                                 RUS=Пользователь по группе пользователей];
                      RunObject=Page 9838;
                      Promoted=Yes;
                      Image=User;
                      PromotedCategory=Process }
      { 13      ;2   ;Action    ;
                      Name=UserGroups;
                      CaptionML=[ENU=User Groups;
                                 RUS=Группы пользователей];
                      RunObject=Page 9830;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Users;
                      PromotedCategory=Process }
      { 9       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 5       ;1   ;ActionGroup;
                      Name=<Functions>;
                      CaptionML=[ENU=F&unctions;
                                 RUS=Ф&ункции];
                      Image=Action }
      { 8       ;2   ;Action    ;
                      Name=<CopyPermissionSet>;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Copy Permission Set;
                                 RUS=Копировать набор разрешений];
                      Enabled=PermissionEditable;
                      Image=Copy;
                      OnAction=VAR
                                 PermissionSet@1035 : Record 2000000004;
                                 CopyPermissionSet@1034 : Report 9802;
                                 NullGUID@1000 : GUID;
                               BEGIN
                                 PermissionSet.SETRANGE("Role ID","Role ID");
                                 CopyPermissionSet.SETTABLEVIEW(PermissionSet);
                                 CopyPermissionSet.RUNMODAL;

                                 IF GET(Scope::System,NullGUID,CopyPermissionSet.GetRoleId) THEN;
                               END;
                                }
      { 18      ;2   ;Action    ;
                      Name=ImportPermissions;
                      CaptionML=[ENU=Import Permissions;
                                 RUS=Импортировать разрешения];
                      Image=Import;
                      OnAction=BEGIN
                                 XMLPORT.RUN(XMLPORT::"Import/Export Permissions",FALSE,TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                CaptionML=[ENU=Permission Set;
                           RUS=Набор разрешений];
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=PermissionSet;
                CaptionML=[ENU=Permission Set;
                           RUS=Набор разрешений];
                ToolTipML=[ENU=Specifies the permission set.;
                           RUS=Определяет набор разрешений.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Role ID";
                Editable=RoleIDEditable }

    { 4   ;2   ;Field     ;
                Name=<Name>;
                CaptionML=[ENU=Name;
                           RUS=Название];
                ToolTipML=[ENU=Specifies the name of the record.;
                           RUS=Определяет имя записи.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Name }

    { 21  ;2   ;Field     ;
                CaptionML=[ENU=Extension Name;
                           RUS=Название расширения];
                ToolTipML=[ENU=Specifies the name of the extension.;
                           RUS=Определяет имя расширения.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="App Name";
                Editable=FALSE }

    { 16  ;    ;Container ;
                ContainerType=FactBoxArea }

    { 17  ;1   ;Part      ;
                CaptionML=[ENU=Permissions;
                           RUS=Права доступа];
                ApplicationArea=#Basic,#Suite;
                SubPageLink=Role ID=FIELD(Role ID);
                PagePartID=Page9804;
                PartType=Page }

    { 19  ;1   ;Part      ;
                CaptionML=[ENU=Tenant Permissions;
                           RUS=Разрешения арендатора];
                ApplicationArea=#Basic,#Suite;
                SubPageLink=Role ID=FIELD(Role ID),
                            App ID=FIELD(App ID);
                PagePartID=Page9840;
                Editable=FALSE;
                PartType=Page }

  }
  CODE
  {
    VAR
      AlterAppPermSetErr@1001 : TextConst 'ENU=You cannot modify application-level permission sets.;RUS=Нельзя изменить наборы разрешений на уровне приложения.';
      PermissionEditable@1000 : Boolean;
      RoleIDEditable@1004 : Boolean;

    BEGIN
    END.
  }
}

