OBJECT Report 5691 Index Insurance
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Index Insurance;
               RUS=Индекс страхования];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF PostingDate = 0D THEN
                    ERROR(Text000,InsuranceJnlLine.FIELDCAPTION("Posting Date"));
                  IF PostingDate <> NORMALDATE(PostingDate) THEN
                    ERROR(Text001);
                  IF IndexFigure = 100 THEN
                    ERROR(Text002);
                  IF IndexFigure <= 0 THEN
                    ERROR(Text003);
                  FASetup.GET;
                  FASetup.TESTFIELD("Insurance Depr. Book");
                  DeprBook.GET(FASetup."Insurance Depr. Book");
                  InsuranceJnlLine.LOCKTABLE;
                  FAJnlSetup.InsuranceJnlName(DeprBook,InsuranceJnlLine,NextLineNo);
                  NoSeries := FAJnlSetup.GetInsuranceNoSeries(InsuranceJnlLine);
                  IF DocumentNo = '' THEN
                    DocumentNo := FAJnlSetup.GetInsuranceJnlDocumentNo(InsuranceJnlLine,PostingDate);
                  InsuranceJnlLine."Posting Date" := PostingDate;
                  Window.OPEN(Text004);
                END;

  }
  DATASET
  {
    { 3794;    ;DataItem;                    ;
               DataItemTable=Table5600;
               OnPreDataItem=BEGIN
                               InsCoverageLedgEntry.SETCURRENTKEY("FA No.","Insurance No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF Blocked OR Inactive THEN
                                    CurrReport.SKIP;
                                  Window.UPDATE(1,"No.");
                                  InsuranceTmp.DELETEALL;
                                  InsCoverageLedgEntry.SETRANGE("FA No.","No.");
                                  InsCoverageLedgEntry.SETRANGE("Posting Date",0D,PostingDate);
                                  IF InsCoverageLedgEntry.FIND('-') THEN
                                    REPEAT
                                      InsuranceTmp."No." := InsCoverageLedgEntry."Insurance No.";
                                      IF InsuranceTmp.INSERT THEN BEGIN
                                        InsCoverageLedgEntry.SETRANGE("Insurance No.",InsCoverageLedgEntry."Insurance No.");
                                        InsCoverageLedgEntry.CALCSUMS(Amount);
                                        InsCoverageLedgEntry.SETRANGE("Insurance No.");
                                        IF InsCoverageLedgEntry.Amount <> 0 THEN BEGIN
                                          IF Insurance.GET(InsCoverageLedgEntry."Insurance No.") THEN BEGIN
                                            IF Insurance.Blocked THEN
                                              CurrReport.SKIP;
                                          END ELSE
                                            CurrReport.SKIP;
                                          InsuranceJnlLine."Line No." := 0;
                                          FAJnlSetup.SetInsuranceJnlTrailCodes(InsuranceJnlLine);
                                          InsuranceJnlLine.VALIDATE("Insurance No.",InsCoverageLedgEntry."Insurance No.");
                                          InsuranceJnlLine.VALIDATE("FA No.","No.");
                                          InsuranceJnlLine.VALIDATE(
                                            Amount,ROUND(InsCoverageLedgEntry.Amount * (IndexFigure / 100 - 1)));
                                          InsuranceJnlLine."Document No." := DocumentNo;
                                          InsuranceJnlLine."Posting No. Series" := NoSeries;
                                          InsuranceJnlLine.Description := PostingDescription;
                                          InsuranceJnlLine."Index Entry" := TRUE;
                                          NextLineNo := NextLineNo + 10000;
                                          InsuranceJnlLine."Line No." := NextLineNo;
                                          InsuranceJnlLine.INSERT(TRUE);
                                        END;
                                      END;
                                    UNTIL InsCoverageLedgEntry.NEXT = 0;
                                END;

               ReqFilterFields=No.,FA Class Code,FA Subclass Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Index Figure;
                             RUS=Цифра индекса];
                  ToolTipML=[ENU="Specifies an index figure that is to calculate the index amounts entered in the journal. For example, if you want to index by 2%, enter 102 in this field; if you want to index by -3% percent, enter 97 in this field.";
                             RUS="Определяет цифру индекса, используемую для расчета сумм переоценки, введенных в журнал. Например, если нужно переоценить на 2%, введите в это поле 102; если нужно переоценить на -3%, введите в это поле 97."];
                  ApplicationArea=#FixedAssets;
                  DecimalPlaces=0:8;
                  SourceExpr=IndexFigure }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             RUS=Дата учета];
                  ToolTipML=[ENU=Specifies the posting date to be used by the batch job. This date appears in the Posting Date field on the insurance journal lines.;
                             RUS=Определяет дату учета, которая должна использоваться пакетным заданием. Эта дата указывается в поле "Дата учета" в строках журнала страхования.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDate }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             RUS=Номер документа];
                  ToolTipML=[ENU=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.;
                             RUS=Определяет следующий доступный номер результирующей строки журнала, если поле оставлено пустым. Если серия номеров не настроена, введите номер документа, который следует присвоить результирующей строке журнала.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DocumentNo }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             RUS=Описание учета];
                  ToolTipML=[ENU=Specifies a posting description to appear on the resulting journal lines.;
                             RUS=Определяет описание учета, которое будет отображаться в строках сформированного журнала.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDescription }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.;RUS=Необходимо указать %1.';
      Text001@1001 : TextConst 'ENU=Posting Date must not be a closing date.;RUS=Дата учета не должна быть датой закрытия.';
      Text002@1002 : TextConst 'ENU=Index Figure must not be 100.;RUS=Индекс не должен быть равен 100.';
      Text003@1003 : TextConst 'ENU=Index Figure must be positive.;RUS=Индекс должен быть положительным.';
      Text004@1004 : TextConst 'ENU=Indexing insurance    #1##########;RUS=Индексация страхования            #1##########';
      FASetup@1005 : Record 5603;
      DeprBook@1006 : Record 5611;
      Insurance@1007 : Record 5628;
      FAJnlSetup@1008 : Record 5605;
      InsuranceTmp@1009 : TEMPORARY Record 5628;
      InsCoverageLedgEntry@1010 : Record 5629;
      InsuranceJnlLine@1011 : Record 5635;
      Window@1012 : Dialog;
      PostingDate@1013 : Date;
      IndexFigure@1014 : Decimal;
      DocumentNo@1015 : Code[20];
      NoSeries@1016 : Code[10];
      PostingDescription@1017 : Text[50];
      NextLineNo@1018 : Integer;

    PROCEDURE InitializeRequest@1(DocumentNoFrom@1003 : Code[20];PostingDescriptionFrom@1002 : Text[50];PostingDateFrom@1001 : Date;IndexFigureFrom@1000 : Decimal);
    BEGIN
      DocumentNo := DocumentNoFrom;
      PostingDescription := PostingDescriptionFrom;
      PostingDate := PostingDateFrom;
      IndexFigure := IndexFigureFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

