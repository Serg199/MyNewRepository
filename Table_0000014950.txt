OBJECT Table 14950 KLADR Address
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVRU6.00.01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               UpdateHierarchy;
             END;

    OnModify=BEGIN
               UpdateHierarchy;
             END;

    OnRename=BEGIN
               UpdateHierarchy;
             END;

    CaptionML=[ENU=KLADR Address;
               RUS=Адрес по КЛАДР];
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code19        ;CaptionML=[ENU=Code;
                                                              RUS=Код] }
    { 2   ;   ;Level               ;Integer       ;CaptionML=[ENU=Level;
                                                              RUS=Уровень];
                                                   Editable=No }
    { 3   ;   ;Parent              ;Code19        ;CaptionML=[ENU=Parent;
                                                              RUS=Родитель];
                                                   Editable=No }
    { 4   ;   ;Name                ;Text40        ;CaptionML=[ENU=Name;
                                                              RUS=Название] }
    { 5   ;   ;Category Code       ;Text10        ;CaptionML=[ENU=Category Code;
                                                              RUS=Код категории] }
    { 6   ;   ;Index               ;Code6         ;CaptionML=[ENU=Index;
                                                              RUS=Индекс] }
    { 7   ;   ;GNINMB              ;Code4         ;CaptionML=[ENU=GNINMB;
                                                              RUS=Код ИФНС] }
    { 8   ;   ;Category Name       ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("KLADR Category".Name WHERE (Code=FIELD(Category Code),
                                                                                                   Level=FIELD(Level)));
                                                   CaptionML=[ENU=Category Name;
                                                              RUS=Имя категории] }
    { 9   ;   ;Building            ;Code10        ;CaptionML=[ENU=Building;
                                                              RUS=Корпус] }
    { 10  ;   ;Status              ;Code1         ;CaptionML=[ENU=Status;
                                                              RUS=Статус] }
    { 11  ;   ;UNO                 ;Code4         ;CaptionML=[ENU=UNO;
                                                              RUS=Код участника ИФНС] }
    { 12  ;   ;OKATO               ;Code11        ;CaptionML=[ENU=OKATO;
                                                              RUS=ОКАТО] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Parent                                   }
    {    ;Level,Parent,Name                        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Result@1210000 : Integer;

    PROCEDURE GetLevel@1470000(AddrCode@1470000 : Code[19]) : Integer;
    VAR
      i@1470001 : Integer;
      SplittedCode@1470002 : ARRAY [7] OF Code[4];
    BEGIN
      SplitCode(AddrCode,SplittedCode);
      i := 6;
      WHILE i > 0 DO BEGIN
        IF EVALUATE(Result,SplittedCode[i]) THEN
          IF Result <> 0 THEN
            EXIT(i);
        i := i - 1;
      END;
      EXIT(0);
    END;

    PROCEDURE UpdateHierarchy@1470001();
    BEGIN
      Level := GetLevel(Code);
      Parent := GetParentCode(Code,Level);
    END;

    PROCEDURE SplitCode@1470002(AddrCode@1470001 : Code[19];VAR SplittedCode@1470000 : ARRAY [7] OF Code[4]);
    BEGIN
      CLEAR(SplittedCode);
      IF STRLEN(AddrCode) > 17 THEN
        SplittedCode[6] := COPYSTR(AddrCode,16,4);
      IF STRLEN(AddrCode) > 13 THEN BEGIN
        SplittedCode[5] := COPYSTR(AddrCode,12,4);
        SplittedCode[7] := COPYSTR(AddrCode,16,2);
      END;
      IF STRLEN(AddrCode) = 13 THEN
        SplittedCode[7] := COPYSTR(AddrCode,12,2);
      SplittedCode[1] := COPYSTR(AddrCode,1,2);
      SplittedCode[2] := COPYSTR(AddrCode,3,3);
      SplittedCode[3] := COPYSTR(AddrCode,6,3);
      SplittedCode[4] := COPYSTR(AddrCode,9,3);
    END;

    PROCEDURE GetParentCode@1470003(AddrCode@1470000 : Code[19];Level@1470001 : Integer) Parent : Code[19];
    BEGIN
      CASE Level OF
        0,1:
          Parent := '';
        2:
          Parent := PADSTR(PADSTR(AddrCode,2),13,'0');
        3:
          Parent := PADSTR(PADSTR(AddrCode,5),13,'0');
        4:
          Parent := PADSTR(PADSTR(AddrCode,8),13,'0');
        5:
          Parent := PADSTR(PADSTR(AddrCode,11),13,'0');
        6:
          Parent := PADSTR(PADSTR(AddrCode,15),17,'0');
        7:
          Parent := AddrCode;
      END;
    END;

    PROCEDURE GetFullAddress@1470005(AddrCode@1470000 : Code[19]) : Text[250];
    VAR
      KLADRAddr@1210000 : Record 14950;
      FullAddress@1470002 : Text[250];
      CurrCode@1470003 : Code[19];
    BEGIN
      CurrCode := AddrCode;
      WHILE KLADRAddr.GET(CurrCode) DO BEGIN
        FullAddress := ', ' + KLADRAddr."Category Code" + ' ' + KLADRAddr.Name + ' ' + FullAddress;
        CurrCode := KLADRAddr.Parent;
      END;
      EXIT(COPYSTR(FullAddress,3));
    END;

    BEGIN
    END.
  }
}

