OBJECT Codeunit 242 Item Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    TableNo=83;
    OnRun=BEGIN
            ItemJnlLine.COPY(Rec);
            Code;
            COPY(ItemJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;RUS=не может фильтроваться при учете типовых журналов';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?;RUS=Учесть строки журнала и напечатать учетный отчет?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;RUS=Здесь нечего учитывать.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;RUS=Строки журнала успешно учтены.';
      Text004@1004 : TextConst 'ENU="The journal lines were successfully posted. ";RUS="Строки журнала успешно учтены. "';
      Text005@1005 : TextConst 'ENU=You are now in the %1 journal.;RUS=Вы сейчас в журнале %1.';
      ItemJnlTemplate@1006 : Record 82;
      ItemJnlLine@1007 : Record 83;
      ItemReg@1008 : Record 46;
      WhseReg@1011 : Record 7313;
      ItemJnlPostBatch@1009 : Codeunit 23;
      TempJnlBatchName@1010 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH ItemJnlLine DO BEGIN
        ItemJnlTemplate.GET("Journal Template Name");
        ItemJnlTemplate.TESTFIELD("Posting Report ID");
        IF ItemJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        ItemJnlPostBatch.RUN(ItemJnlLine);

        IF ItemReg.GET(ItemJnlPostBatch.GetItemRegNo) THEN BEGIN
          ItemReg.SETRECFILTER;
          REPORT.RUN(ItemJnlTemplate."Posting Report ID",FALSE,FALSE,ItemReg);
        END;

        IF WhseReg.GET(ItemJnlPostBatch.GetWhseRegNo) THEN BEGIN
          WhseReg.SETRECFILTER;
          REPORT.RUN(ItemJnlTemplate."Whse. Register Report ID",FALSE,FALSE,WhseReg);
        END;

        IF (ItemJnlPostBatch.GetItemRegNo = 0) AND
           (ItemJnlPostBatch.GetWhseRegNo = 0)
        THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004 +
              Text005,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

