OBJECT Report 50006 Transfer GL Entries To GJ
{
  OBJECT-PROPERTIES
  {
    Date=09.08.17;
    Time=14:06:26;
    Modified=Yes;
    Version List=NOS-26;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Transfer GL Entries To GJ;
               RUS=Перенос Фин. Операций в Фин. Журнал];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   g_recGLSetup.GET;
                   g_recGLSetup.TESTFIELD("User's Gen. Jnl. Templ.");
                   TemplateCode := g_recGLSetup."User's Gen. Jnl. Templ.";
                   IF g_recGJBatch.GET(TemplateCode,COPYSTR(USERID, 1, 10)) THEN
                     BatchCode := g_recGJBatch.Name;

                   UseNewPosting := FALSE;
                   NewPostingDate := 0D;
                   //TDRANCS-2537>
                   g_boolCdate := FALSE;
                   //TDRANCS-2537<
                   g_intRepeat := 1;
                 END;

    OnPreReport=BEGIN
                  IF UseNewPosting THEN
                    IF NewPostingDate = 0D THEN
                      ERROR(Text000);
                  IF NOT UseNewPosting THEN
                    IF NewPostingDate > 0D THEN
                      ERROR(Text001);

                  //TDRANCS-2537>
                  IF g_boolCdate THEN
                    //TDRANCS_2565>
                    IF NewPostingDate <> 0D THEN
                    //TDRANCS_2565<
                      NewPostingDate := CLOSINGDATE(NewPostingDate);
                  //TDRANCS-2537<

                  TransferGLtoGenJnl.TransferLines(GLEntryTmp,NewPostingDate,TemplateCode,
                    BatchCode,ReasonCode,Storno,g_boolSaveCurrFactor,g_intRepeat);
                END;

    OnPostReport=BEGIN
                   MESSAGE(Text55001);
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=VAR
                   GenJnlTemplate@1101495000 : Record 80;
                 BEGIN
                   UseNewPosting := FALSE;
                   NewPostingDate := 0D;
                   //NC NOS-26 > SPA
                   //RequestOptionsForm.UseNewPostDate.ENABLED(FALSE);
                   PostingDateEditable := FALSE;
                   //NC NOS-26 < SPA

                   //TDRANCS-2537>
                   g_boolCdate := FALSE;
                   //TDRANCS-2537<
                   //NC MTF02 > AKU
                   GLEntryTmp.SETRANGE("Source Type", GLEntryTmp."Source Type"::"Fin. Instrument");
                   IF GLEntryTmp.FINDFIRST THEN BEGIN
                     IF GLEntryTmp."Source Code" <> '' THEN BEGIN
                       GenJnlTemplate.SETRANGE("Source Code", GLEntryTmp."Source Code");
                       IF GenJnlTemplate.FINDFIRST THEN
                         TemplateCode := GenJnlTemplate.Name
                       ELSE BEGIN
                         GenJnlTemplate.RESET;
                         GenJnlTemplate.SETRANGE(Type, GenJnlTemplate.Type::FI);
                         IF GenJnlTemplate.FINDFIRST THEN
                           TemplateCode := GenJnlTemplate.Name;
                       END;
                     END;
                   END;
                   GLEntryTmp.RESET;
                   //NC MTF02 < AKU
                 END;

    }
    CONTROLS
    {
      { 1101495000;;Container;
                  ContainerType=ContentArea }

      { 1101495001;1;Field  ;
                  Name=TemplateCode;
                  CaptionML=[ENU=Gen. Journal Template;
                             RUS=Фин. Журнал Шаблон];
                  SourceExpr=TemplateCode;
                  TableRelation="Gen. Journal Template";
                  Editable=FALSE }

      { 1101495002;1;Field  ;
                  Name=BatchCode;
                  CaptionML=[ENU=Gen. Journal Batch.;
                             RUS=Фин. Журнал Раздел];
                  SourceExpr=BatchCode;
                  OnValidate=BEGIN
                               //NC PIF001 > DP
                               IF BatchCode <> '' THEN BEGIN
                                 GenJnlBatch.GET(TemplateCode, BatchCode);
                               END;
                               //NC PIF001 < DP
                             END;

                  OnLookup=BEGIN
                             //NC PIF001 > DP
                             GenJnlBatch.SETRANGE("Journal Template Name", TemplateCode);
                             IF PAGE.RUNMODAL(PAGE::"General Journal Batches", GenJnlBatch) = ACTION::LookupOK THEN BEGIN
                               BatchCode := GenJnlBatch.Name;
                             END;
                             //NC PIF001 < DP
                           END;
                            }

      { 1101495003;1;Field  ;
                  Name=ReasonCode;
                  CaptionML=[ENU=Reason Code;
                             RUS=Код Причины];
                  SourceExpr=ReasonCode;
                  OnValidate=BEGIN
                               //NC PIF001 > DP
                               IF ReasonCode <> '' THEN BEGIN
                                 Reason.GET(ReasonCode);
                               END;
                               //NC PIF001 < DP
                             END;

                  OnLookup=BEGIN
                             //NC PIF001 > DP
                             IF PAGE.RUNMODAL(PAGE::"Reason Codes",Reason) = ACTION::LookupOK THEN BEGIN
                               ReasonCode := Reason.Code;
                             END;
                             //NC PIF001 < DP
                           END;
                            }

      { 1101495004;1;Field  ;
                  Name=UseNewPosting;
                  CaptionML=[ENU=Use New Posting Date;
                             RUS=Исп. Новую Дату Учета];
                  SourceExpr=UseNewPosting;
                  OnValidate=BEGIN
                               PostingDateEditable := UseNewPosting;
                               IF NOT UseNewPosting THEN
                                 NewPostingDate := 0D;
                             END;
                              }

      { 1101495005;1;Field  ;
                  Name=NewPostingDate;
                  CaptionML=[ENU=New Posting Date;
                             RUS=Новая Дата Учета];
                  SourceExpr=NewPostingDate;
                  Enabled=PostingDateEditable }

      { 1101495008;1;Field  ;
                  CaptionML=[ENU=Closing Date;
                             RUS=Закрывающая дата];
                  SourceExpr=g_boolCdate;
                  Editable=PostingDateEditable }

      { 1101495006;1;Field  ;
                  Name=g_boolSaveCurrFactor;
                  CaptionML=[ENU=Save Currency Factor;
                             RUS=Сохранять Курс Валюты];
                  SourceExpr=g_boolSaveCurrFactor }

      { 1101495007;1;Field  ;
                  Name=Storno;
                  CaptionML=[ENU=Correction;
                             RUS=Перенести с Обратным Знаком];
                  SourceExpr=Storno }

      { 1101495009;1;Field  ;
                  CaptionML=[ENU=Repeat N times;
                             RUS=Повторить N раз];
                  SourceExpr=g_intRepeat }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLEntryTmp@1101495014 : TEMPORARY Record 17;
      ConsSetup@1101495008 : Record 50000;
      GenJnlBatch@1101495007 : Record 232;
      Reason@1101495005 : Record 231;
      g_recGLSetup@1101495002 : Record 98;
      g_recGJBatch@1101495001 : Record 232;
      TransferGLtoGenJnl@1101495011 : Codeunit 50004;
      TemplateCode@1101495010 : Code[20];
      BatchCode@1101495009 : Code[20];
      ReasonCode@1101495006 : Code[10];
      Storno@1101495004 : Boolean;
      g_boolSaveCurrFactor@1101495003 : Boolean;
      UseNewPosting@1101495013 : Boolean;
      g_intRepeat@1101495000 : Integer;
      NewPostingDate@1101495012 : Date;
      Text000@1101495018 : TextConst 'ENU=You must specify New Posting Date.;RUS=Вы должны определить Новую Дату Учета.';
      Text001@1101495017 : TextConst 'ENU=You must not specify New Posting Date.;RUS=Вы не должны определять Новую Дату Учета.';
      Text002@1101495016 : TextConst 'ENU=You must not specify a closing date.;RUS=Вы не должны определять закрывающую дату.';
      Text55001@1101495015 : TextConst 'ENU=Entries were transferes to Gen. Journal.;RUS=Операции успешно перенесены в Финансовый Журнал.';
      TempTableErr@1101495019 : TextConst 'ENU=In function was passed an incorrect parameter. Processing completed.;RUS=В функцию передан некорректный параметр. Обработка прекращена.';
      PostingDateEditable@1101495020 : Boolean INDATASET;
      g_boolCdate@1101495021 : Boolean;

    PROCEDURE GetGLEntry@1(VAR GLEntry2Tmp@1000 : Record 17);
    BEGIN
      //NC NOS-26 > SPA
      IF NOT GLEntry2Tmp.ISTEMPORARY THEN
        ERROR(TempTableErr);

      IF NOT GLEntryTmp.ISTEMPORARY THEN
        ERROR(TempTableErr);
      //NC NOS-26 < SPA

      GLEntryTmp.RESET;
      GLEntryTmp.DELETEALL;
      IF GLEntry2Tmp.FINDSET THEN
        REPEAT
          GLEntryTmp := GLEntry2Tmp;
          GLEntryTmp.INSERT;
        UNTIL GLEntry2Tmp.NEXT = 0;
    END;

    BEGIN
    {
      NC MP 15.05.07 Отчет по переносу строк из главной книги в фин. журнал.
      NC MTF02 AKU Выбор шаблона учета ФИ при копировании операций ФИ
      NC MTF02 AKU Свойство SaveValues установлено в No
      TDRANCS-1526
        Currency Factor depend on Posting Date
        Modified OnPreReport
      TDRANCS-1546
        Using only user's journal
          Modified OnInitReport, editable false for Gen. Journal Template on request form
      TDRANCS-1632>
        Repeat N times
      TDRANCS-2537, 2565 closing date for new posting date
      -----------------------------
      NC NOS-26 SPA Миграция на NAV2017
    }
    END.
  }
  RDLDATA
  {
  }
}

