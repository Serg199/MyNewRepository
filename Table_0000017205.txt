OBJECT Table 17205 Tax Register Term Formula
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TaxRegSection.GET("Section Code");
               TaxRegSection.ValidateChangeDeclaration;
             END;

    OnModify=BEGIN
               TaxRegSection.GET("Section Code");
               TaxRegSection.ValidateChangeDeclaration;
             END;

    OnDelete=BEGIN
               TaxRegSection.GET("Section Code");
               TaxRegSection.ValidateChangeDeclaration;
             END;

    CaptionML=[ENU=Tax Register Term Formula;
               RUS=Формула термина налогового регистра];
    LookupPageID=Page17208;
  }
  FIELDS
  {
    { 1   ;   ;Term Code           ;Code20        ;TableRelation="Tax Register Term"."Term Code";
                                                   CaptionML=[ENU=Term Code;
                                                              RUS=Код термина];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              RUS=Номер строки];
                                                   NotBlank=Yes }
    { 3   ;   ;Expression Type     ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Tax Register Term"."Expression Type" WHERE (Term Code=FIELD(Term Code)));
                                                   CaptionML=[ENU=Expression Type;
                                                              RUS=Тип выражения];
                                                   OptionCaptionML=[ENU=Plus/Minus,Multiply/Divide,Compare;
                                                                    RUS=Сложение/вычитание,Умножение/деление,Сравнение];
                                                   OptionString=Plus/Minus,Multiply/Divide,Compare }
    { 4   ;   ;Operation           ;Option        ;OnValidate=BEGIN
                                                                CALCFIELDS("Expression Type");
                                                                IF ("Expression Type" = "Expression Type"::"Plus/Minus") AND
                                                                   NOT (Operation = Operation::"+") AND NOT (Operation = Operation::"-")
                                                                THEN
                                                                  ERROR(Text000);

                                                                IF ("Expression Type" = "Expression Type"::"Multiply/Divide") AND
                                                                   NOT (Operation = Operation::"*") AND NOT (Operation = Operation::"/")
                                                                THEN
                                                                  ERROR(Text001);

                                                                IF ("Expression Type" = "Expression Type"::Compare) AND
                                                                   NOT (Operation = Operation::Negative) AND NOT (Operation = Operation::Zero) AND
                                                                   NOT (Operation = Operation::Positive)
                                                                THEN
                                                                  ERROR(Text002);

                                                                IF Operation <> xRec.Operation THEN BEGIN
                                                                  TaxRegSection.GET("Section Code");
                                                                  TaxRegSection.ValidateChangeDeclaration;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Operation;
                                                              RUS=Операция];
                                                   OptionCaptionML=[ENU=+,-,*,/,Negative,Zero,Positive;
                                                                    RUS=+,-,*,/,Отрицательный,Ноль,Положительный];
                                                   OptionString=+,-,*,/,Negative,Zero,Positive }
    { 5   ;   ;Account Type        ;Option        ;OnValidate=BEGIN
                                                                CALCFIELDS("Expression Type");
                                                                IF "Expression Type" = "Expression Type"::Compare THEN
                                                                  "Account Type" := "Account Type"::Term
                                                                ELSE BEGIN
                                                                  "Account No." := '';
                                                                  "Bal. Account No." := '';
                                                                  "Amount Type" := "Amount Type"::Debit
                                                                END;
                                                                IF "Account Type" <> xRec."Account Type" THEN BEGIN
                                                                  TaxRegSection.GET("Section Code");
                                                                  TaxRegSection.ValidateChangeDeclaration;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Account Type;
                                                              RUS=Тип счета];
                                                   OptionCaptionML=[ENU=Constant,G/L Account,Term,Net Change,Norm;
                                                                    RUS=Постоянная,Счет ГК,Термин,Оборот,Норма];
                                                   OptionString=Constant,G/L Account,Term,Net Change,Norm }
    { 6   ;   ;Account No.         ;Code100       ;TableRelation=IF (Expression Type=FILTER(<>Compare),
                                                                     Account Type=CONST(G/L Account)) "G/L Account".No.
                                                                     ELSE IF (Expression Type=FILTER(<>Compare),
                                                                              Account Type=CONST(Net Change)) "G/L Account".No.
                                                                              ELSE IF (Account Type=CONST(Term)) "Tax Register Term"."Term Code"
                                                                              ELSE IF (Account Type=CONST(Norm)) "Tax Register Norm Group".Code WHERE (Norm Jurisdiction Code=FIELD(Norm Jurisdiction Code),
                                                                                                                                                       Has Details=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Expression Type");
                                                                IF "Expression Type" = "Expression Type"::Compare THEN BEGIN
                                                                  TaxRegTermFormula.RESET;
                                                                  TaxRegTermFormula.SETRANGE("Section Code","Section Code");
                                                                  TaxRegTermFormula.SETRANGE("Term Code","Term Code");
                                                                  TaxRegTermFormula.SETFILTER("Line No.",'<>%1',"Line No.");
                                                                  TaxRegTermFormula.MODIFYALL("Account No.","Account No.",FALSE);
                                                                  TaxRegTermFormula.RESET;
                                                                END;
                                                                IF "Account No." <> xRec."Account No." THEN BEGIN
                                                                  TaxRegSection.GET("Section Code");
                                                                  TaxRegSection.ValidateChangeDeclaration;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Account No.;
                                                              RUS=Номер счета] }
    { 7   ;   ;Amount Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Account Type" = "Account Type"::"Net Change" THEN BEGIN
                                                                  IF "Amount Type" = "Amount Type"::"Net Change" THEN
                                                                    "Amount Type" := "Amount Type"::Debit;
                                                                  IF NOT ("Amount Type" IN ["Amount Type"::Debit,"Amount Type"::Credit]) THEN
                                                                    FIELDERROR("Amount Type",
                                                                      STRSUBSTNO(Text003,"Amount Type","Account Type"));
                                                                END ELSE
                                                                  IF "Account Type" <> "Account Type"::"G/L Account" THEN
                                                                    TESTFIELD("Amount Type","Amount Type"::" ")
                                                                  ELSE
                                                                    TESTFIELD("Amount Type");
                                                                IF "Amount Type" <> xRec."Amount Type" THEN BEGIN
                                                                  TaxRegSection.GET("Section Code");
                                                                  TaxRegSection.ValidateChangeDeclaration;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Amount Type;
                                                              RUS=Тип суммы];
                                                   OptionCaptionML=[ENU=" ,Net Change,Debit,Credit";
                                                                    RUS=" ,Оборот,Дебет,Кредит"];
                                                   OptionString=[ ,Net Change,Debit,Credit] }
    { 8   ;   ;Bal. Account No.    ;Code100       ;TableRelation=IF (Expression Type=CONST(Compare),
                                                                     Account Type=CONST(Term)) "Tax Register Line Setup"."Tax Register No."
                                                                     ELSE IF (Account Type=CONST(Net Change)) "G/L Account".No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Expression Type");
                                                                IF ("Account Type" = "Account Type"::"Net Change") OR
                                                                   ("Expression Type" = "Expression Type"::Compare)
                                                                THEN
                                                                  TESTFIELD("Bal. Account No.");
                                                                IF "Bal. Account No." <> xRec."Bal. Account No." THEN BEGIN
                                                                  TaxRegSection.GET("Section Code");
                                                                  TaxRegSection.ValidateChangeDeclaration;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Bal. Account No.;
                                                              RUS=Номер баланс. счета] }
    { 9   ;   ;Process Sign        ;Option        ;OnValidate=BEGIN
                                                                IF "Process Sign" <> xRec."Process Sign" THEN BEGIN
                                                                  TaxRegSection.GET("Section Code");
                                                                  TaxRegSection.ValidateChangeDeclaration;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Process Sign;
                                                              RUS=Обработка знака];
                                                   OptionCaptionML=[ENU=" ,Skip Negative,Skip Positive,Always Positive,Always Negative";
                                                                    RUS=" ,Пропустить отрицательные,Пропустить положительные,Только положительные,Только отрицательные"];
                                                   OptionString=[ ,Skip Negative,Skip Positive,Always Positive,Always Negative] }
    { 10  ;   ;Process Division by Zero;Option    ;CaptionML=[ENU=Process Division by Zero;
                                                              RUS=Деление на ноль];
                                                   OptionCaptionML=[ENU=Zero,One;
                                                                    RUS=Ноль,Единица];
                                                   OptionString=Zero,One }
    { 13  ;   ;Section Code        ;Code10        ;TableRelation="Tax Register Section";
                                                   CaptionML=[ENU=Section Code;
                                                              RUS=Код секции];
                                                   NotBlank=Yes }
    { 14  ;   ;Norm Jurisdiction Code;Code10      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Tax Register Section"."Norm Jurisdiction Code" WHERE (Code=FIELD(Section Code)));
                                                   CaptionML=[ENU=Norm Jurisdiction Code;
                                                              RUS=Код нормы юрисдикции];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Section Code,Term Code,Line No.         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TaxRegTermFormula@1000 : Record 17205;
      Text000@1001 : TextConst 'ENU=Operation must be + or -.;RUS=Операция должна быть сложением или вычитанием';
      Text001@1002 : TextConst 'ENU=Operation must be * or /.;RUS=Операция должна быть умножением или делением';
      Text002@1003 : TextConst 'ENU=Operation must be Compare.;RUS=Операция должна быть сравнением.';
      TaxRegSection@1004 : Record 17207;
      Text003@1005 : TextConst 'ENU=cannot be %1 if Account Type is %2.;RUS=не может быть %1, если тип счета - %2.';

    BEGIN
    END.
  }
}

