OBJECT Codeunit 12420 Doc. Signature Management
{
  OBJECT-PROPERTIES
  {
    Date=09.09.14;
    Time=12:00:00;
    Version List=NAVRU8.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1470000 : TextConst 'ENU=You must define Employee with type %1.;RUS=Необходимо определить сотрудника с типом %1.';
      DefaultSignSetup@1470001 : Record 12428;
      DocSign@1470002 : Record 12420;
      Text001@1210002 : TextConst 'ENU="You must define %1 in %2 for %3=%4.";RUS="Необходимо определить %1 в %2 для %3=%4."';

    PROCEDURE MoveDocSignToPostedDocSign@63(VAR FromDocSign@1000 : Record 12420;FromTableID@1001 : Integer;FromDocType@1002 : Integer;FromDocNo@1003 : Code[20];ToTableID@1005 : Integer;ToDocNo@1006 : Code[20]);
    VAR
      ToPostedDocSign@1007 : Record 12421;
    BEGIN
      WITH FromDocSign DO BEGIN
        SETRANGE("Table ID",FromTableID);
        SETRANGE("Document Type",FromDocType);
        SETRANGE("Document No.",FromDocNo);
        IF FINDSET THEN
          REPEAT
            ToPostedDocSign.INIT;
            ToPostedDocSign."Table ID" := ToTableID;
            IF FromTableID = DATABASE::"FA Document Header" THEN
              ToPostedDocSign."Document Type" := FromDocType
            ELSE
              ToPostedDocSign."Document Type" := 0;
            ToPostedDocSign."Document No." := ToDocNo;
            ToPostedDocSign."Employee Type" := "Employee Type";
            ToPostedDocSign."Employee No." := "Employee No.";
            ToPostedDocSign."Employee Name" := "Employee Name";
            ToPostedDocSign."Employee Job Title" := "Employee Job Title";
            ToPostedDocSign."Employee Org. Unit" := "Employee Org. Unit";
            ToPostedDocSign."Warrant Description" := "Warrant Description";
            ToPostedDocSign."Warrant No." := "Warrant No.";
            ToPostedDocSign."Warrant Date" := "Warrant Date";
            ToPostedDocSign.INSERT;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE SetDefaults@1470007(TableID@1470002 : Integer;DocType@1470001 : Integer;DocNo@1470000 : Code[20]);
    VAR
      DocSign@1470003 : Record 12420;
    BEGIN
      DocSign.SETRANGE("Table ID",TableID);
      DocSign.SETRANGE("Document Type",DocType);
      DocSign.SETRANGE("Document No.",DocNo);
      IF NOT DocSign.ISEMPTY THEN
        EXIT;
      WITH DefaultSignSetup DO BEGIN
        SETRANGE("Table ID",TableID);
        SETRANGE("Document Type",DocType);
        IF FINDSET THEN
          REPEAT
            DocSign.INIT;
            DocSign."Table ID" := "Table ID";
            DocSign."Document Type" := "Document Type";
            DocSign."Document No." := DocNo;
            DocSign."Employee Type" := "Employee Type";
            IF "Employee No." <> '' THEN
              DocSign.VALIDATE("Employee No.","Employee No.");
            DocSign."Warrant Description" := "Warrant Description";
            DocSign."Warrant No." := "Warrant No.";
            DocSign."Warrant Date" := "Warrant Date";
            DocSign.INSERT;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE GetDocSign@1470000(VAR DocSign@1470004 : Record 12420;TableID@1470002 : Integer;DocType@1470001 : Integer;DocNo@1470000 : Code[20];EmpType@1470003 : Integer;Check@1470005 : Boolean);
    VAR
      SignExists@1470006 : Boolean;
    BEGIN
      DocSign.INIT;
      SignExists := DocSign.GET(TableID,DocType,DocNo,EmpType);
      IF Check THEN
        IF SignExists AND (DocSign."Employee Name" = '') OR (NOT SignExists) THEN BEGIN
          DocSign."Employee Type" := EmpType;
          IF IsMandatory(TableID,DocType,EmpType) THEN
            ERROR(Text000,DocSign."Employee Type");
        END;
    END;

    PROCEDURE GetPostedDocSign@1470001(VAR PostedDocSign@1470009 : Record 12421;TableID@1470008 : Integer;DocType@1470007 : Integer;DocNo@1470006 : Code[20];EmpType@1470005 : Integer;Check@1470000 : Boolean);
    VAR
      SignExists@1470001 : Boolean;
    BEGIN
      PostedDocSign.INIT;
      SignExists := PostedDocSign.GET(TableID,DocType,DocNo,EmpType);
      IF Check AND (PostedDocSign."Employee Name" = '') OR (NOT SignExists) THEN BEGIN
        PostedDocSign."Employee Type" := EmpType;
        IF IsMandatory(TableID,DocType,EmpType) THEN
          ERROR(Text000,PostedDocSign."Employee Type");
      END;
    END;

    PROCEDURE InsertDefault@1470003(TableID@1470000 : Integer;DocType@1470001 : Integer;EmpType@1470002 : 'Director,Accountant,Cashier,ApprovedBy,ReleasedBy,ReceivedBy,PassedBy,RequestedBy,Chairman,Comm1,Comm2,Comm3,StoredBy';EmpNo@1210000 : Code[20];WarrantDesc@1210001 : Text[30];WarrantNo@1210002 : Text[20];WarrantDate@1210003 : Date;Mandatory2@1210004 : Boolean);
    BEGIN
      DefaultSignSetup.INIT;
      DefaultSignSetup."Table ID" := TableID;
      DefaultSignSetup."Document Type" := DocType;
      DefaultSignSetup."Employee Type" := EmpType;
      IF EmpNo <> '' THEN
        DefaultSignSetup.VALIDATE("Employee No.",EmpNo);
      DefaultSignSetup."Warrant Description" := WarrantDesc;
      DefaultSignSetup."Warrant No." := WarrantNo;
      DefaultSignSetup."Warrant Date" := WarrantDate;
      DefaultSignSetup.Mandatory := Mandatory2;
      DefaultSignSetup.INSERT;
    END;

    PROCEDURE IsMandatory@1470004(TableID@1470002 : Integer;DocType@1470001 : Integer;EmpType@1470000 : 'Director,Accountant,Cashier,ApprovedBy,ReleasedBy,ReceivedBy,PassedBy,RequestedBy,Chairman,Comm1,Comm2,Comm3,StoredBy') : Boolean;
    BEGIN
      IF DefaultSignSetup.GET(TableID,DocType,EmpType) THEN
        EXIT(DefaultSignSetup.Mandatory);

      EXIT(FALSE);
    END;

    PROCEDURE DeleteDocSign@17(TableID@1000 : Integer;DocType@1001 : Integer;DocNo@1002 : Code[20]);
    BEGIN
      DocSign.SETRANGE("Table ID",TableID);
      DocSign.SETRANGE("Document Type",DocType);
      DocSign.SETRANGE("Document No.",DocNo);
      DocSign.DELETEALL;
    END;

    PROCEDURE CheckDocSignatures@1210000(TableID@1210001 : Integer;DocumentType@1210002 : Integer;DocumentNo@1210003 : Code[20]);
    VAR
      DocumentSignature@1210000 : Record 12420;
    BEGIN
      DefaultSignSetup.RESET;
      DefaultSignSetup.SETRANGE("Table ID",TableID);
      DefaultSignSetup.SETRANGE("Document Type",DocumentType);
      DefaultSignSetup.SETRANGE(Mandatory,TRUE);
      IF DefaultSignSetup.FINDSET THEN
        REPEAT
          IF NOT DocumentSignature.GET(TableID,DocumentType,DocumentNo,DefaultSignSetup."Employee Type") OR
             (DocumentSignature."Employee No." = '')
          THEN
            ERROR(
              Text001,
              DocumentSignature.FIELDCAPTION("Employee No."),
              DocumentSignature.TABLECAPTION,
              DocumentSignature.FIELDCAPTION("Employee Type"),
              DefaultSignSetup."Employee Type");
        UNTIL DefaultSignSetup.NEXT = 0;
    END;

    PROCEDURE DeletePostedDocSign@1210001(TableID@1210000 : Integer;DocNo@1210001 : Code[20]);
    VAR
      PostedDocSign@1210002 : Record 12421;
    BEGIN
      PostedDocSign.RESET;
      PostedDocSign.SETRANGE("Table ID",TableID);
      PostedDocSign.SETRANGE("Document No.",DocNo);
      PostedDocSign.DELETEALL;
    END;

    BEGIN
    END.
  }
}

