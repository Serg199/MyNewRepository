OBJECT Codeunit 1805 Import Config. Package Files
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    TableNo=2000000170;
    OnRun=VAR
            LogInManagement@1000 : Codeunit 40;
          BEGIN
            LogInManagement.InitializeCompany;
            ImportConfigurationPackageFiles(Rec);
          END;

  }
  CODE
  {
    VAR
      NoPackDefinedMsg@1000 : TextConst '@@@="%1 = Filter String";ENU=Critical Error: No configuration package file is defined within the specified filter %1. Please contact your system administrator.;RUS=Критическая ошибка: в указанном фильтре %1 не определено ни одного файла пакета конфигурации. Обратитесь к системному администратору.';
      ImportStartedMsg@1003 : TextConst '@@@="%1 = Configuration Package Code, %2 = Company Name";ENU=The import of the %1 configuration package to the %2 company has started.;RUS=Запущен импорт пакета конфигурации %1 в организацию %2.';
      ImportSuccessfulMsg@1001 : TextConst '@@@="%1 = Configuration Package Code, %2 = Company Name";ENU=The configuration package %1 was successfully imported to the %2 company.;RUS=Пакет конфигурации %1 успешно импортирован в организацию %2.';
      ApplicationStartedMsg@1002 : TextConst '@@@="%1 = Configuration Package Code, %2 = Company Name";ENU=Application of the %1 configuration package to the %2 company has started.;RUS=Применение пакета конфигурации %1 к компании %2 запущено.';
      ApplicationSuccessfulMsg@1005 : TextConst '@@@="%1 = Configuration Package Code, %2 = Company Name";ENU=The configuration package %1 was successfully applied to the %2 company.;RUS=Пакет конфигурации %1 успешно применен к компании %2.';
      ApplicationFailedMsg@1004 : TextConst '@@@="%1 = No. of errors, %2 = Package Code, %3 = Company Name";ENU=Critical Error: %1 errors occurred during the application of the %2 package to the %3 company. Please contact your system administrator.;RUS=Критическая ошибка: при применении пакета %2 к компании %3 возникли ошибки (%1 шт.). Обратитесь к системному администратору.';

    LOCAL PROCEDURE ImportConfigurationPackageFiles@1(VAR ConfigurationPackageFile@1000 : Record 2000000170);
    VAR
      AssistedCompanySetupStatus@1007 : Record 1802;
      TempConfigSetupSystemRapidStart@1006 : TEMPORARY Record 8627;
      ConfigPackageImport@1005 : Codeunit 8620;
      AssistedCompanySetup@1004 : Codeunit 1800;
      ServerTempFileName@1003 : Text;
      ErrorCount@1002 : Integer;
      TotalNoOfErrors@1001 : Integer;
    BEGIN
      AssistedCompanySetupStatus.GET(COMPANYNAME);
      AssistedCompanySetupStatus."Company Setup Session ID" := SESSIONID;
      AssistedCompanySetupStatus.MODIFY;
      COMMIT;

      ConfigurationPackageFile.SETCURRENTKEY("Processing Order");
      IF ConfigurationPackageFile.FINDSET THEN BEGIN
        REPEAT
          MESSAGE(ImportStartedMsg,ConfigurationPackageFile.Code,COMPANYNAME);
          ServerTempFileName := AssistedCompanySetup.GetConfigurationPackageFile(ConfigurationPackageFile);
          ConfigPackageImport.ImportRapidStartPackage(ServerTempFileName,TempConfigSetupSystemRapidStart);
          MESSAGE(ImportSuccessfulMsg,ConfigurationPackageFile.Code,COMPANYNAME);

          MESSAGE(ApplicationStartedMsg,ConfigurationPackageFile.Code,COMPANYNAME);
          ErrorCount := TempConfigSetupSystemRapidStart.ApplyPackages;
          TotalNoOfErrors += ErrorCount;
          TempConfigSetupSystemRapidStart.DELETE;
          ERASE(ServerTempFileName);
          IF ErrorCount > 0 THEN
            MESSAGE(ApplicationFailedMsg,ErrorCount,ConfigurationPackageFile.Code,COMPANYNAME)
          ELSE
            MESSAGE(ApplicationSuccessfulMsg,ConfigurationPackageFile.Code,COMPANYNAME);
        UNTIL ConfigurationPackageFile.NEXT = 0;
        IF TotalNoOfErrors > 0 THEN
          AssistedCompanySetupStatus.VALIDATE("Import Failed",TRUE)
        ELSE
          AssistedCompanySetupStatus.VALIDATE("Package Imported",TRUE);
      END ELSE BEGIN
        AssistedCompanySetupStatus.VALIDATE("Import Failed",TRUE);
        MESSAGE(NoPackDefinedMsg,ConfigurationPackageFile.GETFILTERS);
      END;
      AssistedCompanySetupStatus."Company Setup Session ID" := 0;
      AssistedCompanySetupStatus.MODIFY;
      COMMIT;
    END;

    BEGIN
    {
      // This code unit is executed in a separate session. Messages and errors will be output to the event log.
    }
    END.
  }
}

