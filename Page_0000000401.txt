OBJECT Page 401 Purch. Credit Memo Statistics
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Purch. Credit Memo Statistics;
               RUS=Статистика кредит-нот покупки];
    LinksAllowed=No;
    SourceTable=Table124;
    PageType=ListPlus;
    OnAfterGetRecord=VAR
                       VendLedgEntry@1000 : Record 25;
                     BEGIN
                       CLEARALL;

                       IF "Currency Code" = '' THEN
                         Currency.InitRoundingPrecision
                       ELSE
                         Currency.GET("Currency Code");

                       PurchCrMemoLine.SETRANGE("Document No.","No.");

                       IF PurchCrMemoLine.FIND('-') THEN
                         REPEAT
                           VendAmount := VendAmount + PurchCrMemoLine.Amount;
                           AmountInclVAT := AmountInclVAT + PurchCrMemoLine."Amount Including VAT";
                           IF "Prices Including VAT" THEN
                             InvDiscAmount := InvDiscAmount + PurchCrMemoLine."Inv. Discount Amount" / (1 + PurchCrMemoLine."VAT %" / 100)
                           ELSE
                             InvDiscAmount := InvDiscAmount + PurchCrMemoLine."Inv. Discount Amount";
                           LineQty := LineQty + PurchCrMemoLine.Quantity;
                           TotalNetWeight := TotalNetWeight + (PurchCrMemoLine.Quantity * PurchCrMemoLine."Net Weight");
                           TotalGrossWeight := TotalGrossWeight + (PurchCrMemoLine.Quantity * PurchCrMemoLine."Gross Weight");
                           TotalVolume := TotalVolume + (PurchCrMemoLine.Quantity * PurchCrMemoLine."Unit Volume");
                           IF PurchCrMemoLine."Units per Parcel" > 0 THEN
                             TotalParcels := TotalParcels + ROUND(PurchCrMemoLine.Quantity / PurchCrMemoLine."Units per Parcel",1,'>');
                           IF PurchCrMemoLine."VAT %" <> VATPercentage THEN
                             IF VATPercentage = 0 THEN
                               VATPercentage := PurchCrMemoLine."VAT %"
                             ELSE
                               VATPercentage := -1;
                         UNTIL PurchCrMemoLine.NEXT = 0;
                       VATAmount := AmountInclVAT - VendAmount;
                       InvDiscAmount := ROUND(InvDiscAmount,Currency."Amount Rounding Precision");

                       IF VATPercentage <= 0 THEN
                         VATAmountText := Text000
                       ELSE
                         VATAmountText := STRSUBSTNO(Text001,VATPercentage);

                       IF "Currency Code" = '' THEN
                         AmountLCY := VendAmount
                       ELSE
                         AmountLCY :=
                           CurrExchRate.ExchangeAmtFCYToLCY(
                             WORKDATE,"Currency Code",VendAmount,"Currency Factor");

                       VendLedgEntry.SETCURRENTKEY("Document No.");
                       VendLedgEntry.SETRANGE("Document No.","No.");
                       VendLedgEntry.SETRANGE("Document Type",VendLedgEntry."Document Type"::"Credit Memo");
                       VendLedgEntry.SETRANGE("Vendor No.","Pay-to Vendor No.");
                       IF VendLedgEntry.FINDFIRST THEN
                         AmountLCY := VendLedgEntry."Purchase (LCY)";

                       IF NOT Vend.GET("Pay-to Vendor No.") THEN
                         CLEAR(Vend);
                       Vend.CALCFIELDS("Balance (LCY)");

                       PurchCrMemoLine.CalcVATAmountLines(Rec,TempVATAmountLine);
                       CurrPage.SubForm.PAGE.SetTempVATAmountLine(TempVATAmountLine);
                       CurrPage.SubForm.PAGE.InitGlobals("Currency Code",FALSE,FALSE,FALSE,FALSE,"VAT Base Discount %");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 29  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           RUS=Общее] }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Amount;
                           RUS=Сумма];
                ToolTipML=[ENU=Specifies the net amount of all the lines in the purchase document.;
                           RUS=Определяет чистую сумму по всем строкам в документе покупки.];
                SourceExpr=VendAmount + InvDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Inv. Discount Amount;
                           RUS=Сумма скидки по счету];
                ToolTipML=[ENU=Specifies the invoice discount amount for the purchase document.;
                           RUS=Определяет сумму скидки по счету для документа покупки.];
                SourceExpr=InvDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Total;
                           RUS=Всего];
                ToolTipML=[ENU=Specifies the total amount, less any invoice discount amount, and excluding VAT for the purchase document.;
                           RUS=Определяет общую сумму за вычетом любой суммы скидки по счету и НДС для документа покупки.];
                SourceExpr=VendAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=VAT Amount;
                           RUS=Сумма НДС];
                ToolTipML=[ENU=Specifies the total VAT amount that has been calculated for all the lines in the purchase document.;
                           RUS=Определяет общую сумму НДС, вычисленную по всем строкам документа покупки.];
                SourceExpr=VATAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT(VATAmountText) }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Total Incl. VAT;
                           RUS=Всего, с учетом НДС];
                ToolTipML=[ENU=Specifies the total amount, including VAT, that will be posted to the vendor's account for all the lines in the purchase document.;
                           RUS=Определяет общую сумму (с НДС), которая будет учтена на счете поставщика для всех строк документа покупки.];
                SourceExpr=AmountInclVAT;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=Purchase (LCY);
                           RUS=Покупка (руб.)];
                ToolTipML=[ENU=Specifies your total purchases.;
                           RUS=Определяет общую сумму покупок.];
                SourceExpr=AmountLCY;
                AutoFormatType=1 }

    { 3   ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           RUS=Кол-во];
                ToolTipML=[ENU=Specifies the total quantity of G/L account entries, items and/or resources in the purchase document.;
                           RUS=Определяет общее количество по операциям счета ГК, товарам и/или ресурсам в документе покупки.];
                DecimalPlaces=0:5;
                SourceExpr=LineQty }

    { 13  ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           RUS=Упаковок];
                ToolTipML=[ENU=Specifies the total number of parcels in the purchase document.;
                           RUS=Определяет общее число упаковок по документу покупки.];
                DecimalPlaces=0:5;
                SourceExpr=TotalParcels }

    { 7   ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           RUS=Вес нетто];
                ToolTipML=[ENU=Specifies the total net weight of the items in the purchase document.;
                           RUS=Определяет суммарный вес нетто товаров по документу покупки.];
                DecimalPlaces=0:5;
                SourceExpr=TotalNetWeight }

    { 10  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           RUS=Вес брутто];
                ToolTipML=[ENU=Specifies the total gross weight of the items in the purchase document.;
                           RUS=Определяет суммарный вес брутто товаров по документу покупки.];
                DecimalPlaces=0:5;
                SourceExpr=TotalGrossWeight }

    { 17  ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           RUS=Объем];
                ToolTipML=[ENU=Specifies the total volume of the items in the purchase document.;
                           RUS=Определяет суммарный объем товаров по документу покупки.];
                DecimalPlaces=0:5;
                SourceExpr=TotalVolume }

    { 18  ;1   ;Part      ;
                Name=SubForm;
                PagePartID=Page576;
                Editable=FALSE }

    { 1901061301;1;Group  ;
                CaptionML=[ENU=Vendor;
                           RUS=Поставщик] }

    { 21  ;2   ;Field     ;
                CaptionML=[ENU=Balance (LCY);
                           RUS=Сальдо (руб.)];
                ToolTipML=[ENU=Specifies the balance in LCY on the vendor's account.;
                           RUS=Определяет сальдо счета поставщика в рублях.];
                SourceExpr=Vend."Balance (LCY)";
                AutoFormatType=1 }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=VAT Amount;RUS=Сумма НДС';
      Text001@1001 : TextConst 'ENU=%1% VAT;RUS=%1% НДС';
      CurrExchRate@1002 : Record 330;
      PurchCrMemoLine@1003 : Record 125;
      Vend@1004 : Record 23;
      TempVATAmountLine@1005 : TEMPORARY Record 290;
      Currency@1018 : Record 4;
      VendAmount@1006 : Decimal;
      AmountInclVAT@1007 : Decimal;
      InvDiscAmount@1008 : Decimal;
      AmountLCY@1009 : Decimal;
      LineQty@1010 : Decimal;
      TotalNetWeight@1011 : Decimal;
      TotalGrossWeight@1012 : Decimal;
      TotalVolume@1013 : Decimal;
      TotalParcels@1014 : Decimal;
      VATAmount@1015 : Decimal;
      VATPercentage@1016 : Decimal;
      VATAmountText@1017 : Text[30];

    BEGIN
    END.
  }
}

