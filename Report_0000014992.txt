OBJECT Report 14992 FA Movement FA-15
{
  OBJECT-PROPERTIES
  {
    Date=09.09.14;
    Time=12:00:00;
    Version List=NAVRU8.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=FA Movement FA-15;
               RUS=Перемещение ОС-15];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  FA15ReportHelper.InitReportTemplate;
                END;

    OnPostReport=BEGIN
                   IF FileName <> '' THEN
                     FA15ReportHelper.ExportDataFile(FileName)
                   ELSE
                     FA15ReportHelper.ExportData;
                 END;

  }
  DATASET
  {
    { 6422;    ;DataItem;                    ;
               DataItemTable=Table12470;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Movement));
               OnAfterGetRecord=BEGIN
                                  FASetup.GET;

                                  GetFAComments(Complect,FAComment.Type::Complect);
                                  GetFAComments(Defect,FAComment.Type::Defect);
                                  GetFAComments(Result,FAComment.Type::Result);
                                  GetFAComments(Reason,FAComment.Type::Reason);
                                  GetFAComments(Conclusion,FAComment.Type::Conclusion);

                                  CheckSignature(ReleasedBy,ReleasedBy."Employee Type"::ReleasedBy);
                                  CheckSignature(ReceivedBy,ReceivedBy."Employee Type"::ReceivedBy);
                                  CheckSignature(StoredBy,StoredBy."Employee Type"::StoredBy);

                                  FA15ReportHelper.FillHeader(
                                    FALocation.GetName("FA Location Code"),Reason[1],"Reason Document No.",FORMAT("Reason Document Date"),
                                    "No.",FORMAT("Posting Date"),FALocation.GetName("New FA Location Code"));
                                END;
                                 }

    { 3337;1   ;DataItem;                    ;
               DataItemTable=Table12477;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnAfterGetRecord=BEGIN
                                  FixetAsset.GET("FA No.");
                                  TESTFIELD("Depreciation Book Code");
                                  TESTFIELD("FA Posting Group");

                                  IF FASetup."FA Location Mandatory" THEN
                                    TESTFIELD("FA Location Code");
                                  IF FASetup."Employee No. Mandatory" THEN
                                    TESTFIELD("FA Employee No.");

                                  FA15ReportHelper.FillBody(
                                    Description,FixetAsset."Factory No.",FixetAsset."Passport No.",FORMAT("FA Posting Date"),
                                    StdRepMgt.FormatReportValue(Quantity,2),StdRepMgt.FormatReportValue("Book Value",2),StdRepMgt.FormatReportValue(Amount,2));
                                END;

               OnPostDataItem=BEGIN
                                FA15ReportHelper.FillLastHeader(Result,Complect,Defect,Conclusion);
                                FA15ReportHelper.FillLastFooter(
                                  ReleasedBy."Employee Job Title",ReleasedBy."Employee Name",
                                  ReceivedBy."Employee Job Title",ReceivedBy."Employee Name",
                                  StoredBy."Employee Job Title",StoredBy."Employee Name");
                              END;

               DataItemLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      FASetup@1210006 : Record 5603;
      FixetAsset@1210001 : Record 5600;
      ReceivedBy@1470001 : Record 12420;
      ReleasedBy@1470000 : Record 12420;
      StoredBy@1470014 : Record 12420;
      FAComment@1470008 : Record 12478;
      FALocation@1210003 : Record 5609;
      DocSignMgt@1470002 : Codeunit 12420;
      StdRepMgt@1210004 : Codeunit 12401;
      FA15ReportHelper@1210002 : Codeunit 14954;
      Result@1470010 : ARRAY [5] OF Text[80];
      Conclusion@1470009 : ARRAY [5] OF Text[80];
      Defect@1470012 : ARRAY [5] OF Text[80];
      Reason@1470011 : ARRAY [5] OF Text[80];
      Complect@1470013 : ARRAY [5] OF Text[80];
      FileName@1210000 : Text;

    PROCEDURE CheckSignature@1470001(VAR DocSign@1470000 : Record 12420;EmpType@1470001 : Integer);
    BEGIN
      DocSignMgt.GetDocSign(
        DocSign,DATABASE::"FA Document Header",
        "FA Document Header"."Document Type","FA Document Header"."No.",EmpType,TRUE);
    END;

    PROCEDURE SetFileNameSilent@1210007(NewFileName@1210001 : Text);
    BEGIN
      FileName := NewFileName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

