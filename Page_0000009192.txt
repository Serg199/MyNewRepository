OBJECT Page 9192 Company Creation Wizard
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create New Company;
               RUS=Создать новую организацию];
    PageType=NavigatePage;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 Step := Step::Start;
                 EnableControls;
                 UpdateDataDescription;
               END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::OK THEN
                         IF NOT CompanyCreated THEN
                           IF NOT CONFIRM(SetupNotCompletedQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 RUS=Назад];
                      ToolTipML=;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackActionEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 RUS=Далее];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextActionEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 5       ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 RUS=Готово];
                      ToolTipML=;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishActionEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 FinishAction;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT FinalStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 7   ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaRepositoryStandard.Image;
                Editable=FALSE;
                ShowCaption=No }

    { 8   ;1   ;Group     ;
                Visible=TopBannerVisible AND FinalStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 9   ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaRepositoryDone.Image;
                Editable=FALSE;
                ShowCaption=No }

    { 10  ;1   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 11  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to assisted setup for creating a company;
                           RUS=Вас приветствует мастер сопровождаемой настройки по созданию организации];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 12  ;3   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=This guide will help you create a new company.;
                                     RUS=Это руководство поможет вам создать новую организацию.] }

    { 24  ;2   ;Group     ;
                CaptionML=[ENU=Let's go!;
                           RUS=Приступим!];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Next to get started.;
                                     RUS=Нажмите "Далее", чтобы начать.] }

    { 13  ;1   ;Group     ;
                Visible=CreationStepVisible;
                GroupType=Group }

    { 14  ;2   ;Group     ;
                CaptionML=[ENU=Specify some basic information;
                           RUS=Укажите основную информацию];
                Visible=CreationStepVisible;
                GroupType=Group }

    { 20  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=Enter a name for the company.;
                                     RUS=Введите название организации.] }

    { 21  ;4   ;Field     ;
                Name=CompanyName;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewCompanyName;
                OnValidate=VAR
                             Company@1000 : Record 2000000006;
                           BEGIN
                             IF Company.GET(NewCompanyName) THEN
                               ERROR(CompanyAlreadyExistsErr);
                           END;

                ShowMandatory=TRUE;
                ShowCaption=No }

    { 16  ;3   ;Group     ;
                CaptionML=[ENU=Select the data and setup to get started.;
                           RUS=Выбор данных и выполнение настройки для начала работы.];
                GroupType=Group }

    { 15  ;4   ;Field     ;
                Name=CompanyData;
                OptionCaptionML=[ENU=Evaluation Data,Standard Data,None;
                                 RUS=Данные оценки,Стандартные данные,Нет];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewCompanyData;
                OnValidate=BEGIN
                             UpdateDataDescription;
                           END;

                ShowCaption=No }

    { 23  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewCompanyDataDescription;
                Editable=FALSE;
                MultiLine=Yes;
                ShowCaption=No }

    { 17  ;1   ;Group     ;
                Visible=FinalStepVisible;
                GroupType=Group }

    { 18  ;2   ;Group     ;
                CaptionML=[ENU=That's it!;
                           RUS=Вот и все!];
                GroupType=Group }

    { 19  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Finish to create the company. This can take a few minutes to complete.;
                                     RUS=Нажмите "Готово", чтобы создать организацию. Процесс создания может занять несколько минут.] }

    { 22  ;3   ;Group     ;
                Visible=ConfigurationPackageExists;
                GroupType=Group;
                InstructionalTextML=[ENU=The company is created and included in the companies list, but before you use it we need time to set up some data and settings for you.;
                                     RUS=Компания создана и добавлена в список компаний, но прежде чем приступить к ее использованию, необходимо подождать завершения настройки некоторых данных и параметров.] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1000 : Record 9400;
      MediaRepositoryDone@1001 : Record 9400;
      Step@1002 : 'Start,Creation,Finish';
      TopBannerVisible@1003 : Boolean;
      FirstStepVisible@1004 : Boolean;
      CreationStepVisible@1005 : Boolean;
      FinalStepVisible@1006 : Boolean;
      FinishActionEnabled@1007 : Boolean;
      BackActionEnabled@1008 : Boolean;
      NextActionEnabled@1009 : Boolean;
      SetupNotCompletedQst@1013 : TextConst 'ENU=The company has not yet been created.\\Are you sure that you want to exit?;RUS=Организация еще не создана.\\Вы действительно хотите выйти?';
      ConfigurationPackageExists@1022 : Boolean;
      NewCompanyName@1010 : Text[30];
      NewCompanyData@1011 : 'Evaluation Data,Standard Data,None';
      CompanyAlreadyExistsErr@1014 : TextConst 'ENU=A company with that name already exists. Try a different name.;RUS=Организация с таким названием уже существует. Попробуйте указать другое название.';
      NewCompanyDataDescription@1012 : Text;
      CompanyCreated@1015 : Boolean;
      SpecifyCompanyNameErr@1016 : TextConst 'ENU=To continue, you must specify a name for the company.;RUS=Для продолжения необходимо указать название организации.';
      CreatingCompanyMsg@1017 : TextConst 'ENU=Creating company...;RUS=Создание организации:';
      CompanySetUpFailedMsg@1021 : TextConst 'ENU=Set up of the new company failed. Contact your system administrator.;RUS=Сбой настройки новой компании. Обратитесь к системному администратору.';
      NoConfigurationPackageFileDefinedMsg@1020 : TextConst 'ENU=No configuration package file is defined for this company type. An empty company will be created.;RUS=Файл пакета конфигурации не определен для этого типа компании. Будет создана пустая компания.';
      NoPermissionsErr@1028 : TextConst 'ENU=You do not have permissions to create a new company. Contact your system administrator.;RUS=У вас нет разрешений на создание новой компании. Обратитесь к системному администратору.';
      StandardTxt@1019 : TextConst '@@@=Must be similar to "Data Type" option in table 101900 Demonstration Data Setup;ENU=Standard;RUS=Стандартная';
      EvaluationTxt@1018 : TextConst '@@@=Must be similar to "Data Type" option in table 101900 Demonstration Data Setup;ENU=Evaluation;RUS=Демонстрационная';
      EvaluationDataTxt@1023 : TextConst 'ENU=\Just want to try it out?\\Create a company with everything you need to evaluate the product. For example, sample invoices and ledger entries let you view charts and reports.;RUS=\Хотите попробовать?\\Создайте компанию с необходимыми данными для оценки продукта. Например, с помощью примеров счетов и операций книги учета можно просмотреть диаграммы и отчеты.';
      StandardDataTxt@1024 : TextConst 'ENU=\Thinking about going into production?\\Create a company with data and set ups like number series, a chart of accounts, and payment methods ready for use. Set up your own items and customers, and start posting right-away.;RUS=\Думаете заняться производством?\\Создайте компанию с готовыми данными и настройками, такими как серии номеров, план счетов и способы оплаты. Настройте собственные товары и клиентов и сразу же начните учет.';
      NoDataTxt@1025 : TextConst 'ENU=\Want to set things up yourself?\\Create a company that does not contain data, and is not already set up for use. For example, select this option when you want to use your own data, and set things up yourself.;RUS=\Хотите настроить параметры самостоятельно?\\Создайте компанию без данных, которая еще не готова к использованию. Например, выберите этот параметр, если вы хотите использоваться собственные данные и самостоятельно настроить параметры.';
      TrialPeriodTxt@1026 : TextConst 'ENU=\\You will be able to use this company for a 30-day trial period.;RUS=\\Вы сможете использовать эту компанию в течение 30-дневного пробного периода.';
      EvalPeriodTxt@1027 : TextConst 'ENU="\\You will be able to use the company to try out the product for as long as you want. ";RUS=\\Вы сможете использовать эту компанию для тестирования продуктов в течение неограниченного периода времени.';

    LOCAL PROCEDURE EnableControls@1();
    BEGIN
      ResetControls;

      CASE Step OF
        Step::Start:
          ShowStartStep;
        Step::Creation:
          ShowCreationStep;
        Step::Finish:
          ShowFinalStep;
      END;
    END;

    LOCAL PROCEDURE FinishAction@2();
    BEGIN
      CreateNewCompany;
      SetUpNewCompany;
      CompanyCreated := TRUE;
      CurrPage.CLOSE;
    END;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      IF (Step = Step::Creation) AND NOT Backwards THEN
        IF NewCompanyName = '' THEN
          ERROR(SpecifyCompanyNameErr);
      IF (Step = Step::Creation) AND NOT Backwards THEN
        ValidateCompanyType;

      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      EnableControls;
    END;

    LOCAL PROCEDURE ShowStartStep@4();
    BEGIN
      FirstStepVisible := TRUE;

      FinishActionEnabled := FALSE;
      BackActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowCreationStep@5();
    BEGIN
      CreationStepVisible := TRUE;

      FinishActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowFinalStep@6();
    BEGIN
      FinalStepVisible := TRUE;
      NextActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ResetControls@7();
    BEGIN
      FinishActionEnabled := TRUE;
      BackActionEnabled := TRUE;
      NextActionEnabled := TRUE;

      FirstStepVisible := FALSE;
      CreationStepVisible := FALSE;
      FinalStepVisible := FALSE;
    END;

    LOCAL PROCEDURE LoadTopBanners@8();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE))
      THEN
        TopBannerVisible := MediaRepositoryDone.Image.HASVALUE;
    END;

    LOCAL PROCEDURE CreateNewCompany@10();
    VAR
      Company@1001 : Record 2000000006;
      GeneralLedgerSetup@1002 : Record 98;
      Window@1000 : Dialog;
    BEGIN
      Window.OPEN(CreatingCompanyMsg);

      Company.INIT;
      Company.Name := NewCompanyName;
      Company.INSERT;

      IF NOT GeneralLedgerSetup.CHANGECOMPANY(NewCompanyName) THEN
        ERROR(NoPermissionsErr);
      IF NOT GeneralLedgerSetup.WRITEPERMISSION THEN
        ERROR(NoPermissionsErr);

      COMMIT;

      Window.CLOSE;
    END;

    LOCAL PROCEDURE SetUpNewCompany@9();
    VAR
      Company@1001 : Record 2000000006;
      ConfigurationPackageFile@1005 : Record 2000000170;
      AssistedCompanySetupStatus@1004 : Record 1802;
      ImportSessionID@1000 : Integer;
    BEGIN
      IF NewCompanyData = NewCompanyData::None THEN
        EXIT;

      CASE NewCompanyData OF
        NewCompanyData::"Evaluation Data":
          ConfigurationPackageFile.SETFILTER(Code,'*' + EvaluationTxt + '*');
        NewCompanyData::"Standard Data":
          ConfigurationPackageFile.SETFILTER(Code,'*' + StandardTxt + '*');
      END;
      ConfigurationPackageFile.SETRANGE("Language ID",GLOBALLANGUAGE);
      ConfigurationPackageFile.SETRANGE("Setup Type",ConfigurationPackageFile."Setup Type"::Company);
      IF ConfigurationPackageFile.ISEMPTY THEN
        EXIT;

      AssistedCompanySetupStatus.SetEnabled(NewCompanyName,NewCompanyData = NewCompanyData::"Standard Data",FALSE);
      Company.GET(NewCompanyName);
      Company."Evaluation Company" := NewCompanyData = NewCompanyData::"Evaluation Data";
      Company.MODIFY;
      SetApplicationArea;

      COMMIT;
      ImportSessionID := 0;
      IF STARTSESSION(ImportSessionID,CODEUNIT::"Import Config. Package Files",NewCompanyName,ConfigurationPackageFile) THEN BEGIN
        AssistedCompanySetupStatus.GET(NewCompanyName);
        AssistedCompanySetupStatus."Company Setup Session ID" := ImportSessionID;
        AssistedCompanySetupStatus.MODIFY;
      END ELSE
        MESSAGE(CompanySetUpFailedMsg);
    END;

    LOCAL PROCEDURE SetApplicationArea@12();
    VAR
      ApplicationAreaSetup@1000 : Record 9178;
    BEGIN
      ApplicationAreaSetup.INIT;
      ApplicationAreaSetup."Company Name" := NewCompanyName;
      ApplicationAreaSetup.Basic := TRUE;
      ApplicationAreaSetup."Relationship Mgmt" := TRUE;
      ApplicationAreaSetup.Suite := NewCompanyData = NewCompanyData::"Standard Data";
      ApplicationAreaSetup.INSERT;
    END;

    LOCAL PROCEDURE ValidateCompanyType@11();
    VAR
      ConfigurationPackageFile@1000 : Record 2000000170;
    BEGIN
      CLEAR(ConfigurationPackageExists);

      IF NewCompanyData = NewCompanyData::None THEN
        EXIT;

      CASE NewCompanyData OF
        NewCompanyData::"Evaluation Data":
          ConfigurationPackageFile.SETFILTER(Code,'*' + EvaluationTxt + '*');
        NewCompanyData::"Standard Data":
          ConfigurationPackageFile.SETFILTER(Code,'*' + StandardTxt + '*');
      END;

      ConfigurationPackageExists := NOT ConfigurationPackageFile.ISEMPTY;
      IF NOT ConfigurationPackageExists THEN
        MESSAGE(NoConfigurationPackageFileDefinedMsg)
    END;

    LOCAL PROCEDURE UpdateDataDescription@17();
    VAR
      TenantLicenseState@1000 : Record 2000000189;
    BEGIN
      CASE NewCompanyData OF
        NewCompanyData::"Evaluation Data":
          NewCompanyDataDescription := EvaluationDataTxt;
        NewCompanyData::"Standard Data":
          NewCompanyDataDescription := StandardDataTxt;
        NewCompanyData::None:
          NewCompanyDataDescription := NoDataTxt;
      END;

      IF NOT TenantLicenseState.FINDLAST THEN
        EXIT;
      IF TenantLicenseState.State = TenantLicenseState.State::Paid THEN
        EXIT;

      CASE NewCompanyData OF
        NewCompanyData::"Evaluation Data":
          NewCompanyDataDescription += EvalPeriodTxt;
        NewCompanyData::"Standard Data",
        NewCompanyData::None:
          NewCompanyDataDescription += TrialPeriodTxt;
      END;
    END;

    BEGIN
    END.
  }
}

