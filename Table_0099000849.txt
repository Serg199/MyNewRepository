OBJECT Table 99000849 Action Message Entry
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Action Message Entry;
               RUS=Указание операции];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              RUS=Номер операции] }
    { 2   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              RUS=Тип];
                                                   OptionCaptionML=[ENU=,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel;
                                                                    RUS=,Новое,Изменение кол-ва,Перепланирование,Переплан. и измен. кол-ва,Отмена];
                                                   OptionString=,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel }
    { 3   ;   ;Reservation Entry   ;Integer       ;TableRelation="Reservation Entry"."Entry No.";
                                                   CaptionML=[ENU=Reservation Entry;
                                                              RUS=Операция резервирования] }
    { 4   ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              RUS=Кол-во];
                                                   DecimalPlaces=0:5 }
    { 5   ;   ;New Date            ;Date          ;CaptionML=[ENU=New Date;
                                                              RUS=Новая дата] }
    { 6   ;   ;Calculation         ;Option        ;CaptionML=[ENU=Calculation;
                                                              RUS=Расчет];
                                                   OptionCaptionML=[ENU=Sum,None;
                                                                    RUS=Сумма,Нет];
                                                   OptionString=Sum,None }
    { 7   ;   ;Suppressed Action Msg.;Boolean     ;CaptionML=[ENU=Suppressed Action Msg.;
                                                              RUS=Сжатое указание] }
    { 10  ;   ;Source Type         ;Integer       ;CaptionML=[ENU=Source Type;
                                                              RUS=Тип источника] }
    { 11  ;   ;Source Subtype      ;Option        ;CaptionML=[ENU=Source Subtype;
                                                              RUS=Подтип источника];
                                                   OptionCaptionML=[ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    RUS=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10 }
    { 12  ;   ;Source ID           ;Code20        ;CaptionML=[ENU=Source ID;
                                                              RUS=Код источника] }
    { 13  ;   ;Source Batch Name   ;Code10        ;CaptionML=[ENU=Source Batch Name;
                                                              RUS=Код раздела источника] }
    { 14  ;   ;Source Prod. Order Line;Integer    ;CaptionML=[ENU=Source Prod. Order Line;
                                                              RUS=Исходная строка произв. заказа] }
    { 15  ;   ;Source Ref. No.     ;Integer       ;CaptionML=[ENU=Source Ref. No.;
                                                              RUS=Номер ссылки на источник] }
    { 16  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              RUS=Код склада] }
    { 17  ;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   CaptionML=[ENU=Bin Code;
                                                              RUS=Код ячейки] }
    { 18  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              RUS=Код варианта] }
    { 19  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              RUS=Код товара] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Reservation Entry,Calculation           ;SumIndexFields=Quantity }
    {    ;Source Type,Source Subtype,Source ID,Source Batch Name,Source Prod. Order Line,Source Ref. No.,Item No.,Location Code,Bin Code,Variant Code;
                                                   SumIndexFields=Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ComponentBinding@1001 : Boolean;
      FirstDate@1002 : Date;
      FirstTime@1003 : Time;

    PROCEDURE SumUp@1(VAR ActionMessageEntry@1000 : Record 99000849);
    VAR
      ActionMessageEntry2@1001 : Record 99000849;
      ReservEntry@1002 : Record 337;
      ReservEntry2@1003 : Record 337;
      ProdOrderComp@1004 : Record 5407;
      TypeArray@1005 : ARRAY [5] OF Boolean;
    BEGIN
      ActionMessageEntry2 := ActionMessageEntry;

      WITH ActionMessageEntry2 DO BEGIN
        SETCURRENTKEY("Source Type","Source Subtype","Source ID","Source Batch Name","Source Prod. Order Line","Source Ref. No.");
        SETRANGE("Source Type","Source Type");
        SETRANGE("Source Subtype","Source Subtype");
        SETRANGE("Source ID","Source ID");
        SETRANGE("Source Batch Name","Source Batch Name");
        SETRANGE("Source Prod. Order Line","Source Prod. Order Line");
        SETRANGE("Source Ref. No.","Source Ref. No.");
        SETRANGE("Location Code","Location Code");
        SETRANGE("Bin Code","Bin Code");
        SETRANGE("Variant Code","Variant Code");
        SETRANGE("Item No.","Item No.");
        ActionMessageEntry."New Date" := 0D;
        ActionMessageEntry.Quantity := 0;
        ActionMessageEntry.Type := 0;

        IF FINDSET THEN
          REPEAT
            IF Quantity <> 0 THEN BEGIN
              ActionMessageEntry.Quantity += Quantity;
              TypeArray[2] := TRUE;
            END;

            IF "New Date" <> 0D THEN BEGIN
              ActionMessageEntry."New Date" := "New Date";
              TypeArray[3] := TRUE;
            END;

          UNTIL NEXT = 0;

        IF TypeArray[2] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::"Change Qty.";

        IF TypeArray[3] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::Reschedule;

        IF TypeArray[2] AND TypeArray[3] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::"Resched. & Chg. Qty.";

        IF TypeArray[1] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::New;

        IF TypeArray[5] THEN
          ActionMessageEntry.Type := ActionMessageEntry.Type::Cancel;

        "New Date" := ActionMessageEntry."New Date";
        Quantity := ActionMessageEntry.Quantity;
        Type := ActionMessageEntry.Type;
        ActionMessageEntry := ActionMessageEntry2;

        ComponentBinding := FALSE;
        IF ActionMessageEntry."Source Type" = DATABASE::"Prod. Order Line" THEN BEGIN
          FirstDate := DMY2DATE(31,12,9999);
          ActionMessageEntry.FilterToReservEntry(ReservEntry);
          ReservEntry.SETRANGE(Binding,ReservEntry.Binding::"Order-to-Order");
          IF ReservEntry.FINDSET THEN
            REPEAT
              IF ReservEntry2.GET(ReservEntry."Entry No.",FALSE) THEN
                IF (ReservEntry2."Source Type" = DATABASE::"Prod. Order Component") AND
                   (ReservEntry2."Source Subtype" = ReservEntry."Source Subtype") AND
                   (ReservEntry2."Source ID" = ReservEntry."Source ID")
                THEN
                  IF ProdOrderComp.GET(
                       ReservEntry2."Source Subtype",ReservEntry2."Source ID",
                       ReservEntry2."Source Prod. Order Line",ReservEntry2."Source Ref. No.")
                  THEN BEGIN
                    ComponentBinding := TRUE;
                    IF ProdOrderComp."Due Date" < FirstDate THEN BEGIN
                      FirstDate := ProdOrderComp."Due Date";
                      FirstTime := ProdOrderComp."Due Time";
                    END;
                  END;
            UNTIL ReservEntry.NEXT = 0;
        END;
      END; // WITH
    END;

    PROCEDURE TransferFromReservEntry@2(VAR ReservEntry@1000 : Record 337);
    BEGIN
      "Reservation Entry" := ReservEntry."Entry No.";
      "Source Type" := ReservEntry."Source Type";
      "Source Subtype" := ReservEntry."Source Subtype";
      "Source ID" := ReservEntry."Source ID";
      "Source Batch Name" := ReservEntry."Source Batch Name";
      "Source Prod. Order Line" := ReservEntry."Source Prod. Order Line";
      "Source Ref. No." := ReservEntry."Source Ref. No.";
      "Location Code" := ReservEntry."Location Code";
      "Variant Code" := ReservEntry."Variant Code";
      "Item No." := ReservEntry."Item No.";
    END;

    PROCEDURE FilterFromReservEntry@3(VAR ReservEntry@1000 : Record 337);
    BEGIN
      SETCURRENTKEY("Source Type","Source Subtype","Source ID","Source Batch Name","Source Prod. Order Line","Source Ref. No.");

      SETRANGE("Source Type",ReservEntry."Source Type");
      SETRANGE("Source Subtype",ReservEntry."Source Subtype");
      SETRANGE("Source ID",ReservEntry."Source ID");
      SETRANGE("Source Batch Name",ReservEntry."Source Batch Name");
      SETRANGE("Source Prod. Order Line",ReservEntry."Source Prod. Order Line");
      SETRANGE("Source Ref. No.",ReservEntry."Source Ref. No.");
      SETRANGE("Location Code",ReservEntry."Location Code");
      SETRANGE("Variant Code",ReservEntry."Variant Code");
      SETRANGE("Item No.",ReservEntry."Item No.");
    END;

    PROCEDURE FilterToReservEntry@7(VAR ReservEntry@1000 : Record 337);
    BEGIN
      ReservEntry.SETCURRENTKEY(
        "Source ID","Source Ref. No.","Source Type","Source Subtype",
        "Source Batch Name","Source Prod. Order Line");

      ReservEntry.SETRANGE("Source ID","Source ID");
      ReservEntry.SETRANGE("Source Ref. No.","Source Ref. No.");
      ReservEntry.SETRANGE("Source Type","Source Type");
      ReservEntry.SETRANGE("Source Subtype","Source Subtype");
      ReservEntry.SETRANGE("Source Batch Name","Source Batch Name");
      ReservEntry.SETRANGE("Source Prod. Order Line","Source Prod. Order Line");
    END;

    PROCEDURE BoundToComponent@6() : Boolean;
    BEGIN
      EXIT(ComponentBinding);
    END;

    PROCEDURE ComponentDueDate@4() : Date;
    BEGIN
      EXIT(FirstDate);
    END;

    PROCEDURE ComponentDueTime@5() : Time;
    BEGIN
      EXIT(FirstTime);
    END;

    BEGIN
    END.
  }
}

