OBJECT Report 50016 Copy Dimensions Gen. Journal
{
  OBJECT-PROPERTIES
  {
    Date=02.08.17;
    Time=11:47:12;
    Modified=Yes;
    Version List=NOS-26;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Dimensions to Gen Jnl;
               RUS=Копирование Измерений в Фин. Журнал];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 7024;    ;DataItem;                    ;
               DataItemTable=Table81;
               DataItemTableView=SORTING(Journal Template Name,Journal Batch Name,Line No.);
               OnPreDataItem=BEGIN
                               IF Dim = '' THEN
                                 ERROR(Text50002);
                               IF DimVal = '' THEN
                                 ERROR(Text50001);
                             END;

               OnAfterGetRecord=BEGIN
                                  DimMgt.GetDimensionSet(TempDimSetEntry, "Dimension Set ID");
                                  IF TempDimSetEntry.GET("Dimension Set ID",Dim) THEN
                                    TempDimSetEntry.DELETE;

                                  TempDimSetEntry.VALIDATE("Dimension Code",Dim);
                                  TempDimSetEntry.VALIDATE("Dimension Value Code",DimVal);
                                  TempDimSetEntry.INSERT;

                                  DimMgt.UpdateGenJnlLineDim("Gen. Journal Line",DimMgt.GetDimensionSetID(TempDimSetEntry));
                                  MODIFY;
                                END;

               ReqFilterFields=Journal Template Name,Journal Batch Name,Account Type,Account No.,Posting Date,Document No.,Posting Group,Description,Bal. Account No.,VAT Bus. Posting Group,VAT Prod. Posting Group }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1101495000;;Container;
                  ContainerType=ContentArea }

      { 1101495001;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры];
                  GroupType=Group }

      { 1101495002;2;Field  ;
                  CaptionML=[ENU=Dimension Code;
                             RUS=Измерение Код];
                  SourceExpr=Dim;
                  TableRelation=Dimension;
                  OnValidate=BEGIN
                               IF DimVal <> '' THEN
                                 IF NOT DimensionValue.GET(Dim,DimVal) THEN
                                   DimVal := '';
                             END;
                              }

      { 1101495003;2;Field  ;
                  CaptionML=[ENU=Dimension Value Code;
                             RUS=Измерение Значение Код];
                  SourceExpr=DimVal;
                  OnAssistEdit=BEGIN
                                 DimensionValue.SETRANGE("Dimension Code",Dim);
                                 IF PAGE.RUNMODAL(0,DimensionValue) = ACTION::LookupOK THEN
                                     DimVal:= DimensionValue.Code;
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Dim@1000000001 : Code[20];
      DimVal@1000000000 : Code[20];
      DimensionValue@1000000003 : Record 349;
      Text50001@1000000004 : TextConst 'ENU=Dimension Value Code is empty!;RUS=Не введено значение измерения!';
      Text50002@1000000005 : TextConst 'ENU=Dimension Code is empty!;RUS=Не введено измерение!';
      TempDimSetEntry@1101495000 : TEMPORARY Record 480;
      DimMgt@1101495001 : Codeunit 408;

    BEGIN
    {
       ВЫЗЫВАЕТСЯ ИЗ ФИНАНСОВОГО ЖУРНАЛА
       ЗАПОЛНЯЕТ НЕСКОЛЬКО СТРОК ОДНИМ И ТЕМ ЖЕ ЗНАЧЕНИЕМ ИЗМРЕНИЯ
      ----------------------
      NC NOS-26 SPA Миграция на NAV2017
    }
    END.
  }
  RDLDATA
  {
  }
}

