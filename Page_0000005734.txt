OBJECT Page 5734 Item Category Attributes
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Category Attributes;
               RUS=Атрибуты категории товаров];
    LinksAllowed=No;
    SourceTable=Table7504;
    SourceTableView=SORTING(Inheritance Level,Attribute Name)
                    ORDER(Ascending);
    PageType=List;
    SourceTableTemporary=Yes;
    RefreshOnActivate=Yes;
    OnClosePage=BEGIN
                  TempRecentlyItemAttributeValueMapping.DELETEALL;
                END;

    OnAfterGetRecord=BEGIN
                       UpdateProperties;
                     END;

    OnInsertRecord=VAR
                     TempItemAttributeValueToInsert@1000 : TEMPORARY Record 7501;
                     ItemAttributeValueMapping@1002 : Record 7505;
                     ItemAttributeManagement@1001 : Codeunit 7500;
                   BEGIN
                     IF ItemCategoryCode <> '' THEN BEGIN
                       ItemAttributeValueMapping."Table ID" := DATABASE::"Item Category";
                       ItemAttributeValueMapping."No." := ItemCategoryCode;
                       ItemAttributeValueMapping."Item Attribute ID" := "Attribute ID";
                       ItemAttributeValueMapping."Item Attribute Value ID" := GetAttributeValueID(TempItemAttributeValueToInsert);
                       ItemAttributeValueMapping.INSERT;
                       ItemAttributeManagement.InsertCategoryItemsBufferedAttributeValueMapping(
                         TempItemAttributeValueToInsert,TempRecentlyItemAttributeValueMapping,ItemCategoryCode);
                       InsertRecentlyAddedCategoryAttribute(ItemAttributeValueMapping);
                     END;
                   END;

    OnDeleteRecord=VAR
                     TempItemAttributeValueToDelete@1000 : TEMPORARY Record 7501;
                     ItemAttributeValue@1001 : Record 7501;
                     ItemAttributeValueMapping@1003 : Record 7505;
                     ItemAttributeManagement@1002 : Codeunit 7500;
                   BEGIN
                     IF "Inherited-From Key Value" <> '' THEN
                       ERROR(DeleteInheritedAttribErr);

                     ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::"Item Category");
                     ItemAttributeValueMapping.SETRANGE("No.",ItemCategoryCode);
                     ItemAttributeValueMapping.SETRANGE("Item Attribute ID","Attribute ID");
                     ItemAttributeValueMapping.FINDFIRST;
                     IF ItemAttributeManagement.SearchCategoryItemsForAttribute(ItemCategoryCode,"Attribute ID") THEN
                       IF CONFIRM(STRSUBSTNO(DeleteItemInheritedParentCategoryAttributesQst,ItemCategoryCode,ItemCategoryCode)) THEN BEGIN
                         ItemAttributeValue.SETRANGE("Attribute ID","Attribute ID");
                         ItemAttributeValue.SETRANGE(ID,ItemAttributeValueMapping."Item Attribute Value ID");
                         ItemAttributeValue.FINDFIRST;
                         TempItemAttributeValueToDelete.TRANSFERFIELDS(ItemAttributeValue);
                         TempItemAttributeValueToDelete.INSERT;
                         DeleteRecentlyItemAttributeValueMapping("Attribute ID");
                         ItemAttributeManagement.DeleteCategoryItemsAttributeValueMapping(TempItemAttributeValueToDelete,ItemCategoryCode);
                       END;
                     ItemAttributeValueMapping.DELETE;
                   END;

    OnAfterGetCurrRecord=BEGIN
                           UpdateProperties;
                         END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Enabled=RowEditable;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                AssistEdit=No;
                CaptionML=[ENU=Attribute;
                           RUS=Атрибут];
                ToolTipML=[ENU=Specifies the item attribute.;
                           RUS=Определяет атрибут товара.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Attribute Name";
                TableRelation="Item Attribute".Name WHERE (Blocked=CONST(No));
                StyleExpr=StyleTxt;
                OnValidate=BEGIN
                             PersistInheritanceData;
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=Value;
                CaptionML=[ENU=Default Value;
                           RUS=Значение по умолчанию];
                ToolTipML=[ENU=Specifies the value of the item attribute.;
                           RUS=Определяет значение атрибута товара.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Value;
                TableRelation=IF (Attribute Type=CONST(Option)) "Item Attribute Value".Value WHERE (Attribute ID=FIELD(Attribute ID),
                                                                                                    Blocked=CONST(No));
                StyleExpr=StyleTxt;
                OnValidate=BEGIN
                             PersistInheritanceData;
                             ChangeDefaultValue;
                           END;
                            }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the unit of measure for the item attribute.;
                           RUS=Определяет единицу измерения атрибута товара.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Unit of Measure";
                StyleExpr=StyleTxt }

    { 5   ;2   ;Field     ;
                CaptionML=[ENU=Inherited From;
                           RUS=Унаследовано от];
                ToolTipML=[ENU=Specifies the parent item category that the item attributes are inherited from.;
                           RUS=Определяет родительскую категорию товаров, из которой наследуются атрибуты товара.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Inherited-From Key Value";
                Editable=FALSE;
                StyleExpr=StyleTxt }

  }
  CODE
  {
    VAR
      TempRecentlyItemAttributeValueMapping@1006 : TEMPORARY Record 7505;
      ItemCategoryCode@1000 : Code[20];
      DeleteInheritedAttribErr@1002 : TextConst 'ENU=You cannot delete attributes that are inherited from a parent item category.;RUS=Невозможно удалить атрибуты, унаследованные из родительской категории товаров.';
      RowEditable@1001 : Boolean;
      StyleTxt@1003 : Text;
      ChangingDefaultValueMsg@1005 : TextConst '@@@=%1 - item category code;ENU=The new default value will not apply to items that use the current item category, ''''%1''''. It will only apply to new items.;RUS=Новое значение по умолчанию не будет применяться к товарам, использующим текущую категорию товаров, ''''%1''''. Оно будет применяться только к новым товарам.';
      DeleteItemInheritedParentCategoryAttributesQst@1004 : TextConst '@@@=%1 - item category code,%2 - item category code;ENU=One or more items belong to item category ''''%1''''.\\Do you want to delete the inherited item attributes for the items in question?;RUS=Один или несколько товаров принадлежат к категории товаров "%1''''.\\Удалить унаследованные атрибуты товаров для рассматриваемых товаров?';

    PROCEDURE LoadAttributes@29(CategoryCode@1004 : Code[20]);
    VAR
      TempItemAttributeValue@1000 : TEMPORARY Record 7501;
      ItemAttributeValue@1002 : Record 7501;
      ItemAttributeValueMapping@1001 : Record 7505;
      ItemCategory@1007 : Record 5722;
      CurrentCategoryCode@1005 : Code[20];
    BEGIN
      RESET;
      DELETEALL;
      IF CategoryCode = '' THEN
        EXIT;
      SortByInheritance;
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::"Item Category");
      SetItemCategoryCode(CategoryCode);
      CurrentCategoryCode := CategoryCode;
      REPEAT
        IF ItemCategory.GET(CurrentCategoryCode) THEN BEGIN
          ItemAttributeValueMapping.SETRANGE("No.",CurrentCategoryCode);
          IF ItemAttributeValueMapping.FINDSET THEN
            REPEAT
              IF ItemAttributeValue.GET(
                   ItemAttributeValueMapping."Item Attribute ID",ItemAttributeValueMapping."Item Attribute Value ID")
              THEN BEGIN
                TempItemAttributeValue.TRANSFERFIELDS(ItemAttributeValue);
                IF TempItemAttributeValue.INSERT THEN
                  IF NOT AttributeExists(TempItemAttributeValue."Attribute ID") THEN BEGIN
                    IF CurrentCategoryCode = ItemCategoryCode THEN
                      InsertRecord(TempItemAttributeValue,DATABASE::"Item Category",'')
                    ELSE
                      InsertRecord(TempItemAttributeValue,DATABASE::"Item Category",CurrentCategoryCode);
                    "Inheritance Level" := ItemCategory.Indentation;
                    MODIFY;
                  END;
              END
            UNTIL ItemAttributeValueMapping.NEXT = 0;
          CurrentCategoryCode := ItemCategory."Parent Category";
        END ELSE
          CurrentCategoryCode := '';
      UNTIL CurrentCategoryCode = '';
      RESET;
      CurrPage.UPDATE(FALSE);
      SortByInheritance;
    END;

    PROCEDURE SaveAttributes@2(CategoryCode@1003 : Code[20]);
    VAR
      TempNewItemAttributeValue@1000 : TEMPORARY Record 7501;
      ItemAttributeValueMapping@1001 : Record 7505;
      ItemAttribute@1002 : Record 7500;
      TempNewCategItemAttributeValue@1008 : TEMPORARY Record 7501;
      TempOldCategItemAttributeValue@1007 : TEMPORARY Record 7501;
      ItemAttributeManagement@1004 : Codeunit 7500;
    BEGIN
      IF CategoryCode = '' THEN
        EXIT;
      TempOldCategItemAttributeValue.LoadCategoryAttributesFactBoxData(CategoryCode);

      SETRANGE("Inherited-From Table ID",DATABASE::"Item Category");
      SETRANGE("Inherited-From Key Value",'');
      PopulateItemAttributeValue(TempNewItemAttributeValue);
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::"Item Category");
      ItemAttributeValueMapping.SETRANGE("No.",CategoryCode);
      ItemAttributeValueMapping.DELETEALL;

      IF TempNewItemAttributeValue.FINDSET THEN
        REPEAT
          ItemAttributeValueMapping."Table ID" := DATABASE::"Item Category";
          ItemAttributeValueMapping."No." := CategoryCode;
          ItemAttributeValueMapping."Item Attribute ID" := TempNewItemAttributeValue."Attribute ID";
          ItemAttributeValueMapping."Item Attribute Value ID" := TempNewItemAttributeValue.ID;
          ItemAttributeValueMapping.INSERT;
          ItemAttribute.GET(ItemAttributeValueMapping."Item Attribute ID");
          ItemAttribute.RemoveUnusedArbitraryValues;
        UNTIL TempNewItemAttributeValue.NEXT = 0;

      TempNewCategItemAttributeValue.LoadCategoryAttributesFactBoxData(CategoryCode);
      ItemAttributeManagement.UpdateCategoryItemsAttributeValueMapping(
        TempNewCategItemAttributeValue,TempOldCategItemAttributeValue,ItemCategoryCode,ItemCategoryCode);
    END;

    LOCAL PROCEDURE PersistInheritanceData@1();
    BEGIN
      "Inherited-From Table ID" := DATABASE::"Item Category";
      CurrPage.SAVERECORD;
    END;

    PROCEDURE SetItemCategoryCode@3(CategoryCode@1000 : Code[20]);
    BEGIN
      IF ItemCategoryCode <> CategoryCode THEN BEGIN
        ItemCategoryCode := CategoryCode;
        TempRecentlyItemAttributeValueMapping.DELETEALL;
      END;
    END;

    PROCEDURE SortByInheritance@4();
    BEGIN
      SETCURRENTKEY("Inheritance Level","Attribute Name");
    END;

    LOCAL PROCEDURE UpdateProperties@6();
    BEGIN
      RowEditable := "Inherited-From Key Value" = '';
      IF RowEditable THEN
        StyleTxt := 'Standard'
      ELSE
        StyleTxt := 'Strong';
    END;

    LOCAL PROCEDURE AttributeExists@9(AttributeID@1001 : Integer) AttribExist : Boolean;
    BEGIN
      SETRANGE("Attribute ID",AttributeID);
      AttribExist := NOT ISEMPTY;
      RESET;
    END;

    LOCAL PROCEDURE ChangeDefaultValue@5();
    VAR
      ItemAttributeValueMapping@1001 : Record 7505;
      TempItemAttributeValueToInsert@1002 : TEMPORARY Record 7501;
      ItemAttributeManagement@1000 : Codeunit 7500;
    BEGIN
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::"Item Category");
      ItemAttributeValueMapping.SETRANGE("No.",ItemCategoryCode);
      ItemAttributeValueMapping.SETRANGE("Item Attribute ID","Attribute ID");
      IF ItemAttributeValueMapping.FINDFIRST THEN BEGIN
        ItemAttributeValueMapping."Item Attribute Value ID" := GetAttributeValueID(TempItemAttributeValueToInsert);
        ItemAttributeValueMapping.MODIFY;
      END;

      IF IsRecentlyAddedCategoryAttribute("Attribute ID") THEN
        UpdateRecentlyItemAttributeValueMapping(TempItemAttributeValueToInsert)
      ELSE
        IF ItemAttributeManagement.SearchCategoryItemsForAttribute(ItemCategoryCode,"Attribute ID") THEN
          MESSAGE(STRSUBSTNO(ChangingDefaultValueMsg,ItemCategoryCode));
    END;

    LOCAL PROCEDURE GetAttributeValueID@8(VAR TempItemAttributeValueToInsert@1000 : TEMPORARY Record 7501) : Integer;
    VAR
      ItemAttributeValue@1001 : Record 7501;
    BEGIN
      ItemAttributeValue.SETRANGE("Attribute ID","Attribute ID");
      ItemAttributeValue.SETRANGE(Value,Value);
      IF NOT ItemAttributeValue.FINDFIRST THEN BEGIN
        ItemAttributeValue."Attribute ID" := "Attribute ID";
        ItemAttributeValue.Value := Value;
        ItemAttributeValue.INSERT;
      END;
      TempItemAttributeValueToInsert.TRANSFERFIELDS(ItemAttributeValue);
      TempItemAttributeValueToInsert.INSERT;
      EXIT(ItemAttributeValue.ID);
    END;

    LOCAL PROCEDURE InsertRecentlyAddedCategoryAttribute@24(ItemAttributeValueMapping@1000 : Record 7505);
    BEGIN
      TempRecentlyItemAttributeValueMapping.TRANSFERFIELDS(ItemAttributeValueMapping);
      IF TempRecentlyItemAttributeValueMapping.INSERT THEN;
    END;

    LOCAL PROCEDURE IsRecentlyAddedCategoryAttribute@7(AttributeID@1001 : Integer) : Boolean;
    BEGIN
      TempRecentlyItemAttributeValueMapping.SETRANGE("Item Attribute ID",AttributeID);
      EXIT(NOT TempRecentlyItemAttributeValueMapping.ISEMPTY)
    END;

    LOCAL PROCEDURE UpdateRecentlyItemAttributeValueMapping@10(VAR TempItemAttributeValueToInsert@1000 : TEMPORARY Record 7501);
    VAR
      ItemAttributeValueMapping@1001 : Record 7505;
    BEGIN
      TempRecentlyItemAttributeValueMapping.SETRANGE("Item Attribute ID",TempItemAttributeValueToInsert."Attribute ID");
      IF TempRecentlyItemAttributeValueMapping.FINDFIRST THEN BEGIN
        ItemAttributeValueMapping.SETRANGE("Table ID",TempRecentlyItemAttributeValueMapping."Table ID");
        ItemAttributeValueMapping.SETRANGE("No.",TempRecentlyItemAttributeValueMapping."No.");
        ItemAttributeValueMapping.SETRANGE("Item Attribute ID",TempRecentlyItemAttributeValueMapping."Item Attribute ID");
        ItemAttributeValueMapping.FINDFIRST;
        ItemAttributeValueMapping.VALIDATE("Item Attribute Value ID",TempItemAttributeValueToInsert.ID);
        ItemAttributeValueMapping.MODIFY;
      END;
    END;

    LOCAL PROCEDURE DeleteRecentlyItemAttributeValueMapping@12(AttributeID@1000 : Integer);
    BEGIN
      TempRecentlyItemAttributeValueMapping.SETRANGE("Item Attribute ID",AttributeID);
      TempRecentlyItemAttributeValueMapping.DELETEALL;
    END;

    BEGIN
    END.
  }
}

