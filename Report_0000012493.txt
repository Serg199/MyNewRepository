OBJECT Report 12493 FA Write-off Act FA-4
{
  OBJECT-PROPERTIES
  {
    Date=09.09.14;
    Time=12:00:00;
    Version List=NAVRU8.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=FA Write-off Act FA-4;
               RUS=Акт списания ОС-4];
    ProcessingOnly=Yes;
    OnInitReport=VAR
                   Employee@1470000 : Record 5200;
                 BEGIN
                   CompanyInfo.GET;
                   FASetup.GET;
                   IF Employee.GET(CompanyInfo."Director No.") THEN
                     DirectorPosition := Employee.GetJobTitleName;
                 END;

    OnPreReport=BEGIN
                  FA4Helper.InitReportTemplate(REPORT::"FA Write-off Act FA-4");
                END;

    OnPostReport=BEGIN
                   IF FileName = '' THEN
                     FA4Helper.ExportData
                   ELSE
                     FA4Helper.ExportDataFile(FileName);
                 END;

  }
  DATASET
  {
    { 6422;    ;DataItem;                    ;
               DataItemTable=Table12470;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Writeoff));
               OnAfterGetRecord=BEGIN
                                  FASetup.GET;

                                  CheckSignature(Chairman,Chairman."Employee Type"::Chairman);
                                  CheckSignature(Member1,Member1."Employee Type"::Member1);
                                  CheckSignature(Member2,Member2."Employee Type"::Member2);
                                END;

               OnPostDataItem=BEGIN
                                FA4Helper.SetDocSheet;
                              END;

               ReqFilterFields=No. }

    { 3337;1   ;DataItem;                    ;
               DataItemTable=Table12477;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnPreDataItem=BEGIN
                               IsHeaderPrinted := FALSE;
                             END;

               OnAfterGetRecord=BEGIN
                                  FA.GET("FA No.");
                                  FADepreciationBook.GET("FA No.","Depreciation Book Code");
                                  FADepreciationBook.CALCFIELDS("Acquisition Cost",Depreciation,"Book Value");

                                  IF FASetup."FA Location Mandatory" THEN
                                    TESTFIELD("FA Location Code");
                                  IF FASetup."Employee No. Mandatory" THEN
                                    TESTFIELD("FA Employee No.");

                                  FactYears := LocMgt.GetPeriodDate(FA."Initial Release Date","FA Document Header"."Posting Date",2);

                                  GetFAComments(Reason,FAComment.Type::Reason);
                                  GetFAComments(Conclusion,FAComment.Type::Conclusion);
                                  GetFAComments(Appendix,FAComment.Type::Appendix);
                                  GetFAComments(Result,FAComment.Type::Result);

                                  FA4Helper.SetDocSheet;
                                  IF NOT IsHeaderPrinted THEN BEGIN
                                    FA4Helper.FillHeader(
                                      StdRepMgt.GetEmpDepartment("FA Employee No."),StdRepMgt.GetEmpName("FA Employee No."),
                                      Reason[1],FORMAT("FA Document Header"."FA Posting Date"),"FA Document Header"."Reason Document No.",
                                      FORMAT("FA Document Header"."Reason Document Date"),"FA Employee No.",
                                      DirectorPosition,"FA Document Header"."No.",FORMAT("FA Document Header"."Posting Date"),
                                      "FA Document Header"."Posting Description");
                                    FA4Helper.FillStatePageHeader;
                                    IsHeaderPrinted := TRUE;
                                  END;

                                  FA4Helper.FillStateLine(
                                    Description,FA."Inventory Number",FA."Factory No.",FA."Manufacturing Year",FORMAT(FA."Initial Release Date"),
                                    FactYears,StdRepMgt.FormatReportValue(FADepreciationBook."Acquisition Cost",2),
                                    StdRepMgt.FormatReportValue(ABS(FADepreciationBook.Depreciation),2),
                                    StdRepMgt.FormatReportValue(FADepreciationBook."Book Value",2));
                                END;

               ReqFilterFields=Line No.;
               DataItemLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.) }

    { 6729;2   ;DataItem;Part 2              ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               FA4Helper.SetAssetSheet;
                             END;

               OnPostDataItem=BEGIN
                                FA4Helper.FillAssetPageFooter;
                              END;
                               }

    { 9342;3   ;DataItem;                    ;
               DataItemTable=Table5640;
               DataItemTableView=SORTING(Main Asset No.,FA No.);
               OnPreDataItem=BEGIN
                               FA4Helper.FillAssetHeader;
                             END;

               OnAfterGetRecord=BEGIN
                                  FA4Helper.FillAssetLine(
                                    Description,StdRepMgt.FormatReportValue(Quantity,2),'','','','','');
                                END;

               DataItemLinkReference=FA Document Line;
               DataItemLink=Main Asset No.=FIELD(FA No.) }

    { 8951;3   ;DataItem;                    ;
               DataItemTable=Table12474;
               DataItemTableView=SORTING(Item Type,No.,Precious Metals Code);
               OnAfterGetRecord=BEGIN
                                  FA4Helper.FillAssetLine(
                                    '','',Name,"Precious Metals Code",StdRepMgt.GetUoMDesc("Unit of Measure Code"),
                                    StdRepMgt.FormatReportValue(Quantity,2),StdRepMgt.FormatReportValue(Mass,2));
                                END;

               DataItemLinkReference=FA Document Line;
               DataItemLink=No.=FIELD(FA No.) }

    { 2746;2   ;DataItem;Part 3              ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               FA4Helper.FillConclusionHeader(
                                 Conclusion[1],Conclusion[2],Appendix[1],Appendix[2],
                                 Chairman."Employee Job Title",Chairman."Employee Name",
                                 Member1."Employee Job Title",Member1."Employee Name",
                                 Member2."Employee Job Title",Member2."Employee Name");
                             END;

               OnPostDataItem=BEGIN
                                FA4Helper.FillConclusionPageFooter;
                              END;
                               }

    { 9336;3   ;DataItem;                    ;
               DataItemTable=Table12453;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.)
                                 WHERE(Document Type=CONST(Receipt));
               OnPreDataItem=BEGIN
                               SETRANGE("Document No.","FA Document Line"."Item Receipt No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  FA4Helper.FillConclusionLine(
                                    "Document No.",Description,"Item No.","Unit of Measure Code",
                                    StdRepMgt.FormatReportValue(Quantity,2),StdRepMgt.FormatReportValue("Unit Amount",2),Amount);
                                END;

               DataItemLinkReference=FA Document Line;
               DataItemLink=Document No.=FIELD(Item Receipt No.) }

    { 3435;2   ;DataItem;Footer              ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               FA4Helper.FillReportFooter(Result[1]);
                             END;
                              }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CompanyInfo@1210005 : Record 79;
      FASetup@1210006 : Record 5603;
      FADepreciationBook@1470003 : Record 5612;
      FA@1210001 : Record 5600;
      FAComment@1470000 : Record 12478;
      Chairman@1210010 : Record 12420;
      Member1@1210009 : Record 12420;
      Member2@1210003 : Record 12420;
      LocMgt@1210004 : Codeunit 12400;
      DocSignMgt@1470001 : Codeunit 12420;
      StdRepMgt@1210008 : Codeunit 12401;
      FA4Helper@1210002 : Codeunit 14953;
      Reason@1470004 : ARRAY [5] OF Text[80];
      Conclusion@1210052 : ARRAY [5] OF Text[80];
      Appendix@1210053 : ARRAY [5] OF Text[80];
      Result@1470002 : ARRAY [5] OF Text[80];
      FactYears@1210018 : Text[30];
      DirectorPosition@1470005 : Text[80];
      FileName@1210000 : Text;
      IsHeaderPrinted@1210007 : Boolean;

    PROCEDURE CheckSignature@1470002(VAR DocSign@1470000 : Record 12420;EmpType@1470001 : Integer);
    BEGIN
      DocSignMgt.GetDocSign(
        DocSign,DATABASE::"FA Document Header",
        "FA Document Header"."Document Type","FA Document Header"."No.",EmpType,TRUE);
    END;

    PROCEDURE SetFileNameSilent@1210005(NewFileName@1210000 : Text);
    BEGIN
      FileName := NewFileName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

