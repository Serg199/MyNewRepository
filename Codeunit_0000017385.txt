OBJECT Codeunit 17385 Release Absence Order
{
  OBJECT-PROPERTIES
  {
    Date=18.12.13;
    Time=12:00:00;
    Version List=NAVRU7.10;
  }
  PROPERTIES
  {
    TableNo=17385;
    OnRun=VAR
            AbsenceLine@1000 : Record 17386;
          BEGIN
            IF Status = Status::Released THEN
              EXIT;

            TESTFIELD("Employee No.");
            TESTFIELD("No.");
            TESTFIELD("Document Date");
            TESTFIELD("HR Order No.");
            TESTFIELD("HR Order Date");
            TESTFIELD("Posting Date");
            CASE "Document Type" OF
              "Document Type"::"Sick Leave":
                // TESTFIELD("Sick Certificate Series");
                // TESTFIELD("Sick Certificate No.");
                // TESTFIELD("Sick Certificate Date");
                // TESTFIELD("Sick Certificate Reason");
                ;
              "Document Type"::Travel:
                BEGIN
                  TESTFIELD("Travel Destination");
                  TESTFIELD("Travel Purpose");
                  IF "Travel Paid By Type" > 0 THEN
                    TESTFIELD("Travel Paid by No.");
                  TESTFIELD("Travel Reason Document");
                END;
            END;

            AbsenceLine.RESET;
            AbsenceLine.SETRANGE("Document Type","Document Type");
            AbsenceLine.SETRANGE("Document No.","No.");
            IF AbsenceLine.FINDSET THEN
              REPEAT
                // NOT NEEDED ANYMORE?
                // IF EmplJobEntry.PositionChangeExist(
                // AbsenceLine."Employee No.",AbsenceLine."Start Date",AbsenceLine."End Date")
                // THEN
                // ERROR(Text004,AbsenceLine."Document Type",AbsenceLine."Start Date",AbsenceLine."End Date");

                AbsenceLine.TESTFIELD("Start Date");
                AbsenceLine.TESTFIELD("End Date");
                AbsenceLine.TESTFIELD("Time Activity Code");
                AbsenceLine.TESTFIELD("Element Code");
                CASE "Document Type" OF
                  "Document Type"::Vacation:
                    BEGIN
                      AbsenceLine.TESTFIELD("Vacation Type");
                      AbsenceLine.TESTFIELD("Calendar Days");
                      // AbsenceLine.TESTFIELD("Work Period Start Date");
                    END;
                  "Document Type"::"Sick Leave":
                    BEGIN
                      AbsenceLine.TESTFIELD("Sick Leave Type");
                      IF AbsenceLine."Sick Leave Type" IN
                         [AbsenceLine."Sick Leave Type"::"Family Member Care",
                          AbsenceLine."Sick Leave Type"::"Child Care 1.5 years",
                          AbsenceLine."Sick Leave Type"::"Child Care 3 years"]
                      THEN
                        AbsenceLine.TESTFIELD("Relative Person No.");
                    END;
                  "Document Type"::Travel:
                    ;
                END;
              UNTIL AbsenceLine.NEXT = 0
            ELSE
              ERROR(Text001,"Document Type","No.");

            Status := Status::Released;
            MODIFY(TRUE);
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=There is nothing to release for %1 Order %2.;RUS=Нечего выпускать для %1 Приказ %2.';

    PROCEDURE Reopen@1(VAR AbsenceHeader@1000 : Record 17385);
    BEGIN
      WITH AbsenceHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;
        Status := Status::Open;
        MODIFY(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

