OBJECT Table 477 Report Inbox
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Report Inbox;
               RUS=Входящие отчеты];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Entry No.;
                                                              RUS=Номер операции] }
    { 2   ;   ;User ID             ;Text65        ;CaptionML=[ENU=User ID;
                                                              RUS=Код пользователя];
                                                   Editable=No }
    { 3   ;   ;Report Output       ;BLOB          ;CaptionML=[ENU=Report Output;
                                                              RUS=Вывод отчета] }
    { 4   ;   ;Created Date-Time   ;DateTime      ;CaptionML=[ENU=Created Date-Time;
                                                              RUS=Дата и время создания];
                                                   Editable=No }
    { 5   ;   ;Job Queue Log Entry ID;GUID        ;CaptionML=[ENU=Job Queue Log Entry ID;
                                                              RUS=Код операции журнала очереди работ];
                                                   Editable=No }
    { 6   ;   ;Output Type         ;Option        ;CaptionML=[ENU=Output Type;
                                                              RUS=Тип вывода];
                                                   OptionCaptionML=[ENU=PDF,Word,Excel;
                                                                    RUS=PDF,Word,Excel];
                                                   OptionString=PDF,Word,Excel;
                                                   Editable=No }
    { 7   ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              RUS=Описание];
                                                   Editable=No }
    { 8   ;   ;Report ID           ;Integer       ;CaptionML=[ENU=Report ID;
                                                              RUS=Номер отчета];
                                                   Editable=No }
    { 9   ;   ;Report Name         ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Report ID)));
                                                   CaptionML=[ENU=Report Name;
                                                              RUS=Название отчета];
                                                   Editable=No }
    { 10  ;   ;Read                ;Boolean       ;CaptionML=[ENU=Read;
                                                              RUS=Чтение] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;User ID,Created Date-Time                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FileDownLoadTxt@1000 : TextConst 'ENU=Export;RUS=Экспорт';
      ReportIsEmptyMsg@1001 : TextConst 'ENU=The report is empty.;RUS=Отчет пуст.';
      NoReportsToShowErr@1002 : TextConst 'ENU=There are no reports in the list.;RUS=В списке нет отчетов.';

    PROCEDURE ShowReport@1();
    VAR
      Instr@1003 : InStream;
      Downloaded@1002 : Boolean;
      FileName@1004 : Text;
    BEGIN
      IF "Entry No." = 0 THEN
        ERROR(NoReportsToShowErr);

      CALCFIELDS("Report Output","Report Name");
      IF NOT "Report Output".HASVALUE THEN BEGIN
        Read := TRUE;
        MODIFY;
        COMMIT;
        MESSAGE(ReportIsEmptyMsg);
        EXIT;
      END;
      FileName := DELCHR("Report Name",'=','\/:*?"<>|') + Suffix;
      "Report Output".CREATEINSTREAM(Instr);
      Downloaded := DOWNLOADFROMSTREAM(Instr,FileDownLoadTxt,'','',FileName);

      IF NOT Read AND Downloaded THEN BEGIN
        Read := TRUE;
        MODIFY;
      END;
    END;

    LOCAL PROCEDURE Suffix@5() : Text;
    BEGIN
      CASE "Output Type" OF
        "Output Type"::PDF:
          EXIT('.pdf');
        "Output Type"::Word:
          EXIT('.docx');
        "Output Type"::Excel:
          EXIT('.xlsx');
      END;
    END;

    BEGIN
    END.
  }
}

