OBJECT Report 1086 Job Calculate WIP
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job Calculate WIP;
               RUS=Расчет НЗП по работам];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  NewNoSeriesCode@1000 : Code[10];
                BEGIN
                  JobsSetup.GET;

                  IF DocNo = '' THEN BEGIN
                    JobsSetup.TESTFIELD("Job Nos.");
                    NoSeriesMgt.InitSeries(JobsSetup."Job WIP Nos.",JobsSetup."Job WIP Nos.",0D,DocNo,NewNoSeriesCode);
                  END;

                  IF PostingDate = 0D THEN
                    PostingDate := WORKDATE;

                  JobCalculateBatches.BatchError(PostingDate,DocNo);
                END;

    OnPostReport=VAR
                   WIPPosted@1001 : Boolean;
                   WIPQst@1002 : Text;
                 BEGIN
                   JobWIPEntry.SETCURRENTKEY("Job No.");
                   JobWIPEntry.SETFILTER("Job No.",Job.GETFILTER("No."));
                   WIPPosted := JobWIPEntry.FINDFIRST;
                   COMMIT;

                   IF WIPPosted THEN BEGIN
                     IF WIPPostedWithWarnings THEN
                       MESSAGE(Text002)
                     ELSE
                       MESSAGE(Text000);
                     IF DIALOG.CONFIRM(PreviewQst) THEN BEGIN
                       JobWIPEntry.SETRANGE("Job No.",Job."No.");
                       PAGE.RUNMODAL(PAGE::"Job WIP Entries",JobWIPEntry);

                       WIPQst := STRSUBSTNO(RunWIPFunctionsQst,'Job Post WIP to G/L');
                       IF DIALOG.CONFIRM(WIPQst) THEN
                         REPORT.RUN(REPORT::"Job Post WIP to G/L",FALSE,FALSE,Job)
                       ELSE
                         REPORT.RUNMODAL(REPORT::"Job Post WIP to G/L",TRUE,FALSE,Job);
                     END;
                   END ELSE
                     MESSAGE(Text001);
                 END;

  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table167;
               DataItemTableView=SORTING(No.);
               OnAfterGetRecord=VAR
                                  JobCalculateWIP@1000 : Codeunit 1000;
                                BEGIN
                                  JobCalculateWIP.JobCalcWIP(Job,PostingDate,DocNo);
                                  CALCFIELDS("WIP Warnings");
                                  WIPPostedWithWarnings := WIPPostedWithWarnings OR "WIP Warnings";
                                END;

               ReqFilterFields=No.,Planning Date Filter,Posting Date Filter }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=VAR
                   NewNoSeriesCode@1000 : Code[10];
                 BEGIN
                   IF PostingDate = 0D THEN
                     PostingDate := WORKDATE;

                   JobsSetup.GET;

                   JobsSetup.TESTFIELD("Job Nos.");
                   NoSeriesMgt.InitSeries(JobsSetup."Job WIP Nos.",JobsSetup."Job WIP Nos.",0D,DocNo,NewNoSeriesCode);
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             RUS=Параметры] }

      { 1   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             RUS=Дата учета];
                  ToolTipML=[ENU=Specifies the posting date for the document.;
                             RUS=Определяет дату учета документа.];
                  ApplicationArea=#Jobs;
                  SourceExpr=PostingDate }

      { 5   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[ENU=Document No.;
                             RUS=Номер документа];
                  ToolTipML=[ENU=Specifies the number of a document that the calculation will apply to.;
                             RUS=Определяет номер документа, к которому будет относится расчет.];
                  ApplicationArea=#Jobs;
                  SourceExpr=DocNo }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      JobWIPEntry@1004 : Record 1004;
      JobsSetup@1008 : Record 315;
      JobCalculateBatches@1000 : Codeunit 1005;
      NoSeriesMgt@1009 : Codeunit 396;
      PostingDate@1001 : Date;
      DocNo@1003 : Code[20];
      Text000@1006 : TextConst 'ENU=WIP was successfully calculated.;RUS=НЗП успешно вычислено.';
      Text001@1007 : TextConst 'ENU=There were no new WIP entries created.;RUS=Не создано новых операций НЗП.';
      Text002@1002 : TextConst 'ENU=WIP was calculated with warnings.;RUS=НЗП было вычислено с предупреждениями.';
      WIPPostedWithWarnings@1005 : Boolean;
      PreviewQst@1010 : TextConst 'ENU=Do you want to preview the posting accounts?;RUS=Открыть счета учета для предварительного просмотра?';
      RunWIPFunctionsQst@1011 : TextConst '@@@="%1 = The name of the Job Post WIP to G/L report";ENU=You must run the %1 function to post the completion entries for this job. \Do you want to run this function now?;RUS=Необходимо выполнить функцию %1, чтобы учесть операции выполнения этой работы. \Выполнить эту функцию?';

    PROCEDURE InitializeRequest@1();
    BEGIN
      PostingDate := WORKDATE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

