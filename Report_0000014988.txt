OBJECT Report 14988 FA Movement FA-3
{
  OBJECT-PROPERTIES
  {
    Date=09.09.14;
    Time=12:00:00;
    Version List=NAVRU8.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=FA Movement FA-3;
               RUS=Перемещение ОС-3];
    ProcessingOnly=Yes;
    OnInitReport=VAR
                   Employee@1470000 : Record 5200;
                 BEGIN
                   CompanyInfo.GET;
                   IF Employee.GET(CompanyInfo."Director No.") THEN
                     DirectorPosition := Employee.GetJobTitleName;
                 END;

    OnPreReport=BEGIN
                  FA3Helper.InitReportTemplate;
                END;

    OnPostReport=BEGIN
                   IF FileName = '' THEN
                     FA3Helper.ExportData
                   ELSE
                     FA3Helper.ExportDataFile(FileName);
                 END;

  }
  DATASET
  {
    { 6422;    ;DataItem;                    ;
               DataItemTable=Table12470;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Movement));
               OnAfterGetRecord=BEGIN
                                  FASetup.GET;

                                  GetFAComments(Conclusion,FAComment.Type::Conclusion);
                                  GetFAComments(Appendix,FAComment.Type::Appendix);

                                  CheckSignature(ReleasedBy,ReleasedBy."Employee Type"::ReleasedBy);
                                  CheckSignature(ReceivedBy,ReceivedBy."Employee Type"::ReceivedBy);
                                  CheckSignature(Chairman,Chairman."Employee Type"::Chairman);
                                  CheckSignature(Member1,Member1."Employee Type"::Member1);
                                  CheckSignature(Member2,Member2."Employee Type"::Member2);

                                  ConsNo := '0';

                                  FA3Helper.FillHeader(
                                    FALocation.GetName("New FA Location Code"),FALocation.GetName("FA Location Code"),
                                    "No.",FORMAT("Posting Date"),DirectorPosition);
                                END;

               OnPostDataItem=BEGIN
                                FA3Helper.FillReportFooter(
                                  Conclusion[1],Conclusion[2],Appendix,
                                  Chairman."Employee Job Title",Chairman."Employee Name",
                                  Member1."Employee Job Title",Member1."Employee Name",
                                  Member2."Employee Job Title",Member2."Employee Name",
                                  ReleasedBy."Employee Job Title",ReleasedBy."Employee Name",
                                  ReceivedBy."Employee Job Title",ReceivedBy."Employee Name");
                              END;
                               }

    { 3337;1   ;DataItem;                    ;
               DataItemTable=Table12477;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnPreDataItem=BEGIN
                               FA3Helper.FillPageHeader;
                             END;

               OnAfterGetRecord=BEGIN
                                  FixedAsset.GET("FA No.");
                                  TESTFIELD("Depreciation Book Code");
                                  TESTFIELD("FA Posting Group");

                                  IF FASetup."FA Location Mandatory" THEN
                                    TESTFIELD("FA Location Code");
                                  IF FASetup."Employee No. Mandatory" THEN
                                    TESTFIELD("FA Employee No.");

                                  ActualUse := LocMgt.GetPeriodDate(FixedAsset."Initial Release Date","Posting Date",2);
                                  ConsNo := INCSTR(ConsNo);

                                  FA3Helper.FillLine(
                                    ConsNo,Description,FixedAsset."Inventory Number",FixedAsset."Passport No.",
                                    FixedAsset."Factory No.",StdRepMgt.FormatReportValue(Amount,2),ActualUse);
                                END;

               OnPostDataItem=BEGIN
                                FA3Helper.FillExpFooter;
                              END;

               DataItemLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      FASetup@1210006 : Record 5603;
      CompanyInfo@1210005 : Record 79;
      FixedAsset@1210001 : Record 5600;
      ReceivedBy@1470001 : Record 12420;
      ReleasedBy@1470000 : Record 12420;
      Chairman@1470005 : Record 12420;
      Member1@1470006 : Record 12420;
      Member2@1470007 : Record 12420;
      FAComment@1470008 : Record 12478;
      FALocation@1210003 : Record 5609;
      LocMgt@1210013 : Codeunit 12400;
      StdRepMgt@1210004 : Codeunit 12401;
      DocSignMgt@1470002 : Codeunit 12420;
      FA3Helper@1210002 : Codeunit 14952;
      Appendix@1210053 : ARRAY [5] OF Text[80];
      Conclusion@1470009 : ARRAY [5] OF Text[80];
      ActualUse@1470003 : Text[30];
      FileName@1210000 : Text;
      ConsNo@1470004 : Code[10];
      DirectorPosition@1470010 : Text[80];

    PROCEDURE CheckSignature@1470001(VAR DocSign@1470000 : Record 12420;EmpType@1470001 : Integer);
    BEGIN
      DocSignMgt.GetDocSign(
        DocSign,DATABASE::"FA Document Header",
        "FA Document Header"."Document Type","FA Document Header"."No.",EmpType,TRUE);
    END;

    PROCEDURE SetFileNameSilent@1210005(NewFileName@1210000 : Text);
    BEGIN
      FileName := NewFileName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

