OBJECT Codeunit 17387 Absence Order-Post
{
  OBJECT-PROPERTIES
  {
    Date=15.09.15;
    Time=12:00:00;
    Version List=NAVRU9.00;
  }
  PROPERTIES
  {
    TableNo=17385;
    Permissions=TableData 17387=rimd,
                TableData 17388=rimd,
                TableData 17389=rimd,
                TableData 17413=rimd;
    OnRun=VAR
            PayrollPeriodFrom@1210000 : Record 17426;
            PayrollPeriodTo@1210001 : Record 17426;
            RecordLinkManagement@1210003 : Codeunit 447;
            WagePeriodCode@1210002 : Code[10];
          BEGIN
            CLEARALL;
            HRSetup.GET;
            HRSetup.TESTFIELD("Change Vacation Accr. By Doc");
            AbsenceHeader := Rec;
            WITH AbsenceHeader DO BEGIN
              TESTFIELD("Employee No.");
              TESTFIELD("Posting Date");
              CALCFIELDS("Start Date","End Date","Calendar Days");

              CheckDim;

              Window.OPEN(
                '#1#################################\\' +
                Text000);

              Window.UPDATE(1,STRSUBSTNO('%1 %2',TABLECAPTION,"No."));

              IF Status = Status::Open THEN
                CODEUNIT.RUN(CODEUNIT::"Release Absence Order",AbsenceHeader);

              AbsenceLine.LOCKTABLE;
              LOCKTABLE;
              EmplAbsenceEntry.LOCKTABLE;

              SourceCodeSetup.GET;
              CASE "Document Type" OF
                "Document Type"::Vacation:
                  SourceCodeSetup.TESTFIELD("Vacation Order");
                "Document Type"::"Sick Leave":
                  SourceCodeSetup.TESTFIELD("Sick Leave Order");
                "Document Type"::Travel:
                  SourceCodeSetup.TESTFIELD("Travel Order");
                "Document Type"::"Other Absence":
                  SourceCodeSetup.TESTFIELD("Other Absence Order");
              END;

              IF EmplAbsenceEntry.FINDLAST THEN
                NextEntryNo := EmplAbsenceEntry."Entry No."
              ELSE
                NextEntryNo := 0;

              // Insert posted absence header
              PostedAbsenceHeader.LOCKTABLE;
              PostedAbsenceHeader.INIT;
              PostedAbsenceHeader.TRANSFERFIELDS(AbsenceHeader);
              PostedAbsenceHeader.INSERT;

              CopyCommentLines("No.",PostedAbsenceHeader."No.",FALSE);
              RecordLinkManagement.CopyLinks(Rec,PostedAbsenceHeader);

              // Lines
              PostedAbsenceLine.LOCKTABLE;

              LineCount := 0;
              AbsenceLine.RESET;
              AbsenceLine.SETRANGE("Document Type","Document Type");
              AbsenceLine.SETRANGE("Document No.","No.");
              IF AbsenceLine.FINDSET THEN
                REPEAT
                  LineCount := LineCount + 1;
                  Window.UPDATE(2,LineCount);
                  PayrollElement.GET(AbsenceLine."Element Code");
                  Employee.GET(AbsenceLine."Employee No.");

                  // insert posted lines
                  PostedAbsenceLine.INIT;
                  PostedAbsenceLine.TRANSFERFIELDS(AbsenceLine);
                  PostedAbsenceLine.INSERT;

                  ApplyEmplAbsenceEntry(AbsenceLine);

                  CheckVacationPeriodExpand(AbsenceLine);

                  // Insert and post employee journal lines by periods
                  PayrollPeriod.CheckPeriodExistence(AbsenceLine."Start Date");
                  PayrollPeriod.CheckPeriodExistence(AbsenceLine."End Date");
                  PayrollPeriodFrom.GET(PayrollPeriod.PeriodByDate(AbsenceLine."Start Date"));
                  PayrollPeriodTo.GET(PayrollPeriod.PeriodByDate(AbsenceLine."End Date"));

                  IF PayrollElement."Distribute by Periods" THEN BEGIN
                    FirstEntryNo := 0;
                    PayrollPeriod.RESET;
                    PayrollPeriod.SETRANGE(Code,PayrollPeriodFrom.Code,PayrollPeriodTo.Code);
                    IF PayrollPeriod.FINDSET THEN
                      REPEAT
                        AbsenceLine2 := AbsenceLine;
                        IF PayrollPeriodFrom.Code <> PayrollPeriodTo.Code THEN
                          CASE PayrollPeriod.Code OF
                            PayrollPeriodFrom.Code:
                              BEGIN
                                AbsenceLine2."Start Date" := AbsenceLine."Start Date";
                                AbsenceLine2."End Date" := PayrollPeriodFrom."Ending Date";
                              END;
                            PayrollPeriodTo.Code:
                              BEGIN
                                AbsenceLine2."Start Date" := PayrollPeriodTo."Starting Date";
                                AbsenceLine2."End Date" := AbsenceLine."End Date";
                              END;
                            ELSE BEGIN
                              AbsenceLine2."Start Date" := PayrollPeriod."Starting Date";
                              AbsenceLine2."End Date" := PayrollPeriod."Ending Date";
                            END;
                          END
                        ELSE BEGIN
                          AbsenceLine2."Start Date" := AbsenceLine."Start Date";
                          AbsenceLine2."End Date" := AbsenceLine."End Date";
                        END;
                        WagePeriodCode := PayrollPeriod.PeriodByDate(AbsenceLine2."End Date");

                        IF NOT AbsenceLine2.Terminated THEN
                          AbsenceLine2.CalcDays
                        ELSE
                          AbsenceLine2."Calendar Days" :=
                            CalendarMgt.GetPeriodInfo(
                              Employee."Calendar Code",AbsenceLine2."Start Date",AbsenceLine2."End Date",1);

                        CASE "Document Type" OF
                          "Document Type"::Vacation:
                            InsertEmplJnlLine(
                              WagePeriodCode,
                              AbsenceLine2."Start Date",AbsenceLine2."End Date",
                              AbsenceLine2."Calendar Days",
                              AbsenceLine2."Payment Days",AbsenceLine2."Payment Percent",0);
                          "Document Type"::"Sick Leave":
                            BEGIN
                              IF AbsenceLine."Days Paid by Employer" +
                                 AbsenceLine."Payment Days" + AbsenceLine."Special Payment Days" +
                                 AbsenceLine."Days Not Paid" <> 0
                              THEN BEGIN
                                IF AbsenceLine."Days Paid by Employer" > 0 THEN
                                  IF AbsenceLine."Days Paid by Employer" >= AbsenceLine2."Calendar Days" THEN BEGIN
                                    AbsenceLine2."End Date" :=
                                      NextCalEndDate(AbsenceLine2."Start Date",AbsenceLine2."Calendar Days" - 1);
                                    InsertEmplJnlLine(
                                      WagePeriodCode,
                                      AbsenceLine2."Start Date",AbsenceLine2."End Date",
                                      AbsenceLine2."Calendar Days",
                                      AbsenceLine2."Calendar Days",AbsenceLine."Payment Percent",0);
                                    AbsenceLine."Days Paid by Employer" -= AbsenceLine2."Calendar Days";
                                    AbsenceLine2."Calendar Days" := 0;
                                    AbsenceLine2."Start Date" := NextCalEndDate(AbsenceLine2."End Date",1);
                                  END ELSE BEGIN
                                    AbsenceLine2."End Date" :=
                                      NextCalEndDate(AbsenceLine2."Start Date",AbsenceLine."Days Paid by Employer" - 1);
                                    InsertEmplJnlLine(
                                      WagePeriodCode,
                                      AbsenceLine2."Start Date",AbsenceLine2."End Date",
                                      AbsenceLine."Days Paid by Employer",
                                      AbsenceLine."Days Paid by Employer",AbsenceLine."Payment Percent",0);
                                    AbsenceLine2."Calendar Days" -= AbsenceLine."Days Paid by Employer";
                                    AbsenceLine."Days Paid by Employer" := 0;
                                    AbsenceLine2."Start Date" := NextCalEndDate(AbsenceLine2."End Date",1);
                                  END;
                                IF (AbsenceLine2."Calendar Days" > 0) AND (AbsenceLine."Payment Days" > 0) THEN
                                  IF AbsenceLine."Payment Days" >= AbsenceLine2."Calendar Days" THEN BEGIN
                                    AbsenceLine2."End Date" :=
                                      NextCalEndDate(AbsenceLine2."Start Date",AbsenceLine2."Calendar Days" - 1);
                                    InsertEmplJnlLine(
                                      WagePeriodCode,
                                      AbsenceLine2."Start Date",AbsenceLine2."End Date",
                                      AbsenceLine2."Calendar Days",
                                      AbsenceLine2."Calendar Days",AbsenceLine."Payment Percent",1);
                                    AbsenceLine."Payment Days" -= AbsenceLine2."Calendar Days";
                                    AbsenceLine2."Calendar Days" := 0;
                                    AbsenceLine2."Start Date" := NextCalEndDate(AbsenceLine2."End Date",1);
                                  END ELSE BEGIN
                                    AbsenceLine2."End Date" :=
                                      NextCalEndDate(AbsenceLine2."Start Date",AbsenceLine2."Payment Days" - 1);
                                    InsertEmplJnlLine(
                                      WagePeriodCode,
                                      AbsenceLine2."Start Date",AbsenceLine2."End Date",
                                      AbsenceLine."Payment Days",
                                      AbsenceLine."Payment Days",AbsenceLine."Payment Percent",1);
                                    AbsenceLine2."Calendar Days" -= AbsenceLine."Payment Days";
                                    AbsenceLine."Payment Days" := 0;
                                    AbsenceLine2."Start Date" := NextCalEndDate(AbsenceLine2."End Date",1);
                                  END;
                                IF (AbsenceLine2."Calendar Days" > 0) AND (AbsenceLine."Special Payment Days" > 0) THEN
                                  IF AbsenceLine."Special Payment Days" >= AbsenceLine2."Calendar Days" THEN BEGIN
                                    AbsenceLine2."End Date" :=
                                      NextCalEndDate(AbsenceLine2."Start Date",AbsenceLine2."Calendar Days" - 1);
                                    InsertEmplJnlLine(
                                      WagePeriodCode,
                                      AbsenceLine2."Start Date",AbsenceLine2."End Date",
                                      AbsenceLine2."Calendar Days",
                                      AbsenceLine2."Calendar Days",AbsenceLine."Special Payment Percent",1);
                                    AbsenceLine."Special Payment Days" -= AbsenceLine2."Calendar Days";
                                    AbsenceLine2."Calendar Days" := 0;
                                    AbsenceLine2."Start Date" := NextCalEndDate(AbsenceLine2."End Date",1);
                                  END ELSE BEGIN
                                    AbsenceLine2."End Date" :=
                                      NextCalEndDate(AbsenceLine2."Start Date",AbsenceLine2."Special Payment Days" - 1);
                                    InsertEmplJnlLine(
                                      WagePeriodCode,
                                      AbsenceLine2."Start Date",AbsenceLine2."End Date",
                                      AbsenceLine."Special Payment Days",
                                      AbsenceLine."Special Payment Days",AbsenceLine."Special Payment Percent",1);
                                    AbsenceLine2."Calendar Days" -= AbsenceLine."Special Payment Days";
                                    AbsenceLine."Special Payment Days" := 0;
                                    AbsenceLine2."Start Date" := NextCalEndDate(AbsenceLine2."End Date",1);
                                  END;
                                IF AbsenceLine."Days Not Paid" > 0 THEN BEGIN
                                  IF AbsenceLine."Days Not Paid" > AbsenceLine2."Calendar Days" THEN BEGIN
                                    AbsenceLine2."End Date" := PayrollPeriod."Ending Date";
                                    AbsenceLine2."Days Not Paid" := AbsenceLine2."Calendar Days";
                                  END ELSE
                                    AbsenceLine2."End Date" :=
                                      NextCalEndDate(AbsenceLine2."Start Date",AbsenceLine2."Days Not Paid" - 1);
                                  IF AbsenceLine2."Days Not Paid" <> 0 THEN BEGIN
                                    InsertEmplJnlLine(
                                      WagePeriodCode,
                                      AbsenceLine2."Start Date",AbsenceLine2."End Date",
                                      AbsenceLine2."Days Not Paid",
                                      AbsenceLine2."Calendar Days",0,1);
                                    AbsenceLine."Days Not Paid" -= AbsenceLine2."Days Not Paid";
                                  END;
                                END;
                              END ELSE
                                IF NOT AbsenceLine2.Terminated THEN
                                  TimesheetMgt.CreateFromLine(
                                    AbsenceLine2."Employee No.",AbsenceLine2."Time Activity Code",
                                    AbsenceLine2."Start Date",AbsenceLine2."End Date",
                                    "Document Type","No.","Document Date");
                            END;
                          "Document Type"::Travel:
                            InsertEmplJnlLine(
                              WagePeriodCode,
                              AbsenceLine2."Start Date",AbsenceLine2."End Date",
                              AbsenceLine2."Working Days",
                              AbsenceLine2."Payment Days",AbsenceLine2."Payment Percent",0);
                          "Document Type"::"Other Absence":
                            InsertEmplJnlLine(
                              WagePeriodCode,
                              AbsenceLine2."Start Date",AbsenceLine2."End Date",
                              AbsenceLine2."Calendar Days",
                              AbsenceLine2."Payment Days",AbsenceLine2."Payment Percent",0);
                        END;
                      UNTIL PayrollPeriod.NEXT = 0;
                  END ELSE
                    CASE "Document Type" OF
                      "Document Type"::Vacation,
                      "Document Type"::"Sick Leave",
                      "Document Type"::"Other Absence":
                        InsertEmplJnlLine(
                          "Period Code",
                          AbsenceLine."Start Date",AbsenceLine."End Date",
                          AbsenceLine."Calendar Days",
                          AbsenceLine."Payment Days",AbsenceLine."Payment Percent",0);
                      "Document Type"::Travel:
                        InsertEmplJnlLine(
                          "Period Code",
                          AbsenceLine."Start Date",AbsenceLine."End Date",
                          AbsenceLine."Working Days",
                          AbsenceLine."Payment Days",AbsenceLine."Payment Percent",0);
                    END;

                  // Update vacation request
                  IF VacationRequest.GET(AbsenceLine."Vacation Request No.") THEN
                    VacationRequest.MarkUsed;

                UNTIL AbsenceLine.NEXT = 0;

              // Delete posted order
              AbsenceLine.DELETEALL;
              DELETE;

              COMMIT;
            END;
          END;

  }
  CODE
  {
    VAR
      HRSetup@1210020 : Record 5218;
      AbsenceHeader@1210004 : Record 17385;
      AbsenceLine@1210002 : Record 17386;
      AbsenceLine2@1210028 : Record 17386;
      PostedAbsenceHeader@1210015 : Record 17387;
      PostedAbsenceLine@1210016 : Record 17388;
      VacationRequest@1210012 : Record 17434;
      EmplAbsenceEntry@1210001 : Record 17389;
      Text000@1210005 : TextConst 'ENU=Posting              #2######;RUS=Учет              #2######';
      SourceCodeSetup@1210011 : Record 242;
      HROrderComment@1210017 : Record 17368;
      PayrollPeriod@1210019 : Record 17426;
      PayrollElement@1210026 : Record 17400;
      TimeActivityGroup@1210029 : Record 17444;
      Employee@1210018 : Record 5200;
      EmplJnlPostLine@1210010 : Codeunit 17382;
      TimesheetMgt@1210031 : Codeunit 17440;
      CalendarMgt@1210027 : Codeunit 17430;
      DimMgt@1210014 : Codeunit 408;
      Window@1210006 : Dialog;
      LineCount@1210007 : Integer;
      NextEntryNo@1210008 : Integer;
      FirstEntryNo@1210003 : Integer;
      Text028@1210033 : TextConst 'ENU=The combination of dimensions used in %1 %2 is blocked. %3;RUS=Комбинация измерений, использованная в %1 %2, заблокирована. %3';
      Text029@1210032 : TextConst 'ENU=The combination of dimensions used in %1 %2, line no. %3 is blocked. %4;RUS=Комбинация измерений, использованная в %1 %2, в строке ном. %3, заблокирована. %4';
      Text030@1210025 : TextConst 'ENU=The dimensions used in %1 %2 are invalid. %3;RUS=Измерения, использованные в %1 %2, недопустимы. %3';
      Text031@1210009 : TextConst 'ENU=The dimensions used in %1 %2, line no. %3 are invalid. %4;RUS=Измерения, использованные в %1 %2, в строке ном. %3, недопустимы. %4';

    PROCEDURE InsertEmplJnlLine@1210004(WagePeriodCode@1210000 : Code[10];StartDate@1210003 : Date;EndDate@1210004 : Date;CalendarDays@1210001 : Decimal;PaymentDays@1210002 : Decimal;PaymentPercent@1210005 : Decimal;PaymentSource@1210007 : 'Employer,FSI');
    VAR
      EmplJnlLine@1210010 : Record 17382;
      EntryNo@1210011 : Integer;
    BEGIN
      EmplJnlLine.INIT;
      EmplJnlLine."Document Type" := AbsenceLine."Document Type" + 1;
      EmplJnlLine.VALIDATE("Employee No.",AbsenceHeader."Employee No.");
      EmplJnlLine.VALIDATE("Time Activity Code",AbsenceLine."Time Activity Code");
      EmplJnlLine.VALIDATE("Element Code",AbsenceLine."Element Code");
      EmplJnlLine."Posting Date" := AbsenceHeader."Posting Date";
      EmplJnlLine."Vacation Type" := AbsenceLine."Vacation Type";
      EmplJnlLine."Sick Leave Type" := AbsenceLine."Sick Leave Type";
      IF EmplJnlLine."Sick Leave Type" IN
         [EmplJnlLine."Sick Leave Type"::"Child Care 1.5 years",
          EmplJnlLine."Sick Leave Type"::"Child Care 3 years"]
      THEN
        EmplJnlLine."Period Code" := WagePeriodCode
      ELSE
        EmplJnlLine."Period Code" := AbsenceHeader."Period Code";
      IF AbsenceLine."Previous Document No." = '' THEN
        EmplJnlLine.Quantity := 1
      ELSE
        EmplJnlLine.Quantity := 0;
      EmplJnlLine."Payment Days" := PaymentDays;
      EmplJnlLine."Payment Percent" := PaymentPercent;
      IF AbsenceHeader."Document Type" = AbsenceHeader."Document Type"::"Sick Leave" THEN BEGIN
        EmplJnlLine."Payment Source" := PaymentSource;
        EmplJnlLine."Relative Person No." := AbsenceLine."Relative Person No.";
        EmplJnlLine."Child Grant Type" := AbsenceLine."Child Grant Type";
      END;
      EmplJnlLine."Starting Date" := StartDate;
      EmplJnlLine."Ending Date" := EndDate;
      EmplJnlLine."Wage Period From" := WagePeriodCode;
      EmplJnlLine."Wage Period To" := WagePeriodCode;

      EmplJnlLine."AE Period From" := AbsenceLine."AE Period From";
      EmplJnlLine."AE Period To" := AbsenceLine."AE Period To";
      EmplJnlLine."Document No." := AbsenceHeader."No.";
      EmplJnlLine."Document Date" := AbsenceHeader."Document Date";
      EmplJnlLine."HR Order No." := AbsenceHeader."HR Order No.";
      EmplJnlLine."HR Order Date" := AbsenceHeader."HR Order Date";
      EmplJnlLine.Description := AbsenceLine.Description;
      EmplJnlLine."Reason Code" := AbsenceHeader."Reason Code";
      EmplJnlLine.Terminated := AbsenceLine.Terminated;
      CASE AbsenceHeader."Document Type" OF
        AbsenceHeader."Document Type"::Vacation:
          EmplJnlLine."Source Code" := SourceCodeSetup."Vacation Order";
        AbsenceHeader."Document Type"::"Sick Leave":
          EmplJnlLine."Source Code" := SourceCodeSetup."Sick Leave Order";
        AbsenceHeader."Document Type"::Travel:
          EmplJnlLine."Source Code" := SourceCodeSetup."Travel Order";
        AbsenceHeader."Document Type"::"Other Absence":
          EmplJnlLine."Source Code" := SourceCodeSetup."Other Absence Order";
      END;
      EmplJnlLine."Dimension Set ID" := AbsenceLine."Dimension Set ID";
      IF FirstEntryNo <> 0 THEN BEGIN
        EmplJnlLine."Applies-to Entry" := FirstEntryNo;
        EmplJnlLine.Quantity := 0;
        EmplJnlPostLine.RunWithCheck(EmplJnlLine);
      END ELSE BEGIN
        EntryNo := EmplJnlPostLine.RunWithCheck(EmplJnlLine);
        FirstEntryNo := EntryNo;
      END;
    END;

    LOCAL PROCEDURE CheckDim@34();
    BEGIN
      AbsenceLine2."Line No." := 0;
      CheckDimValuePosting(AbsenceLine2);
      CheckDimComb(AbsenceLine2);

      AbsenceLine2.SETRANGE("Document Type",AbsenceHeader."Document Type");
      AbsenceLine2.SETRANGE("Document No.",AbsenceHeader."No.");
      IF AbsenceLine2.FINDSET THEN
        REPEAT
          CheckDimComb(AbsenceLine2);
          CheckDimValuePosting(AbsenceLine2);
        UNTIL AbsenceLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckDimComb@30(AbsenceLine@1000 : Record 17386);
    BEGIN
      IF AbsenceLine."Line No." = 0 THEN
        IF NOT DimMgt.CheckDimIDComb(AbsenceHeader."Dimension Set ID") THEN
          ERROR(
            Text028,
            AbsenceHeader."Document Type",AbsenceHeader."No.",DimMgt.GetDimCombErr);

      IF AbsenceLine."Line No." <> 0 THEN
        IF NOT DimMgt.CheckDimIDComb(AbsenceLine."Dimension Set ID") THEN
          ERROR(
            Text029,
            AbsenceHeader."Document Type",AbsenceHeader."No.",AbsenceLine."Line No.",DimMgt.GetDimCombErr);
    END;

    LOCAL PROCEDURE CheckDimValuePosting@28(VAR AbsenceLine2@1000 : Record 17386);
    VAR
      TableIDArr@1002 : ARRAY [10] OF Integer;
      NumberArr@1003 : ARRAY [10] OF Code[20];
    BEGIN
      IF AbsenceLine2."Line No." = 0 THEN BEGIN
        TableIDArr[1] := DATABASE::Employee;
        NumberArr[1] := AbsenceHeader."Employee No.";
        IF NOT DimMgt.CheckDimValuePosting(TableIDArr,NumberArr,AbsenceHeader."Dimension Set ID") THEN
          ERROR(
            Text030,
            AbsenceHeader."Document Type",AbsenceHeader."No.",DimMgt.GetDimValuePostingErr);
      END ELSE BEGIN
        TableIDArr[1] := DATABASE::Employee;
        NumberArr[1] := AbsenceLine2."Employee No.";
        IF NOT DimMgt.CheckDimValuePosting(TableIDArr,NumberArr,AbsenceLine2."Dimension Set ID") THEN
          ERROR(
            Text031,
            AbsenceHeader."Document Type",AbsenceHeader."No.",AbsenceLine2."Line No.",DimMgt.GetDimValuePostingErr);
      END;
    END;

    LOCAL PROCEDURE CopyCommentLines@22(FromNumber@1002 : Code[20];ToNumber@1003 : Code[20];CancelOrder@1210001 : Boolean);
    VAR
      HROrderComment2@1210000 : Record 17368;
    BEGIN
      IF CancelOrder THEN
        HROrderComment.SETRANGE("Table Name",HROrderComment."Table Name"::"P.Absence Order")
      ELSE
        HROrderComment.SETRANGE("Table Name",HROrderComment."Table Name"::"Absence Order");
      HROrderComment.SETRANGE("No.",FromNumber);
      IF HROrderComment.FINDSET THEN BEGIN
        REPEAT
          HROrderComment2 := HROrderComment;
          IF CancelOrder THEN
            HROrderComment2."Table Name" := HROrderComment2."Table Name"::"Absence Order"
          ELSE
            HROrderComment2."Table Name" := HROrderComment2."Table Name"::"P.Absence Order";
          HROrderComment2."No." := ToNumber;
          HROrderComment2.INSERT;
        UNTIL HROrderComment.NEXT = 0;
        HROrderComment.DELETEALL;
      END;
    END;

    PROCEDURE ApplyEmplAbsenceEntry@1210002(AbsenceLine@1210000 : Record 17386);
    VAR
      TimeActivity@1210004 : Record 5206;
      EmplAbsenceEntry2@1210001 : Record 17389;
      DaysToUse@1210003 : Decimal;
      RemainingDays@1210002 : Decimal;
    BEGIN
      TimeActivity.GET(AbsenceLine."Time Activity Code");
      IF TimeActivity."Use Accruals" THEN BEGIN
        DaysToUse := AbsenceLine."Calendar Days";
        EmplAbsenceEntry2.RESET;
        EmplAbsenceEntry2.SETRANGE("Employee No.",AbsenceLine."Employee No.");
        EmplAbsenceEntry2.SETRANGE("Time Activity Code",AbsenceLine."Time Activity Code");
        EmplAbsenceEntry2.SETRANGE("Entry Type",EmplAbsenceEntry2."Entry Type"::Accrual);
        IF EmplAbsenceEntry2.FINDSET THEN BEGIN
          REPEAT
            EmplAbsenceEntry2.CALCFIELDS("Used Calendar Days");
            RemainingDays := EmplAbsenceEntry2."Calendar Days" - EmplAbsenceEntry2."Used Calendar Days";
            IF RemainingDays <> 0 THEN
              IF RemainingDays >= DaysToUse THEN BEGIN
                InsertEmplAbsenceEntry(AbsenceLine,DaysToUse,0,EmplAbsenceEntry2."Entry No.");
                DaysToUse := 0;
              END ELSE BEGIN
                InsertEmplAbsenceEntry(AbsenceLine,RemainingDays,0,EmplAbsenceEntry2."Entry No.");
                DaysToUse := DaysToUse - RemainingDays;
              END;
          UNTIL (EmplAbsenceEntry2.NEXT = 0) OR (DaysToUse = 0);
          IF DaysToUse <> 0 THEN
            REPEAT
              IF EmplAbsenceEntry2.GET(InsertVacationAccrual(AbsenceLine)) THEN BEGIN
                EmplAbsenceEntry2.CALCFIELDS("Used Calendar Days");
                RemainingDays := EmplAbsenceEntry2."Calendar Days" - EmplAbsenceEntry2."Used Calendar Days";
                IF RemainingDays >= DaysToUse THEN BEGIN
                  InsertEmplAbsenceEntry(AbsenceLine,DaysToUse,0,EmplAbsenceEntry2."Entry No.");
                  DaysToUse := 0;
                END ELSE BEGIN
                  InsertEmplAbsenceEntry(AbsenceLine,RemainingDays,0,EmplAbsenceEntry2."Entry No.");
                  DaysToUse := DaysToUse - RemainingDays;
                END;
              END;
            UNTIL DaysToUse = 0;
        END;
      END ELSE
        InsertEmplAbsenceEntry(AbsenceLine,AbsenceLine."Calendar Days",AbsenceLine."Working Days",0);
    END;

    PROCEDURE InsertEmplAbsenceEntry@1210000(AbsenceLine@1210000 : Record 17386;CalendarDays@1210001 : Decimal;WorkingDays@1210003 : Decimal;AccruedEntryNo@1210002 : Integer);
    BEGIN
      EmplAbsenceEntry.INIT;
      NextEntryNo := NextEntryNo + 1;
      EmplAbsenceEntry."Entry No." := NextEntryNo;
      EmplAbsenceEntry."Employee No." := AbsenceHeader."Employee No.";
      EmplAbsenceEntry."Time Activity Code" := AbsenceLine."Time Activity Code";
      EmplAbsenceEntry."Element Code" := AbsenceLine."Element Code";
      EmplAbsenceEntry.Description := AbsenceLine.Description;
      EmplAbsenceEntry."Entry Type" := EmplAbsenceEntry."Entry Type"::Usage;
      EmplAbsenceEntry."Start Date" := AbsenceLine."Start Date";
      EmplAbsenceEntry."End Date" := AbsenceLine."End Date";
      EmplAbsenceEntry."Calendar Days" := CalendarDays;
      EmplAbsenceEntry."Working Days" := WorkingDays;
      EmplAbsenceEntry."Accrual Entry No." := AccruedEntryNo;
      EmplAbsenceEntry."Document Type" := AbsenceLine."Document Type" + 1;
      EmplAbsenceEntry."Document No." := AbsenceLine."Document No.";
      EmplAbsenceEntry."Document Date" := AbsenceHeader."Document Date";
      EmplAbsenceEntry."HR Order No." := AbsenceHeader."HR Order No.";
      EmplAbsenceEntry."HR Order Date" := AbsenceHeader."HR Order Date";
      EmplAbsenceEntry."Vacation Type" := AbsenceLine."Vacation Type";
      EmplAbsenceEntry."Sick Leave Type" := AbsenceLine."Sick Leave Type";
      EmplAbsenceEntry."Person No." := AbsenceLine."Person No.";
      EmplAbsenceEntry."Relative Code" := AbsenceLine."Relative Person No.";
      EmplAbsenceEntry.INSERT;
    END;

    PROCEDURE CheckVacationPeriodExpand@1210003(AbsenceLine@1210005 : Record 17386);
    VAR
      TimeActivity@1210004 : Record 5206;
      EmplAbsenceEntry2@1210000 : Record 17389;
      LinkedEmplAbsenceEntry@1210001 : Record 17389;
      VacExtDays@1210003 : Decimal;
      EndDate@1210002 : Date;
    BEGIN
      TimeActivity.GET(AbsenceLine."Time Activity Code");
      IF IsTACInVacExpandGroup(AbsenceLine."Time Activity Code",AbsenceLine."End Date") THEN BEGIN
        EmplAbsenceEntry2.SETRANGE("Employee No.",AbsenceLine."Employee No.");
        EmplAbsenceEntry2.SETRANGE("Entry Type",EmplAbsenceEntry2."Entry Type"::Accrual);
        EmplAbsenceEntry2.SETFILTER("End Date",'>%1',AbsenceLine."End Date");
        EmplAbsenceEntry2.SETFILTER("Start Date",'<%1',AbsenceLine."End Date");
        EmplAbsenceEntry2.SETFILTER("Time Activity Code",GetAnnualVacTimeActFilter(AbsenceLine."End Date"));
        IF EmplAbsenceEntry2.FINDLAST THEN BEGIN
          VacExtDays :=
            TimesheetMgt.GetTimesheetInfo(
              AbsenceLine."Employee No.",
              AbsenceLine."Time Activity Code",
              EmplAbsenceEntry2."Start Date",
              EmplAbsenceEntry2."End Date",
              4) +
            AbsenceLine."Calendar Days";
          IF VacExtDays > TimeActivity."Min Days Allowed per Year" THEN BEGIN
            EmplAbsenceEntry.INIT;
            EmplAbsenceEntry.TRANSFERFIELDS(EmplAbsenceEntry2);
            NextEntryNo := NextEntryNo + 1;
            LinkedEmplAbsenceEntry.SETRANGE("Accrual Entry No.",EmplAbsenceEntry2."Entry No.");
            LinkedEmplAbsenceEntry.SETRANGE("Entry Type",LinkedEmplAbsenceEntry."Entry Type"::Accrual);
            IF LinkedEmplAbsenceEntry.FINDLAST THEN
              EndDate := LinkedEmplAbsenceEntry."End Date"
            ELSE
              EndDate := EmplAbsenceEntry2."End Date";
            EmplAbsenceEntry."Entry No." := NextEntryNo;
            EmplAbsenceEntry."Start Date" := EndDate + 1;
            EmplAbsenceEntry."End Date" :=
              EmplAbsenceEntry."Start Date" + VacExtDays - TimeActivity."Min Days Allowed per Year" - 1;
            EmplAbsenceEntry."Calendar Days" := 0;
            EmplAbsenceEntry."Working Days" := 0;
            EmplAbsenceEntry."Accrual Entry No." := EmplAbsenceEntry2."Entry No.";
            EmplAbsenceEntry."Document No." := AbsenceLine."Document No.";
            EmplAbsenceEntry."Document Date" := AbsenceHeader."Document Date";
            EmplAbsenceEntry.Description := TimeActivity.Description;
            EmplAbsenceEntry.INSERT;
          END;
        END;
      END;
    END;

    PROCEDURE IsTACInVacExpandGroup@1210001(TimeActivityCode@1210000 : Code[10];AbsenceEndDate@1210003 : Date) : Boolean;
    BEGIN
      TimeActivityGroup.GET(HRSetup."Change Vacation Accr. By Doc");
      EXIT(TimeActivityGroup.TimeActivityInGroup(TimeActivityCode,AbsenceEndDate));
    END;

    PROCEDURE NextCalEndDate@1210005(StartDate@1210000 : Date;CalDays@1210001 : Decimal) : Date;
    BEGIN
      EXIT(CALCDATE('<' + FORMAT(CalDays) + 'D>',StartDate));
    END;

    PROCEDURE GetAnnualVacTimeActFilter@1210006(StartDate@1210000 : Date) : Code[250];
    VAR
      TimeActivityFilter@1210001 : Record 17445;
    BEGIN
      HRSetup.TESTFIELD("Annual Vacation Group Code");
      TimesheetMgt.GetTimeGroupFilter(HRSetup."Annual Vacation Group Code",StartDate,TimeActivityFilter);
      EXIT(TimeActivityFilter."Activity Code Filter");
    END;

    PROCEDURE CancelOrder@1210007(VAR PostedAbsenceHeader@1210000 : Record 17387;VAR NewDocNo@1210010 : Code[20]);
    VAR
      EmplLedgEntry@1210001 : Record 17413;
      EmplAbsenceEntry@1210002 : Record 17389;
      TimesheetLine@1210005 : Record 17441;
      PostedAbsenceLine@1210006 : Record 17388;
      AbsenceHeader@1210007 : Record 17385;
      AbsenceLine@1210008 : Record 17386;
      TimeSheetStatus@1210012 : Record 17440;
      PayrollStatus@1210014 : Record 17457;
      EmplJnlPostLine@1210011 : Codeunit 17382;
      CurrentDate@1210004 : Date;
      PayrollRegNo@1210013 : Integer;
    BEGIN
      PayrollStatus.CheckPayrollStatus(PostedAbsenceHeader."Period Code",PostedAbsenceHeader."Employee No.");
      TimeSheetStatus.GET(PostedAbsenceHeader."Period Code",PostedAbsenceHeader."Employee No.");
      TimeSheetStatus.TESTFIELD(Status,TimeSheetStatus.Status::Open);

      EmplLedgEntry.RESET;
      EmplLedgEntry.SETCURRENTKEY("Employee No.");
      EmplLedgEntry.SETRANGE("Employee No.",PostedAbsenceHeader."Employee No.");
      EmplLedgEntry.SETRANGE("Document Type",PostedAbsenceHeader."Document Type" + 1);
      EmplLedgEntry.SETRANGE("Document No.",PostedAbsenceHeader."No.");
      IF EmplLedgEntry.FINDFIRST THEN BEGIN
        PayrollRegNo := GetPayrollReg(EmplLedgEntry);
        EmplLedgEntry.DELETEALL;
        IF PayrollRegNo <> 0 THEN
          EmplJnlPostLine.CancelRegister(PayrollRegNo);
      END;

      EmplAbsenceEntry.RESET;
      EmplAbsenceEntry.SETRANGE("Employee No.",PostedAbsenceHeader."Employee No.");
      EmplAbsenceEntry.SETRANGE("Document Type",PostedAbsenceHeader."Document Type" + 1);
      EmplAbsenceEntry.SETRANGE("Document No.",PostedAbsenceHeader."No.");
      EmplAbsenceEntry.DELETEALL;

      AbsenceHeader.INIT;
      AbsenceHeader.TRANSFERFIELDS(PostedAbsenceHeader);
      AbsenceHeader.INSERT;
      NewDocNo := AbsenceHeader."No.";

      PostedAbsenceLine.SETRANGE("Document Type",PostedAbsenceHeader."Document Type");
      PostedAbsenceLine.SETRANGE("Document No.",PostedAbsenceHeader."No.");
      IF PostedAbsenceLine.FINDSET THEN
        REPEAT
          AbsenceLine.INIT;
          AbsenceLine.TRANSFERFIELDS(PostedAbsenceLine);
          AbsenceLine."Document No." := AbsenceHeader."No.";
          AbsenceLine.INSERT;

          TimesheetMgt.IsPostedOrderCancellation(TRUE);

          CurrentDate := PostedAbsenceLine."Start Date";
          WHILE CurrentDate <= PostedAbsenceLine."End Date" DO BEGIN
            TimesheetLine.GET(PostedAbsenceHeader."Employee No.",CurrentDate);
            TimesheetMgt.InsertTimesheetDetails(
              PostedAbsenceHeader."Employee No.",TimesheetLine.Date,TimesheetLine."Time Activity Code",
              TimesheetLine."Planned Hours",0,'',0,'',0D);
            CurrentDate := CALCDATE('<1D>',CurrentDate);
          END;

        UNTIL PostedAbsenceLine.NEXT = 0;

      CopyCommentLines(PostedAbsenceHeader."No.",AbsenceHeader."No.",TRUE);

      PostedAbsenceHeader.DELETE(TRUE);
    END;

    PROCEDURE GetPayrollReg@1210009(VAR EmplLedgEntry@1210000 : Record 17413) : Integer;
    VAR
      EmplLedgEntry2@1210001 : Record 17413;
      PayrollReg@1210002 : Record 17424;
      FromEntryNo@1210004 : Integer;
      ToEntryNo@1210003 : Integer;
    BEGIN
      EmplLedgEntry2.COPY(EmplLedgEntry);
      IF EmplLedgEntry2.FINDFIRST THEN
        FromEntryNo := EmplLedgEntry2."Entry No.";
      IF EmplLedgEntry2.FINDLAST THEN
        ToEntryNo := EmplLedgEntry2."Entry No.";
      IF (FromEntryNo <> 0) AND (ToEntryNo <> 0) THEN BEGIN
        PayrollReg.SETFILTER("From Entry No.",'>=%1',FromEntryNo);
        PayrollReg.SETFILTER("To Entry No.",'<=%1',ToEntryNo);
        IF PayrollReg.FINDFIRST THEN
          EXIT(PayrollReg."No.")
      END;

      EXIT(0);
    END;

    PROCEDURE InsertVacationAccrual@1210008(AbsenceLine@1210000 : Record 17386) : Integer;
    VAR
      VacationAccrualEntry@1210001 : Record 17389;
    BEGIN
      EmplAbsenceEntry.RESET;
      EmplAbsenceEntry.SETRANGE("Employee No.",AbsenceLine."Employee No.");
      EmplAbsenceEntry.SETRANGE("Entry Type",EmplAbsenceEntry."Entry Type"::Accrual);
      EmplAbsenceEntry.SETRANGE("Accrual Entry No.",0);
      EmplAbsenceEntry.SETFILTER(
        "Time Activity Code",
        GetAnnualVacTimeActFilter(CALCDATE('<CY>',AbsenceLine."End Date")));
      IF EmplAbsenceEntry.FINDLAST THEN BEGIN
        NextEntryNo := NextEntryNo + 1;
        VacationAccrualEntry.INIT;
        VacationAccrualEntry.TRANSFERFIELDS(EmplAbsenceEntry);
        VacationAccrualEntry."Entry No." := NextEntryNo;
        VacationAccrualEntry."Start Date" := CALCDATE('<+1D>',EmplAbsenceEntry."End Date");
        VacationAccrualEntry."End Date" := CALCDATE('<1Y-1D>',VacationAccrualEntry."Start Date");
        VacationAccrualEntry."Accrual Entry No." := 0;
        VacationAccrualEntry."Calendar Days" := EmplAbsenceEntry."Calendar Days";
        VacationAccrualEntry."Working Days" := 0;
        VacationAccrualEntry.INSERT;
        EXIT(VacationAccrualEntry."Entry No.");
      END;

      EXIT(0);
    END;

    BEGIN
    END.
  }
}

