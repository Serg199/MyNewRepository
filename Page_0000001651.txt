OBJECT Page 1651 Curr. Exch. Rate Service Card
{
  OBJECT-PROPERTIES
  {
    Date=25.10.16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Currency Exch. Rate Service;
               RUS=Служба валютных курсов];
    SourceTable=Table1650;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Setup;
                                RUS=Создать,Обработать,Отчет,Настройка];
    OnOpenPage=BEGIN
                 CODEUNIT.RUN(CODEUNIT::"Check App. Area Only Basic");

                 UpdateBasedOnEnable;
               END;

    OnInsertRecord=VAR
                     TempField@1002 : TEMPORARY Record 2000000041;
                     MapCurrencyExchangeRate@1001 : Codeunit 1280;
                   BEGIN
                     MapCurrencyExchangeRate.GetSuggestedFields(TempField);
                     CurrPage.SimpleDataExchSetup.PAGE.SetSuggestedField(TempField);
                     UpdateSimpleMappingsPart;
                   END;

    OnQueryClosePage=BEGIN
                       IF NOT Enabled THEN
                         IF NOT CONFIRM(STRSUBSTNO(EnableServiceQst,CurrPage.CAPTION),TRUE) THEN
                           EXIT(FALSE);
                     END;

    OnAfterGetCurrRecord=BEGIN
                           GetWebServiceURL(WebServiceURL);
                           IF WebServiceURL <> '' THEN
                             GenerateXMLStructure;

                           UpdateSimpleMappingsPart;
                           UpdateBasedOnEnable;
                         END;

    ActionList=ACTIONS
    {
      { 9       ;    ;ActionContainer;
                      Name=Process;
                      CaptionML=[ENU=Process;
                                 RUS=Процесс];
                      ActionContainerType=ActionItems }
      { 10      ;1   ;Action    ;
                      Name=Preview;
                      CaptionML=[ENU=Preview;
                                 RUS=Просмотр];
                      ToolTipML=[ENU=Test the setup of the currency exchange rate service to make sure the service is working.;
                                 RUS=Тестирование работоспособности настройки службы валютных курсов.];
                      ApplicationArea=#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReviewWorksheet;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TempCurrencyExchangeRate@1001 : TEMPORARY Record 330;
                                 UpdateCurrencyExchangeRates@1000 : Codeunit 1281;
                               BEGIN
                                 TESTFIELD(Code);
                                 VerifyServiceURL;
                                 VerifyDataExchangeLineDefinition;
                                 UpdateCurrencyExchangeRates.GenerateTempDataFromService(TempCurrencyExchangeRate,Rec);
                                 PAGE.RUN(PAGE::"Currency Exchange Rates",TempCurrencyExchangeRate);
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=JobQueueEntry;
                      CaptionML=[ENU=Job Queue Entry;
                                 RUS=Операция очереди работ];
                      ToolTipML=[ENU=View or edit the job that updates the exchange rates from the service. For example, you can see the status or change how often rates are updated.;
                                 RUS=Просмотр или изменение работы, которая обновляет валютные курсы с использованием службы. Например, вы можете видеть состояние службы или изменить периодичность обновления курсов.];
                      ApplicationArea=#Suite;
                      Promoted=Yes;
                      Enabled=Enabled;
                      Image=JobListSetup;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowJobQueueEntry;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 5   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           RUS=Общее];
                GroupType=Group }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the setup of a service to update currency exchange rates.;
                           RUS=Определяет настройку службы обновления валютных курсов.];
                ApplicationArea=#Suite;
                SourceExpr=Code;
                Editable=EditableByNotEnabled }

    { 3   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the setup of a service to update currency exchange rates.;
                           RUS=Определяет настройку службы обновления валютных курсов.];
                ApplicationArea=#Suite;
                SourceExpr=Description;
                Editable=EditableByNotEnabled }

    { 7   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies if the currency exchange rate service is enabled. Only one service can be enabled at a time.;
                           RUS=Указывает, включена ли служба валютных курсов. Единовременно может быть включена только одна служба.];
                ApplicationArea=#Suite;
                SourceExpr=Enabled;
                OnValidate=BEGIN
                             EditableByNotEnabled := NOT Enabled;
                             CurrPage.UPDATE;
                           END;
                            }

    { 16  ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=ShowEnableWarning;
                Enabled=NOT EditableByNotEnabled;
                Editable=false;
                OnDrillDown=BEGIN
                              DrilldownCode;
                            END;
                             }

    { 4   ;1   ;Group     ;
                CaptionML=[ENU=Service;
                           RUS=Сервис];
                GroupType=Group }

    { 11  ;2   ;Field     ;
                Name=ServiceURL;
                CaptionML=[ENU=Service URL;
                           RUS=URL-адрес службы];
                ToolTipML=[ENU=Specifies if the currency exchange rate service is enabled. Only one service can be enabled at a time.;
                           RUS=Указывает, включена ли служба валютных курсов. Единовременно может быть включена только одна служба.];
                ApplicationArea=#Suite;
                SourceExpr=WebServiceURL;
                Editable=EditableByNotEnabled;
                MultiLine=Yes;
                OnValidate=BEGIN
                             SetWebServiceURL(WebServiceURL);
                             GenerateXMLStructure;
                           END;
                            }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the name of the service provider.;
                           RUS=Определяет название поставщика службы.];
                ApplicationArea=#Suite;
                SourceExpr="Service Provider";
                Editable=EditableByNotEnabled }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the URL of the service provider's terms of service.;
                           RUS=Определяет URL-адрес условий предоставления услуг поставщиком службы.];
                ApplicationArea=#Suite;
                SourceExpr="Terms of Service";
                Editable=EditableByNotEnabled }

    { 15  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies if web requests occurring in connection with the service are logged. The log is located in the server Temp folder.;
                           RUS=Определяет, ведется ли журнал веб-запросов, которые имеют место при обращении к службе. Журнал находится в папке Temp сервера.];
                ApplicationArea=#Suite;
                SourceExpr="Log Web Requests";
                Editable=EditableByNotEnabled }

    { 6   ;1   ;Part      ;
                Name=SimpleDataExchSetup;
                ApplicationArea=#Suite;
                PagePartID=Page1265;
                PartType=Page }

  }
  CODE
  {
    VAR
      TempXMLBuffer@1001 : TEMPORARY Record 1235;
      WebServiceURL@1000 : Text;
      EditableByNotEnabled@1002 : Boolean;
      EnabledWarningTok@1004 : TextConst 'ENU=You must disable the service before you can make changes.;RUS=Перед внесением изменений необходимо отключить службу.';
      DisableEnableQst@1003 : TextConst 'ENU=Do you want to disable currency exchange rate service?;RUS=Отключить службу валютных курсов?';
      ShowEnableWarning@1005 : Text;
      EnableServiceQst@1006 : TextConst '@@@="%1 = This Page Caption (Currency Exch. Rate Service)";ENU=The %1 is not enabled. Are you sure you want to exit?;RUS=%1 не включен. Вы действительно хотите выйти?';
      XmlStructureIsNotSupportedErr@1007 : TextConst 'ENU=" The provided url does not contain a supported structure.";RUS=" Указанный URL-адрес не содержит поддерживаемую структуру."';

    LOCAL PROCEDURE UpdateSimpleMappingsPart@2();
    BEGIN
      CurrPage.SimpleDataExchSetup.PAGE.SetDataExchDefCode("Data Exch. Def Code");
      CurrPage.SimpleDataExchSetup.PAGE.UpdateData;
      CurrPage.SimpleDataExchSetup.PAGE.UPDATE(FALSE);
      CurrPage.SimpleDataExchSetup.PAGE.SetSourceToBeMandatory("Web Service URL".HASVALUE);
    END;

    LOCAL PROCEDURE GenerateXMLStructure@1();
    BEGIN
      TempXMLBuffer.RESET;
      TempXMLBuffer.DELETEALL;
      IF GetXMLStructure(TempXMLBuffer,WebServiceURL) THEN BEGIN
        TempXMLBuffer.RESET;
        CurrPage.SimpleDataExchSetup.PAGE.SetXMLDefinition(TempXMLBuffer);
      END ELSE
        ERROR(XmlStructureIsNotSupportedErr);
    END;

    LOCAL PROCEDURE UpdateBasedOnEnable@4();
    BEGIN
      EditableByNotEnabled := NOT Enabled;
      ShowEnableWarning := '';
      IF CurrPage.EDITABLE AND Enabled THEN
        ShowEnableWarning := EnabledWarningTok;
    END;

    LOCAL PROCEDURE DrilldownCode@3();
    BEGIN
      IF CONFIRM(DisableEnableQst,TRUE) THEN BEGIN
        Enabled := FALSE;
        UpdateBasedOnEnable;
        CurrPage.UPDATE;
      END;
    END;

    BEGIN
    END.
  }
}

